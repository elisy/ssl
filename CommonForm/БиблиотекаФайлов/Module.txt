
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектСсылка = Параметры.ОбъектСсылка;
	ОбновитьСписокФайлов();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокФайлов()
	
	СписокФайлов.Очистить();
	
	Прокси = РаботаСБиблиотекойФайлов.ПолучитьПрокси();
	
	Папка = РаботаСБиблиотекойФайлов.ПолучитьПапкуФайлов(ОбъектСсылка, Ложь);
	
	Если Папка <> Неопределено Тогда
		ПапкаТип = Прокси.ФабрикаXDTO.Тип("http://www.1c.ru/docmng", "Folder");
		ПапкаXDTO = Прокси.ФабрикаXDTO.Создать(ПапкаТип);
		ПапкаXDTO.Code = Папка.Code;
		ПапкаXDTO.Name = Папка.Name;
		ПапкаXDTO.Description = Папка.Description;
		
		Файлы = Прокси.GetFileList(ПапкаXDTO);
		
		Для каждого СведенияОФайле Из Файлы.Files Цикл
			НоваяСтрока = СписокФайлов.Добавить();
			
			НоваяСтрока.Файл = СведенияОФайле.Name;
			НоваяСтрока.Расширение = СведенияОФайле.Extension;
			НоваяСтрока.Описание = СведенияОФайле.Description;
			НоваяСтрока.Размер = СведенияОФайле.Size;
			НоваяСтрока.Код = СведенияОФайле.Code;
			НоваяСтрока.КодТипаФайла = ФайловыеФункцииКлиентСервер.ПолучитьИндексПиктограммыФайла(НоваяСтрока.Расширение);
		КонецЦикла;
		
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура Добавить(Команда)
	
	АдресВременногоХранилищаФайла = "";
	ИмяБезРасширения = "";
	Расширение = "";
		
	РасширениеПодключено = ПодключитьРасширениеРаботыСФайлами();
	Если РасширениеПодключено Тогда
		ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		ВыборФайла.МножественныйВыбор = Ложь;
		ВыборФайла.Заголовок = НСтр("ru = 'Выбор файла'");
		ВыборФайла.Фильтр = НСтр("ru = 'Все файлы (*.*)|*.*'");

		Результат = ВыборФайла.Выбрать();
		ПолноеИмяФайла = ВыборФайла.ПолноеИмяФайла;
	
		Если НЕ Результат Тогда
			Возврат;
		КонецЕсли;
		
		Файл = Новый Файл(ПолноеИмяФайла);
		Расширение = Файл.Расширение;
		ИмяБезРасширения = Файл.ИмяБезРасширения;
		
		ПомещаемыеФайлы = Новый Массив;
		Описание = Новый ОписаниеПередаваемогоФайла(Файл.ПолноеИмя, "");
		ПомещаемыеФайлы.Добавить(Описание);
		
		ПомещенныеФайлы = Новый Массив;

		Если НЕ ПоместитьФайлы(ПомещаемыеФайлы, ПомещенныеФайлы, , Ложь, УникальныйИдентификатор) Тогда		
			ВызватьИсключение
			  СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при помещении файла в хранилище: %1'"), Файл.ПолноеИмя);
		КонецЕсли;
		
		Если ПомещенныеФайлы.Количество() = 1 Тогда
			АдресВременногоХранилищаФайла = ПомещенныеФайлы[0].Хранение;
		КонецЕсли;
		
	Иначе 
		// Если веб-клиент

		ИмяФайла = "";	
		// Поместим Файл в ВременноеХранилище
		Если НЕ ПоместитьФайл(АдресВременногоХранилищаФайла, ИмяФайла, ИмяФайла, Истина, УникальныйИдентификатор) Тогда
			Возврат;
		КонецЕсли;

		СтрокиПути = ФайловыеФункцииКлиентСервер.РазложитьСтрокуПоТочкамИСлэшам(ИмяФайла);
		Если СтрокиПути.Количество() >= 2 Тогда
			Расширение = СтрокиПути[СтрокиПути.Количество()-1];
			ИмяБезРасширения = СтрокиПути[СтрокиПути.Количество()-2];
		Иначе
			ВызватьИсключение
			  СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при помещении файла в хранилище: %1'"),ИмяФайла);
		КонецЕсли;
		
	КонецЕсли;
	
	ДобавитьСервер(
		ИмяБезРасширения,
		Расширение,
		АдресВременногоХранилищаФайла);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСервер(ИмяБезРасширения, Расширение, АдресВременногоХранилищаФайла)
	
	Прокси = РаботаСБиблиотекойФайлов.ПолучитьПрокси();
	
	Папка = РаботаСБиблиотекойФайлов.ПолучитьПапкуФайлов(ОбъектСсылка, Истина);
	
	ПапкаТип = Прокси.ФабрикаXDTO.Тип("http://www.1c.ru/docmng", "Folder");
	ПапкаXDTO = Прокси.ФабрикаXDTO.Создать(ПапкаТип);	
	ПапкаXDTO.Code = Папка.Code;
	ПапкаXDTO.Name = Папка.Name;
	ПапкаXDTO.Description = Папка.Description;
	
	ФайлТип = Прокси.ФабрикаXDTO.Тип("http://www.1c.ru/docmng", "File");
	Файл = Прокси.ФабрикаXDTO.Создать(ФайлТип);
	Файл.Code = "";
	Файл.Name = ИмяБезРасширения;
	Файл.Extension = Расширение;
	Файл.BinaryData = ПолучитьИзВременногоХранилища(АдресВременногоХранилищаФайла);
	
	СведенияОФайле = Прокси.AddFile(ПапкаXDTO, Файл);
	ОбновитьСписокФайлов();

КонецПроцедуры	

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	
	ОткрытьВыбранныйФайл();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВыбранныйФайл()
	
	АдресВременногоХранилища = ПолучитьАдресФайла(Элементы.СписокФайлов.ТекущиеДанные.Код);
	
	#Если ВебКлиент Тогда
		ПолучитьФайл(АдресВременногоХранилища, , Истина);
	#Иначе
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла(Элементы.СписокФайлов.ТекущиеДанные.Расширение);
		
		Если ПолучитьФайл(АдресВременногоХранилища, ИмяВременногоФайла, Ложь) = Истина Тогда
			ЗапуститьПриложение(ИмяВременногоФайла);
		КонецЕсли;
	#КонецЕсли	
	
КонецПроцедуры

&НаСервере
Функция ПолучитьАдресФайла(Код)
	
	Прокси = РаботаСБиблиотекойФайлов.ПолучитьПрокси();
	ДанныеФайла = Прокси.GetFile(Код);
	
 	Адрес = ПоместитьВоВременноеХранилище(ДанныеФайла.BinaryData);
	
	Возврат Адрес;
	
КонецФункции

&НаКлиенте
Процедура СписокФайловВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОткрытьВыбранныйФайл();
	
КонецПроцедуры

&НаКлиенте
Процедура Удалить(Команда)
	
	УдалитьФайл(Элементы.СписокФайлов.ТекущиеДанные.Код);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьФайл(Код)
	
	Прокси = РаботаСБиблиотекойФайлов.ПолучитьПрокси();
	Прокси.DeleteFile(Код);
	ОбновитьСписокФайлов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточку(Команда)
	
	ПараметрыФормы = Новый Структура(
		"Код, Файл, Расширение, Размер, Описание", 
		Элементы.СписокФайлов.ТекущиеДанные.Код,
		Элементы.СписокФайлов.ТекущиеДанные.Файл,
		Элементы.СписокФайлов.ТекущиеДанные.Расширение,
		Элементы.СписокФайлов.ТекущиеДанные.Размер,
		Элементы.СписокФайлов.ТекущиеДанные.Описание);
		
	ОткрытьФорму("ОбщаяФорма.КарточкаФайлаИзБиблиотекиФайлов", ПараметрыФормы);
		
КонецПроцедуры
