
////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры

&НаСервере
Процедура ЗаполнитьРеквизитыФормыИзXDTO(ИмяРеквизитаФормы, ОбъектXDTO, ИмяСвойстваXDTO)
	
	Если ОбъектXDTO.Установлено(ИмяСвойстваXDTO) Тогда
		ЗначениеСвойстваXDTO = ОбъектXDTO[ИмяСвойстваXDTO];
		Если ТипЗнч(ЗначениеСвойстваXDTO) = Тип("ОбъектXDTO") Тогда 
			ЭтаФорма[ИмяРеквизитаФормы + "Тип"] = ЗначениеСвойстваXDTO.objectId.type;
			ЭтаФорма[ИмяРеквизитаФормы + "ID"] 	= ЗначениеСвойстваXDTO.objectId.id;
			ЭтаФорма[ИмяРеквизитаФормы] 		= ЗначениеСвойстваXDTO.name;
		Иначе	
			ЭтаФорма[ИмяРеквизитаФормы] = ЗначениеСвойстваXDTO;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьXDTOизРеквизитовФормы(Прокси, ОбъектXDTO, ИмяСвойстваXDTO, ИмяРеквизитаФормы)
	
	ТипСвойства = ОбъектXDTO.Тип().Свойства.Получить(ИмяСвойстваXDTO).Тип;
	ТипРодитель = Прокси.ФабрикаXDTO.Тип("http://www.1c.ru/dm", "DMObject");
	
	Если ТипЗнч(ТипСвойства) = Тип("ТипОбъектаXDTO") И ТипРодитель.ЭтоПотомок(ТипСвойства) Тогда 
		
		Реквизит    = ЭтаФорма[ИмяРеквизитаФормы];
		РеквизитID  = ЭтаФорма[ИмяРеквизитаФормы + "ID"];
		РеквизитТип = ЭтаФорма[ИмяРеквизитаФормы + "Тип"];
		
		ЗначениеСвойства = Прокси.ФабрикаXDTO.Создать(ТипСвойства);
		
		// заполнить из потребителя
		Если Метаданные.НайтиПоПолномуИмени(РеквизитТип) <> Неопределено Тогда 
			РеквизитТип = СтрЗаменить(РеквизитТип, "ПланВидовХарактеристик.", "ПланыВидовХарактеристик.");
			РеквизитТип = СтрЗаменить(РеквизитТип, "БизнесПроцесс.", "БизнесПроцессы.");
			РеквизитТип = СтрЗаменить(РеквизитТип, "Справочник.", "Справочники.");
			РеквизитТип = СтрЗаменить(РеквизитТип, "Документ.",	"Документы.");
			РеквизитТип = СтрЗаменить(РеквизитТип, "Задача.", "Задачи.");
			
			СсылкаНаПотребителя = Неопределено;
			Выполнить("СсылкаНаПотребителя = " + РеквизитТип + ".ПолучитьСсылку(Новый УникальныйИдентификатор(РеквизитID))");
			
			РаботаС1СДокументооборот.ЗаполнитьРеквизитыИзПотребителя(Прокси, ЗначениеСвойства, СсылкаНаПотребителя);
			
		ИначеЕсли РеквизитТип = "Строка" Тогда 
			
			ЗначениеСвойства.objectId = РаботаС1СДокументооборот.СоздатьObjectID(Прокси, "", "");
			ЗначениеСвойства.name = Реквизит;
			
			ExternalObject = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "ExternalObject");
			ExternalObject.id = "";
			ExternalObject.type = "";
			ExternalObject.name = "";
			
			ЗначениеСвойства.externalObject = ExternalObject;
			
		ИначеЕсли ЗначениеЗаполнено(Реквизит) Тогда 
			
			ЗначениеСвойства.objectId = РаботаС1СДокументооборот.СоздатьObjectID(Прокси, РеквизитID, РеквизитТип); 
			ЗначениеСвойства.name = Реквизит;
			
		Иначе	
			
			ЗначениеСвойства.objectId = РаботаС1СДокументооборот.СоздатьObjectID(Прокси, "", ""); 
			ЗначениеСвойства.name = "";
			
		КонецЕсли;
		
		ОбъектXDTO.Установить(ИмяСвойстваXDTO, ЗначениеСвойства);
		
	Иначе
		ОбъектXDTO.Установить(ИмяСвойстваXDTO, ЭтаФорма[ИмяРеквизитаФормы]);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииВидаДокумента()
	
	РаботаС1СДокументооборот.ПриИзмененииВидаНаФормеДокумента(ЭтаФорма);
	
КонецПроцедуры	

&НаСервере
Процедура УстановитьДоступностьПоСостоянию(ОбъектXDTO)
	
	ДосупныеПоля = Новый Массив;
	Для Каждого ИмяПоля Из ОбъектXDTO.enabledProperties Цикл
		ДосупныеПоля.Добавить(НРег(ИмяПоля));
	КонецЦикла;	
	
	СоответсвиеРеквизитов = РаботаС1СДокументооборот.СоответсвиеСвойствXDTOиРеквизитовФормыДокумента(ЭтаФорма.ИмяФормы);
	Для Каждого СтрокаСоответсвия Из СоответсвиеРеквизитов Цикл
		Если ДосупныеПоля.Количество() = 0 Или ДосупныеПоля.Найти(НРег(СтрокаСоответсвия.Ключ)) <> Неопределено Тогда 
			Элементы[СтрокаСоответсвия.Значение].ТолькоПросмотр = Ложь;
		Иначе	
			Элементы[СтрокаСоответсвия.Значение].ТолькоПросмотр = Истина;
		КонецЕсли;	
	КонецЦикла;
	
	Если ДосупныеПоля.Количество() = 0 Или ДосупныеПоля.Найти("addFile") <> Неопределено Тогда 
		Элементы.ФайлыСоздатьФайл.Доступность = Истина;
		Элементы.ФайлыКонтекстноеМенюСоздатьФайл.Доступность = Истина;
		Элементы.Файлы.ИзменятьСоставСтрок = Истина;
	Иначе	
		Элементы.ФайлыСоздатьФайл.Доступность = Ложь;
		Элементы.ФайлыКонтекстноеМенюСоздатьФайл.Доступность = Ложь;
		Элементы.Файлы.ИзменятьСоставСтрок = Ложь;
	КонецЕсли;	
	
	Если ДосупныеПоля.Количество() = 0 Или ДосупныеПоля.Найти("openFile") <> Неопределено Тогда 
		Элементы.ФайлыОткрытьФайл.Доступность = Истина;
		Элементы.ФайлыКонтекстноеМенюОткрытьФайл.Доступность = Истина;
	Иначе	
		Элементы.ФайлыОткрытьФайл.Доступность = Ложь;
		Элементы.ФайлыКонтекстноеМенюОткрытьФайл.Доступность = Ложь;
	КонецЕсли;	
	
	Если ДосупныеПоля.Количество() = 0 Или ДосупныеПоля.Найти("saveFile") <> Неопределено Тогда 
		Элементы.ФайлыСохранитьКак.Доступность = Истина;
		Элементы.ФайлыКонтекстноеМенюСохранитьКак.Доступность = Истина;
	Иначе	
		Элементы.ФайлыСохранитьКак.Доступность = Ложь;
		Элементы.ФайлыКонтекстноеМенюСохранитьКак.Доступность = Ложь;
	КонецЕсли;
	
	Если ДосупныеПоля.Количество() = 0 Или ДосупныеПоля.Найти("editFile") <> Неопределено Тогда 
		Элементы.ФайлыОбновитьИзФайлаНаДиске.Доступность = Истина;
		Элементы.ФайлыКонтекстноеМенюОбновитьИзФайлаНаДиске.Доступность = Истина;
	Иначе	
		Элементы.ФайлыОбновитьИзФайлаНаДиске.Доступность = Ложь;
		Элементы.ФайлыКонтекстноеМенюОбновитьИзФайлаНаДиске.Доступность = Ложь;
	КонецЕсли;
	
	Если ДосупныеПоля.Количество() = 0 Или ДосупныеПоля.Найти("register") <> Неопределено Тогда 
		Элементы.Зарегистрировать.Доступность = Истина;
	Иначе
		Элементы.Зарегистрировать.Доступность = Ложь;
	КонецЕсли;	
	
КонецПроцедуры	

&НаСервере
Процедура УстановитьДоступностьПоЭЦП(ОбъектXDTO)
	
	Если ОбъектXDTO.signatures.Количество() <> 0 Тогда
		МассивИменПолей = ПолучитьИменаКлючевыхПолей();
		
		Для Каждого ИмяПоля Из МассивИменПолей Цикл
			Элементы[ИмяПоля].ТолькоПросмотр = Истина;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
// Возвращает массив имен ключевых полей формы
Функция ПолучитьИменаКлючевыхПолей()
	
	МассивИмен = Новый Массив;
	
	МассивИмен.Добавить("ЗаголовокДокумента");
	МассивИмен.Добавить("Описание");
	
	Возврат МассивИмен;
	
КонецФункции	


////////////////////////////////////////////////////////////////////////////////
// События формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ID = Параметры.ID;
	Тип = Параметры.Тип;
	Если НЕ ЗначениеЗаполнено(Тип) Тогда
		Тип = "DMInternalDocument";
	КонецЕсли;
	ВнешнийОбъект = Параметры.ВнешнийОбъект;
	КаталогДляСохраненияДанных = РаботаС1СДокументооборотВызовСервера.ПолучитьЛокальныйКаталогФайлов();
	
	// считать данные документа
	Прокси = РаботаС1СДокументооборотВызовСервера.ПолучитьПрокси();
	Если ЗначениеЗаполнено(ID) И ЗначениеЗаполнено(Тип) Тогда 
		Запрос = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "DMRetrieveRequest");
		
		ОбъектИд = РаботаС1СДокументооборот.СоздатьObjectID(Прокси, ID, Тип);
		Запрос.objectIds.Добавить(ОбъектИд);
		
		Результат = Прокси.execute(Запрос);
		РаботаС1СДокументооборотВызовСервера.ПроверитьВозвратВебСервиса(Прокси, Результат);	
		
		Объект = Результат.objects[0];
		Заголовок = Объект.name + НСтр("ru = ' (Внутренний документ)'");
	Иначе
		Запрос = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetNewObjectRequest");
		Запрос.type = Тип;
		
		Результат = Прокси.execute(Запрос);
		РаботаС1СДокументооборотВызовСервера.ПроверитьВозвратВебСервиса(Прокси, Результат);
		
		Объект = Результат;
		Заголовок = НСтр("ru = 'Внутренний документ (создание)'");
	КонецЕсли;	
		
	// перенести данные в форму
	ПрочитатьОбъектВФорму(Объект);
	
	// файлы
	РаботаС1СДокументооборотВызовСервера.ОбновитьСписокФайлов(Объект.files, Файлы, Элементы.ГруппаФайлы);
	
	// ЭЦП
	ИспользоватьЭлектронныеЦифровыеПодписиВ1СДокументооборот 
		= РаботаС1СДокументооборотПовтИсп.ПолучитьНастройки().ИспользоватьЭлектронныеЦифровыеПодписи;
		
	ИспользоватьЭлектронныеЦифровыеПодписиВБСП = РаботаС1СДокументооборотВызовСервера.ПолучитьИспользоватьЭлектронныеЦифровыеПодписи();
	
	Если ИспользоватьЭлектронныеЦифровыеПодписиВ1СДокументооборот = Ложь Тогда
		Элементы.ФайлыПодписанЭЦП.Видимость = Ложь;
		Элементы.ГруппаЭЦП.Видимость = Ложь;
		
		Элементы.ФайлыСохранитьВместеСЭЦП.Видимость = Ложь;
		Элементы.ФайлыКонтекстноеМенюСохранитьВместеСЭЦП.Видимость = Ложь;
		
	Иначе
		ЗаполнитьСписокПодписей(Объект.signatures, Объект.files);
		
		Если Объект.Установлено("keyPropertiesValue") Тогда
			АдресСлепкаДокумента = ПоместитьВоВременноеХранилище(Объект.keyPropertiesValue, УникальныйИдентификатор);
		КонецЕсли;	
	КонецЕсли;	
	
	Если ИспользоватьЭлектронныеЦифровыеПодписиВ1СДокументооборот = Ложь ИЛИ ИспользоватьЭлектронныеЦифровыеПодписиВБСП = Ложь Тогда
		Элементы.ФайлыПодписать.Видимость = Ложь;
		Элементы.ФайлыКонтекстноеМенюПодписать.Видимость = Ложь;
		Элементы.ФайлыДобавитьЭЦПИзФайла.Видимость = Ложь;
		Элементы.ФайлыКонтекстноеМенюДобавитьЭЦПИзФайла.Видимость = Ложь;
		Элементы.ФормаПодписатьДокумент.Видимость = Ложь;
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(ID) И ЗначениеЗаполнено(ВнешнийОбъект) Тогда
		РаботаС1СДокументооборот.ЗаполнитьФормуИзПотребителя(Параметры.ВнешнийОбъект, ЭтаФорма);
	КонецЕсли;
	
	Элементы.ФайлыЗаполнитьКопированием.Видимость = Ложь;
	Если ЗначениеЗаполнено(ВнешнийОбъект) Тогда
		Если ФайловыеФункцииПереопределяемый.ЕстьХранимыеФайлы(ВнешнийОбъект) Тогда
			Элементы.ФайлыЗаполнитьКопированием.Видимость = Истина;
			Элементы.ФайлыЗаполнитьКопированием.Доступность = (Файлы.Количество() = 0);
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.ТолькоПросмотр Тогда 
		ЭтаФорма.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьОбъектВФорму(Объект)
	
	// заполнение реквизитов
	СоответсвиеРеквизитов = РаботаС1СДокументооборот.СоответсвиеСвойствXDTOиРеквизитовФормыДокумента(ЭтаФорма.ИмяФормы);
	Для Каждого СтрокаСоответсвия Из СоответсвиеРеквизитов Цикл
		ЗаполнитьРеквизитыФормыИзXDTO(СтрокаСоответсвия.Значение, Объект, СтрокаСоответсвия.Ключ);
	КонецЦикла;	
	Представление = Объект.name;
	
	НачальныйРегистрационныйНомер = РегистрационныйНомер;
	НачальнаяДатаРегистрации = ДатаРегистрации;
	
	// доступность по состоянию
	УстановитьДоступностьПоСостоянию(Объект);
	УстановитьДоступностьПоЭЦП(Объект);
	
	Элементы.Организация.Видимость 		  = (Объект.organizationEnabled   		= Истина);
	Элементы.ВопросДеятельности.Видимость = (Объект.activityMatterEnabled 		= Истина);
	Элементы.ГрифДоступа.Видимость 		  = (Объект.accessLevelEnabled    		= Истина);
	Элементы.Файлы.Видимость 			  = (Объект.filesEnabled 		  		= Истина);
	Элементы.ВидДокумента.Видимость 	  = (Объект.documentTypeEnabled   		= Истина);
	Элементы.Состояние.Видимость 	  	  = (Объект.statusEnabled 		  		= Истина);
	Элементы.Состояние.Доступность 	  	  = (Объект.statusChangeEnabled   		= Истина);
	ОграничиватьДоступностьПолей 		  = (Объект.limitPropertiesAvailability = Истина);
	
	// по виду 
	Элементы.СрокИсполнения.Видимость 	  = (Объект.documentType <> Неопределено И Объект.documentType.performanceDateEnabled = Истина); 
	Элементы.Корреспондент.Видимость  	  = (Объект.documentType <> Неопределено И Объект.documentType.correspondentEnabled = Истина); 
	Элементы.КонтактноеЛицо.Видимость 	  = (Объект.documentType <> Неопределено И Объект.documentType.correspondentEnabled = Истина); 
	Элементы.Сумма.Видимость 		  	  = (Объект.documentType <> Неопределено И Объект.documentType.sumEnabled = Истина); 
	Элементы.Валюта.Видимость 		  	  = (Объект.documentType <> Неопределено И Объект.documentType.sumEnabled = Истина); 
	Элементы.Бессрочный.Видимость 	  	  = (Объект.documentType <> Неопределено И Объект.documentType.durationEnabled = Истина); 
	Элементы.ПорядокПродления.Видимость   = (Объект.documentType <> Неопределено И Объект.documentType.durationEnabled = Истина); 
	Элементы.ДатаНачалаДействия.Видимость = (Объект.documentType <> Неопределено И Объект.documentType.durationEnabled = Истина); 
	Элементы.ДатаОкончанияДействия.Видимость = (Объект.documentType <> Неопределено И Объект.documentType.durationEnabled = Истина); 
	
	АвтоматическаяНумерация = (Объект.documentType <> Неопределено И Объект.documentType.automaticNumeration = Истина);
	Если АвтоматическаяНумерация Тогда 
		Элементы.РегистрационныйНомер.ОтображениеПредупрежденияПриРедактировании = ОтображениеПредупрежденияПриРедактировании.Отображать;
		Элементы.ДатаРегистрации.ОтображениеПредупрежденияПриРедактировании = ОтображениеПредупрежденияПриРедактировании.Отображать;
		Элементы.Зарегистрировать.Доступность = Истина;
	Иначе
		Элементы.РегистрационныйНомер.ОтображениеПредупрежденияПриРедактировании = ОтображениеПредупрежденияПриРедактировании.НеОтображать;
		Элементы.ДатаРегистрации.ОтображениеПредупрежденияПриРедактировании = ОтображениеПредупрежденияПриРедактировании.НеОтображать;
		Элементы.Зарегистрировать.Доступность = Ложь;
	КонецЕсли;
	
	Элементы.ДатаОкончанияДействия.Доступность = Не Бессрочный;
	Элементы.ПорядокПродления.Доступность = Не Бессрочный;
	
	// дополнительные реквизиты
	РаботаС1СДокументооборот.ПоместитьДополнительныеРеквизитыНаФорму(ЭтаФорма, Объект);
	
КонецПроцедуры	

&НаСервере
Процедура ДобавитьПодписиКОбъекту(Прокси, Объект)
	
	// тут ЭЦП добавить
	МассивПодписей = ПолучитьМассивСуществующихПодписейФайла(ID);
	ПодписьТип = Прокси.ФабрикаXDTO.Тип("http://www.1c.ru/dm", "DMSignature");
	Для Каждого ДанныеПодписи Из МассивПодписей Цикл
		ПодписьXDTO = Прокси.ФабрикаXDTO.Создать(ПодписьТип);
		РаботаС1СДокументооборотВызовСервера.ЗаполнитьXDTOПодпись(Прокси, ПодписьXDTO, ДанныеПодписи);
		Объект.Signatures.Добавить(ПодписьXDTO);
	КонецЦикла;	
	
КонецПроцедуры	

&НаСервере
Процедура ЗаписатьОбъект()
	
	Прокси = РаботаС1СДокументооборотВызовСервера.ПолучитьПрокси();
	Объект = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "DMInternalDocument");
	
	РаботаС1СДокументооборот.СформироватьДополнительныеСвойства(Прокси, ЭтаФорма, Объект);
	
	СоответсвиеРеквизитов = РаботаС1СДокументооборот.СоответсвиеСвойствXDTOиРеквизитовФормыДокумента(ЭтаФорма.ИмяФормы);
	Для Каждого СтрокаСоответсвия Из СоответсвиеРеквизитов Цикл
		ЗаполнитьXDTOизРеквизитовФормы(Прокси, Объект, СтрокаСоответсвия.Ключ, 	СтрокаСоответсвия.Значение);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ID) И ЗначениеЗаполнено(Тип) Тогда // обновление
		
		Объект.objectId = РаботаС1СДокументооборот.СоздатьObjectID(Прокси, ID, Тип);
		Объект.name = Представление;
		
		ДобавитьПодписиКОбъекту(Прокси, Объект);
		
		Запрос = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "DMUpdateRequest");
		Запрос.objects.Добавить(Объект);
		
		Результат = Прокси.execute(Запрос);
		РаботаС1СДокументооборотВызовСервера.ПроверитьВозвратВебСервиса(Прокси, Результат);
		
		Объект = Результат.objects[0];
		
	Иначе // создание
		
		Объект.objectId = РаботаС1СДокументооборот.СоздатьObjectID(Прокси, "", "");
		Объект.name = Представление;
		
		Если ЗначениеЗаполнено(ВнешнийОбъект) Тогда 
			ExternalObject = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "ExternalObject");
			ExternalObject.id = Строка(ВнешнийОбъект.УникальныйИдентификатор());
			ExternalObject.type = Строка(ТипЗнч(ВнешнийОбъект));
			ExternalObject.name = Строка(ВнешнийОбъект);
			
			Объект.externalObject = ExternalObject;
		КонецЕсли;
		
		Запрос = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "DMCreateRequest");
		Запрос.object = Объект;
		
		Результат = Прокси.execute(Запрос);
		РаботаС1СДокументооборотВызовСервера.ПроверитьВозвратВебСервиса(Прокси, Результат);	
		
		Объект = Результат.object;
		ID 	= Объект.objectId.id;
		Тип = Объект.objectId.type;
		
	КонецЕсли;	
	
	// перечитать объект в форму
	ПрочитатьОбъектВФорму(Объект);
	
	Если Объект.Установлено("keyPropertiesValue") Тогда
		АдресСлепкаДокумента = ПоместитьВоВременноеХранилище(Объект.keyPropertiesValue, УникальныйИдентификатор);
	КонецЕсли;	
	
	Модифицированность = Ложь;
	Заголовок = Представление + НСтр("ru = ' (Внутренний документ)'");
	
КонецПроцедуры	

&НаКлиенте
Процедура Записать(Команда)
	
	Если ОграничиватьДоступностьПолей И ЗначениеЗаполнено(РегистрационныйНомер) И Не ЗначениеЗаполнено(НачальныйРегистрационныйНомер) Тогда 
		ТекстСообщения = НСтр("ru = 'При записи документ будет зарегистрирован и станет недоступным для изменения.
			|Продолжить?'");
	 
    	Ответ = Вопрос(ТекстСообщения, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
		Если Ответ <> КодВозвратаДиалога.Да Тогда 
			Возврат;
		КонецЕсли;
	КонецЕсли;	
	
	ЗаписатьОбъект();
	Оповестить("Изменен", ЭтаФорма.ВладелецФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если ОграничиватьДоступностьПолей И ЗначениеЗаполнено(РегистрационныйНомер) И Не ЗначениеЗаполнено(НачальныйРегистрационныйНомер) Тогда 
		ТекстСообщения = НСтр("ru = 'При записи документ будет зарегистрирован и станет недоступным для изменения.
			|Продолжить?'");
	 
    	Ответ = Вопрос(ТекстСообщения, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
		Если Ответ <> КодВозвратаДиалога.Да Тогда 
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗаписатьОбъект();
	Оповестить("Изменен", ЭтаФорма.ВладелецФормы, ЭтаФорма);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если ЗакрытиеСПараметром Тогда 
		Возврат;
	КонецЕсли;	
	
	Если Модифицированность Тогда 
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена,,КодВозвратаДиалога.Да);
		Если Ответ = КодВозвратаДиалога.Да Тогда 
			ЗаписатьОбъект();
			Оповестить("Изменен", ЭтаФорма.ВладелецФормы, ЭтаФорма);
		ИначеЕсли Ответ <> КодВозвратаДиалога.Нет Тогда 
			Отказ = Истина;
			Возврат;
		КонецЕсли;	
	КонецЕсли;	
	
	Отказ = Истина;
	Результат = Новый Структура;
	Результат.Вставить("ID", ID);
	Результат.Вставить("Type", Тип);
	Результат.Вставить("Name", Представление);
	
	ЗакрытиеСПараметром = Истина;
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ЗарегистрироватьНаСервере()
	
	ЗаписатьОбъект();
	
	Прокси = РаботаС1СДокументооборотВызовСервера.ПолучитьПрокси();
	
	Объект = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "DMInternalDocument");
	Объект.objectId = РаботаС1СДокументооборот.СоздатьObjectID(Прокси, ID, Тип);
	Объект.name = Представление;
	
	Запрос = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "DMDocumentRegistrationRequest");
	Запрос.document = Объект;
		
	Результат = Прокси.execute(Запрос);
	РаботаС1СДокументооборотВызовСервера.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	ЗаполнитьРеквизитыФормыИзXDTO("ДатаРегистрации", 		Результат.document, "regDate");
	ЗаполнитьРеквизитыФормыИзXDTO("РегистрационныйНомер", 	Результат.document, "regNumber");
	ЗаполнитьРеквизитыФормыИзXDTO("Состояние", 				Результат.document, "status");
	Представление = Результат.document.name;
	
	// доступность по состоянию
	УстановитьДоступностьПоСостоянию(Результат.document);
	УстановитьДоступностьПоЭЦП(Результат.document);
	
	НачальныйРегистрационныйНомер = РегистрационныйНомер;
	НачальнаяДатаРегистрации = ДатаРегистрации;
	
КонецПроцедуры

&НаКлиенте
Процедура Зарегистрировать(Команда)
	
	Если ОграничиватьДоступностьПолей Тогда 
		ТекстСообщения = НСтр("ru = 'После регистрации документ станет недоступным для изменения.
			|Продолжить?'");
	 
    	Ответ = Вопрос(ТекстСообщения, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
		Если Ответ <> КодВозвратаДиалога.Да Тогда 
			Возврат;
		КонецЕсли;
	КонецЕсли;	
		
	ЗарегистрироватьНаСервере();
	Оповестить("Изменен", ЭтаФорма.ВладелецФормы, ЭтаФорма);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// Выбор ссылочных реквизитов

&НаКлиенте
Процедура ПапкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ВыбратьЗначениеИзСписка("DMInternalDocumentFolder", "Папка", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПапкаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMInternalDocumentFolder", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("Папка", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПапкаАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMInternalDocumentFolder", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПапкаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("Папка", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры



&НаКлиенте
Процедура ПодготовилНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ВыбратьПользователяИзДереваПодразделений("Подготовил", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготовилОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMUser", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("Подготовил", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготовилАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMUser", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготовилОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("Подготовил", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры



&НаКлиенте
Процедура ПодписалНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ВыбратьПользователяИзДереваПодразделений("Подписал", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписалОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMUser", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("Подписал", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписалАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMUser", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписалОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("Подписал", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры



&НаКлиенте
Процедура ПодразделениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ВыбратьЗначениеИзСписка("DMSubdivision", "Подразделение", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMSubdivision", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("Подразделение", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMSubdivision", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("Подразделение", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры



&НаКлиенте
Процедура КорреспондентНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ВыбратьЗначениеИзСписка("DMCorrespondent", "Корреспондент", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КорреспондентОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMCorrespondent", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("Корреспондент", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КорреспондентАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMCorrespondent", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КорреспондентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("Корреспондент", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры



&НаКлиенте
Процедура КонтактноеЛицоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(КорреспондентID) И ЗначениеЗаполнено(КорреспондентТип) Тогда 
		
		Correspondent = Новый Структура;
		Correspondent.Вставить("ID",   КорреспондентID);
		Correspondent.Вставить("Type", КорреспондентТип);
		
		Отбор = Новый Структура;
		Отбор.Вставить("сorrespondent", Correspondent);
		
		РаботаС1СДокументооборотКлиент.ВыбратьЗначениеИзСписка("DMContactPerson", "КонтактноеЛицо", ЭтаФорма, Отбор);
		
	Иначе	
		ТекстСообщения = НСтр("ru = 'Не указано поле ""Корреспондент""'");
		Предупреждение(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактноеЛицоОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMContactPerson", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("КонтактноеЛицо", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактноеЛицоАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMContactPerson", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактноеЛицоОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("КонтактноеЛицо", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры



&НаКлиенте
Процедура ПорядокПродленияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ВыбратьЗначениеИзСписка("DMProlongationProcedure", "ПорядокПродления", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядокПродленияОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMProlongationProcedure", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("ПорядокПродления", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядокПродленияАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMProlongationProcedure", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядокПродленияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("ПорядокПродления", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры



&НаКлиенте
Процедура ГрифДоступаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ВыбратьЗначениеИзСписка("DMAccessLevel", "ГрифДоступа", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ГрифДоступаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMAccessLevel", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("ГрифДоступа", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГрифДоступаАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMAccessLevel", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГрифДоступаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("ГрифДоступа", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры



&НаКлиенте
Процедура ВидДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если РаботаС1СДокументооборотКлиент.ВыбратьЗначениеИзСписка("DMInternalDocumentType", "ВидДокумента", ЭтаФорма) Тогда 
		ПриИзмененииВидаДокумента();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДокументаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMInternalDocumentType", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("ВидДокумента", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
			
			ПриИзмененииВидаДокумента();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДокументаАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMInternalDocumentType", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДокументаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("ВидДокумента", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
	ПриИзмененииВидаДокумента();
	
КонецПроцедуры




&НаКлиенте
Процедура СостояниеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ВыбратьЗначениеИзСписка("DMDocumentStatus", "Состояние", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMDocumentStatus", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("Состояние", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMDocumentStatus", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("Состояние", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры



&НаКлиенте
Процедура ОрганизацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ВыбратьЗначениеИзСписка("DMOrganization", "Организация", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMOrganization", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("Организация", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMOrganization", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("Организация", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры



&НаКлиенте
Процедура ВопросДеятельностиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ВыбратьЗначениеИзСписка("DMActivityMatter", "ВопросДеятельности", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросДеятельностиОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMActivityMatter", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("ВопросДеятельности", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросДеятельностиАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMActivityMatter", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросДеятельностиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("ВопросДеятельности", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры



&НаКлиенте
Процедура ВалютаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ВыбратьЗначениеИзСписка("DMCurrency", "Валюта", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMCurrency", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		Если ДанныеВыбора.Количество() = 1 Тогда 
			РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("Валюта", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMCurrency", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("Валюта", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры




&НаКлиенте
Процедура ОтветственныйНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ВыбратьПользователяИзДереваПодразделений("Ответственный", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветственныйОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMUser", ДанныеВыбора, Текст, СтандартнаяОбработка);
		
		 Если ДанныеВыбора.Количество() = 1 Тогда 
			РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("Ответственный", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветственныйАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		РаботаС1СДокументооборотВызовСервера.ПолучитьДанныеДляАвтоПодбора("DMUser", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветственныйОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("Ответственный", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры



&НаКлиенте
Процедура СвойстваЗначениеПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаС1СДокументооборотКлиент.ВыбратьЗначениеДополнительногоРеквизита(ЭтаФорма, Элемент, СтандартнаяОбработка);
			
КонецПроцедуры

&НаКлиенте
Процедура БессрочныйПриИзменении(Элемент)
	
	Если Бессрочный Тогда 
		ДатаОкончанияДействия = '00010101';
		Элементы.ДатаОкончанияДействия.Доступность = Ложь;
		Элементы.ПорядокПродления.Доступность = Ложь;
		ПорядокПродления = "";
		РаботаС1СДокументооборотКлиент.ОчиститьСсылочныйРеквизит("ПорядокПродления", ЭтаФорма);
	Иначе
		Элементы.ДатаОкончанияДействия.Доступность = Истина;
		Элементы.ПорядокПродления.Доступность = Истина;
	КонецЕсли;	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Команды работы с файлами

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	ОткрытьФайлВыполнить();
КонецПроцедуры

&НаКлиенте
Процедура ФайлыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФайлВыполнить();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайлВыполнить()
	
	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ИдентификаторФайла = ТекущиеДанные.УникальныйИдентификатор;
	Расширение = ТекущиеДанные.Расширение;
	Имя = ТекущиеДанные.Наименование;
	Размер = ТекущиеДанные.Размер * 1024; // преобразуем из КБ в байты
	ДатаМодификацииУниверсальная = ТекущиеДанные.ДатаМодификацииУниверсальная;
	
	РаботаС1СДокументооборотКлиент.ОткрытьФайл(ИдентификаторФайла, Расширение, Имя, 
		Размер, ДатаМодификацииУниверсальная,
		КаталогДляСохраненияДанных, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	ОткрытьКарточкуВыполнить();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточкуВыполнить()
	
	Если Элементы.Файлы.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ПараметрыФормы = Новый Структура("УникальныйИдентификатор", Элементы.Файлы.ТекущиеДанные.УникальныйИдентификатор);
	ОткрытьФорму("ОбщаяФорма.ДокументооборотКарточкаФайла", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьФайл(Команда)
	СоздатьФайлВыполнить();
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
	СоздатьФайлВыполнить();
КонецПроцедуры

&НаКлиенте
Процедура СоздатьФайлВыполнить()
	
	Если ПустаяСтрока(ID) Тогда
		ЗаписатьОбъект();
		Оповестить("Изменен", ЭтаФорма.ВладелецФормы, ЭтаФорма);
	КонецЕсли;	
	
	СоздатьИзФайлаНаДиске();
КонецПроцедуры

&НаКлиенте
Процедура СоздатьИзФайлаНаДиске()
	
	РасширениеПодключено = ПодключитьРасширениеРаботыСФайлами();
	ИдентификаторСозданногоФайла = Неопределено;
	
	Если РасширениеПодключено Тогда
		
		ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		ВыборФайла.МножественныйВыбор = Ложь;
		ВыборФайла.Заголовок = НСтр("ru = 'Выбор файла'");
		ВыборФайла.Фильтр = НСтр("ru = 'Все файлы (*.*)|*.*'");
		
		Результат = ВыборФайла.Выбрать();
		ПолноеИмяФайла = ВыборФайла.ПолноеИмяФайла;
	
		Если НЕ Результат Тогда
			Возврат;
		КонецЕсли;
		
		ИдентификаторСозданногоФайла = РаботаС1СДокументооборотКлиент.СоздатьИзФайлаНаДискеКлиент(ПолноеИмяФайла, УникальныйИдентификатор, ID, Тип, Представление);
		
	Иначе 
		// Если веб-клиент без расширения
		
		ПараметрыСоздания = Новый Структура("Имя, Расширение, Размер, ВремяИзменения, ВремяИзмененияУниверсальное, АдресВременногоХранилищаФайла, Текст, ВебКлиент");

		ВремяИзменения = ТекущаяДата(); // Т.к. не можем получить дату модификации файла на диске
		ВремяИзмененияУниверсальное = УниверсальноеВремя(ТекущаяДата());
		Размер = 0;						// Т.к. не можем получить размер файла на диске
		ИмяБезРасширения = "";
		Расширение = "";
		АдресВременногоХранилищаТекста = "";

		// Поместим Файл в ВременноеХранилище
		АдресВременногоХранилищаФайла = "";
		ИмяФайла = "";
		Если НЕ ПоместитьФайл(АдресВременногоХранилищаФайла, ИмяФайла, ИмяФайла, Истина, УникальныйИдентификатор) Тогда
			Возврат;
		КонецЕсли;

		СтрокиПути = ФайловыеФункцииКлиентСервер.РазложитьСтрокуПоТочкамИСлэшам(ИмяФайла);
		Если СтрокиПути.Количество() >= 2 Тогда
			Расширение = СтрокиПути[СтрокиПути.Количество()-1];
			ИмяБезРасширения = СтрокиПути[СтрокиПути.Количество()-2];
		Иначе
			ВызватьИсключение
			  СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			    НСтр("ru = 'Ошибка при помещении файла в хранилище: %1'"),ИмяФайла);
		КонецЕсли;
		
		ПараметрыСоздания.АдресВременногоХранилищаФайла = АдресВременногоХранилищаФайла;
		ПараметрыСоздания.Расширение = Расширение;
		ПараметрыСоздания.ВремяИзменения = ВремяИзменения;
		ПараметрыСоздания.ВремяИзмененияУниверсальное = ВремяИзмененияУниверсальное;
		ПараметрыСоздания.Имя = ИмяБезРасширения;
		ПараметрыСоздания.Размер = Размер;
		ПараметрыСоздания.Текст = "";
		ПараметрыСоздания.ВебКлиент = Истина;
		
		ТекстПояснения =
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Идет сохранение файла ""%1"".
			|Пожалуйста, подождите...'"),
			ИмяФайла);
		
		Состояние(ТекстПояснения);
		
		ИдентификаторСозданногоФайла = РаботаС1СДокументооборотВызовСервера.СоздатьИзФайлаНаДискеСервер(ПараметрыСоздания, ID, Тип, Представление);
		Состояние();
		
	КонецЕсли;
	
	
	Если ИдентификаторСозданногоФайла <> Неопределено Тогда
		// обновить список
		ОбновитьСписокФайловКлиент(ИдентификаторСозданногоФайла);
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокПодписей(Подписи, ФайлыXDTO)

	РаботаС1СДокументооборотВызовСервера.ЗаполнитьСписокПодписейСервер(Подписи, ФайлыXDTO, Представление, ID, Тип, ТаблицаПодписей, 
		УникальныйИдентификатор, Элементы.ГруппаЭЦП, Файлы);

КонецПроцедуры	

&НаКлиенте
Процедура ОбновитьСписокФайловКлиент(ИдентификаторФайла = Неопределено)
	
	Если ИдентификаторФайла <> Неопределено Тогда
		ТекущийИдентификаторФайла = ИдентификаторФайла;
	ИначеЕсли Элементы.Файлы.ТекущиеДанные <> Неопределено Тогда
		ТекущийИдентификаторФайла = Элементы.Файлы.ТекущиеДанные.УникальныйИдентификатор;	
	КонецЕсли;	
	
	ПрочитатьИОбновитьСписокФайлов();
	Элементы.Файлы.Обновить();
	
	// восстановим положение в списке
	Для Каждого Строка Из Файлы Цикл
		Если Строка.УникальныйИдентификатор = ТекущийИдентификаторФайла Тогда
			Элементы.Файлы.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
			Прервать;
		КонецЕсли;		
	КонецЦикла;	
	
	Элементы.ФайлыЗаполнитьКопированием.Доступность = (Файлы.Количество() = 0);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьИОбновитьСписокФайлов()
	
	Прокси = РаботаС1СДокументооборотВызовСервера.ПолучитьПрокси();
	ОбъектИд = РаботаС1СДокументооборот.СоздатьObjectID(Прокси, ID, Тип);
	
	Запрос = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "DMRetrieveRequest");
	Запрос.objectIds.Добавить(ОбъектИд);
	Запрос.columnSet.Добавить("files");
	
	Результат = Прокси.execute(Запрос);
	РаботаС1СДокументооборотВызовСервера.ПроверитьВозвратВебСервиса(Прокси, Результат);

	Объект = Результат.objects[0];
	РаботаС1СДокументооборотВызовСервера.ОбновитьСписокФайлов(Объект.files, Файлы, Элементы.ГруппаФайлы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ФайлДокументооборотИзменен" Тогда
		ОбновитьСписокФайловКлиент();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	Если Элементы.Файлы.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	СтрокаВопроса = "";
	
	Если Элементы.Файлы.ВыделенныеСтроки.Количество() = 1 Тогда
		СтрокаВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			    НСтр("ru = 'Пометить ""%1"" на удаление?'"), Элементы.Файлы.ТекущиеДанные.Наименование);
	Иначе			
		СтрокаВопроса = НСтр("ru='Пометить выделенные элементы на удаление?'");
	КонецЕсли;		
	
	Если Вопрос(СтрокаВопроса, РежимДиалогаВопрос.ДаНет) <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;			
	
	ПометитьНаУдаление(Элементы.Файлы.ВыделенныеСтроки);
	// обновить список
	ОбновитьСписокФайловКлиент();
	
КонецПроцедуры

&НаСервере
Процедура ПометитьНаУдаление(Знач ВыделенныеСтроки)
	
	Прокси = РаботаС1СДокументооборотВызовСервера.ПолучитьПрокси();
	Запрос = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "DMDeleteRequest");
	
	Для Каждого НомерСтроки Из ВыделенныеСтроки Цикл
		
		Данные = Файлы.НайтиПоИдентификатору(НомерСтроки);
		Объект = РаботаС1СДокументооборот.СоздатьObjectID(Прокси, Данные.УникальныйИдентификатор, "DMFile");
		Запрос.objectIds.Добавить(Объект);
		
	КонецЦикла;
	
	Ответ = Прокси.execute(Запрос);
	РаботаС1СДокументооборотВызовСервера.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКак(Команда)
	
	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ИдентификаторФайла = ТекущиеДанные.УникальныйИдентификатор;
	Расширение = ТекущиеДанные.Расширение;
	Имя = ТекущиеДанные.Наименование;
	Размер = ТекущиеДанные.Размер * 1024; // преобразуем из КБ в байты
	ДатаМодификацииУниверсальная = ТекущиеДанные.ДатаМодификацииУниверсальная;
	
	РаботаС1СДокументооборотКлиент.СохранитьКак(ИдентификаторФайла, Расширение, Имя, 
		Размер, ДатаМодификацииУниверсальная, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИзФайлаНаДиске(Команда)
	
	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ИдентификаторФайла = ТекущиеДанные.УникальныйИдентификатор;
	Если РаботаС1СДокументооборотКлиент.ОбновитьИзФайлаНаДиске(ИдентификаторФайла, УникальныйИдентификатор) Тогда	
		ОбновитьСписокФайловКлиент();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокФайловПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура СписокФайловПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	СтандартнаяОбработка = Ложь;
	
	Если ПустаяСтрока(ID) Тогда
		ЗаписатьОбъект();
		Оповестить("Изменен", ЭтаФорма.ВладелецФормы, ЭтаФорма);
	КонецЕсли;	
	
	МассивИменФайлов = Новый Массив;
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Файл") И ПараметрыПеретаскивания.Значение.ЭтоФайл() = Истина Тогда
		МассивИменФайлов.Добавить(ПараметрыПеретаскивания.Значение.ПолноеИмя);
	ИначеЕсли ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") Тогда
		
		Если ПараметрыПеретаскивания.Значение.Количество() >= 1 И ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("Файл") Тогда
			Для Каждого ФайлПринятый Из ПараметрыПеретаскивания.Значение Цикл
				Если ФайлПринятый.ЭтоФайл() Тогда
					МассивИменФайлов.Добавить(ФайлПринятый.ПолноеИмя);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
			
	КонецЕсли;
	
	ИдентификаторСозданногоФайла = Неопределено;
	Для Каждого ИмяФайла Из МассивИменФайлов Цикл
		ИдентификаторСозданногоФайла = РаботаС1СДокументооборотКлиент.СоздатьИзФайлаНаДискеКлиент(ИмяФайла, УникальныйИдентификатор, ID, Тип, Представление);
	КонецЦикла;
	
	// обновить список
	ОбновитьСписокФайловКлиент(ИдентификаторСозданногоФайла);
			
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ЭЦП

&НаКлиенте
Процедура УстановитьДоступностьКомандСпискаЭЦП()
	
	ЭтоПодпись = Истина;
	ЕстьПодписи = (ТаблицаПодписей.ПолучитьЭлементы().Количество() <> 0);
	
	ТекущиеДанные = Элементы.ТаблицаПодписей.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ЭтоПодпись = НЕ ПустаяСтрока(ТекущиеДанные.Объект);
	КонецЕсли;
		
	Элементы.ТаблицаПодписейПроверить.Доступность = ЕстьПодписи И ЭтоПодпись;
	Элементы.ТаблицаПодписейПроверитьВсе.Доступность = ЕстьПодписи;
	Элементы.ТаблицаПодписейОткрытьПодпись.Доступность = ЕстьПодписи И ЭтоПодпись;
	Элементы.ТаблицаПодписейУдалить.Доступность = ЕстьПодписи И ЭтоПодпись;
	Элементы.ТаблицаПодписейСохранить.Доступность = ЕстьПодписи И ЭтоПодпись;
	
	Элементы.ТаблицаПодписейПроверить.Доступность = ЕстьПодписи;
	Элементы.ТаблицаПодписейПроверитьВсе.Доступность = ЕстьПодписи;
	Элементы.ТаблицаПодписейОткрытьПодпись.Доступность = ЕстьПодписи;
	Элементы.ТаблицаПодписейУдалить.Доступность = ЕстьПодписи;
	Элементы.ТаблицаПодписейСохранить.Доступность = ЕстьПодписи;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьДоступностьКомандСпискаЭЦП();
КонецПроцедуры

&НаСервере
// вернет массив подписей файла
Функция ПолучитьМассивСуществующихПодписейФайла(ИдентификаторФайла) 
	Возврат РаботаС1СДокументооборотВызовСервера.ПолучитьМассивСуществующихПодписейФайла(ИдентификаторФайла, ТаблицаПодписей);
КонецФункции		

&НаКлиенте
Процедура Подписать(Команда)
	
	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ИдентификаторФайла = ТекущиеДанные.УникальныйИдентификатор;
	ИмяФайла = ТекущиеДанные.Наименование;
	Описание = ТекущиеДанные.Описание;
	Редактируется = ТекущиеДанные.Редактируется;
	Зашифрован = ТекущиеДанные.Зашифрован;
	
	МассивСуществующихПодписейФайла = ПолучитьМассивСуществующихПодписейФайла(ИдентификаторФайла);
	
	РаботаС1СДокументооборотКлиент.ПодписатьФайл(ИдентификаторФайла, ИмяФайла, Редактируется, Зашифрован, Описание,
		МассивСуществующихПодписейФайла);	
		
	ПрочитатьИОбновитьСписокПодписей();
	УстановитьДоступностьКомандСпискаЭЦП();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЭЦПИзФайла(Команда)
	
	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ИдентификаторФайла = ТекущиеДанные.УникальныйИдентификатор;
	ИмяФайла = ТекущиеДанные.Наименование;
	Описание = ТекущиеДанные.Описание;
	Редактируется = ТекущиеДанные.Редактируется;
	Зашифрован = ТекущиеДанные.Зашифрован;
	
	МассивСуществующихПодписейФайла = ПолучитьМассивСуществующихПодписейФайла(ИдентификаторФайла);
	
	РаботаС1СДокументооборотКлиент.ДобавитьЭЦПИзФайла(ИдентификаторФайла, ИмяФайла, УникальныйИдентификатор,
		Описание, МассивСуществующихПодписейФайла);
	
	ПрочитатьИОбновитьСписокПодписей();
	УстановитьДоступностьКомандСпискаЭЦП();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВместеСЭЦП(Команда)
	
	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ИдентификаторФайла = ТекущиеДанные.УникальныйИдентификатор;
	Расширение = ТекущиеДанные.Расширение;
	Имя = ТекущиеДанные.Наименование;
	Размер = ТекущиеДанные.Размер * 1024; // преобразуем из КБ в байты
	ДатаМодификацииУниверсальная = ТекущиеДанные.ДатаМодификацииУниверсальная;
	
	РаботаС1СДокументооборотКлиент.СохранитьВместеСЭЦП(ИдентификаторФайла, Расширение, Имя, 
		Размер, ДатаМодификацииУниверсальная, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПодпись(Команда)
	ЭлектроннаяЦифроваяПодписьКлиент.ОткрытьПодпись(Элементы.ТаблицаПодписей.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПодписейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЭлектроннаяЦифроваяПодписьКлиент.ОткрытьПодпись(Элементы.ТаблицаПодписей.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если Элементы.ТаблицаПодписей.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(Элементы.ТаблицаПодписей.ТекущиеДанные.Объект) Тогда	
		ЭлектроннаяЦифроваяПодписьКлиент.СохранитьПодпись(Элементы.ТаблицаПодписей.ТекущиеДанные.АдресПодписи);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Проверить(Команда)
	
	ВыполнятьПроверкуЭЦПНаСервере = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьПерсональныеНастройкиРаботыСЭЦП().ВыполнятьПроверкуЭЦПНаСервере;
	
	Если ВыполнятьПроверкуЭЦПНаСервере Тогда
		ПроверитьНаСервере();
	Иначе
		МенеджерКриптографии = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьМенеджерКриптографии();
		РаботаС1СДокументооборотКлиент.ПроверитьПодписи(Элементы.ТаблицаПодписей.ВыделенныеСтроки, 
			ТаблицаПодписей, УникальныйИдентификатор, АдресСлепкаДокумента);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьНаСервере()
	РаботаС1СДокументооборотВызовСервера.ПроверитьПодписиНаСервере(Элементы.ТаблицаПодписей.ВыделенныеСтроки, 
		ТаблицаПодписей, УникальныйИдентификатор, АдресСлепкаДокумента);
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВсе(Команда)
	
	ВыполнятьПроверкуЭЦПНаСервере = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьПерсональныеНастройкиРаботыСЭЦП().ВыполнятьПроверкуЭЦПНаСервере;
	
	Если ВыполнятьПроверкуЭЦПНаСервере Тогда
		
		ПроверитьВсеНаСервере();
		
	Иначе
	
		МенеджерКриптографии = ЭлектроннаяЦифроваяПодписьКлиент.ПолучитьМенеджерКриптографии();
		Подписи = РаботаС1СДокументооборотКлиент.ПолучитьМассивДанныхПодписей(ТаблицаПодписей);
		РаботаС1СДокументооборотКлиент.ПроверитьПодписи(Подписи, ТаблицаПодписей, УникальныйИдентификатор,
			АдресСлепкаДокумента);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьВсеНаСервере()
	РаботаС1СДокументооборотВызовСервера.ПроверитьВсеПодписиНаСервере(ТаблицаПодписей, УникальныйИдентификатор, 
		АдресСлепкаДокумента);
КонецПроцедуры

&НаКлиенте
Процедура Удалить(Команда)
	
	Если Вопрос(НСтр("ru = 'Удалить выделенные подписи?'"), РежимДиалогаВопрос.ДаНет) <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;	
	
	УдалитьПодписиИОбновитьСписок();	
	УстановитьДоступностьКомандСпискаЭЦП();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьПодписиИОбновитьСписок()
	
	//получаем массив для удаления
	ТаблицаУдаленныеСтроки = РаботаС1СДокументооборотВызовСервера.ПолучитьВыделенныеПодписи(
		Элементы.ТаблицаПодписей.ВыделенныеСтроки, ТаблицаПодписей);
		
	Если ТаблицаУдаленныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;	
		
	Соответствие = РаботаС1СДокументооборотВызовСервера.УдалитьПодписиДокумента(ТаблицаУдаленныеСтроки, ТаблицаПодписей, ЭтаФорма, 
		ID, Тип, Файлы, Представление);	
	
	МассивXDTOОбъектов = Новый Массив;
	Прокси = РаботаС1СДокументооборотВызовСервера.ПолучитьПрокси();
	
	Для Каждого ПараКлючЗначение Из Соответствие Цикл
		
		ИдОбъекта = ПараКлючЗначение.Ключ;
		ДанныеВладельца = ПараКлючЗначение.Значение;
		ТипОбъекта = ДанныеВладельца.Тип;
		
		Если ТипОбъекта <> "DMFile" Тогда // документ
			
			Объект = СоздатьXDTOДокумент(Прокси, ДанныеВладельца.МассивПодписей);
			МассивXDTOОбъектов.Добавить(Объект);
			
		Иначе // файл
			
			Для Каждого Файл Из Файлы Цикл
				
				Если Файл.УникальныйИдентификатор = ИдОбъекта Тогда
					Объект = РаботаС1СДокументооборотВызовСервера.СоздатьXDTOФайл(Прокси, ДанныеВладельца.МассивПодписей, Файл);
					МассивXDTOОбъектов.Добавить(Объект);
					Прервать;
				КонецЕсли;
			
			КонецЦикла;
			
			
		КонецЕсли;	
	
	КонецЦикла;
	
	РаботаС1СДокументооборот.ЗаписатьОбъекты(Прокси, МассивXDTOОбъектов);
		
	ПрочитатьИОбновитьСписокПодписей();	
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьИОбновитьСписокПодписей()
	
	Прокси = РаботаС1СДокументооборотВызовСервера.ПолучитьПрокси();
	ОбъектИд = РаботаС1СДокументооборот.СоздатьObjectID(Прокси, ID, Тип);
	
	Запрос = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "DMRetrieveRequest");
	Запрос.objectIds.Добавить(ОбъектИд);
	Запрос.columnSet.Добавить("signatures");
	Запрос.columnSet.Добавить("files"); // также и файлы обновляем - чтобы иконку Подписан расставить
	Запрос.columnSet.Добавить("enabledProperties");
	Запрос.columnSet.Добавить("keyPropertiesValue");
	
	Результат = Прокси.execute(Запрос);
	РаботаС1СДокументооборотВызовСервера.ПроверитьВозвратВебСервиса(Прокси, Результат);

	Объект = Результат.objects[0];
	
	ЗаполнитьСписокПодписей(Объект.signatures, Объект.files);
	РаботаС1СДокументооборотВызовСервера.ОбновитьСписокФайлов(Объект.files, Файлы, Элементы.ГруппаФайлы);
	
	УстановитьДоступностьПоСостоянию(Объект);// доступность по состоянию
	УстановитьДоступностьПоЭЦП(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьДокумент(Команда)
	
	Если ПустаяСтрока(ID) Тогда 
		
		ТекстВопроса = НСтр("ru = 'Данные еще не записаны.
			|Выполнение действия ""Подписать"" возможно только после записи данных.
			|Данные будут записаны.'");
		Результат = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		
		Если Результат <> КодВозвратаДиалога.ОК Тогда 
			Возврат;
		КонецЕсли;	
		
		ЗаписатьОбъект();
		
	КонецЕсли;
	
	МассивДанныхДляЗанесенияВБазу = Новый Массив;
	МассивАдресов = Новый Массив;
	
	// УстановкаПодписиЭЦП
	
	Если РаботаС1СДокументооборотКлиент.СформироватьПодписьОбъекта(ID, Представление, Тип, УникальныйИдентификатор, 
			МассивДанныхДляЗанесенияВБазу, МассивАдресов, Файлы, АдресСлепкаДокумента) Тогда
		
		ПодписатьЭЦПСервер(МассивДанныхДляЗанесенияВБазу, МассивАдресов);
		
		РаботаС1СДокументооборотКлиент.ИнформироватьОПодписании(Представление);  
		УстановитьДоступностьКомандСпискаЭЦП();
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция СоздатьXDTOДокумент(Прокси, МассивПодписей)
	
	Объект = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "DMInternalDocument");
	
	РаботаС1СДокументооборот.СформироватьДополнительныеСвойства(Прокси, ЭтаФорма, Объект);
	
	СоответсвиеРеквизитов = РаботаС1СДокументооборот.СоответсвиеСвойствXDTOиРеквизитовФормыДокумента(ЭтаФорма.ИмяФормы);
	Для Каждого СтрокаСоответсвия Из СоответсвиеРеквизитов Цикл
		ЗаполнитьXDTOизРеквизитовФормы(Прокси, Объект, СтрокаСоответсвия.Ключ, 	СтрокаСоответсвия.Значение);
	КонецЦикла;
	
	Объект.objectID = РаботаС1СДокументооборот.СоздатьObjectID(Прокси, ID, Тип);
	Объект.name = Представление;
	
	// тут ЭЦП добавить
	ПодписьТип = Прокси.ФабрикаXDTO.Тип("http://www.1c.ru/dm", "DMSignature");
	
	Для Каждого ДанныеПодписи Из МассивПодписей Цикл
		
		ПодписьXDTO = Прокси.ФабрикаXDTO.Создать(ПодписьТип);
		РаботаС1СДокументооборотВызовСервера.ЗаполнитьXDTOПодпись(Прокси, ПодписьXDTO, ДанныеПодписи);
		Объект.Signatures.Добавить(ПодписьXDTO);
		
	КонецЦикла;	
	
	Возврат Объект;
	
КонецФункции	

&НаСервере
Процедура ПодписатьЭЦПСервер(ДобавленныеПодписи, МассивАдресов)
	
	Соответствие = РаботаС1СДокументооборотВызовСервера.ПодписатьДокумент(
		ДобавленныеПодписи, ТаблицаПодписей, ЭтаФорма, 
		ID, Тип, Файлы, Представление);
	
	МассивXDTOОбъектов = Новый Массив;
	Прокси = РаботаС1СДокументооборотВызовСервера.ПолучитьПрокси();
	
	Для Каждого ПараКлючЗначение Из Соответствие Цикл
		
		ИдОбъекта = ПараКлючЗначение.Ключ;
		ДанныеВладельца = ПараКлючЗначение.Значение;
		ТипОбъекта = ДанныеВладельца.Тип;
		
		Если ТипОбъекта <> "DMFile" Тогда // документ
			
			Объект = СоздатьXDTOДокумент(Прокси, ДанныеВладельца.МассивПодписей);
			МассивXDTOОбъектов.Добавить(Объект);
			
		Иначе // файл
			
			Для Каждого Файл Из Файлы Цикл
				
				Если Файл.УникальныйИдентификатор = ИдОбъекта Тогда
					Объект = РаботаС1СДокументооборотВызовСервера.СоздатьXDTOФайл(Прокси, ДанныеВладельца.МассивПодписей, Файл);
					МассивXDTOОбъектов.Добавить(Объект);
					Прервать;
				КонецЕсли;
			
			КонецЦикла;
			
			
		КонецЕсли;	
	
	КонецЦикла;
	
	РаботаС1СДокументооборот.ЗаписатьОбъекты(Прокси, МассивXDTOОбъектов);
	
	ПрочитатьИОбновитьСписокПодписей();
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПриАктивизацииСтроки(Элемент)
	ОбновитьДоступностьКомандСпискаФайлов();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДоступностьКомандСпискаФайлов()
	УстановитьДоступностьКомманд(Элементы.Файлы.ТекущиеДанные);
КонецПроцедуры	

&НаКлиенте
Процедура УстановитьДоступностьКомманды(Команда, Доступность)
	Команда.Доступность = Доступность;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКомманд(ТекущиеДанные)
	
	Если ТекущиеДанные = Неопределено Тогда 
		
		УстановитьДоступностьКомманды(Элементы.ФайлыКонтекстноеМенюОткрытьФайл, Ложь);
		УстановитьДоступностьКомманды(Элементы.ФайлыКонтекстноеМенюСохранитьКак, Ложь);
		УстановитьДоступностьКомманды(Элементы.ФайлыКонтекстноеМенюОбновитьИзФайлаНаДиске, Ложь);
		УстановитьДоступностьКомманды(Элементы.ФайлыКонтекстноеМенюПодписать, Ложь);
		УстановитьДоступностьКомманды(Элементы.ФайлыКонтекстноеМенюДобавитьЭЦПИзФайла, Ложь);
		УстановитьДоступностьКомманды(Элементы.ФайлыКонтекстноеМенюСохранитьВместеСЭЦП, Ложь);
		
		УстановитьДоступностьКомманды(Элементы.ФайлыОткрытьФайл, Ложь);
		УстановитьДоступностьКомманды(Элементы.ФайлыСохранитьКак, Ложь);
		УстановитьДоступностьКомманды(Элементы.ФайлыОбновитьИзФайлаНаДиске, Ложь);
		УстановитьДоступностьКомманды(Элементы.ФайлыПодписать, Ложь);
		УстановитьДоступностьКомманды(Элементы.ФайлыДобавитьЭЦПИзФайла, Ложь);
		УстановитьДоступностьКомманды(Элементы.ФайлыСохранитьВместеСЭЦП, Ложь);
		
	Иначе	
		
		Редактируется = ТекущиеДанные.Редактируется;
		ПодписанЭЦП 	= ТекущиеДанные.ПодписанЭЦП;
		Зашифрован 	= ТекущиеДанные.Зашифрован;
		
		УстановитьДоступностьКомманды(Элементы.ФайлыКонтекстноеМенюОткрытьФайл, Истина);
		УстановитьДоступностьКомманды(Элементы.ФайлыКонтекстноеМенюСохранитьКак, Истина);
		УстановитьДоступностьКомманды(Элементы.ФайлыКонтекстноеМенюОбновитьИзФайлаНаДиске, Истина);
		УстановитьДоступностьКомманды(Элементы.ФайлыКонтекстноеМенюПодписать, НЕ Редактируется И НЕ Зашифрован);
		УстановитьДоступностьКомманды(Элементы.ФайлыКонтекстноеМенюДобавитьЭЦПИзФайла, НЕ Редактируется И НЕ Зашифрован);
		УстановитьДоступностьКомманды(Элементы.ФайлыКонтекстноеМенюСохранитьВместеСЭЦП, ПодписанЭЦП);
		
		УстановитьДоступностьКомманды(Элементы.ФайлыОткрытьФайл, Истина);
		УстановитьДоступностьКомманды(Элементы.ФайлыСохранитьКак, Истина);
		УстановитьДоступностьКомманды(Элементы.ФайлыОбновитьИзФайлаНаДиске, Истина);
		УстановитьДоступностьКомманды(Элементы.ФайлыПодписать, НЕ Редактируется И НЕ Зашифрован);
		УстановитьДоступностьКомманды(Элементы.ФайлыДобавитьЭЦПИзФайла, НЕ Редактируется И НЕ Зашифрован);
		УстановитьДоступностьКомманды(Элементы.ФайлыСохранитьВместеСЭЦП, ПодписанЭЦП);
		
	КонецЕсли;
	
КонецПроцедуры

// Бизнес-процессы

&НаКлиенте
Процедура СоздатьБизнесПроцесс(Команда)
	
	Если ЗначениеЗаполнено(ID) Тогда
		РаботаС1СДокументооборотКлиент.СоздатьБизнесПроцессПоДокументуИзДО(ID, Тип);
	Иначе
		ТекстВопроса = НСтр("ru = 'Данные еще не записаны." + Символы.ВК + "Создание бизнес-процесса возможно только после записи данных." + Символы.ВК + "Данные будут записаны.'");
		Режим = РежимДиалогаВопрос.ОКОтмена;
		Ответ = Вопрос(ТекстВопроса, Режим, 0);
		Если Ответ = КодВозвратаДиалога.ОК Тогда
		    Записать(Неопределено);
			РаботаС1СДокументооборотКлиент.СоздатьБизнесПроцессПоДокументуИзДО(ID, Тип);
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьКопированием(Команда)
	
	Ответ = Вопрос(НСтр("ru = 'Заполнить файлы копированием из присоединенных файлов?'"), РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
	Если Ответ <> КодВозвратаДиалога.Да Тогда 
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(ID) Тогда
		ЗаписатьОбъект();
		Оповестить("Изменен", ЭтаФорма.ВладелецФормы, ЭтаФорма);
	КонецЕсли;	
	
	Состояние(НСтр("ru = 'Идет заполнение файлов копированием из присоединенных файлов ....'"));
	
	ЗаполнитьКопированиемСервер();
	
	Состояние();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКопированиемСервер()
	
	РаботаС1СДокументооборотВызовСервера.ЗаполнитьКопированием(ВнешнийОбъект, ID, Тип, Представление, УникальныйИдентификатор);
	ПрочитатьИОбновитьСписокФайлов();	
	Элементы.ФайлыЗаполнитьКопированием.Доступность = (Файлы.Количество() = 0);
	
КонецПроцедуры

