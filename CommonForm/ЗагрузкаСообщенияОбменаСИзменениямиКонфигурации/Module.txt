
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	УзелИнформационнойБазы = ПланыОбмена.ГлавныйУзел();
	
	Если УзелИнформационнойБазы = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// получаем вид транспорта сообщений обмена по умолчанию для узла;
	// если значение по умолчанию не задано, то устанавливаем значение FILE
	ВидТранспортаСообщенийОбмена = РегистрыСведений.НастройкиТранспортаОбмена.ВидТранспортаСообщенийОбменаПоУмолчанию(УзелИнформационнойБазы);
	Если Не ЗначениеЗаполнено(ВидТранспортаСообщенийОбмена) Тогда
		ВидТранспортаСообщенийОбмена = Перечисления.ВидыТранспортаСообщенийОбмена.FILE;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ВыполнитьЗагрузкуДанных(Команда)
	
	Отказ = Ложь;
	
	Состояние(НСтр("ru = 'Выполняется загрузка данных...'"));
	
	// выполняем загрузку данных
	ОбменДаннымиСервер.ВыполнитьОбменДаннымиДляУзлаИнформационнойБазы(Отказ, УзелИнформационнойБазы, Истина, Ложь, ВидТранспортаСообщенийОбмена);
	
	Если Отказ Тогда
		
		НСтрока = НСтр("ru = 'При загрузке данных возникли ошибки.
							|Перейти в журнал регистрации?'");
		//
		Ответ = Вопрос(НСтрока, РежимДиалогаВопрос.ДаНет, ,КодВозвратаДиалога.Да);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			
			ОбменДаннымиКлиент.ПерейтиВЖурналРегистрацииСобытийДанныхМодально(УзелИнформационнойБазы, ЭтаФорма, "ЗагрузкаДанных");
			
		КонецЕсли;
		
	Иначе
		
		Предупреждение(НСтр("ru = 'Загрузка данных успешно завершена.'"), 30);
		
		Закрыть(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВЖурналРегистрацииСобытийЗагрузкиДанных(Команда)
	
	ОбменДаннымиКлиент.ПерейтиВЖурналРегистрацииСобытийДанныхМодально(УзелИнформационнойБазы, ЭтаФорма, "ЗагрузкаДанных");
	
КонецПроцедуры
