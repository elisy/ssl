
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьДеревоПодразделений(Дерево.ПолучитьЭлементы(), "DMSubdivision", "");
	ЭлементВсеПользователи = Дерево.ПолучитьЭлементы().Вставить(0);
	ЭлементВсеПользователи.Наименование = НСтр("ru = 'Все пользователи'");
	ЭлементВсеПользователи.ID = "";	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоПодразделений(ВеткаДерева, ТипОбъектаВыбора, ИдентификаторПапки = Неопределено, СтрокаПоиска = Неопределено)
	
	ВеткаДерева.Очистить();
	Прокси = РаботаС1СДокументооборотВызовСервера.ПолучитьПрокси();
	
	УсловияОтбораОбъектов =	РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListQuery");
	Если ИдентификаторПапки <> Неопределено Тогда
		РодительИд = РаботаС1СДокументооборот.СоздатьObjectID(Прокси, ИдентификаторПапки, ТипОбъектаВыбора);
		
		Условие = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
		Условие.property = "parent";
		Условие.value = РодительИд;
		
		УсловияОтбораОбъектов.conditions.Добавить(Условие);
	КонецЕсли;
	
	Если СтрокаПоиска <> Неопределено Тогда
		Условие = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
		Условие.property = "name";
		Условие.value = СтрокаПоиска;
		
		УсловияОтбораОбъектов.conditions.Добавить(Условие);
	КонецЕсли;
	
	Если ТипЗнч(Отбор) = Тип("Структура") Тогда 
		Для Каждого СтрокаОтбора Из Отбор Цикл
			Условие = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
			Условие.property = СтрокаОтбора.Ключ;
			
			Если ТипЗнч(СтрокаОтбора.Значение) = Тип("Структура") Тогда
				Условие.value = РаботаС1СДокументооборот.СоздатьObjectID(Прокси, СтрокаОтбора.Значение.id, СтрокаОтбора.Значение.type);
			Иначе
				Условие.value = СтрокаОтбора.Значение;
			КонецЕсли;	
		
			УсловияОтбораОбъектов.conditions.Добавить(Условие);
		КонецЦикла;	
	КонецЕсли;	
	
	Запрос = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetObjectListRequest");
	Запрос.type = ТипОбъектаВыбора;
	Запрос.query = УсловияОтбораОбъектов;
	
	Результат = Прокси.execute(Запрос);
	РаботаС1СДокументооборотВызовСервера.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	Для Каждого Элемент Из Результат.items Цикл
		
		НоваяСтрока = ВеткаДерева.Добавить();
		НоваяСтрока.Наименование = Элемент.object.name;
		НоваяСтрока.ID = Элемент.object.objectId.id;
		НоваяСтрока.Тип = Элемент.object.objectId.type;
		
		Если ТипОбъектаВыбора = "DMFileFolder" Тогда
			НоваяСтрока.Картинка = 0;
		Иначе
			Если Элемент.isFolder Тогда
				НоваяСтрока.Картинка = 0;
			Иначе	
				НоваяСтрока.Картинка = 2;
			КонецЕсли;	
		КонецЕсли;
		
		Если Элемент.CanHaveChildren И (СтрокаПоиска = Неопределено) Тогда
			НоваяСтрока.ПодпапкиСчитаны = Ложь;
			ФиктивныйЛист = НоваяСтрока.ПолучитьЭлементы().Добавить(); // чтобы появился плюсик
		Иначе
			НоваяСтрока.ПодпапкиСчитаны = Истина;
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокПользователей(ПодразделениеИД)
	
	Прокси = РаботаС1СДокументооборотВызовСервера.ПолучитьПрокси();
	
	//получение руководителя текущего подразделения
	Если ЗначениеЗаполнено(ПодразделениеИД) Тогда
		Подразделения = РаботаС1СДокументооборот.ПолучитьОбъект("DMSubdivision", ПодразделениеИД); 
		IDРуководителя = Подразделения.objects[0].head.objectId.id;
	КонецЕсли;
	
	//заполнение списка пользователей	
	Запрос = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetObjectListRequest");
	Запрос.type = "DMUser";
	
	УсловияОтбораОбъектов = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListQuery");
	Если ЗначениеЗаполнено(ПодразделениеИД) Тогда
		Условие = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
		Условие.property = "Subdivision";
		Условие.value = РаботаС1СДокументооборот.СоздатьObjectID(Прокси, ПодразделениеИД, "DMSubdivision");
		
		УсловияОтбораОбъектов.conditions.Добавить(Условие);
		Запрос.query = УсловияОтбораОбъектов;
	КонецЕсли;
			
	Результат = Прокси.execute(Запрос);
	РаботаС1СДокументооборотВызовСервера.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	СписокПользователей.Очистить();
	Для Каждого ПользовательВСписке Из Результат.Items Цикл
		НоваяСтрока = СписокПользователей.Добавить();
		НоваяСтрока.Наименование = ПользовательВСписке.object.name;
		НоваяСтрока.ID = ПользовательВСписке.object.objectId.id;
		НоваяСтрока.Тип = ПользовательВСписке.object.objectId.type;
		НоваяСтрока.Руководитель = (IDРуководителя = ПользовательВСписке.object.objectId.id);
	КонецЦикла;
	
	СписокПользователей.Сортировать("Наименование Возр");
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПередРазворачиванием(Элемент, Строка, Отказ)
	
	Лист = Дерево.НайтиПоИдентификатору(Строка);
	
	Если Лист = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(Лист.ID) И НЕ Лист.ПодпапкиСчитаны Тогда
		ЗаполнитьДеревоПапокПоИдентификатору(Строка, Лист.ID);
		Лист.ПодпапкиСчитаны = Истина;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоПапокПоИдентификатору(ИдентификаторЭлементаДерева, ИдентификаторПапки)
	
	Лист = Дерево.НайтиПоИдентификатору(ИдентификаторЭлементаДерева);
	
	Если Лист = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ЗаполнитьДеревоПодразделений(Лист.ПолучитьЭлементы(), "DMSubdivision", Лист.ID);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбработкаОжидания", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОжидания()
	
	ЗаполнитьСписокПользователей(Элементы.Дерево.ТекущиеДанные.ID);	
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	ВыбратьВыполнить();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПользователейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбратьВыполнить();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВыполнить()
	
	ТекущиеДанные = Элементы.СписокПользователей.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Результат = Новый Структура;
	Результат.Вставить("РеквизитID",  ТекущиеДанные.ID);
	Результат.Вставить("РеквизитТип", ТекущиеДанные.Тип);
	Результат.Вставить("РеквизитПредставление", ТекущиеДанные.Наименование);
	
	Закрыть(Результат);
	
КонецПроцедуры

