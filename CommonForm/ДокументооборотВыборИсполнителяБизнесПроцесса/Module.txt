
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьДеревоПодразделений(Дерево.ПолучитьЭлементы(), "DMSubdivision", "");
	ЭлементВсеПользователи = Дерево.ПолучитьЭлементы().Вставить(0);
	ЭлементВсеПользователи.Наименование = НСтр("ru = 'Все пользователи'");
	ЭлементВсеПользователи.ID = "";
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоПодразделений(ВеткаДерева, ТипОбъектаВыбора, ИдентификаторПапки = Неопределено, СтрокаПоиска = Неопределено)
	
	ВеткаДерева.Очистить();
	Прокси = РаботаС1СДокументооборотВызовСервера.ПолучитьПрокси();
	
	УсловияОтбораОбъектов = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListQuery");
	Если ИдентификаторПапки <> Неопределено Тогда
		Условие = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
		Условие.property = "Parent";
		Условие.value = РаботаС1СДокументооборот.СоздатьObjectID(Прокси, ИдентификаторПапки, ТипОбъектаВыбора);
		
		УсловияОтбораОбъектов.conditions.Добавить(Условие);
	КонецЕсли;
	
	Если СтрокаПоиска <> Неопределено Тогда
		Условие = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectListCondition");
		Условие.property = "Name";
		Условие.value = СтрокаПоиска;
		
		УсловияОтбораОбъектов.conditions.Добавить(Условие);
	КонецЕсли;
	
	Запрос = РаботаС1СДокументооборот.СоздатьОбъект(Прокси, "DMGetObjectListRequest");
	Запрос.Type = ТипОбъектаВыбора;
	Запрос.query = УсловияОтбораОбъектов;
	
	Результат = Прокси.execute(Запрос);
	РаботаС1СДокументооборотВызовСервера.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	Для Каждого Элемент Из Результат.Items Цикл
		
		НоваяСтрока = ВеткаДерева.Добавить();
		НоваяСтрока.Наименование = Элемент.object.name;
		НоваяСтрока.ID = Элемент.object.objectId.id;
		НоваяСтрока.Тип = Элемент.object.objectId.type;
		
		Если ТипОбъектаВыбора = "DMFileFolder" Тогда
			НоваяСтрока.Картинка = 0;
		Иначе
			Если Элемент.isFolder Тогда
				НоваяСтрока.Картинка = 0;
			Иначе	
				НоваяСтрока.Картинка = 2;
			КонецЕсли;	
		КонецЕсли;
		
		Если Элемент.canHaveChildren И (СтрокаПоиска = Неопределено) Тогда
			НоваяСтрока.ПодпапкиСчитаны = Ложь;
			ФиктивныйЛист = НоваяСтрока.ПолучитьЭлементы().Добавить(); // чтобы появился плюсик
		Иначе
			НоваяСтрока.ПодпапкиСчитаны = Истина;
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПользователи(ПодразделениеИД)
	
	Прокси = РаботаС1СДокументооборотВызовСервера.ПолучитьПрокси();
	
	Если ЗначениеЗаполнено(ПодразделениеИД) Тогда
		//получение руководителя текущего подразделения
		Подразделения = РаботаС1СДокументооборот.ПолучитьОбъект("DMSubdivision", ПодразделениеИД); 
		IDРуководителя = Подразделения.objects[0].head.objectId.id;
	КонецЕсли;
	
	//заполнение списка пользователей	
	ОбъектИдТип = Прокси.ФабрикаXDTO.Тип("http://www.1c.ru/dm", "DMObjectID");
	УсловияОтбораОбъектовТип = Прокси.ФабрикаXDTO.Тип("http://www.1c.ru/dm", "DMObjectListQuery");
	ЗапросТип = Прокси.ФабрикаXDTO.Тип("http://www.1c.ru/dm", "DMGetObjectListRequest");
	ЗапросУсловиеТип = Прокси.ФабрикаXDTO.Тип("http://www.1c.ru/dm", "DMObjectListCondition");
	
	Запрос = Прокси.ФабрикаXDTO.Создать(ЗапросТип);
	Запрос.Type = "DMUser";
	
	Если ЗначениеЗаполнено(ПодразделениеИД) Тогда
		УсловияОтбораОбъектов = Прокси.ФабрикаXDTO.Создать(УсловияОтбораОбъектовТип);
		ПодрИД = Прокси.ФабрикаXDTO.Создать(ОбъектИдТип);
		ПодрИД.id = ПодразделениеИД;
		ПодрИД.type = "DMSubdivision";
		
		Условие = Прокси.ФабрикаXDTO.Создать(ЗапросУсловиеТип);
		Условие.property = "Subdivision";
		Условие.value = ПодрИД;
		
		УсловияОтбораОбъектов.conditions.Добавить(Условие);
				
		Запрос.query = УсловияОтбораОбъектов;
	КонецЕсли;
	
	Результат = Прокси.execute(Запрос);

	РаботаС1СДокументооборотВызовСервера.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	Пользователи.Очистить();
	Для Каждого ПользовательВСписке Из Результат.Items Цикл
		НоваяСтрока = Пользователи.Добавить();
		НоваяСтрока.Пользователь = ПользовательВСписке.object.name;
		НоваяСтрока.ПользовательID = ПользовательВСписке.object.objectId.id;
		НоваяСтрока.ПользовательТип = ПользовательВСписке.object.objectId.type;
		НоваяСтрока.Руководитель = (IDРуководителя = ПользовательВСписке.object.objectId.id);
	КонецЦикла;
	
	Пользователи.Сортировать("Пользователь Возр");
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПередРазворачиванием(Элемент, Строка, Отказ)
	
	Лист = Дерево.НайтиПоИдентификатору(Строка);
	
	Если Лист = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	Если НЕ Лист.ПодпапкиСчитаны Тогда
		ЗаполнитьДеревоПапокПоИдентификатору(Строка, Лист.ID);
		Лист.ПодпапкиСчитаны = Истина;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоПапокПоИдентификатору(ИдентификаторЭлементаДерева, ИдентификаторПапки)
	
	Лист = Дерево.НайтиПоИдентификатору(ИдентификаторЭлементаДерева);
	
	Если Лист = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ЗаполнитьДеревоПодразделений(Лист.ПолучитьЭлементы(), "DMSubdivision", Лист.ID);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбработкаОжидания", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОжидания()
	
	ЗаполнитьПользователи(Элементы.Дерево.ТекущиеДанные.ID);	
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаПользователиРолиПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если Роли.Количество() = 0 Тогда
		ЗаполнитьСписокДоступныхРолей();
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокДоступныхРолей()

	Прокси = РаботаС1СДокументооборотВызовСервера.ПолучитьПрокси();
	УсловияОтбораОбъектовТип = Прокси.ФабрикаXDTO.Тип("http://www.1c.ru/dm", "DMObjectListQuery");
	ЗапросТип = Прокси.ФабрикаXDTO.Тип("http://www.1c.ru/dm", "DMGetObjectListRequest");
	ЗапросУсловиеТип = Прокси.ФабрикаXDTO.Тип("http://www.1c.ru/dm", "DMObjectListCondition");
	
	Запрос = Прокси.ФабрикаXDTO.Создать(ЗапросТип);
	Запрос.type = "DMBusinessProcessExecutorRole";
	
	УсловияОтбораОбъектов = Прокси.ФабрикаXDTO.Создать(УсловияОтбораОбъектовТип);
		
	Запрос.query = УсловияОтбораОбъектов;
	
	Результат = Прокси.execute(Запрос);
	РаботаС1СДокументооборотВызовСервера.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	Роли.Очистить();
	Для Каждого Элемент Из Результат.Items Цикл
		НоваяСтрока = Роли.Добавить();
		НоваяСтрока.Роль = Элемент.object.name;
		НоваяСтрока.РольID = Элемент.object.objectId.id;
		НоваяСтрока.РольТип = Элемент.object.objectId.type;
	КонецЦикла;	

	Роли.Сортировать("Роль Возр");
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	ВыбратьВыполнить();	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВыполнить()
	
	Если Элементы.ГруппаПользователиРоли.ТекущаяСтраница = Элементы.ГруппаПользователи Тогда
		
		Если Элементы.Пользователи.ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		ДанныеВозврата = Новый Структура;
		ДанныеВозврата.Вставить("Исполнитель", Элементы.Пользователи.ТекущиеДанные.Пользователь);	
		ДанныеВозврата.Вставить("ИсполнительID", Элементы.Пользователи.ТекущиеДанные.ПользовательID);
		ДанныеВозврата.Вставить("ИсполнительТип", Элементы.Пользователи.ТекущиеДанные.ПользовательТип);
		
	ИначеЕсли Элементы.ГруппаПользователиРоли.ТекущаяСтраница = Элементы.ГруппаРоли Тогда
		
				Если Элементы.Роли.ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ДанныеВозврата = РаботаС1СДокументооборотКлиент.ПолучитьОбъектыАдресацииРоли(Элементы.Роли.ТекущиеДанные.Роль, Элементы.Роли.ТекущиеДанные.РольТип, Элементы.Роли.ТекущиеДанные.РольID, ЭтаФорма);  

		Если ДанныеВозврата = Неопределено Тогда
			Возврат;
		КонецЕсли;
			
	КонецЕсли;
	
	Закрыть (ДанныеВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбратьВыполнить();

КонецПроцедуры

&НаКлиенте
Процедура РолиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбратьВыполнить();
	
КонецПроцедуры

