
////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы
//

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если НЕ ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Настройки = РегламентныеЗаданияСервер.ПолучитьНастройкиВыполненияРегламентныхЗаданий();
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Настройки);
	
	Если НЕ ПравоДоступа("Администрирование", Метаданные) Тогда
		Элементы.АвтоматическиЗапускатьОтдельныйСеансДляВыполненияРегламентныхЗаданий.Доступность = Ложь;
	КонецЕсли;
	
	Элементы.ЗапуститьОтдельныйСеансДляВыполненияРегламентныхЗаданий.Видимость = НЕ Параметры.СкрытьКомандуЗапускаОтдельногоСеанса;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		КодВозврата = Вопрос(НСтр("ru = 'Записать изменения?'"), РежимДиалогаВопрос.ДаНетОтмена);
		Если КодВозврата = КодВозвратаДиалога.Да Тогда
			ЗаписатьИзменения();
		ИначеЕсли КодВозврата = КодВозвратаДиалога.Отмена Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ЗапуститьОтдельныйСеансДляВыполненияРегламентныхЗаданий(Команда)
	
	ПодключитьОбработчикОжидания("ЗапуститьОтдельныйСеансДляВыполненияРегламентныхЗаданийЧерезОбработчикОжидания", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрытьВыполнить()
	
	ЗаписатьИзменения();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	ЗаписатьИзменения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодУведомленияОНекорректномВыполненииРегламентныхЗаданийПриИзменении(Элемент)
	
	Если ПериодУведомленияОНекорректномВыполненииРегламентныхЗаданий <= 0 Тогда
		ПериодУведомленияОНекорректномВыполненииРегламентныхЗаданий = 1;
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры и функции формы
//

&НаКлиенте
Процедура ЗаписатьИзменения()
	
	ЗаписатьИзмененияНаСервере();
	Модифицированность = Ложь;
	РегламентныеЗаданияКлиент.ОтключитьГлобальныйОбработчикОжидания("УведомлятьОНекорректномВыполненииРегламентныхЗаданий");
	Если УведомлятьОНекорректномВыполненииРегламентныхЗаданий Тогда
		РегламентныеЗаданияКлиент.ПодключитьГлобальныйОбработчикОжидания("УведомлятьОНекорректномВыполненииРегламентныхЗаданий",
		                                                                 ПериодУведомленияОНекорректномВыполненииРегламентныхЗаданий * 60,
		                                                                 Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИзмененияНаСервере()
	
	Настройки = РегламентныеЗаданияСервер.ПолучитьНастройкиВыполненияРегламентныхЗаданий();
	Если ПравоДоступа("Администрирование", Метаданные) Тогда
		ЗаполнитьЗначенияСвойств(Настройки, ЭтаФорма);
	Иначе
		ЗаполнитьЗначенияСвойств(
			Настройки,
			ЭтаФорма,
			,
			"АвтоматическиЗапускатьОтдельныйСеансДляВыполненияРегламентныхЗаданий");
	КонецЕсли;
	РегламентныеЗаданияСервер.УстановитьНастройкиВыполненияРегламентныхЗаданий(Настройки);
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Настройки);
	
КонецПроцедуры

