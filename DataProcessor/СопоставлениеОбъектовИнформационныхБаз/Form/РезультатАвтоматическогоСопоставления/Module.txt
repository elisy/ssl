&НаКлиенте
Перем мВыдаватьПредупреждениеПриЗакрытииФормы;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// проверка, что форма открыта программно
	Если Не Параметры.Свойство("ИмяФайлаСообщенияОбмена") Тогда
		
		НСтрока = НСтр("ru = 'Форма не может быть открыта интерактивно!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтрока,,,, Отказ);
		Возврат;
		
	КонецЕсли;
	
	// инициализируем обработку переданными параметрами
	ЗаполнитьЗначенияСвойств(Объект, Параметры,, "СписокИспользуемыхПолей, СписокПолейТаблицы");
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	// определяем свойство "СписокИспользуемыхПолей"
	ОбработкаОбъект.СписокИспользуемыхПолей.Очистить();
	ОбщегоНазначенияКлиентСервер.ЗаполнитьКоллекциюСвойств(Параметры.СписокИспользуемыхПолей, ОбработкаОбъект.СписокИспользуемыхПолей);
	
	// определяем свойство "СписокПолейТаблицы"
	ОбработкаОбъект.СписокПолейТаблицы.Очистить();
	ОбщегоНазначенияКлиентСервер.ЗаполнитьКоллекциюСвойств(Параметры.СписокПолейТаблицы, ОбработкаОбъект.СписокПолейТаблицы);
	
	// загружаем таблицу неутвержденных связей
	ТаблицаНеутвержденныхСвязей = ПолучитьИзВременногоХранилища(Параметры.АдресВременногоХранилищаТаблицыНеутвержденныхСвязей);
	УдалитьИзВременногоХранилища(Параметры.АдресВременногоХранилищаТаблицыНеутвержденныхСвязей);
	ОбработкаОбъект.ТаблицаНеутвержденныхСвязей.Загрузить(ТаблицаНеутвержденныхСвязей);
	
	// получаем таблицу автоматического сопоставления объектов
	ОбработкаОбъект.ВыполнитьАвтоматическоеСопоставлениеОбъектов(Отказ, Параметры.СписокПолейСопоставления);
	
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
	// устанавливаем заголовки и видимость полей таблицы на форме
	УстановитьВидимостьПолейТаблицы("ТаблицаАвтоматическиСопоставленныхОбъектов", Параметры.МаксимальноеКоличествоПользовательскихПолей);
	
	// установка заголока формы
	Заголовок = Параметры.Заголовок;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если  Объект.ТаблицаАвтоматическиСопоставленныхОбъектов.Количество() > 0
		И мВыдаватьПредупреждениеПриЗакрытииФормы = Истина Тогда
			
		Предупреждение(НСтр("ru = 'Форма содержит данные автоматического сопоставления. Действие отменено.'"));
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура Применить(Команда)
	
	мВыдаватьПредупреждениеПриЗакрытииФормы = Ложь;
	
	// контекстный вызов сервера
	Закрыть(ПоместитьТаблицуАвтоматическиСопоставленныхОбъектовВоВременноеХранилище());
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	мВыдаватьПредупреждениеПриЗакрытииФормы = Ложь;
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьПометки(Команда)
	
	УстановитьПометкиНаСервере(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометки(Команда)
	
	УстановитьПометкиНаСервере(Истина);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Функция ПоместитьТаблицуАвтоматическиСопоставленныхОбъектовВоВременноеХранилище()
	
	Возврат ПоместитьВоВременноеХранилище(Объект.ТаблицаАвтоматическиСопоставленныхОбъектов.Выгрузить(Новый Структура("Пометка", Истина), "УникальныйИдентификаторПриемника, УникальныйИдентификаторИсточника, ТипИсточника, ТипПриемника"));
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьПолейТаблицы(Знач ИмяТаблицыФормы, Знач МаксимальноеКоличествоПользовательскихПолей)
	
	ИмяПоляИсточника = СтрЗаменить("#ИмяТаблицыФормы#ИсточникПолеNN","#ИмяТаблицыФормы#", ИмяТаблицыФормы);
	ИмяПоляПриемника = СтрЗаменить("#ИмяТаблицыФормы#ПриемникПолеNN","#ИмяТаблицыФормы#", ИмяТаблицыФормы);
	
	// снимаем видимость всех полей таблицы сопоставления
	Для НомерПоля = 1 По МаксимальноеКоличествоПользовательскихПолей Цикл
		
		ПолеИсточника = СтрЗаменить(ИмяПоляИсточника, "NN", Строка(НомерПоля));
		ПолеПриемника = СтрЗаменить(ИмяПоляПриемника, "NN", Строка(НомерПоля));
		
		ЭтаФорма.Элементы[ПолеИсточника].Видимость = Ложь;
		ЭтаФорма.Элементы[ПолеПриемника].Видимость = Ложь;
		
	КонецЦикла;
	
	// устанавливаем видимость полей таблицы сопоставления выбранных пользователем
	Для Каждого Элемент ИЗ Объект.СписокИспользуемыхПолей Цикл
		
		НомерПоля = Объект.СписокИспользуемыхПолей.Индекс(Элемент) + 1;
		
		ПолеИсточника = СтрЗаменить(ИмяПоляИсточника, "NN", Строка(НомерПоля));
		ПолеПриемника = СтрЗаменить(ИмяПоляПриемника, "NN", Строка(НомерПоля));
		
		// устанавливаем видимость полей
		ЭтаФорма.Элементы[ПолеИсточника].Видимость = Элемент.Пометка;
		ЭтаФорма.Элементы[ПолеПриемника].Видимость = Элемент.Пометка;
		
		// устанавливаем заголовки полей
		ЭтаФорма.Элементы[ПолеИсточника].Заголовок = Элемент.Значение;
		ЭтаФорма.Элементы[ПолеПриемника].Заголовок = Элемент.Значение;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПометкиНаСервере(Пометка)
	
	ТаблицаЗначений = Объект.ТаблицаАвтоматическиСопоставленныхОбъектов.Выгрузить();
	
	ТаблицаЗначений.ЗаполнитьЗначения(Пометка, "Пометка");
	
	Объект.ТаблицаАвтоматическиСопоставленныхОбъектов.Загрузить(ТаблицаЗначений);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИНИЦИАЛИЗАЦИЯ

мВыдаватьПредупреждениеПриЗакрытииФормы = Истина;
