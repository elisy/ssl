////////////////////////////////////////////////////////////////////////////////
// Блок обработчиков событий формы и элементов формы
//

// Обработчик события "при создании на сервере" 
// Устанавливает даты начала и конца для загрузки курсов по умолчанию
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.НачалоПериодаЗагрузки = НачалоМесяца(ТекущаяДата());
	Объект.ОкончаниеПериодаЗагрузки = ТекущаяДата();
	
КонецПроцедуры

// Обработчик события "обработка выбора" формы
// Обрабатывает выбор валюты в форме выбора
//
&НаКлиенте
Процедура ОбработкаВыбора(РезультатВыбора, ИсточникВыбора)
	
	Если ТипЗнч(РезультатВыбора) = Тип("СправочникСсылка.Валюты") Тогда
		
		Если Объект.СписокВалют.НайтиСтроки(Новый Структура("Валюта", РезультатВыбора)).Количество() = 0 Тогда
			ДобавитьВалютуВСписок(РезультатВыбора);
		Иначе
			Предупреждение(НСтр("ru = 'Валюта уже присутствует в списке'"));
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события "перед началом добавления" элемента формы СписокВалют
// Вызывает форму выбора валют в режиме выбора
//
&НаКлиенте
Процедура СписокВалютПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	ПараметрыФорма = Новый Структура("ЗагружаемыеВалюты", Истина);
	ОткрытьФорму("Справочник.Валюты.ФормаВыбора", ПараметрыФорма, ЭтаФорма);
	Отказ = Истина;
	
КонецПроцедуры

// Обработчик события "выбор" табличной части "СписокВалют" формы
// Открывает форму валюты
//
&НаКлиенте
Процедура СписокВалютВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьЗначение(Элементы.СписокВалют.ТекущиеДанные.Валюта);
	
КонецПроцедуры

// Обработчик события нажания на кнопку "Очистить"
// Очищает список валют
//
&НаКлиенте
Процедура КомандаОчиститьВыполнить()
	
	Объект.СписокВалют.Очистить();
	
КонецПроцедуры

// Обработчик нажатия на кнопку "Заполнить"
// Вызывает функциональность заполнения списка валют
//
&НаКлиенте
Процедура КомандаЗаполнитьСписокВалютВыполнить()
	
	ЗаполнитьВалюты();
	
КонецПроцедуры

// Обработчик нажатия на кнопку "Подбор"
// Вызывает форму выбора валют в режиме подбора
//
&НаКлиенте
Процедура КомандаПодборВалютВыполнить()
	
	ПараметрыФормы = Новый Структура("ЗакрыватьПриВыборе, Отбор", Ложь, Новый Структура("ЗагружаетсяИзИнтернета", Истина));
	ОткрытьФорму("Справочник.Валюты.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

// Обработчик нажаитя на кнопку "Загрузить"
// Вызывает функциональность загрузки курсов
//
&НаКлиенте
Процедура КомандаЗагрузитьВыполнить()
	
	Если Не ЗначениеЗаполнено(Объект.НачалоПериодаЗагрузки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					НСтр("ru = 'Не задана дата начала периода загрузки.'"), ,
					"Объект.НачалоПериодаЗагрузки");
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ОкончаниеПериодаЗагрузки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					НСтр("ru = 'Не задана дата окончания периода загрузки.'"), ,
					"Объект.ОкончаниеПериодаЗагрузки");
		Возврат;
	КонецЕсли;
	
	Если Объект.СписокВалют.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					НСтр("ru = 'Для загрузки курсов с веб-сервера РБК необходимо добавить в список хотя бы одну валюту.'"), ,
					"Объект.СписокВалют");
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	Состояние(НСтр("ru = 'Загружаются курсы валют...'"));
	СтатусОперации = ЗагрузитьКурсыСРБК();
	
	Если СтатусОперации Тогда
		Оповестить("ИзменениеКурсовВалют");
		ТекстСообщенияОЗавершенииЗагрузки = НСтр("ru = 'Загрузка курсов валют завершена.'");
		Состояние(ТекстСообщенияОЗавершенииЗагрузки);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Секция служебных процедур
//

&НаКлиенте
Функция ЗагрузитьКурсыСРБК()
	
	ПоясняющееСообщение = "";
	
#Если ВебКлиент Тогда
	Результат = ЗагрузитьКурсыСРБК_Сервер(
			Объект.СписокВалют,
			Объект.НачалоПериодаЗагрузки,
			Объект.ОкончаниеПериодаЗагрузки);
#Иначе
	Результат = РаботаСКурсамиВалютКлиентСервер.ЗагрузитьКурсыВалютПоПараметрам(
				Объект.СписокВалют,
				Объект.НачалоПериодаЗагрузки,
				Объект.ОкончаниеПериодаЗагрузки);
#КонецЕсли
	
	ЕстьУспешноЗагруженныеКурсы = Ложь;
	
	Для Каждого СостояниеЗагрузки Из Результат Цикл
		Если СостояниеЗагрузки.СтатусОперации Тогда
			ЕстьУспешноЗагруженныеКурсы = Истина;
		Иначе
			Индекс = Объект.СписокВалют.Индекс(Объект.СписокВалют.НайтиСтроки(Новый Структура("Валюта", СостояниеЗагрузки.Валюта))[0]);
			ИмяПоля = СтрЗаменить("СписокВалют[x].Валюта", "x", Строка(Индекс));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СостояниеЗагрузки.Сообщение,, ИмяПоля, "Объект");
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьУспешноЗагруженныеКурсы Тогда
		ОбновитьСведенияВСпискеВалют();
	КонецЕсли;
	
	Возврат ЕстьУспешноЗагруженныеКурсы;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗагрузитьКурсыСРБК_Сервер(знач СписокВалют, знач НачалоПериодаЗагрузки, знач ОкончаниеПериодаЗагрузки)
	
	Возврат РаботаСКурсамиВалютКлиентСервер.ЗагрузитьКурсыВалютПоПараметрам(
				СписокВалют, НачалоПериодаЗагрузки, ОкончаниеПериодаЗагрузки);
	
КонецФункции

// Процедура заполняет табличную часть списком валют. В список попадают только 
// валюты, курс которых не зависит от курса других валют.
//
&НаСервере
Процедура ЗаполнитьВалюты()
	
	ОкончаниеПериодаЗагрузки = Объект.ОкончаниеПериодаЗагрузки;
	СписокВалют = Объект.СписокВалют;
	СписокВалют.Очистить();
	
	ЗагружаемыеВалюты = РаботаСКурсамиВалют.ПолучитьМассивЗагружаемыхВалют();
	
	Для Каждого ЭлементВалюта Из ЗагружаемыеВалюты Цикл
		ДобавитьВалютуВСписок(ЭлементВалюта);
	КонецЦикла;
	
КонецПроцедуры

// Процедура добавляет в список валют новую строку и заполняет ее информацией
// о курсе на основе ссылки на валюту
//
// Параметры:
// Валюта - Справочник.Валюты / Ссылка - ссылка на валюту, которая добавляется
//                 в список
//
&НаСервере
Процедура ДобавитьВалютуВСписок(Валюта)
	
	НоваяСтрока = Объект.СписокВалют.Добавить();
	ЗаполнитьДанныеСтрокиТаблицыНаОсновеВалюты(НоваяСтрока, Валюта);
	
КонецПроцедуры

// Процедура обновляет записи о курсах валют в списке.
//
&НаСервере
Процедура ОбновитьСведенияВСпискеВалют()
	
	Для Каждого СтрокаДанных Из Объект.СписокВалют Цикл
		СсылкаНаВалюту = СтрокаДанных.Валюта;
		ЗаполнитьДанныеСтрокиТаблицыНаОсновеВалюты(СтрокаДанных, СсылкаНаВалюту);
	КонецЦикла;
	
КонецПроцедуры

// Процедура заполняет строку табличной части информацией о курсе на основе
// ссылки на валюту.
//
// Параметры:
// ТекСтрока       - ДанныеФормыЭлементКоллекции - ссылка на строку табличной
//                  части, которую необходимо заполнить информацией о курсе 
//                  валюты
// ВыбраннаяВалюта - Справочник.Валюты / Ссылка - ссылка на валюту, информацию
//                  о которой необходимо получить.
//
&НаСервере
Процедура ЗаполнитьДанныеСтрокиТаблицыНаОсновеВалюты(ТекСтрока, СсылкаНаВалюту);
	
	ТекСтрока.Валюта = СсылкаНаВалюту;
	ТекСтрока.КодВалюты = СсылкаНаВалюту.Код;
	
	ДанныеКурса = РаботаСКурсамиВалют.ЗаполнитьДанныеКурсаДляВалюты(СсылкаНаВалюту);
	
	Если ТипЗнч(ДанныеКурса) = Тип ("Структура") Тогда
		ТекСтрока.ДатаКурса = ДанныеКурса.ДатаКурса;
		ТекСтрока.Курс      = ДанныеКурса.Курс;
		ТекСтрока.Кратность = ДанныеКурса.Кратность;
	КонецЕсли;
	
КонецПроцедуры
