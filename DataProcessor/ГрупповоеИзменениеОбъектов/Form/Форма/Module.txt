///////////////////////////////////////////////////////////////////////////////
// Создаваемые реквизиты в реквизите ИзменяемыеОбъекты имеют имена  Реквизит_<поля>
// Создаваемые поля на форме в таблице формы ОбъектыДляИзменения имеют имена Элемент_<имя>
//
// ОПИСАНИЕ РЕКВИЗИТОВ
//
// --- Настройки транзакционной обработки ---------------------------------------
// ТОНастройкаПорции - число
//   1 - одной порцией
//   2 - порциями по числу объектов
//   3 - порциями по проценту объектов
// ТОПроцентОбъектовВПорции - содержит значение если НастройкаПорцииИзменения = 2
// ТОЧислоОбъектовВПорции - содержит значение если НастройкаПорцииИзменения = 3
// ------------------------------------------------------------------------------
//
// --- Изменение блокируемых реквизитов -----------------------------------------
// ЕстьФормаРазблокированияРеквизитов - имеет смысл использовать для объектов
//						с блокируемыми реквизитами, Истина - если есть форма, для работы
//						с блокируемыми реквизитами (ФормаРаботыСБлокируемымиРеквизитами).
//
// ПолноеИмяФормыРаботыСБлокируемымиРеквизитами - строка - полное имя формы для использования
//						в функции ОткрытьФормуМодально, Например,
//						"Справочник.Номенклатура.Форма.ФормаРаботыСБлокируемымиРеквизитами"
// ------------------------------------------------------------------------------

///////////////////////////////////////////////////////////////////////////////
// Блок процедур и функций работы с историей изменений

&НаСервере
Процедура ДобавитьИзменениеВИсторию(СтруктураИзменения, ПредставлениеИзменения)
	
	// Настройки истории изменений это массив структур с ключами:
	// Изменение - массив со структурой изменения
	// Представление - представление настройки пользователю
	Настройки = ХранилищеОбщихНастроек.Загрузить("ГрупповоеИзменениеОбъектов", "ИсторияИзменений/"+ПолноеИмя,, ОбщегоНазначения.ТекущийПользователь());
	
	Если Настройки = Неопределено Тогда
		Настройки = Новый Массив;
	КонецЕсли;
	
	Найденный = -1;
	
	Для Индекс = 0 ПО Настройки.ВГраница() Цикл
		Если Настройки.Получить(Индекс).Представление = ПредставлениеИзменения Тогда
			Найденный = Индекс;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Найденный <> -1 Тогда
		Настройки.Удалить(Найденный);
	КонецЕсли;
	
	Настройки.Вставить(0, Новый Структура("Изменение, Представление", СтруктураИзменения, ПредставлениеИзменения));
	
	Если Настройки.Количество() > 20 Тогда
		Настройки.Удалить(19);
	КонецЕсли;
	
	ХранилищеОбщихНастроек.Сохранить("ГрупповоеИзменениеОбъектов", "ИсторияИзменений/"+ПолноеИмя, Настройки, , ОбщегоНазначения.ТекущийПользователь());
	
	ЗагрузитьИсториюОпераций();
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьИсториюОпераций()
	
	ИсторияОпераций = ХранилищеОбщихНастроек.Загрузить("ГрупповоеИзменениеОбъектов", "ИсторияИзменений/"+ПолноеИмя,, ОбщегоНазначения.ТекущийПользователь());
	
	Если ИсторияОпераций = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИсторияОперацийСписок.Очистить();
	
	Для Каждого ОписаниеОперации Из ИсторияОпераций Цикл
		ИсторияОперацийСписок.Добавить(ОписаниеОперации.Изменение, ОписаниеОперации.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОперацииИзменения(МассивОпераций)
	
	ЕстьЗаблокированные = Ложь;
	
	Для Каждого ОписаниеИзменения Из МассивОпераций Цикл
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("ВидОперации", ОписаниеИзменения.ВидОперации);
		
		Если ОписаниеИзменения.ВидОперации = 1 Тогда // реквизит объекта
			СтруктураПоиска.Вставить("ИмяРеквизита", ОписаниеИзменения.ИмяРеквизита);
		Иначе
			СтруктураПоиска.Вставить("Свойство", ОписаниеИзменения.Свойство);
		КонецЕсли;
		
		НайденныеСтроки = Объект.Операции.НайтиСтроки(СтруктураПоиска);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			Если НайденныеСтроки[0].ЗаблокированныйРеквизит  Тогда
				ЕстьЗаблокированные = Истина;
				Продолжить;
			КонецЕсли;
			НайденныеСтроки[0].Значение = ОписаниеИзменения.Значение;
			НайденныеСтроки[0].Изменять = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЕстьЗаблокированные Тогда
		Предупреждение(НСтр("ru = 'Некоторые реквизиты заблокированы для изменения, изменения не установлены.'"));
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Блок вспомогательных процедур и функций

&НаСервере
Процедура ЗаполнитьДеревоОбъектовДляИзменения(МассивЗаполненных)
	
	ТаблицаОтработанных = Новый ТаблицаЗначений;
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(ТипЗнч(Параметры.МассивОбъектов[0]));
	ТаблицаОтработанных.Колонки.Добавить("Ссылка", Новый ОписаниеТипов(МассивТипов));
	
	Для Каждого Элемент Из МассивЗаполненных Цикл
		ТаблицаОтработанных.Добавить().Ссылка = Элемент;
	КонецЦикла;
	
	ОбъектМетаданных = Параметры.МассивОбъектов[0].Метаданные();
	
	ВидОбъектаМетаданных = ОбщегоНазначения.ВидОбъектаПоСсылке(Параметры.МассивОбъектов[0]);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Ссылка
		|Поместить
		|	Отработанные
		|ИЗ
		|	&ТаблицаОтработанных КАК Отработанные
		|;
		|ВЫБРАТЬ
		|	Таблица.Ссылка КАК Ссылка,
		|	0 КАК КодОшибки,
		|	ВЫБОР КОГДА Отработанные.Ссылка ЕСТЬ NULL 
		|		ТОГДА Истина
		|	ИНАЧЕ Ложь
		|КОНЕЦ КАК Изменять,";
		
	Если УчитыватьИерархию Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	ВЫБОР КОГДА Таблица.ПометкаУдаления ТОГДА
		|		ВЫБОР КОГДА Таблица.ЭтоГруппа ТОГДА	1
		|		ИНАЧЕ 3
		|		КОНЕЦ
		|	ИНАЧЕ
		|		ВЫБОР КОГДА Таблица.ЭтоГруппа ТОГДА	0
		|		ИНАЧЕ 2
		|		КОНЕЦ
		|	Конец КАК КодКартинки";
	Иначе
		ТекстЗапроса = ТекстЗапроса + "
		|	ВЫБОР КОГДА Таблица.ПометкаУдаления ТОГДА 3
		|	ИНАЧЕ 2
		|	КОНЕЦ КАК КодКартинки";
	КонецЕсли;
	
	Если УчитыватьИерархию Тогда
		ТекстЗапроса = ТекстЗапроса + "," + "
			|	Таблица.ЭтоГруппа КАК ЭтоГруппа";
	Иначе
		ТекстЗапроса = ТекстЗапроса + "," + "
			|	Ложь КАК ЭтоГруппа";
	КонецЕсли;
	
	СчетчикДопРеквизитов = 0;
	СчетчикДопСведений = 0;
	
	ЛевоеСоединениеПоДопРеквизитам = "";
	ЛевоеСоединениеПоДопСведениям = "";
	
	ПараметрыСвойства = Новый Соответствие;
	
	Для Каждого ОписаниеКолонки Из РеквизитФормыВЗначение("ИзменяемыеОбъекты").Колонки Цикл
		
		Если ПроверитьНеизменяемыйРеквизит(ОписаниеКолонки.Имя) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ИмеетПрефикс(ОписаниеКолонки.Имя, ПрефиксИмениРеквизита()) Тогда
			
			ТекстЗапроса = ТекстЗапроса + "," + Символы.ПС + СтрокаБезПрефикса(ОписаниеКолонки.Имя, ПрефиксИмениРеквизита()) + " КАК " + ОписаниеКолонки.Имя
			
		ИначеЕсли ИмеетПрефикс(ОписаниеКолонки.Имя, ПрефиксИмениДопРеквизита()) Тогда
			
			СчетчикДопРеквизитов = СчетчикДопРеквизитов + 1;
			ТекстЗапроса = ТекстЗапроса + "," + Символы.ПС + 
			СтрЗаменить("ДополнительныеРеквизиты[x].Значение КАК ", "[x]", Строка(СчетчикДопРеквизитов)) + ОписаниеКолонки.Имя;
			
			ЛевоеСоединениеПоДопРеквизитам = ЛевоеСоединениеПоДопРеквизитам + "
			|		ЛЕВОЕ СОЕДИНЕНИЕ
			|			[ВидОбъектаМетаданных].[ИмяОбъектаМетаданных].ДополнительныеРеквизиты КАК ДополнительныеРеквизиты[x]
			|			ПО ДополнительныеРеквизиты[x].Ссылка = Таблица.Ссылка
			|			 И ДополнительныеРеквизиты[x].Свойство = &ДопРеквизит[x]";
			
			ЛевоеСоединениеПоДопРеквизитам = СтрЗаменить(ЛевоеСоединениеПоДопРеквизитам, "[x]", Строка(СчетчикДопРеквизитов));
			
			УнИдСтрока = СтрЗаменить(СтрокаБезПрефикса(ОписаниеКолонки.Имя, ПрефиксИмениДопРеквизита()), "_", "-");
			УнИд = Новый УникальныйИдентификатор(УнИдСтрока);
			Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПолучитьСсылку(УнИд);
			
			ПараметрыСвойства.Вставить(СтрЗаменить("ДопРеквизит[x]", "[x]", Строка(СчетчикДопРеквизитов)), Свойство);
		
		ИначеЕсли ИмеетПрефикс(ОписаниеКолонки.Имя, ПрефиксИмениДопСведения()) Тогда
			СчетчикДопСведений = СчетчикДопСведений + 1;
			ТекстЗапроса = ТекстЗапроса + "," + Символы.ПС + 
			СтрЗаменить("ДополнительныеСведения[x].Значение КАК ", "[x]", Строка(СчетчикДопСведений)) + ОписаниеКолонки.Имя;
			
			ЛевоеСоединениеПоДопСведениям = ЛевоеСоединениеПоДопСведениям + "
			|		ЛЕВОЕ СОЕДИНЕНИЕ
			|			РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения[x]
			|			ПО ДополнительныеСведения[x].Объект = Таблица.Ссылка
			|			 И ДополнительныеСведения[x].Свойство = &ДопСведение[x]";
			
			ЛевоеСоединениеПоДопСведениям = СтрЗаменить(ЛевоеСоединениеПоДопСведениям, "[x]", Строка(СчетчикДопСведений));
			
			УнИдСтрока = СтрЗаменить(СтрокаБезПрефикса(ОписаниеКолонки.Имя, ПрефиксИмениДопСведения()), "_", "-");
			УнИд = Новый УникальныйИдентификатор(УнИдСтрока);
			Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПолучитьСсылку(УнИд);
			
			ПараметрыСвойства.Вставить(СтрЗаменить("ДопСведение[x]", "[x]", Строка(СчетчикДопСведений)), Свойство);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТекстЗапроса = ТекстЗапроса + "
		|ИЗ
		|	[ВидОбъектаМетаданных].[ИмяОбъектаМетаданных] КАК Таблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ Отработанные
		|			ПО Таблица.Ссылка = Отработанные.Ссылка";
	
	ТекстЗапроса = ТекстЗапроса + ЛевоеСоединениеПоДопРеквизитам;
	ТекстЗапроса = ТекстЗапроса + ЛевоеСоединениеПоДопСведениям;
	
	ТекстЗапроса = ТекстЗапроса + "
		|ГДЕ";
	
	Если ОбрабатыватьРекурсивно И УчитыватьИерархию Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	Таблица.Ссылка В ИЕРАРХИИ(&МассивСсылок)";
	Иначе
		ТекстЗапроса = ТекстЗапроса + "
		|	Таблица.Ссылка В (&МассивСсылок)";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
		|УПОРЯДОЧИТЬ ПО";
	
	Если ОбрабатыватьРекурсивно И УчитыватьИерархию Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	Таблица.ЭтоГруппа ИЕРАРХИЯ ВОЗР,";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
		|	Таблица.Ссылка ВОЗР";
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[ВидОбъектаМетаданных]", ВидОбъектаМетаданных);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[ИмяОбъектаМетаданных]", ОбъектМетаданных.Имя);
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = ТекстЗапроса;
	Запрос.Параметры.Вставить("МассивСсылок", Параметры.МассивОбъектов);
	Запрос.УстановитьПараметр("ТаблицаОтработанных", ТаблицаОтработанных);
	
	Для Каждого ПараметрСвойство Из ПараметрыСвойства Цикл
		Запрос.УстановитьПараметр(ПараметрСвойство.Ключ, ПараметрСвойство.Значение);
	КонецЦикла;
	
	ЗначениеВДанныеФормы(Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией), ИзменяемыеОбъекты);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСплошнойСписокОбъектовДляИзменения(КоллекцияЭлементов)
	
	Для Каждого ЭлементКоллекции Из КоллекцияЭлементов Цикл
		НоваяСтрока = Объект.ОбъектыДляИзменения.Добавить();
		НоваяСтрока.Ссылка = ЭлементКоллекции.Ссылка;
		ЗаполнитьСплошнойСписокОбъектовДляИзменения(ЭлементКоллекции.ПолучитьЭлементы());
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ТекстПодсказкиСтраницыДействий(КоличествоОбъектов)
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '
							|Нажмите кнопку ""Далее"", чтобы перейти к просмотру изменяемых объектов (%1)'"),
				КоличествоОбъектов);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
// В зависимости от текущей страницы устанавливает доступность команд на форме
//
Процедура УстановитьСостояниеЭлементовФормы(Элементы, Количество = 0)
	
	Если Количество > 0 Тогда
		Элементы.ПодсказкаСтраницыДействий.Заголовок = ТекстПодсказкиСтраницыДействий(Количество);
	КонецЕсли;
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.НастройкаИзменений Тогда
		Элементы.ФормаНазад.Доступность = Ложь;
		Элементы.ФормаДалее.Доступность = Истина;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.ИзменениеОбъектов Тогда
		Элементы.ФормаНазад.Доступность = Истина;
		Элементы.ФормаДалее.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьМассивИзмененных(Массив, Элементы)
	
	Для Каждого Элемент Из Элементы Цикл
		Если НЕ Элемент.Изменять Тогда
			Массив.Добавить(Элемент.Ссылка);
		КонецЕсли;
		Дочерние = Элемент.ПолучитьЭлементы();
		Если Дочерние.Количество() > 0 Тогда
			ЗаполнитьМассивИзмененных(Массив, Дочерние);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьИЗаполнитьДеревоИзменяемыхОбъектов()
	
	МассивЗаполненных = Новый Массив;
	
	ЗаполнитьМассивИзмененных(МассивЗаполненных, ИзменяемыеОбъекты.ПолучитьЭлементы());
	
	СформироватьДеревоИзменяемыхОбъектов();
	ЗаполнитьДеревоОбъектовДляИзменения(МассивЗаполненных);
	
	ИзменитьТаблицуОбъектов = Ложь;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СтрокаБезПрефикса(ИмяРеквизитаСПрефиксом, Префикс)
	Возврат Прав(ИмяРеквизитаСПрефиксом, СтрДлина(ИмяРеквизитаСПрефиксом) - СтрДлина(Префикс));
КонецФункции

&НаСервере
// При отборе операций с объектами необходимо изменить реквизит
// ИзменяемыеОбъекты (добавить новые колонки) и изменить таблицу на форме
// (добавить новые колонки на форме).
//
Процедура СформироватьДеревоИзменяемыхОбъектов()
	
	СуществующийСоставРеквизитов = Новый Массив;
	
	Для Каждого ОписаниеКолонки Из РеквизитФормыВЗначение("ИзменяемыеОбъекты").Колонки Цикл
		
		Если ПроверитьНеизменяемыйРеквизит(ОписаниеКолонки.Имя) Тогда
			Продолжить;
		КонецЕсли;
		
		СуществующийСоставРеквизитов.Добавить(ОписаниеКолонки.Имя);
	КонецЦикла;
	
	НовыйСоставРеквизитов = Новый Массив;
	
	Для Каждого Операция Из Объект.Операции Цикл
		Если      Операция.Изменять И Операция.ВидОперации = 1 Тогда
			НовыйСоставРеквизитов.Добавить(ПрефиксИмениРеквизита()+Операция.ИмяРеквизита);
		ИначеЕсли Операция.Изменять И Операция.ВидОперации = 2 Тогда
			НовыйСоставРеквизитов.Добавить(ПрефиксИмениДопРеквизита()+СтрЗаменить(Операция.Свойство.УникальныйИдентификатор(), "-", "_"));
		ИначеЕсли Операция.Изменять И Операция.ВидОперации = 3 Тогда
			НовыйСоставРеквизитов.Добавить(ПрефиксИмениДопСведения()+СтрЗаменить(Операция.Свойство.УникальныйИдентификатор(), "-", "_"));
		КонецЕсли;
	КонецЦикла;
	
	ИменаДобавляемыхРеквизитов = РазницаМассивов(НовыйСоставРеквизитов, СуществующийСоставРеквизитов);
	ИменаУдаляемыхРеквизитов = РазницаМассивов(СуществующийСоставРеквизитов, НовыйСоставРеквизитов);
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	Для Каждого ИмяРеквизита Из ИменаДобавляемыхРеквизитов Цикл
		
		Если ИмеетПрефикс(ИмяРеквизита, ПрефиксИмениРеквизита()) Тогда
		
			Операция = Объект.Операции.НайтиСтроки(Новый Структура("ИмяРеквизита", СтрокаБезПрефикса(ИмяРеквизита, ПрефиксИмениРеквизита())))[0];
			ДопустимыеТипыМассив = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Операция.ДопустимыеТипы, ";");
			
			ТипыМассив = Новый Массив;
			
			Для Каждого ДопустимыйТипСтрока Из ДопустимыеТипыМассив Цикл
				ТипыМассив.Добавить(Тип(ДопустимыйТипСтрока));
			КонецЦикла;
			
			ОписаниеТипов = Новый ОписаниеТипов(ТипыМассив);
			
		ИначеЕсли ИмеетПрефикс(ИмяРеквизита, ПрефиксИмениДопРеквизита())
			  ИЛИ ИмеетПрефикс(ИмяРеквизита, ПрефиксИмениДопСведения()) Тогда
			
			Префикс = ?(ИмеетПрефикс(ИмяРеквизита, ПрефиксИмениДопРеквизита()),
							ПрефиксИмениДопРеквизита(),
							ПрефиксИмениДопСведения());
			
			УнИд = Новый УникальныйИдентификатор(СтрЗаменить(СтрокаБезПрефикса(ИмяРеквизита, Префикс), "_", "-"));
			
			Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПолучитьСсылку(УнИд);
			
			ОписаниеТипов = Свойство.ТипЗначения;
			
			Операция = Объект.Операции.НайтиСтроки(Новый Структура("Свойство", Свойство))[0];
			
		КонецЕсли;
		
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(ИмяРеквизита, ОписаниеТипов, "ИзменяемыеОбъекты", Операция.Представление));
		
	КонецЦикла;
	
	УдаляемыеРеквизиты = Новый Массив;
	
	Для Каждого ИмяРеквизита Из ИменаУдаляемыхРеквизитов Цикл
		УдаляемыеРеквизиты.Добавить("ИзменяемыеОбъекты."+ИмяРеквизита);
	КонецЦикла;
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты, УдаляемыеРеквизиты);
	
	УдаляемыеПоляФормы = Новый Массив;
	
	Для Каждого ПолеФормы Из Элементы.ОбъектыДляИзменения.ПодчиненныеЭлементы Цикл
		Если ТипЗнч(ПолеФормы) = Тип("ПолеФормы") И ПолеФормы.ПутьКДанным = "" Тогда
			УдаляемыеПоляФормы.Добавить(ПолеФормы);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ДобавленныйРеквизит Из ДобавляемыеРеквизиты Цикл
		НовыйЭлементФормы = Элементы.Добавить(ПрефиксИмениПоляФормы()+ДобавленныйРеквизит.Имя, Тип("ПолеФормы"), Элементы.ОбъектыДляИзменения);
		НовыйЭлементФормы.ПутьКДанным = "ИзменяемыеОбъекты."+ДобавленныйРеквизит.Имя;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РазницаМассивов(МассивИсточник, МассивВычитаемый)
	
	Результат = Новый Массив;
	
	Для Каждого ЭлементМассива Из МассивИсточник Цикл
		Если МассивВычитаемый.Найти(ЭлементМассива) = Неопределено Тогда
			Результат.Добавить(ЭлементМассива);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// Блок процедур - обработчиков событий элементов формы

&НаКлиенте
// Обработчик события ПередНачаломДобавления таблицы формы ТаблицаТаблицаОпераций
//
Процедура ТаблицаОперацийПередНачаломИзменения(Элемент, Отказ)
	
	Если (Элемент.ТекущийЭлемент.Имя = "ТаблицаОперацийИзменять"
		ИЛИ Элемент.ТекущийЭлемент.Имя = "ТаблицаОперацийЗначение")
	   И Элемент.ТекущиеДанные.ЗаблокированныйРеквизит Тогда
		ВопросПерейтиКРазблокированиюРеквизитов();
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	УстановитьОграниченияВыбираемыхТиповИПараметрыВыбора(Элемент);
	
КонецПроцедуры

&НаКлиенте
// Обработчик события выбор таблицы формы ТаблицаТаблицаОпераций
// устанавливает ограничение типа для редактируемого реквизита
//
Процедура ТаблицаОперацийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ТаблицаОперацийЗначение"
	   И Элемент.ТекущиеДанные.ЗаблокированныйРеквизит Тогда
		
		ВопросПерейтиКРазблокированиюРеквизитов();
		СтандартнаяОбработка = Ложь;
		
		Возврат;
		
	КонецЕсли;
	
	УстановитьОграниченияВыбираемыхТиповИПараметрыВыбора(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПерейтиКРазблокированиюРеквизитов()
	
	ТекстВопроса = НСтр("ru = 'Внимание! Реквизит заблокирован, перейти к разблокированию реквизитов?'");
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Да'"));
	Кнопки.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru = 'Отмена'"));
	
	Результат = Вопрос(ТекстВопроса, Кнопки, , КодВозвратаДиалога.Да, НСтр("ru = 'Реквизит заблокирован'"));
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		РазрешитьРедактированиеРеквизитов();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораСервер(ПараметрыВыбора, ПараметрыВыбораМассив)
	
		Для Индекс = 1 по СтрЧислоСтрок(ПараметрыВыбора) Цикл
			
			ПараметрыВыбораСтрока = СтрПолучитьСтроку(ПараметрыВыбора, Индекс);
		
			ПозицияИмениПоля = Найти(ПараметрыВыбораСтрока, ";") - 1;
			ИмяПоляОтбора = Сред(ПараметрыВыбораСтрока, 1, ПозицияИмениПоля);
			ПараметрыВыбораСтрока = Прав(ПараметрыВыбораСтрока, СтрДлина(ПараметрыВыбораСтрока)-ПозицияИмениПоля-1);
			ПозицияИмениТипа = Найти(ПараметрыВыбораСтрока, ";") - 1;
			ИмяТипа = Сред(ПараметрыВыбораСтрока, 1, ПозицияИмениТипа);
			ПараметрыВыбораСтрока = Прав(ПараметрыВыбораСтрока, СтрДлина(ПараметрыВыбораСтрока)-ПозицияИмениТипа-1);
			Значение = ПараметрыВыбораСтрока;
			
			ПараметрыВыбораМассив.Добавить(Новый ПараметрВыбора(ИмяПоляОтбора,XMLЗначение(Тип(ИмяТипа), Значение)) );
		
		КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
// Устанавливает ограничения выбираемых типов значений и параметры выбора
//
Процедура УстановитьОграниченияВыбираемыхТиповИПараметрыВыбора(Элемент)
	
	Поле = Элемент.ПодчиненныеЭлементы.ТаблицаОперацийЗначение;
	
	ДопустимыеТипыСтрока = Элемент.ТекущиеДанные.ДопустимыеТипы;
	
	ДопустимыеТипыМассив = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ДопустимыеТипыСтрока, ";");
	
	МассивТипов = Новый Массив;
	
	Для Каждого ТипСтрока Из ДопустимыеТипыМассив Цикл
		МассивТипов.Добавить(Тип(ТипСтрока));
	КонецЦикла;
	
	Поле.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);
	
	ПараметрыВыбораМассив = Новый Массив;
	
	Если НЕ ПустаяСтрока(Элемент.ТекущиеДанные.ПараметрыВыбора) Тогда
#Если ВебКлиент Тогда
		УстановитьПараметрыВыбораСервер(Элемент.ТекущиеДанные.ПараметрыВыбора, ПараметрыВыбораМассив)
#Иначе
		Для Индекс = 1 по СтрЧислоСтрок(Элемент.ТекущиеДанные.ПараметрыВыбора) Цикл
			
			ПараметрыВыбораСтрока = СтрПолучитьСтроку(Элемент.ТекущиеДанные.ПараметрыВыбора, Индекс);
		
			ПозицияИмениПоля = Найти(ПараметрыВыбораСтрока, ";") - 1;
			ИмяПоляОтбора = Сред(ПараметрыВыбораСтрока, 1, ПозицияИмениПоля);
			ПараметрыВыбораСтрока = Прав(ПараметрыВыбораСтрока, СтрДлина(ПараметрыВыбораСтрока)-ПозицияИмениПоля-1);
			ПозицияИмениТипа = Найти(ПараметрыВыбораСтрока, ";") - 1;
			ИмяТипа = Сред(ПараметрыВыбораСтрока, 1, ПозицияИмениТипа);
			ПараметрыВыбораСтрока = Прав(ПараметрыВыбораСтрока, СтрДлина(ПараметрыВыбораСтрока)-ПозицияИмениТипа-1);
			Значение = ПараметрыВыбораСтрока;
			
			ПараметрыВыбораМассив.Добавить(Новый ПараметрВыбора(ИмяПоляОтбора,XMLЗначение(Тип(ИмяТипа), Значение)) );
		
		КонецЦикла;
#КонецЕсли
	КонецЕсли;
	
	Если Не ПустаяСтрока(Элемент.ТекущиеДанные.СвязиПараметровВыбора) Тогда
	
		Для Индекс = 1 по СтрЧислоСтрок(Элемент.ТекущиеДанные.СвязиПараметровВыбора) Цикл
			
			СвязьПараметровВыбораСтрока = СтрПолучитьСтроку(Элемент.ТекущиеДанные.СвязиПараметровВыбора, Индекс);
			РазложенныеСтроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СвязьПараметровВыбораСтрока, ";");
			ИмяПараметра = СокрЛП(РазложенныеСтроки[0]);
			ИмяРеквизита = СокрЛП(РазложенныеСтроки[1]);
			НайденныеСтроки = Объект.Операции.НайтиСтроки(Новый Структура("ВидОперации,ИмяРеквизита", 1, ИмяРеквизита));
			Если НайденныеСтроки.Количество() = 1 Тогда
				Значение = НайденныеСтроки[0].Значение;
				Если ЗначениеЗаполнено(Значение) Тогда
					ПараметрыВыбораМассив.Добавить(Новый ПараметрВыбора(ИмяПараметра, Значение));
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
	
	КонецЕсли;
	
	Поле.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораМассив);
	
	ВыборГруппИЭлементов = Элемент.ТекущиеДанные.ВыборГруппИЭлементов;
	
	Если ВыборГруппИЭлементов <> "" Тогда
		Если	  ВыборГруппИЭлементов = "Группы" Тогда
			Поле.ВыборГруппИЭлементов = ГруппыИЭлементы.Группы;
		ИначеЕсли ВыборГруппИЭлементов = "ГруппыИЭлементы" Тогда
			Поле.ВыборГруппИЭлементов = ГруппыИЭлементы.ГруппыИЭлементы;
		ИначеЕсли ВыборГруппИЭлементов = "Элементы" Тогда
			Поле.ВыборГруппИЭлементов = ГруппыИЭлементы.Элементы;
		Иначе
			Поле.ВыборГруппИЭлементов = ГруппыИЭлементы.Авто;
		КонецЕсли;
	Иначе
		Поле.ВыборГруппИЭлементов = ГруппыИЭлементы.Авто;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаОперацийПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если Элемент.ТекущийЭлемент.Имя = "ТаблицаОперацийЗначение" Тогда
		Если ОтменаРедактирования Тогда
			
		ИначеЕсли НЕ ЗначениеЗаполнено(Элемент.ТекущиеДанные.Значение) Тогда
			Элемент.ТекущиеДанные.Изменять = Ложь;
			ИзменитьТаблицуОбъектов = Истина;
		Иначе
			Элемент.ТекущиеДанные.Изменять = Истина;
			ИзменитьТаблицуОбъектов = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыДляИзмененияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.ОбъектыДляИзмененияСсылка Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьОбъектПоСсылке();
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Блок процедур - обработчиков событий формы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НекорректныйВызовОбработки Тогда
		Отказ = Истина;
		Предупреждение(НСтр("ru = 'Обработка используется только вместе с объектами информационной базы'"),0,НСтр("ru = ''"));
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	СохранитьНастройкиОбработки(
			ПолноеИмя,
			Объект.ИзменятьВТранзакции,
			Объект.ПрерыватьПриОшибке,
			ТОНастройкаПорции,
			ТОПроцентОбъектовВПорции,
			ТОЧислоОбъектовВПорции,
			ОбрабатыватьРекурсивно);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьНастройкиОбработки(ПолноеИмя, ИзменятьВТранзакции, ПрерыватьПриОшибке,
			ТОНастройкаПорции, ТОПроцентОбъектовВПорции, ТОЧислоОбъектовВПорции, ОбрабатыватьРекурсивно)
	
	СтруктураНастроек = Новый Структура;
	
	СтруктураНастроек.Вставить("ИзменятьВТранзакции",		ИзменятьВТранзакции);
	СтруктураНастроек.Вставить("ПрерыватьПриОшибке",		ПрерыватьПриОшибке);
	СтруктураНастроек.Вставить("ТОНастройкаПорции",			ТОНастройкаПорции);
	СтруктураНастроек.Вставить("ТОПроцентОбъектовВПорции",	ТОПроцентОбъектовВПорции);
	СтруктураНастроек.Вставить("ТОЧислоОбъектовВПорции",	ТОЧислоОбъектовВПорции);
	СтруктураНастроек.Вставить("ОбрабатыватьРекурсивно",	ОбрабатыватьРекурсивно);
	
	ХранилищеОбщихНастроек.Сохранить("Обработка.ГрупповоеИзменениеОбъектов", ПолноеИмя, СтруктураНастроек, , ОбщегоНазначения.ТекущийПользователь());
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройкиОбработки()
	
	СтруктураНастроек = ХранилищеОбщихНастроек.Загрузить("Обработка.ГрупповоеИзменениеОбъектов", ПолноеИмя, , ОбщегоНазначения.ТекущийПользователь());
	
	Если СтруктураНастроек = Неопределено Тогда
		Объект.ИзменятьВТранзакции = Истина;
		Объект.ПрерыватьПриОшибке = Истина;
		ТОНастройкаПорции = 1;
		ТОПроцентОбъектовВПорции = 100;
		ТОЧислоОбъектовВПорции = 1;
		ОбрабатыватьРекурсивно = Ложь;
	Иначе
		Объект.ИзменятьВТранзакции			= СтруктураНастроек.ИзменятьВТранзакции;
		Объект.ПрерыватьПриОшибке			= СтруктураНастроек.ПрерыватьПриОшибке;
		ТОНастройкаПорции					= СтруктураНастроек.ТОНастройкаПорции;
		ТОПроцентОбъектовВПорции			= СтруктураНастроек.ТОПроцентОбъектовВПорции;
		ТОЧислоОбъектовВПорции				= СтруктураНастроек.ТОЧислоОбъектовВПорции;
		ОбрабатыватьРекурсивно				= СтруктураНастроек.ОбрабатыватьРекурсивно;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗапретРедактированияВстроен = Метаданные.НайтиПоПолномуИмени("ОбщийМодуль.ЗапретРедактированияРеквизитовОбъектовКлиент") <> Неопределено;
	
	НекорректныйВызовОбработки = Ложь;
	
	Если ТипЗнч(Параметры.МассивОбъектов) <> Тип("Массив") Тогда
		НекорректныйВызовОбработки = Истина;
		Возврат;
	КонецЕсли;
	
	// Оптимизация, для того, что бы не переформировывать таблицу
	// изменяемых объектов при простом перелистывании
	ИзменитьТаблицуОбъектов = Ложь;
	
	МетаданныеОбъекта = Параметры.МассивОбъектов[0].Метаданные();
	
	ПолноеИмя = МетаданныеОбъекта.ПолноеИмя();
	
	ТекущийПользователь = Пользователи.АвторизованныйПользователь();
	
	// Подгружаем настройки обработки
	ЗагрузитьНастройкиОбработки();
	
	// Подгружаем историю операций с данным типом объектов
	ЗагрузитьИсториюОпераций();
	
	// Устанавливаем заголовок для таблицы изменяемых объектов
	Элементы.ОбъектыДляИзмененияСсылка.Заголовок = МетаданныеОбъекта.Синоним;
	
	// Объект иерархический
	УчитыватьИерархию = ОбъектМетаданныхИерархический();
	
	// По переданному составу объектов и типу объекта заполнить допустимые действия с объектом
	ЗаполнитьДеревоОбъектовИОпераций();
	
	// Если нет заблокированных реквизитов скрываем кнопку "РазрешитьРедактированиеРеквизитов"
	Если НЕ ЗапретРедактированияВстроен
		ИЛИ Объект.Операции.НайтиСтроки(
			Новый Структура("ЗаблокированныйРеквизит", Истина) ).Количество() = 0 Тогда
		Если Элементы.Найти("РазрешитьРедактированиеРеквизитов") <> Неопределено Тогда
			Элементы.РазрешитьРедактированиеРеквизитов.Видимость = Ложь;
		КонецЕсли;
	Иначе
		Если МетаданныеОбъекта.Формы.Найти("РазблокированиеРеквизитов") = Неопределено Тогда
			ЕстьФормаРазблокированияРеквизитов = Ложь;
		Иначе
			ЕстьФормаРазблокированияРеквизитов = Истина;
			ПолноеИмяФормыРаботыСБлокируемымиРеквизитами = ПолноеИмя + ".Форма.РазблокированиеРеквизитов";
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоОбъектовИОпераций()
	
	МассивЗаполненных = Новый Массив;
	ЗаполнитьМассивИзмененных(МассивЗаполненных, ИзменяемыеОбъекты.ПолучитьЭлементы());
	
	Объект.ОбъектыДляИзменения.Очистить();
	Объект.Операции.Очистить();
	ИзменяемыеОбъекты.ПолучитьЭлементы().Очистить();
	
	ЗаполнитьДеревоОбъектовДляИзменения(МассивЗаполненных);
	
	ЗаполнитьСплошнойСписокОбъектовДляИзменения(ИзменяемыеОбъекты.ПолучитьЭлементы());
	
	ОбъектОбработка = РеквизитФормыВЗначение("Объект");
	ОбъектОбработка.ЗаполнитьТаблицуОперацийСОбъектом();
	ЗначениеВРеквизитФормы(ОбъектОбработка, "Объект");
	
	УстановитьСостояниеЭлементовФормы(Элементы, Объект.ОбъектыДляИзменения.Количество());
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Блок процедур - обработчиков команд

&НаКлиенте
Процедура ИсторияОпераций(Команда)
	
	СтандартнаяОбработка = Ложь;
	
	Если ИсторияОперацийСписок.Количество() = 0 Тогда
		Предупреждение(НСтр("ru = 'Для данного типа объектов нет истории ранее выполненных групповых изменений.'"));
		Возврат;
	КонецЕсли;
	
	СписокЗначений = Новый СписокЗначений;
	
	Для Каждого ОписаниеИзменения Из ИсторияОперацийСписок Цикл
		СписокЗначений.Добавить(ОписаниеИзменения.Значение, ОписаниеИзменения.Представление);
	КонецЦикла;
	
	Выбор = ВыбратьИзМеню(СписокЗначений, Элементы.ИсторияОпераций);
	
	Если Выбор <> Неопределено Тогда
		УстановитьОперацииИзменения(Выбор.Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.ИзменениеОбъектов Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.НастройкаИзменений;
		УстановитьСостояниеЭлементовФормы(Элементы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Далее(Команда)
	
	ПерейтиНаСтраницуИзмененияОбъектов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаСтраницуИзмененияОбъектов()
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.НастройкаИзменений Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.ИзменениеОбъектов;
		УстановитьСостояниеЭлементовФормы(Элементы);
		Если ИзменитьТаблицуОбъектов Тогда
			СформироватьИЗаполнитьДеревоИзменяемыхОбъектов();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОбъектПоСсылкеОбработчикКоманды(Команда)
	
	ОткрытьОбъектПоСсылке();
	
КонецПроцедуры

&НаКлиенте
// Обработчик команды "Изменить"
//
Процедура ИзменитьОбработчикКоманды(Команда)
	
	Если Элементы.ФормаИзменить.Заголовок = НСтр("ru = 'Изменить'") Тогда
	
		Если Объект.Операции.НайтиСтроки(Новый Структура("Изменять", Истина)).Количество() = 0 Тогда
			
			ТекстВопроса = НСтр("ru = 'Внимание, не указано ни одного изменения, продолжить?'");
			Результат = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ОКОтмена, , , "Изменение объектов");
			
			Если Результат <> КодВозвратаДиалога.ОК Тогда
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		
		УстановитьКнопкиНаВремяИзменения(Истина);
		
		ПерейтиНаСтраницуИзмененияОбъектов();
		
		ПодключитьОбработчикОжидания("ИзменитьОбъекты", 0.1, Истина);
		
	ИначеЕсли ТекущееСостояниеИзменения <> Неопределено Тогда
		
		ТекущееСостояниеИзменения.ПрерватьИзменение = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьКнопкиНаВремяИзменения(НачалоИзменения)
	
	Элементы.ФормаНазад.Доступность = НЕ НачалоИзменения;
	Элементы.ФормаОтмена.Доступность = НЕ НачалоИзменения;
	
	Если НачалоИзменения Тогда
		Если Объект.ИзменятьВТранзакции Тогда
			Элементы.ФормаИзменить.Доступность = Ложь;
		Иначе
			Элементы.ФормаИзменить.Заголовок = НСтр("ru = 'Прервать'");
		КонецЕсли;
	Иначе
		Элементы.ФормаИзменить.Доступность = Истина;
		Элементы.ФормаИзменить.Заголовок = НСтр("ru = 'Изменить'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПодготовитьДеревоДляИзменения(КоллекцияЭлементов, ТекущиеИзменяемыеОбъекты)
	
	КоличествоИзменяемых = 0;
	
	Для Каждого ИзменяемыйЭлемент Из КоллекцияЭлементов Цикл
		ИзменяемыйЭлемент.КодОшибки = 0;
		Если ИзменяемыйЭлемент.Изменять Тогда
			КоличествоИзменяемых = КоличествоИзменяемых + 1;
			ТекущиеИзменяемыеОбъекты.Добавить(ИзменяемыйЭлемент.Ссылка, ИзменяемыйЭлемент.ПолучитьИдентификатор());
		КонецЕсли;
		КоличествоИзменяемых = КоличествоИзменяемых + ПодготовитьДеревоДляИзменения(ИзменяемыйЭлемент.ПолучитьЭлементы(), ТекущиеИзменяемыеОбъекты);
	КонецЦикла;
	
	Возврат КоличествоИзменяемых;
	
КонецФункции

&НаКлиенте
// Основной цикл изменения объектов
//
Процедура ИзменитьОбъекты()
	
	ОчиститьСообщения();
	
	ТекущееСостояниеИзменения = Новый Структура;
	
	ТекущиеИзменяемыеОбъекты = Новый СписокЗначений;
	
	КоличествоОбъектовДляОбработки = ПодготовитьДеревоДляИзменения(ИзменяемыеОбъекты.ПолучитьЭлементы(), ТекущиеИзменяемыеОбъекты);
	
	Если КоличествоОбъектовДляОбработки = 0 Тогда
		УстановитьКнопкиНаВремяИзменения(Ложь);
		Предупреждение(НСтр("ru = 'Не указаны объекты для изменения'"));
		Возврат;
	КонецЕсли;
	
	ТекущееСостояниеИзменения.Вставить("ТекущиеИзменяемыеОбъекты", ТекущиеИзменяемыеОбъекты);
	
	Если Объект.ИзменятьВТранзакции Тогда
		
		Если ТОНастройкаПорции = 1 Тогда // обработка одним вызовом
			
			ПоказатьОповещениеПользователя(НСтр("ru = 'Групповое измнение объектов'"), ,НСтр("ru = 'Пожалуйста ждите, обработка может занять некоторое время...'"));
			ПоказыватьПроцентОбработанных = Ложь;
			
			РазмерПорции = КоличествоОбъектовДляОбработки;
			
		Иначе
			
			ПоказыватьПроцентОбработанных = Истина;
			
			Если ТОНастройкаПорции = 2 Тогда // порциями по числу объектов
				РазмерПорции = ?(ТОЧислоОбъектовВПорции < КоличествоОбъектовДляОбработки, 
									ТОЧислоОбъектовВПорции, КоличествоОбъектовДляОбработки);
			Иначе // порциями по проценту объектов
				РазмерПорции = Окр(КоличествоОбъектовДляОбработки * ТОПроцентОбъектовВПорции / 100);
				Если РазмерПорции = 0 Тогда
					РазмерПорции = 1;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
	Иначе
		
		Если КоличествоОбъектовДляОбработки >= НетранзакционнаяПорцияГраницаПерехода() Тогда
			// Число объектов - постоянная величина
			РазмерПорции = НетранзакционнаяПорцияПолученияДанныхОбъектов();
		Иначе
			// Число объектов - переменная величина, процент от общего числа
			РазмерПорции = Окр(КоличествоОбъектовДляОбработки * НетранзакционнаяПорцияПолученияДанныхПроцент() / 100);
			Если РазмерПорции = 0 Тогда
				РазмерПорции = 1;
			КонецЕсли;
		КонецЕсли;
		
		Состояние(НСтр("ru = 'Обрабатываются объекты...'"), 0, НСтр("ru = 'Групповое измнение объектов'"));
		
		ПоказыватьПроцентОбработанных = Истина;
	КонецЕсли;
	
	ТекущееСостояниеИзменения.Вставить("ЕстьЭлементыДляОбработки", Истина);
	ТекущееСостояниеИзменения.Вставить("ТекущаяПозиция", 0); 			// позиция последнего обработанного элемента. 1 - первый элемент.
	ТекущееСостояниеИзменения.Вставить("КоличествоОшибок", 0);			// инициализируем счетчик ошибок
	ТекущееСостояниеИзменения.Вставить("КоличествоИзмененных", 0);		// инициализируем счетчик измененных
	ТекущееСостояниеИзменения.Вставить("ОстанавливатьИзменениеПриОшибке", Объект.ПрерыватьПриОшибке);
	ТекущееСостояниеИзменения.Вставить("КоличествоОбъектовДляОбработки", КоличествоОбъектовДляОбработки);
	ТекущееСостояниеИзменения.Вставить("РазмерПорции", РазмерПорции);
	ТекущееСостояниеИзменения.Вставить("ПоказыватьПроцентОбработанных", ПоказыватьПроцентОбработанных);
	ТекущееСостояниеИзменения.Вставить("ПрерватьИзменение", Ложь);
	
	ПодключитьОбработчикОжидания("ИзменитьПорциюОбъектов", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
// Используется для изменения порции объектов
Процедура ИзменитьПорциюОбъектов()
	
	Перем КоличествоОшибок, КоличествоИзмененных;
	
	НачальнаяПозиция = ТекущееСостояниеИзменения.ТекущаяПозиция;
	ОконечнаяПозиция = ТекущееСостояниеИзменения.ТекущаяПозиция+ТекущееСостояниеИзменения.РазмерПорции;
	
	Пока Истина Цикл
		ПорцияМассив = ПолучитьПорциюОбрабатываемыхОбъектов(НачальнаяПозиция+1, ОконечнаяПозиция);
		
		// Изменяем порцию на сервере
		РезультатИзменения = ИзменитьНаСервере(ПорцияМассив, ТекущееСостояниеИзменения.ОстанавливатьИзменениеПриОшибке);
		
		// Переносим информацию по обработанным объектам в таблицу
		ЗаполнитьСостояниеОбработанных(РезультатИзменения, КоличествоОшибок, КоличествоИзмененных);
		
		ТекущееСостояниеИзменения.КоличествоОшибок = КоличествоОшибок + ТекущееСостояниеИзменения.КоличествоОшибок;
		ТекущееСостояниеИзменения.КоличествоИзмененных = КоличествоИзмененных + ТекущееСостояниеИзменения.КоличествоИзмененных;
		
		Если НЕ (ТекущееСостояниеИзменения.ОстанавливатьИзменениеПриОшибке И РезультатИзменения.ЕстьОшибки) Тогда
			Прервать;
		КонецЕсли;
		
		// При наличии ошибок в транзакции - откатываем всю транзакцию
		Если Объект.ИзменятьВТранзакции Тогда
			ТекстПредупреждения = НСтр("ru = 'При изменении объектов обнаружены ошибки - изменения отменены.'");
			ПодключитьОбработчикОжидания("ЗавершитьИзменениеОбъектов", 0.1, Истина);
			Возврат; // досрочный выход из цикла и процедуры
		КонецЕсли;
		
		ТекстВопроса = НСтр("ru = 'Внимание, при изменении объекта (группы объектов) обнаружены ошибки.
									|Прервать изменение объектов и перейти к просмотру ошибок?
									|'");
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Прервать, НСтр("ru = 'Прервать'"));
		Кнопки.Добавить(КодВозвратаДиалога.Пропустить, НСтр("ru = 'Продолжить'"));
		Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Больше не спрашивать'"));
		
		Результат = Вопрос(ТекстВопроса, Кнопки, , КодВозвратаДиалога.Прервать, НСтр("ru = 'Ошибки при изменении объектов'"));
		
		Если Результат = Неопределено Или Результат = КодВозвратаДиалога.Прервать Тогда
			ПодключитьОбработчикОжидания("ЗавершитьИзменениеОбъектов", 0.1, Истина);
			Возврат; // досрочный выход из цикла и процедуры
		КонецЕсли;
			
		Если Результат = КодВозвратаДиалога.Пропустить ИЛИ Результат = КодВозвратаДиалога.Нет Тогда
			// переходим к дообработке объектов порции
			
			Если Результат = КодВозвратаДиалога.Нет Тогда
				// более в этом цикле изменения не останавливаться по ошибке
				ТекущееСостояниеИзменения.ОстанавливатьИзменениеПриОшибке = Ложь;
			КонецЕсли;
			
			Если ПорцияМассив.Количество() <= 1 Тогда
				Прервать;
			КонецЕсли;
			
			Для Каждого СостояниеОбработанногоОбъекта Из РезультатИзменения.СостояниеОбработки Цикл
				НачальнаяПозиция = НачальнаяПозиция + 1;
				Если СостояниеОбработанногоОбъекта.Значение.КодОшибки > 1 Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Продолжить;
			
		КонецЕсли;
	КонецЦикла;
	
	Элементы.ОбъектыДляИзменения.ТекущаяСтрока = ТекущееСостояниеИзменения.ТекущаяПозиция;
	
	ТекущееСостояниеИзменения.ТекущаяПозиция = ТекущееСостояниеИзменения.ТекущаяПозиция + ТекущееСостояниеИзменения.РазмерПорции;
	
	Если ТекущееСостояниеИзменения.ПоказыватьПроцентОбработанных Тогда
		// вычисляем текущий процент обработанных объектов
		ТекущийПроцент = ТекущееСостояниеИзменения.ТекущаяПозиция / ТекущееСостояниеИзменения.КоличествоОбъектовДляОбработки * 100;
		Состояние(НСтр("ru = 'Обрабатываются объекты...'"), ТекущийПроцент, НСтр("ru = 'Групповое измнение объектов'"));
	КонецЕсли;
	
	ЕстьЭлементыДляОбработки = ?(ТекущееСостояниеИзменения.ТекущаяПозиция < ТекущееСостояниеИзменения.КоличествоОбъектовДляОбработки, Истина, Ложь);
	
	Если ЕстьЭлементыДляОбработки И НЕ ТекущееСостояниеИзменения.ПрерватьИзменение Тогда
		ПодключитьОбработчикОжидания("ИзменитьПорциюОбъектов", 0.1, Истина);
	Иначе
		ПодключитьОбработчикОжидания("ЗавершитьИзменениеОбъектов", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Используется для изменения порции объектов
Процедура ЗавершитьИзменениеОбъектов()
	
	Если Объект.ИзменятьВТранзакции
	   И ТОНастройкаПорции = 1 Тогда // обработка одним вызовом
		ПоказатьОповещениеПользователя(НСтр("ru = 'Групповое измнение объектов'"), , НСтр("ru = 'Обработка объектов завершена...'"));
	КонецЕсли;
	
	ЗавершающиеДействияПриИзмененииСервер();
	
	УстановитьКнопкиНаВремяИзменения(Ложь);
	
	ОповеститьОбИзменении(ТипЗнч(Параметры.МассивОбъектов[0]));
	
	ТекущееСостояниеИзменения = Неопределено;
	
КонецПроцедуры

&НаСервере
// Завершающие действия при изменении на сервере.
//  - меняется текущая страница с результатом изменения
//  - в историю изменений заносится текущее изменение
//
Процедура ЗавершающиеДействияПриИзмененииСервер()
	
	Если ТекущееСостояниеИзменения.КоличествоОшибок > 0 Тогда
		
		Элементы.СтраницыПодсказокИзмененияОбъектов.ТекущаяСтраница =
			Элементы.СтраницаПодсказкаПослеИзмененияЕстьОшибки;
		
		Если Объект.ИзменятьВТранзакции Тогда
			КоличествоИзмененных = 0;
		Иначе
			КоличествоИзмененных = Строка(ТекущееСостояниеИзменения.КоличествоИзмененных);
		КонецЕсли;
		
		Элементы.ПодсказкаПослеИзмененияЕстьОшибки.Заголовок = 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось изменить объекты. Измененно объектов %1 из %2.'"),
				КоличествоИзмененных,
				Строка(ТекущееСостояниеИзменения.КоличествоОбъектовДляОбработки));
		
	Иначе
		Элементы.СтраницыПодсказокИзмененияОбъектов.ТекущаяСтраница =
			Элементы.СтраницаПодсказкаУспешноеИзменение;
		Элементы.ПодсказкаУспешноеИзменение.Заголовок = 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Изменение объектов успешно завершено.
							|Изменено объектов: %1.'"),
				Строка(ТекущееСостояниеИзменения.КоличествоИзмененных));
		
	КонецЕсли;
	
	КоллекцияОпраций = Объект.Операции.НайтиСтроки(Новый Структура("Изменять", Истина));
	
	ОписаниеИзменения = Новый Массив;
	ШаблонПредставления = "[Поле]=<Значение>";
	ПредставлениеИзменения = "";
	
	Для Каждого ОписаниеОперации Из КоллекцияОпраций Цикл
		
		СтруктураИзменения = Новый Структура;
		СтруктураИзменения.Вставить("ВидОперации",	ОписаниеОперации.ВидОперации);
		СтруктураИзменения.Вставить("ИмяРеквизита",	ОписаниеОперации.ИмяРеквизита);
		СтруктураИзменения.Вставить("Свойство",		ОписаниеОперации.Свойство);
		СтруктураИзменения.Вставить("Значение",		ОписаниеОперации.Значение);
		
		ОписаниеИзменения.Добавить(СтруктураИзменения);
		ТекущаяОперация = СтрЗаменить(ШаблонПредставления, "[Поле]", СокрЛП(Строка(ОписаниеОперации.Представление)));
		ТекущаяОперация = СтрЗаменить(ТекущаяОперация, "<Значение>", СокрЛП(Строка(ОписаниеОперации.Значение)));
		ПредставлениеИзменения = ПредставлениеИзменения + ТекущаяОперация +"; ";
	КонецЦикла;
	
	ПредставлениеИзменения = Лев(ПредставлениеИзменения, СтрДлина(ПредставлениеИзменения) - 2);
	
	Если ЗначениеЗаполнено(ПредставлениеИзменения) Тогда
		ДобавитьИзменениеВИсторию(ОписаниеИзменения, ПредставлениеИзменения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Параметры СостояниеОбработки - соответствие,
// ключ - ссылка на объект, значение - структура с ключами КодОшибки,СообщениеОбОшибке
//				КодОшибки - 0 -ошибок нет
//				СообщениеОбОшибке - текстовое представление ошибки
//
Процедура ЗаполнитьСостояниеОбработанных(РезультатИзменения, КоличествоОшибок, КоличествоИзмененных)
	
	КоличествоОшибок = 0;
	КоличествоИзмененных = 0;
	
	Для Каждого СостояниеОбработанногоОбъекта Из РезультатИзменения.СостояниеОбработки Цикл
		
		НомерСтроки = -1;
		
		Идентификатор = Число(ТекущееСостояниеИзменения.ТекущиеИзменяемыеОбъекты.НайтиПоЗначению(СостояниеОбработанногоОбъекта.Ключ).Представление);
		
		ЭлементДерева = ИзменяемыеОбъекты.НайтиПоИдентификатору(Идентификатор);
		
		ЭлементДерева.КодОшибки = СостояниеОбработанногоОбъекта.Значение.КодОшибки;
		
		Если СостояниеОбработанногоОбъекта.Значение.КодОшибки > 1 Тогда
			КоличествоОшибок = КоличествоОшибок + 1;
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					СостояниеОбработанногоОбъекта.Значение.СообщениеОбОшибке
					+ "(" + Строка(СостояниеОбработанногоОбъекта.Ключ) + ")", ,
					"ИзменяемыеОбъекты["+Строка(Идентификатор)+"].Ссылка");
		Иначе
			КоличествоИзмененных = КоличествоИзмененных + 1;
			
			Если НЕ (Объект.ИзменятьВТранзакции И РезультатИзменения.ЕстьОшибки) Тогда
				Для Каждого ИзмененныйРеквизит Из СостояниеОбработанногоОбъекта.Значение.ЗначенияИзмененныхРеквизитов Цикл
					ИмяСвязанногоРеквизита = ПрефиксИмениРеквизита() + ИзмененныйРеквизит.Ключ;
					Если ЭлементДерева.Свойство(ИмяСвязанногоРеквизита) Тогда
						ЭлементДерева[ИмяСвязанногоРеквизита] = ИзмененныйРеквизит.Значение;
					КонецЕсли;
				КонецЦикла;
				Для Каждого ИзмененныйРеквизит Из СостояниеОбработанногоОбъекта.Значение.ЗначенияИзмененныхДопРеквизитов Цикл
					Если ЭлементДерева.Свойство(ИзмененныйРеквизит.Ключ) Тогда
						ЭлементДерева[ИзмененныйРеквизит.Ключ] = ИзмененныйРеквизит.Значение;
					КонецЕсли;
				КонецЦикла;
				Для Каждого ИзмененныйРеквизит Из СостояниеОбработанногоОбъекта.Значение.ЗначенияИзмененныхДопСведений Цикл
					Если ЭлементДерева.Свойство(ИзмененныйРеквизит.Ключ) Тогда
						ЭлементДерева[ИзмененныйРеквизит.Ключ] = ИзмененныйРеквизит.Значение;
					КонецЕсли;
				КонецЦикла;
				
				ЭлементДерева.Изменять = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
// Получает множество ссылок на обрабатываемые объекты в интервале
//
// Параметры
//  НижняяГраница  - индекс элемента. 1 - первый элемент
//  ВерхняяГраница - индекс элемента, 1 - первый элемент
//
Функция ПолучитьПорциюОбрабатываемыхОбъектов(НижняяГраница, ВерхняяГраница)
	
	Порция = Новый Массив;
	
	Для Индекс = НижняяГраница По ВерхняяГраница Цикл
		Если Индекс <= ТекущееСостояниеИзменения.КоличествоОбъектовДляОбработки Тогда
			Порция.Добавить(
				ИзменяемыеОбъекты.НайтиПоИдентификатору(
					Число(ТекущееСостояниеИзменения.ТекущиеИзменяемыеОбъекты.Получить(Индекс-1).Представление)).Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Порция;
	
КонецФункции

&НаСервере
Функция ИзменитьНаСервере(ОбъектыДляОбработки, знач ОстанавливатьИзменениеПриОшибке)
	
	Результат = РеквизитФормыВЗначение("Объект").ВыполнитьИзменениеОбъектов(ОбъектыДляОбработки, ОстанавливатьИзменениеПриОшибке);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура НастроитьПараметрыИзменения(Команда)
	
	Настройки = Новый Структура;
	
	Настройки.Вставить("ИзменятьВТранзакции",		Объект.ИзменятьВТранзакции);
	Настройки.Вставить("ОбрабатыватьРекурсивно",	ОбрабатыватьРекурсивно);
	Настройки.Вставить("ПрерыватьПриОшибке",		Объект.ПрерыватьПриОшибке);
	Настройки.Вставить("НастройкаПорции",			ТОНастройкаПорции);
	Настройки.Вставить("ПроцентОбъектовВПорции",	ТОПроцентОбъектовВПорции);
	Настройки.Вставить("ЧислоОбъектовВПорции",		ТОЧислоОбъектовВПорции);
	Настройки.Вставить("УчитыватьИерархию",			УчитыватьИерархию);
	
	Результат = ОткрытьФормуМодально("Обработка.ГрупповоеИзменениеОбъектов.Форма.Настройка", Настройки);
	
	ПереформироватьСоставОперацийИДеревоОбъектов = Ложь;
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Если УчитыватьИерархию И ОбрабатыватьРекурсивно <> Результат.ОбрабатыватьРекурсивно Тогда
			ОбрабатыватьРекурсивно	= Результат.ОбрабатыватьРекурсивно;
			ПереформироватьСоставОперацийИДеревоОбъектов = Истина;
		КонецЕсли;
		Объект.ИзменятьВТранзакции		= Результат.ИзменятьВТранзакции;
		Объект.ПрерыватьПриОшибке		= Результат.ПрерыватьПриОшибке;
		ТОНастройкаПорции				= Результат.НастройкаПорции;
		ТОПроцентОбъектовВПорции		= Результат.ПроцентОбъектовВПорции;
		ТОЧислоОбъектовВПорции			= Результат.ЧислоОбъектовВПорции;
	КонецЕсли;
	
	Если ПереформироватьСоставОперацийИДеревоОбъектов Тогда
		ЗаполнитьДеревоОбъектовИОпераций();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВЖурналРегистрации(Команда)
	
	ПараметрыЖР = Новый Структура("Пользователь", ТекущийПользователь);
	
	ОткрытьФормуМодально("Обработка.ЖурналРегистрации.Форма", ПараметрыЖР, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьРедактированиеРеквизитовОбработчикКоманды(Команда)
	
	РазрешитьРедактированиеРеквизитов();
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьРедактированиеРеквизитов()
	
	ЗаблокированныеРеквизитыСтроки = Объект.Операции.НайтиСтроки(Новый Структура("ЗаблокированныйРеквизит", Истина));
	
	Если ЕстьФормаРазблокированияРеквизитов Тогда
		
		РазрешенныеРеквизиты = ОткрытьФормуМодально(ПолноеИмяФормыРаботыСБлокируемымиРеквизитами);
		
		Если ТипЗнч(РазрешенныеРеквизиты) = Тип("Массив")
		   И РазрешенныеРеквизиты.Количество() > 0 Тогда
			
			Для Каждого ОписаниеОперацииСтрока Из ЗаблокированныеРеквизитыСтроки Цикл
				Если ОписаниеОперацииСтрока.ЗаблокированныйРеквизит
				   И РазрешенныеРеквизиты.Найти(ОписаниеОперацииСтрока.ИмяРеквизита) <> Неопределено Тогда
					ОписаниеОперацииСтрока.ЗаблокированныйРеквизит = Ложь;
				КонецЕсли;
			КонецЦикла;
			
			Если Объект.Операции.НайтиСтроки(Новый Структура("ЗаблокированныйРеквизит", Истина)).Количество() = 0 Тогда
				Если Элементы.Найти("РазрешитьРедактированиеРеквизитов") <> Неопределено Тогда
					Элементы.РазрешитьРедактированиеРеквизитов.Доступность = Ложь;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
	Иначе
		
		МассивСсылок = Новый Массив;
		
		ЗаполнитьМассивИзменяемыхОбъектов(ИзменяемыеОбъекты.ПолучитьЭлементы(), МассивСсылок);
		
		СинонимыРеквизитов = Новый Массив;
		
		Для Каждого ОписаниеОперацииСтрока Из ЗаблокированныеРеквизитыСтроки Цикл
			СинонимыРеквизитов.Добавить(ОписаниеОперацииСтрока.Представление);
		КонецЦикла;
		
		ЗапретРедактированияРеквизитовОбъектовКлиентМодуль = Вычислить("ЗапретРедактированияРеквизитовОбъектовКлиент");
		
		Если ЗапретРедактированияРеквизитовОбъектовКлиентМодуль.ПроверитьСсылкиНаОбъект(МассивСсылок, СинонимыРеквизитов) Тогда
			Если Элементы.Найти("РазрешитьРедактированиеРеквизитов") <> Неопределено Тогда
				Элементы.РазрешитьРедактированиеРеквизитов.Доступность = Ложь;
			КонецЕсли;
			
			Для Каждого ОписаниеОперацииСтрока Из ЗаблокированныеРеквизитыСтроки Цикл
				ОписаниеОперацииСтрока.ЗаблокированныйРеквизит = Ложь;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьМассивИзменяемыхОбъектов(КоллекцияСтрок, МассивСсылок)
	
	Для Каждого СтрокаИзменяемыйОбъект Из КоллекцияСтрок Цикл
		МассивСсылок.Добавить(СтрокаИзменяемыйОбъект.Ссылка);
		ЗаполнитьМассивИзменяемыхОбъектов(СтрокаИзменяемыйОбъект.ПолучитьЭлементы(), МассивСсылок)
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОбъектПоСсылке()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ТолькоПросмотр", Истина);
	СтруктураПараметров.Вставить("Ключ", Элементы.ОбъектыДляИзменения.ТекущиеДанные.Ссылка);
	
	Если Элементы.ОбъектыДляИзменения.ТекущиеДанные.ЭтоГруппа Тогда
		ОткрытьФорму(ПолноеИмя + ".ФормаГруппы", СтруктураПараметров);
	Иначе
		ОткрытьФорму(ПолноеИмя + ".ФормаОбъекта", СтруктураПараметров);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПометкаИзмененияПометитьВсе(Команда)
	
	УстановитьЗначениеПометкиИзменения(ИзменяемыеОбъекты.ПолучитьЭлементы(), Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПометкаИзмененияСнятьВсе(Команда)
	
	УстановитьЗначениеПометкиИзменения(ИзменяемыеОбъекты.ПолучитьЭлементы(), Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначениеПометкиИзменения(КоллекцияЭлементов, Значение)
	
	Для Каждого ЭлементКоллекции Из КоллекцияЭлементов Цикл
		ЭлементКоллекции.Изменять = Значение;
		УстановитьЗначениеПометкиИзменения(ЭлементКоллекции.ПолучитьЭлементы(), Значение);
	КонецЦикла;
	
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////
// Служебные функции

&НаКлиентеНаСервереБезКонтекста
Функция ПроверитьНеизменяемыйРеквизит(ИмяРеквизита)
	
	Если ИмяРеквизита = "Ссылка"
	 ИЛИ ИмяРеквизита = "Изменять"
	 ИЛИ ИмяРеквизита = "КодОшибки"
	 ИЛИ ИмяРеквизита = "КодКартинки"
	 ИЛИ ИмяРеквизита = "ЭтоГруппа"
	Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ОбъектМетаданныхИерархический()
	
	СсылкаПервого = Параметры.МассивОбъектов[0];
	ВидОбъектаПоСсылке = ОбщегоНазначения.ВидОбъектаПоСсылке(СсылкаПервого);
	
	Если ((ВидОбъектаПоСсылке = "Справочник" ИЛИ ВидОбъектаПоСсылке = "ПланВидовХарактеристик") И СсылкаПервого.Метаданные().Иерархический)
	 ИЛИ (ВидОбъектаПоСсылке = "ПланСчетов") Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмеетПрефикс(ИмяРеквизита, Префикс)
	
	Возврат Лев(ИмяРеквизита, СтрДлина(Префикс)) = Префикс;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПрефиксИмениРеквизита()
	Возврат "Реквизит_";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПрефиксИмениДопРеквизита()
	Возврат "ДопРеквизит_";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПрефиксИмениДопСведения()
	Возврат "ДопСведение_";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПрефиксИмениПоляФормы()
	Возврат "Элемент_";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НетранзакционнаяПорцияГраницаПерехода()
	
	Возврат 100; // если в списке изменяемых более 100 объектов
				 // изменение происходит для постоянного количества объектов
				 // см . НетранзакционнаяПорцияПолученияДанныхОбъектов()
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НетранзакционнаяПорцияПолученияДанныхПроцент()
	
	Возврат 10;	// если в списке изменяемых менее 100 объектов
				// изменение происходит порциями по проценту объектов от общей массы
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НетранзакционнаяПорцияПолученияДанныхОбъектов()
	
	Возврат 10;	// если в списке изменяемых более 100 объектов
				// изменение происходит порциями по постоянному
				// числу объектов
	
КонецФункции
