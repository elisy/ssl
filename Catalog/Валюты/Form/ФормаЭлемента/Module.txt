////////////////////////////////////////////////////////////////////////////////
// СЕКЦИЯ ОБРАБОТЧИКОВ СОБЫТИЙ
//

// Обработчик события "при создании на сервере" формы
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("КодВалюты") Тогда
		Объект.Код = Параметры.КодВалюты;
	КонецЕсли;
	
	Если Параметры.Свойство("НаименованиеКраткое") Тогда
		Объект.Наименование = Параметры.НаименованиеКраткое;
	КонецЕсли;
	
	Если Параметры.Свойство("НаименованиеПолное") Тогда
		Объект.НаименованиеПолное = Параметры.НаименованиеПолное;
	КонецЕсли;
	
	Если Параметры.Свойство("Загружается") Тогда
		Объект.ВалютаЗагружается = Параметры.Загружается;
	КонецЕсли;
	
	Если Объект.ВалютаЗагружается Тогда
		Объект.ПодчиненныйКурсОт = Справочники.Валюты.ПустаяСсылка();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ПодчиненныйКурсОт) Тогда
		ЗависимыйКурсВалют = Истина;
	КонецЕсли;
	
	ПрочитатьПараметрыПрописи();
	
КонецПроцедуры

// Обработчик события "ПриОткрытии" формы
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьСвойстваЭлементовГруппыЗависимойВалюты();
	УстановитьСклоненияПараметровПрописи();
	
КонецПроцедуры

// Обработчик события "ПриИзменении" элемента формы ВалютаЗагружаетсяССайтаРБК
//
&НаКлиенте
Процедура ВалютаЗагружаетсяССайтаРБКПриИзменении(Элемент)
	
	Если Объект.ВалютаЗагружается Тогда
		ЗависимыйКурсВалют = Ложь;
		Объект.ПодчиненныйКурсОт = NULL;
		Объект.КоэффициентПодчиненногоКурса = 0;
	КонецЕсли;
	
	УстановитьСвойстваЭлементовГруппыЗависимойВалюты();
	
КонецПроцедуры

// Обработчик события "ПриИзменении" элемента формы ВалютаЗагружаетсяССайтаРБК
//
&НаКлиенте
Процедура ЗависимыйКурсВалютПриИзменении(Элемент)
	
	УстановитьСвойстваЭлементовГруппыЗависимойВалюты();
	
КонецПроцедуры

// Обработчик события "НачалоВыбора" элемента формы ПодчиненныйКурсОт
//
&НаКлиенте
Процедура ПодчиненныйКурсОтНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПодготовитьДанныеВыбораПодчиненнойВалюты(ДанныеВыбора, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ЗависимыйКурсВалют Тогда
		Если Не ЗначениеЗаполнено(Объект.ПодчиненныйКурсОт) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Необходимо указать основную валюту'"), ,
				"Объект.ПодчиненныйКурсОт", ,
				Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Объект.КоэффициентПодчиненногоКурса) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Необходимо установить коэфициент подчинения курса'"), ,
				"Объект.КоэффициентПодчиненногоКурса", ,
				Отказ);
		КонецЕсли;
			
	КонецЕсли;
	
	// Устанавливаем реквизиты загрузки и зависимости валюты
	Если НЕ ЗависимыйКурсВалют Тогда
		Объект.ПодчиненныйКурсОт = NULL;
		Объект.КоэффициентПодчиненногоКурса = 0;
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события ПередЗаписьюНаСервере
//
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.ПараметрыПрописиНаРусском = ПолеПрописи1наРусском + ", "
											+ ПолеПрописи2наРусском + ", "
											+ ПолеПрописи3наРусском + ", "
											+ НРег(Лев(ПолеПрописи4наРусском, 1)) + ", "
											+ ПолеПрописи5наРусском + ", "
											+ ПолеПрописи6наРусском + ", "
											+ ПолеПрописи7наРусском + ", "
											+ НРег(Лев(ПолеПрописи8наРусском, 1)) + ", "
											+ ДлинаДробнойЧасти;
	
КонецПроцедуры

// Обработчик события "ПриИзменении" элемента формы ПолеПрописи4наРусском
//
&НаКлиенте
Процедура ПолеПрописи4наРусскомПриИзменении(Элемент)
	УстановитьСклоненияПараметровПрописи();
КонецПроцедуры

// Обработчик события "ПриИзменении" элемента формы ПолеПрописи8наРусском
//
&НаКлиенте
Процедура ПолеПрописи8наРусскомПриИзменении(Элемент)
	УстановитьСклоненияПараметровПрописи();
КонецПроцедуры

&НаКлиенте
Процедура ДлинаДробнойЧастиАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	ДанныеВыбора = АвтоПодборПоСпискуВыбора(Элемент, Текст, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДлинаДробнойЧастиОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	ДанныеВыбора = ОкончаниеВводаТекстаПоСпискуВыбора(Элемент, Текст, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи4наРусскомАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	ДанныеВыбора = АвтоПодборПоСпискуВыбора(Элемент, Текст, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи4наРусскомОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	ДанныеВыбора = ОкончаниеВводаТекстаПоСпискуВыбора(Элемент, Текст, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи8наРусскомАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	ДанныеВыбора = АвтоПодборПоСпискуВыбора(Элемент, Текст, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПрописи8наРусскомОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	ДанныеВыбора = ОкончаниеВводаТекстаПоСпискуВыбора(Элемент, Текст, СтандартнаяОбработка);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЕКЦИЯ СЛУЖЕБНЫХ ФУНКЦИЙ И ПРОЦЕДУР
//

// Процедура считывает параметры прописи и заполняет соответствующие поля диалога.
//
&НаСервере
Процедура ПрочитатьПараметрыПрописи()
	
	СтрокаПараметров = СтрЗаменить(Объект.ПараметрыПрописиНаРусском, ",", Символы.ПС);
	
	ПолеПрописи1наРусском = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 1));
	ПолеПрописи2наРусском = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 2));
	ПолеПрописи3наРусском = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 3));
	
	Род = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 4));
	
	Если	  НРег(Род) = "м" Тогда
		ПолеПрописи4наРусском = "Мужской";
	ИначеЕсли НРег(Род) = "ж" Тогда
		ПолеПрописи4наРусском = "Женский";
	ИначеЕсли НРег(Род) = "с" Тогда
		ПолеПрописи4наРусском = "Средний";
	КонецЕсли;
	
	ПолеПрописи5наРусском = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 5));
	ПолеПрописи6наРусском = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 6));
	ПолеПрописи7наРусском = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 7));
	
	Род = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 8));
	
	Если	  НРег(Род = "м") Тогда
		ПолеПрописи8наРусском = "Мужской";
	ИначеЕсли НРег(Род = "ж") Тогда
		ПолеПрописи8наРусском = "Женский";
	ИначеЕсли НРег(Род = "с") Тогда
		ПолеПрописи8наРусском = "Средний";
	КонецЕсли;
	
	ДлинаДробнойЧасти     = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 9));
	
КонецПроцедуры

// Процедура устанавливает видимость элементов управления формы, ответственных
// за установление параметров завимисомти от базовой валюты (ссылка на базовую
// валюту и коэфициент).
//
&НаКлиенте
Процедура УстановитьСвойстваЭлементовГруппыЗависимойВалюты()
	
	Если Объект.ВалютаЗагружается Тогда
		Элементы.ГруппаЗависимыйКурсВалют.Доступность = Ложь;
		ФлагПараметровЗависимогоКурса = Ложь;
	Иначе
		Элементы.ГруппаЗависимыйКурсВалют.Доступность = Истина;
		ФлагПараметровЗависимогоКурса = ? (ЗависимыйКурсВалют, Истина, Ложь);
	КонецЕсли;
	
	Элементы.ПодчиненныйКурсОт.АвтоОтметкаНезаполненного = ФлагПараметровЗависимогоКурса;
	Элементы.КоэффициентПодчиненногоКурса.АвтоОтметкаНезаполненного = ФлагПараметровЗависимогоКурса;
	Элементы.ПодчиненныйКурсОт.Доступность = ФлагПараметровЗависимогоКурса;
	Элементы.КоэффициентПодчиненногоКурса.Доступность = ФлагПараметровЗависимогоКурса;
	Если Не ФлагПараметровЗависимогоКурса Тогда
		Элементы.ПодчиненныйКурсОт.ОтметкаНезаполненного = ФлагПараметровЗависимогоКурса;
		Элементы.КоэффициентПодчиненногоКурса.ОтметкаНезаполненного = ФлагПараметровЗависимогоКурса;
	КонецЕсли
	
КонецПроцедуры

// Процедура устанавливает склонение заголовков элементов формы
// хранящих за параметры прописи.
//
&НаКлиенте
Процедура УстановитьСклоненияПараметровПрописи()
	
	Если	  ПолеПрописи4наРусском = "Женский" Тогда
		Элементы.ПолеПрописи1наРусском.Заголовок = "одна";
		Элементы.ПолеПрописи2наРусском.Заголовок = "две";
	ИначеЕсли ПолеПрописи4наРусском = "Мужской" Тогда
		Элементы.ПолеПрописи1наРусском.Заголовок = "один";
		Элементы.ПолеПрописи2наРусском.Заголовок = "два";
	Иначе
		Элементы.ПолеПрописи1наРусском.Заголовок = "одно";
		Элементы.ПолеПрописи2наРусском.Заголовок = "два";
	КонецЕсли;
	
	Если	  ПолеПрописи8наРусском = "Женский" Тогда
		Элементы.ПолеПрописи5наРусском.Заголовок = "одна";
		Элементы.ПолеПрописи6наРусском.Заголовок = "две";
	ИначеЕсли ПолеПрописи8наРусском = "Мужской" Тогда
		Элементы.ПолеПрописи5наРусском.Заголовок = "один";
		Элементы.ПолеПрописи6наРусском.Заголовок = "два";
	Иначе
		Элементы.ПолеПрописи5наРусском.Заголовок = "одно";
		Элементы.ПолеПрописи6наРусском.Заголовок = "два";
	КонецЕсли;
	
КонецПроцедуры

// Подготавливает список выбора для подчиненной валюты таким образом,
// что бы в список не попала сама подчиненная валюта
//
&НаСервереБезКонтекста
Процедура ПодготовитьДанныеВыбораПодчиненнойВалюты(ДанныеВыбора, Ссылка)
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ Ссылка, НаименованиеПолное
	               |ИЗ
	               |	Справочник.Валюты
	               |ГДЕ
	               |	Ссылка <> &Ссылка
	               |И
	               |	ПодчиненныйКурсОт  = Значение(Справочник.Валюты.ПустаяСсылка)
	               |УПОРЯДОЧИТЬ ПО НаименованиеПолное";
	
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Ссылка, Выборка.НаименованиеПолное);
	КонецЦикла;
	
КонецПроцедуры

// Вспомогательные функции управлением вводом

&НаКлиенте
Функция АвтоПодборПоСпискуВыбора(Элемент, Текст, СтандартнаяОбработка)
	
	Для Каждого ЭлементВыбора Из Элемент.СписокВыбора Цикл
		Если ВРег(Текст) = ВРег(Лев(ЭлементВыбора.Представление, СтрДлина(Текст))) Тогда
			Результат = Новый СписокЗначений;
			Результат.Добавить(ЭлементВыбора.Значение, ЭлементВыбора.Представление);
			СтандартнаяОбработка = Ложь;
			Возврат Результат;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Функция ОкончаниеВводаТекстаПоСпискуВыбора(Элемент, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Для Каждого ЭлементВыбора Из Элемент.СписокВыбора Цикл
		Если ВРег(Текст) = ВРег(ЭлементВыбора.Представление) Тогда
			СтандартнаяОбработка = Истина;
		ИначеЕсли ВРег(Текст) = ВРег(Лев(ЭлементВыбора.Представление, СтрДлина(Текст))) Тогда
			СтандартнаяОбработка = Ложь;
			Результат = Новый СписокЗначений;
			Результат.Добавить(ЭлементВыбора.Значение, ЭлементВыбора.Представление);
			Возврат Результат;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции
