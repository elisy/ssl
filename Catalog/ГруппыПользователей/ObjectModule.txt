
Перем СтарыйРодитель;

// Обработчик ПередЗаписью блокирует недопустимые действия с предопределенной
// группой "Все пользователи".
//
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Ссылка = Справочники.ГруппыПользователей.ВсеПользователи Тогда
		Если НЕ Родитель.Пустая() Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Предопределенная группа ""Все пользователи"" может быть только в корне!'"), , , , Отказ);
			Возврат;
		КонецЕсли;
		Если Состав.Количество() > 0 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Добавление пользователей в группу ""Все пользователи"" не поддерживается.'"), , , , Отказ);
			Возврат;
		КонецЕсли;
	Иначе
		Если Родитель = Справочники.ГруппыПользователей.ВсеПользователи Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Предопределенная группа ""Все пользователи"" не может быть родителем!'"), , , , Отказ);
			Возврат;
		КонецЕсли;
		
		СтарыйРодитель = ?(Ссылка.Пустая(), Неопределено, ОбщегоНазначения.ПолучитьЗначениеРеквизита(Ссылка, "Родитель"));
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Пользователи.ОбновитьСоставГруппПользователей(Ссылка);
		
	Если ЗначениеЗаполнено(СтарыйРодитель) И СтарыйРодитель <> Родитель Тогда
		Пользователи.ОбновитьСоставГруппПользователей(СтарыйРодитель);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Родитель = Справочники.ГруппыПользователей.ВсеПользователи Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Предопределенная группа ""Все пользователи"" не может быть родителем!'"),, "Объект.Родитель",, Отказ);
	КонецЕсли;
	
КонецПроцедуры

