
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ТипЗнч(Параметры.вариант) = Тип("СправочникСсылка.ВариантыОтчетов") и Параметры.вариант.Пустая() тогда
		СписокПользователей = Параметры.Пользователи;
		Видимость           = Параметры.Видимость;
	Иначе
		ТЗ = "ВЫБРАТЬ
		|	ВариантыОтчетовНастройкаВидимости.Пользователь,
		|	ВариантыОтчетовНастройкаВидимости.Видимость,
		|	ВариантыОтчетов.Видимость КАК ВидимостьОбщая
		|ИЗ
		|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОтчетов.НастройкаВидимости КАК ВариантыОтчетовНастройкаВидимости
		|		ПО ВариантыОтчетов.Ссылка = ВариантыОтчетовНастройкаВидимости.Ссылка
		|ГДЕ
		|	ВариантыОтчетов.Ссылка = &вариант";
			 
		Запрос = Новый Запрос(ТЗ);
		Запрос.УстановитьПараметр("вариант", Параметры.вариант);
		
		УстановитьПривилегированныйРежим(истина);
		Выборка = Запрос.Выполнить().Выбрать();
		УстановитьПривилегированныйРежим(ложь);
		
		СписокПользователей = Новый СписокЗначений;
		
		Пока Выборка.Следующий() Цикл
			СписокПользователей.Добавить(Выборка.Пользователь, ,Выборка.Видимость);
			Видимость = Выборка.ВидимостьОбщая;
		КонецЦикла;
		
		Если Видимость = Неопределено тогда
			Видимость = истина;
		КонецЕсли;
		
	КонецЕсли;
	
	ТЗ = "ВЫБРАТЬ
	|	Пользователи.Ссылка КАК Пользователь
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|
	|УПОРЯДОЧИТЬ ПО
	|	Пользователи.Наименование";
		 
	Запрос = Новый Запрос(ТЗ);
	УстановитьПривилегированныйРежим(истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(ложь);
	
	Пока Выборка.Следующий() Цикл
		
 		СтрокаПользователя              = ДоступностьПользователей.Добавить();
		СтрокаПользователя.Пользователь = Выборка.Пользователь;
		ЭлементСписка = СписокПользователей.НайтиПоЗначению(Выборка.Пользователь);
		Если СписокПользователей.НайтиПоЗначению(Выборка.Пользователь) <> Неопределено тогда
			СтрокаПользователя.Видимость = ЭлементСписка.Пометка;
		Иначе
			СтрокаПользователя.Видимость = Видимость;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Принять(Команда)
	
	СписокПользователи = Новый СписокЗначений;
	
	Для каждого СтрокаПользователя из ДоступностьПользователей Цикл
		Если СтрокаПользователя.Видимость <> Видимость тогда
			СписокПользователи.Добавить(СтрокаПользователя.Пользователь, , СтрокаПользователя.Видимость);
		КонецЕсли;
	КонецЦикла;
	
	Структура = Новый Структура("Видимость, Пользователи", Видимость, СписокПользователи);
	Закрыть(Структура);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	
	Для каждого Строкапользователя из ДоступностьПользователей Цикл
		СтрокаПользователя.Видимость = истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметки(Команда)
	
	Для каждого СтрокаПользователя из ДоступностьПользователей Цикл
		СтрокаПользователя.Видимость = ложь;
	КонецЦикла;
	
КонецПроцедуры
