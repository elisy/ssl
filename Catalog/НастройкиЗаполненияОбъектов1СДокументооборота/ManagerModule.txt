
Функция ПолучитьРеквизитыОбъектаДокументооборота(ТипОбъектаДокументооборота, Параметры = Неопределено) Экспорт 
	
	Реквизиты = Новый ТаблицаЗначений;
	Реквизиты.Колонки.Добавить("Имя");
	Реквизиты.Колонки.Добавить("Представление");
	Реквизиты.Колонки.Добавить("Тип");
	Реквизиты.Колонки.Добавить("ДопРеквизит");
	Реквизиты.Колонки.Добавить("ДопРеквизитID");
	Реквизиты.Колонки.Добавить("ДопРеквизитТип");
	
	// общие настройки
	Прокси = РаботаС1СДокументооборотВызовСервера.ПолучитьПрокси();
	Запрос = Прокси.ФабрикаXDTO.Создать(Прокси.ФабрикаXDTO.Тип("http://www.1c.ru/dm", "DMGetNewObjectRequest"));
	Запрос.type = ТипОбъектаДокументооборота;
	Запрос.columnSet.Добавить("additionalProperties");
	Запрос.columnSet.Добавить("documentTypeEnabled");
	Запрос.columnSet.Добавить("accessLevelEnabled");
	Запрос.columnSet.Добавить("activityMatterEnabled");
	Запрос.columnSet.Добавить("organizationEnabled");
	
	Результат = Прокси.execute(Запрос);
	РаботаС1СДокументооборотВызовСервера.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	ОбщиеНастройки = Результат;
	ДопРеквизиты = Результат.additionalProperties;
	
	
	Если Параметры <> Неопределено И Параметры.Свойство("DocumentType") И Параметры.DocumentType <> Неопределено Тогда 
		
		// настройки по виду
		ОбъектИд = Прокси.ФабрикаXDTO.Создать(Прокси.ФабрикаXDTO.Тип("http://www.1c.ru/dm", "DMObjectID"));
		ОбъектИд.id = Параметры.DocumentType.ID;
		ОбъектИд.type = Параметры.DocumentType.Тип;
		
		Запрос = Прокси.ФабрикаXDTO.Создать(Прокси.ФабрикаXDTO.Тип("http://www.1c.ru/dm", "DMRetrieveRequest"));
		Запрос.objectIds.Добавить(ОбъектИд);
		
		Результат = Прокси.execute(Запрос);
		РаботаС1СДокументооборотВызовСервера.ПроверитьВозвратВебСервиса(Прокси, Результат);	
	 	НастройкиВида = Результат.objects[0];
		
		
		// доп реквизиты по виду
		Объект = Прокси.ФабрикаXDTO.Создать(Прокси.ФабрикаXDTO.Тип("http://www.1c.ru/dm", ТипОбъектаДокументооборота));
		Объект.name = "";
		Объект.objectId = Прокси.ФабрикаXDTO.Создать(Прокси.ФабрикаXDTO.Тип("http://www.1c.ru/dm", "DMObjectID"));
		Объект.objectId.id = "";
		Объект.objectId.type = ТипОбъектаДокументооборота;
		
		Объект.documentType = Прокси.ФабрикаXDTO.Создать(Прокси.ФабрикаXDTO.Тип("http://www.1c.ru/dm", ТипОбъектаДокументооборота + "Type"));
		Объект.documentType.name = Параметры.DocumentType.Представление;
		Объект.documentType.objectId = Прокси.ФабрикаXDTO.Создать(Прокси.ФабрикаXDTO.Тип("http://www.1c.ru/dm", "DMObjectID"));
		Объект.documentType.objectId.id = Параметры.DocumentType.ID;
		Объект.documentType.objectId.type = Параметры.DocumentType.Тип;
		
		Запрос = Прокси.ФабрикаXDTO.Создать(Прокси.ФабрикаXDTO.Тип("http://www.1c.ru/dm", "DMGetObjectAdditionalPropertiesRequest"));
		Запрос.object = Объект;
	
		Результат = Прокси.execute(Запрос);
		РаботаС1СДокументооборотВызовСервера.ПроверитьВозвратВебСервиса(Прокси, Результат);
		
		ДопРеквизиты = Результат.additionalProperties;
		
	Иначе	
		НастройкиВида = Неопределено;
	КонецЕсли;	
	
	
	Если ТипОбъектаДокументооборота = "DMIncomingDocument" Тогда 
		
		Если ОбщиеНастройки.documentTypeEnabled = Истина Тогда 
			ДобавитьСтрокуТаблицы(Реквизиты, "documentType", "Вид документа", 		 	"DMIncomingDocumentType");
		КонецЕсли;	
			
		ДобавитьСтрокуТаблицы(Реквизиты, "correspondent", 	"Отправитель", 			 	"DMCorrespondent");
		ДобавитьСтрокуТаблицы(Реквизиты, "externalNumber", 	"№ отправителя", 		 	"Строка");
		ДобавитьСтрокуТаблицы(Реквизиты, "externalDate", 	"Дата отправителя", 	 	"Дата");
		
		Если ОбщиеНастройки.organizationEnabled = Истина Тогда 
			ДобавитьСтрокуТаблицы(Реквизиты, "organization", "Организация", 			 "DMOrganization");
		КонецЕсли;	
		
		Если НастройкиВида = Неопределено Или НастройкиВида.sumEnabled = Истина Тогда 
			ДобавитьСтрокуТаблицы(Реквизиты, "sum", 		"Сумма", 				 	"Число");
			ДобавитьСтрокуТаблицы(Реквизиты, "currency", 	"Валюта", 				 	"DMCurrency");
		КонецЕсли;
		
		ДобавитьСтрокуТаблицы(Реквизиты, "title", 			"Наименование", 		 	"Строка");
		ДобавитьСтрокуТаблицы(Реквизиты, "summary", 		"Содержание", 			 	"Строка");
		ДобавитьСтрокуТаблицы(Реквизиты, "regNumber", 		"Регистрационный номер", 	"Строка");
		ДобавитьСтрокуТаблицы(Реквизиты, "regDate", 		"Дата регистрации", 	 	"Дата");
		
		ДобавитьСтрокуТаблицы(Реквизиты, "signer", 			"Подписал", 			 	"DMContactPerson");
		ДобавитьСтрокуТаблицы(Реквизиты, "addressee", 		"Адресат", 				 	"DMUser");
		ДобавитьСтрокуТаблицы(Реквизиты, "subdivision", 	"Подразделение",		 	"DMSubdivision");
		
		Если НастройкиВида = Неопределено Или НастройкиВида.performanceDateEnabled = Истина Тогда 
			ДобавитьСтрокуТаблицы(Реквизиты, "performanceDate", "Срок исполнения", 		 	"Дата");
		КонецЕсли;	
		
		Если ОбщиеНастройки.accessLevelEnabled  = Истина Тогда 
			ДобавитьСтрокуТаблицы(Реквизиты, "accessLevel", "Гриф доступа",			 	"DMAccessLevel");
		КонецЕсли;
		Если ОбщиеНастройки.activityMatterEnabled = Истина Тогда 
			ДобавитьСтрокуТаблицы(Реквизиты, "activityMatter", 	"Вопрос деятельности", 	 	"DMActivityMatter");
		КонецЕсли;	
		ДобавитьСтрокуТаблицы(Реквизиты, "deliveryMethod",  "Способ получения", 	 	"DMDeliveryMethod");
		
		ДобавитьСтрокуТаблицы(Реквизиты, "comment", 		"Комментарий", 			 	"Строка");
		ДобавитьСтрокуТаблицы(Реквизиты, "responsible", 	"Ответсвенный", 		 	"DMUser");
		
		
	ИначеЕсли ТипОбъектаДокументооборота = "DMOutgoingDocument" Тогда 
		
		Если ОбщиеНастройки.documentTypeEnabled = Истина Тогда
			ДобавитьСтрокуТаблицы(Реквизиты, "documentType", "Вид документа", 		 	"DMOutgoingDocumentType");
		КонецЕсли;	
			
		ДобавитьСтрокуТаблицы(Реквизиты, "correspondent", 	"Получатель", 			 	"DMCorrespondent");
		
		Если НастройкиВида = Неопределено Или НастройкиВида.externalNumberEnabled = Истина Тогда 
			ДобавитьСтрокуТаблицы(Реквизиты, "externalNumber", 	"№ получателя", 		 "Строка");
			ДобавитьСтрокуТаблицы(Реквизиты, "externalDate", 	"Дата получателя", 		 "Дата");
		КонецЕсли;	
		
		Если ОбщиеНастройки.OrganizationEnabled = Истина Тогда
			ДобавитьСтрокуТаблицы(Реквизиты, "organization", "Организация", 			 "DMOrganization");
		КонецЕсли;
		
		Если НастройкиВида = Неопределено Или НастройкиВида.sumEnabled = Истина Тогда 
			ДобавитьСтрокуТаблицы(Реквизиты, "sum", 		"Сумма", 				 	"Число");
			ДобавитьСтрокуТаблицы(Реквизиты, "currency", 	"Валюта", 				 	"DMCurrency");
		КонецЕсли;	
		
		ДобавитьСтрокуТаблицы(Реквизиты, "title", 			"Заголовок", 			 	"Строка");
		ДобавитьСтрокуТаблицы(Реквизиты, "summary", 		"Содержание", 			 	"Строка");
		ДобавитьСтрокуТаблицы(Реквизиты, "regNumber", 		"Регистрационный номер", 	"Строка");
		ДобавитьСтрокуТаблицы(Реквизиты, "regDate", 		"Дата регистрации", 	 	"Дата");
		
		ДобавитьСтрокуТаблицы(Реквизиты, "addressee", 		"Адресат", 				 	"DMContactPerson");
		ДобавитьСтрокуТаблицы(Реквизиты, "sent", 			"Отправлен", 			 	"Булево");
		ДобавитьСтрокуТаблицы(Реквизиты, "sendDate", 		"Дата отправки", 		 	"Дата");
		
		ДобавитьСтрокуТаблицы(Реквизиты, "signer", 			"Подписал", 			 	"DMUser");
		ДобавитьСтрокуТаблицы(Реквизиты, "author", 			"Подготовил", 			 	"DMUser");
		ДобавитьСтрокуТаблицы(Реквизиты, "subdivision", 	"Подразделение", 		 	"DMSubdivision");
		
		Если НастройкиВида = Неопределено Или НастройкиВида.performanceDateEnabled = Истина Тогда 
			ДобавитьСтрокуТаблицы(Реквизиты, "performanceDate", "Срок исполнения", 		 "Дата");
		КонецЕсли;	
		
		Если ОбщиеНастройки.accessLevelEnabled  = Истина Тогда
			ДобавитьСтрокуТаблицы(Реквизиты, "accessLevel", "Гриф доступа", 			"DMAccessLevel");
		КонецЕсли;
		
		Если ОбщиеНастройки.activityMatterEnabled = Истина Тогда
			ДобавитьСтрокуТаблицы(Реквизиты, "activityMatter", 	"Вопрос деятельности", 	"DMActivityMatter");
		КонецЕсли;	
		
		ДобавитьСтрокуТаблицы(Реквизиты, "deliveryMethod", 	"Способ отправки", 			"DMDeliveryMethod");
		ДобавитьСтрокуТаблицы(Реквизиты, "comment", 		"Комментарий", 				"Строка");
		ДобавитьСтрокуТаблицы(Реквизиты, "responsible", 	"Ответственный", 			"DMUser");
		
		
	ИначеЕсли ТипОбъектаДокументооборота = "DMInternalDocument" Тогда 	
		
		Если ОбщиеНастройки.documentTypeEnabled = Истина Тогда
			ДобавитьСтрокуТаблицы(Реквизиты, "documentType", "Вид документа", 			"DMInternalDocumentType");
		КонецЕсли;
		
		ДобавитьСтрокуТаблицы(Реквизиты, "folder", 			"Папка", 					"DMInternalDocumentFolder");
		
		Если ОбщиеНастройки.organizationEnabled = Истина Тогда
			ДобавитьСтрокуТаблицы(Реквизиты, "organization", "Организация", 			"DMOrganization");
		КонецЕсли;	
		
		Если НастройкиВида = Неопределено Или НастройкиВида.sumEnabled = Истина Тогда 
			ДобавитьСтрокуТаблицы(Реквизиты, "sum", 		"Сумма", 					"Число");
			ДобавитьСтрокуТаблицы(Реквизиты, "currency", 	"Валюта", 					"DMCurrency");
		КонецЕсли;	
		
		ДобавитьСтрокуТаблицы(Реквизиты, "title", 			"Заголовок", 				"Строка");
		ДобавитьСтрокуТаблицы(Реквизиты, "summary", 		"Содержание",				"Строка");
		ДобавитьСтрокуТаблицы(Реквизиты, "regNumber", 		"Регистрационный номер", 	"Строка");
		ДобавитьСтрокуТаблицы(Реквизиты, "regDate", 		"Дата регистрации", 		"Дата");
		
		ДобавитьСтрокуТаблицы(Реквизиты, "signer", 			"Утвердил", 				"DMUser");
		ДобавитьСтрокуТаблицы(Реквизиты, "author", 			"Подготовил", 				"DMUser");
		ДобавитьСтрокуТаблицы(Реквизиты, "subdivision", 	"Подразделение", 			"DMSubdivision");
		
		Если НастройкиВида = Неопределено Или НастройкиВида.correspondentEnabled = Истина Тогда 
			ДобавитьСтрокуТаблицы(Реквизиты, "correspondent", 	"Корреспондент", 			"DMCorrespondent");
			ДобавитьСтрокуТаблицы(Реквизиты, "contactPerson", 	"Контактное лицо", 			"DMContactPerson");
		КонецЕсли;	
		
		Если НастройкиВида = Неопределено Или НастройкиВида.durationEnabled = Истина Тогда 
			ДобавитьСтрокуТаблицы(Реквизиты, "beginDate", 			  "Дата начала действия", 	 "Дата");
			ДобавитьСтрокуТаблицы(Реквизиты, "endDate", 			  "Дата окончания действия", "Дата");
			ДобавитьСтрокуТаблицы(Реквизиты, "openEnded", 			  "Бессрочный", 			 "Булево");
			ДобавитьСтрокуТаблицы(Реквизиты, "prolongationProcedure", "Порядок продления", 		 "DMProlongationProcedure");
		КонецЕсли;	
		
		Если ОбщиеНастройки.accessLevelEnabled  = Истина Тогда
			ДобавитьСтрокуТаблицы(Реквизиты, "accessLevel", "Гриф доступа", 			"DMAccessLevel");
		КонецЕсли;	
		
		Если ОбщиеНастройки.activityMatterEnabled = Истина Тогда
			ДобавитьСтрокуТаблицы(Реквизиты, "activityMatter", 	"Вопрос деятельности", "DMActivityMatter");
		КонецЕсли;	
		
		Если НастройкиВида = Неопределено Или НастройкиВида.performanceDateEnabled = Истина Тогда 
			ДобавитьСтрокуТаблицы(Реквизиты, "performanceDate", "Срок исполнения", 			"Дата");
		КонецЕсли;	
		
		ДобавитьСтрокуТаблицы(Реквизиты, "comment", 		"Комментарий", 				"Строка");
		ДобавитьСтрокуТаблицы(Реквизиты, "responsible", 	"Ответственный", 			"DMUser");
		
	КонецЕсли;	
	
	Для Каждого ДопСвойство Из ДопРеквизиты Цикл
		
		Типы = Новый СписокЗначений;
		Для Каждого ОписаниеТипа Из ДопСвойство.propertyValueTypes Цикл
			
			Если ОписаниеТипа.xdtoClassName = "integer" Тогда
				ТипЗначения = "Число";
			ИначеЕсли ОписаниеТипа.xdtoClassName = "boolean" Тогда
				ТипЗначения = "Булево";
			ИначеЕсли ОписаниеТипа.xdtoClassName = "string" Тогда
				ТипЗначения = "Строка";
			ИначеЕсли ОписаниеТипа.xdtoClassName = "date" Тогда
				ТипЗначения = "Дата";
			Иначе	
				ТипЗначения = ОписаниеТипа.xdtoClassName;
			КонецЕсли;
			
			Типы.Добавить(ТипЗначения, ОписаниеТипа.presentation);
		КонецЦикла;
		
		ДобавитьСтрокуТаблицы(Реквизиты, "", ДопСвойство.name, Типы, Истина, ДопСвойство.objectId.id, ДопСвойство.objectID.type);
		
	КонецЦикла;
	
	
	Возврат Реквизиты;
	
КонецФункции

Процедура ДобавитьСтрокуТаблицы(Таблица, Имя, Представление, Тип, 
	ДопРеквизит = Ложь, ДопРеквизитID = "", ДопРеквизитТип = "")
	
	НоваяСтрока = Таблица.Добавить();
	НоваяСтрока.Имя = Имя;
	НоваяСтрока.Представление = Представление;
	
	Если ТипЗнч(Тип) <> Тип("СписокЗначений") Тогда 
		Типы = Новый СписокЗначений;
		Типы.Добавить(Тип);
		НоваяСтрока.Тип = Типы;
	Иначе
		НоваяСтрока.Тип = Тип;
	КонецЕсли;	
	
	НоваяСтрока.ДопРеквизит = ДопРеквизит;
	НоваяСтрока.ДопРеквизитID = ДопРеквизитID;
	НоваяСтрока.ДопРеквизитТип = ДопРеквизитТип;	
	
КонецПроцедуры	
