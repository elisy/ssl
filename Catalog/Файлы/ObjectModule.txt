


// Обработка события ПередЗаписью
//
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("КонвертацияФайлов") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВладелецФайла) Тогда
		ВызватьИсключение НСтр("ru = 'Нельзя записать файл, если не указан владелец файла.'");
	КонецЕсли;
	
	Если Не ЭтоНовый() Тогда
		
		УстановленаПометкаУдаления = ПометкаУдаления И Не ПометкаУдаленияВИБ();
		
		ЗаписьПодписанногоОбъекта = Ложь;
		Если ДополнительныеСвойства.Свойство("ЗаписьПодписанногоОбъекта") Тогда
			ЗаписьПодписанногоОбъекта = ДополнительныеСвойства.ЗаписьПодписанногоОбъекта;
		КонецЕсли;	
		
		// разрешаем ставить пометку удаления на подписанный файл
		Если НЕ ПривилегированныйРежим() И ЗаписьПодписанногоОбъекта <> Истина И НЕ УстановленаПометкаУдаления Тогда
			
			Если ЗначениеЗаполнено(Ссылка) Тогда
				
				СтруктураРеквизитов = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(Ссылка, "ПодписанЭЦП, Зашифрован");
				СсылкаПодписан = СтруктураРеквизитов.ПодписанЭЦП;
				СсылкаЗашифрован = СтруктураРеквизитов.Зашифрован;
				
				Если ПодписанЭЦП И СсылкаПодписан Тогда
					ВызватьИсключение НСтр("ru = 'Подписанный файл нельзя редактировать.'");
				КонецЕсли;	
				
				Если Зашифрован И СсылкаЗашифрован И ПодписанЭЦП И НЕ СсылкаПодписан Тогда
					ВызватьИсключение НСтр("ru = 'Зашифрованный файл нельзя подписывать.'");
				КонецЕсли;	
				
			КонецЕсли;	
			
		КонецЕсли;	
		
		Если Не ТекущаяВерсия.Пустая() Тогда
			
			// Сменим индекс картинки, возможно, появилась версия или сменился индекс картинки у версии
			РеквизитыТекущейВерсии = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ТекущаяВерсия, 
				"ИндексКартинки, Размер, ДатаСоздания, Автор, Расширение, НомерВерсии, Том, ПутьКФайлу, Код, ПолноеНаименование");
			
			ИндексКартинки = РеквизитыТекущейВерсии.ИндексКартинки;
			
			// копируем реквизиты для ускорения работы RLS
			ТекущаяВерсияРазмер = РеквизитыТекущейВерсии.Размер;
			ТекущаяВерсияДатаСоздания = РеквизитыТекущейВерсии.ДатаСоздания;
			ТекущаяВерсияАвтор = РеквизитыТекущейВерсии.Автор;
			ТекущаяВерсияРасширение = РеквизитыТекущейВерсии.Расширение;
			ТекущаяВерсияНомерВерсии = РеквизитыТекущейВерсии.НомерВерсии;
			ТекущаяВерсияТом = РеквизитыТекущейВерсии.Том;
			ТекущаяВерсияПутьКФайлу = РеквизитыТекущейВерсии.ПутьКФайлу;
			ТекущаяВерсияКод = РеквизитыТекущейВерсии.Код;
			
			// Проверим равенство имени файла и его текущей версии
			// Если имена отличаются - имя у версии должно стать как у карточки с файлом
			Если РеквизитыТекущейВерсии.ПолноеНаименование <> ПолноеНаименование Тогда
				Объект = ТекущаяВерсия.ПолучитьОбъект();
				Если Объект <> Неопределено И Объект.Ссылка <> Неопределено Тогда
					ЗаблокироватьДанныеДляРедактирования(Объект.Ссылка);
					УстановитьПривилегированныйРежим(Истина);
					Объект.ПолноеНаименование = ПолноеНаименование;
					Объект.Записать();
					УстановитьПривилегированныйРежим(Ложь);
				КонецЕсли;	
			КонецЕсли;
		Иначе
			ИндексКартинки = ФайловыеФункцииКлиентСервер.ПолучитьИндексПиктограммыФайла(Неопределено);
		КонецЕсли;
		
		Если УстановленаПометкаУдаления Тогда
			
			// Проверка права "Пометка на удаление".
			Если НЕ РаботаСФайламиПереопределяемый.ПометкаУдаленияРазрешена(ВладелецФайла) Тогда
				ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				                     НСтр("ru = 'У вас нет права ""Пометка на удаление"" файла ""%1"".'"),
				                     Строка(Ссылка));
			КонецЕсли;
			
			// Попытка установить пометку удаления
			Если Не Редактирует.Пустая() Тогда
				ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				                    НСтр("ru = 'Нельзя удалить файл ""%1"", т.к. он занят для редактирования пользователем ""%2"".'"),
				                    ПолноеНаименование,
				                    Строка(Редактирует) );
			КонецЕсли;
			
		КонецЕсли;
		
		НаименованиеВИБ = НаименованиеВИБ();
		Если ПолноеНаименование <> НаименованиеВИБ Тогда 
			Если Не Редактирует.Пустая() Тогда
				ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				                    НСтр("ru = 'Нельзя переименовать файл ""%1"", т.к. он занят для редактирования пользователем ""%2"".'"),
				                    НаименованиеВИБ,
				                    Строка(Редактирует) );
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	
	Наименование = СокрЛП(ПолноеНаименование);
КонецПроцедуры

// Возвращает текущее значение пометки удаления в информационной базе
Функция ПометкаУдаленияВИБ()
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Файлы.ПометкаУдаления
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|ГДЕ
		|	Файлы.Ссылка = &Ссылка";

	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	Результат = Запрос.Выполнить();

	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.ПометкаУдаления;
	КонецЕсли;	
	
	Возврат Неопределено;
КонецФункции

// Возвращает текущее значение наименования в информационной базе
Функция НаименованиеВИБ()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Файлы.ПолноеНаименование
	|ИЗ
	|	Справочник.Файлы КАК Файлы
	|ГДЕ
	|	Файлы.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.ПолноеНаименование;
	КонецЕсли;
	
	Возврат Неопределено;	
	
КонецФункции

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ЭтоНовый() Тогда
		ДатаСоздания = ТекущаяДата();
		ХранитьВерсии = Истина;
		ИндексКартинки = ФайловыеФункцииКлиентСервер.ПолучитьИндексПиктограммыФайла(Неопределено);
		
		Автор = ОбщегоНазначения.ТекущийПользователь();
		ДатаСоздания = ТекущаяДата();
	КонецЕсли;
КонецПроцедуры

