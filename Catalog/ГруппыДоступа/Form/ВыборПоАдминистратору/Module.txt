
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Выбранные",                 Параметры.Выбранные);
	Запрос.УстановитьПараметр("ПользовательГрупп",         Параметры.ПользовательГрупп);
	Запрос.УстановитьПараметр("Администратор",             Пользователи.АвторизованныйПользователь());
	Запрос.УстановитьПараметр("ПолноправныйОтветственный", Пользователи.ЭтоПолноправныйПользовательИБ());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ГруппыДоступа.Ссылка,
	|	ГруппыДоступа.Наименование,
	|	ГруппыДоступа.ЭтоГруппа,
	|	ВЫБОР
	|		КОГДА ГруппыДоступа.ЭтоГруппа
	|				И (НЕ ГруппыДоступа.ПометкаУдаления)
	|			ТОГДА 0
	|		КОГДА ГруппыДоступа.ЭтоГруппа
	|				И ГруппыДоступа.ПометкаУдаления
	|			ТОГДА 1
	|		КОГДА (НЕ ГруппыДоступа.ЭтоГруппа)
	|				И (НЕ ГруппыДоступа.ПометкаУдаления)
	|			ТОГДА 2
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК НомерКартинки,
	|	ЛОЖЬ КАК Пометка
	|ИЗ
	|	Справочник.ГруппыДоступа КАК ГруппыДоступа
	|ГДЕ
	|	ГруппыДоступа.Ссылка <> ЗНАЧЕНИЕ(Справочник.ГруппыДоступа.Администраторы)
	|	И (НЕ ГруппыДоступа.Ссылка В (&Выбранные))
	|	И ВЫБОР
	|			КОГДА &ПолноправныйОтветственный
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ГруппыДоступа.Администратор = &Администратор
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА ГруппыДоступа.ЭтоГруппа
	|				ТОГДА ИСТИНА
	|			КОГДА ГруппыДоступа.Пользователь = НЕОПРЕДЕЛЕНО
	|				ТОГДА ИСТИНА
	|			КОГДА ГруппыДоступа.Пользователь = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			КОГДА ГруппыДоступа.Пользователь = ЗНАЧЕНИЕ(Справочник.ВнешниеПользователи.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ГруппыДоступа.Пользователь = &ПользовательГрупп
	|		КОНЕЦ
	|	И (НЕ ГруппыДоступа.ПометкаУдаления)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГруппыДоступа.Ссылка ИЕРАРХИЯ";
	
	НовоеДерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	Папки = НовоеДерево.Строки.НайтиСтроки(Новый Структура("ЭтоГруппа", Истина), Истина);
	УдалитьПапки = Новый Соответствие;
	НетПапок = Истина;
	Для каждого Папка Из Папки Цикл
		Если Папка.Родитель = Неопределено
		   И Папка.Строки.Количество() = 0
		 ИЛИ Папка.Строки.НайтиСтроки(Новый Структура("ЭтоГруппа", Ложь), Истина).Количество() = 0 Тогда
			//
			УдалитьПапки.Вставить(?(Папка.Родитель = Неопределено, НовоеДерево.Строки, Папка.Родитель.Строки), Папка);
		Иначе
			НетПапок = Ложь;
		КонецЕсли;
	КонецЦикла;
	Для каждого УдалитьПапку Из УдалитьПапки Цикл
		Если УдалитьПапку.Ключ.Индекс(УдалитьПапку.Значение) > -1 Тогда
			УдалитьПапку.Ключ.Удалить(УдалитьПапку.Значение);
		КонецЕсли;
	КонецЦикла;
	
	НовоеДерево.Строки.Сортировать("ЭтоГруппа Убыв, Наименование Возр", Истина);
	ЗначениеВРеквизитФормы(НовоеДерево, "ГруппыДоступа");
	
	Если НетПапок Тогда
		Элементы.ГруппыДоступа.Отображение = ОтображениеТаблицы.Список;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура Выбрать(Команда)
	
	ПриВыборе();
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыДоступаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПриВыборе();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриВыборе()
	
	Если Элементы.ГруппыДоступа.ТекущиеДанные <> Неопределено Тогда
		Если Элементы.ГруппыДоступа.ТекущиеДанные.ЭтоГруппа Тогда
			Если Элементы.ГруппыДоступа.Развернут(Элементы.ГруппыДоступа.ТекущаяСтрока) Тогда
				Элементы.ГруппыДоступа.Свернуть(Элементы.ГруппыДоступа.ТекущаяСтрока);
			Иначе
				Элементы.ГруппыДоступа.Развернуть(Элементы.ГруппыДоступа.ТекущаяСтрока);
			КонецЕсли;
		Иначе
			Закрыть(Элементы.ГруппыДоступа.ТекущиеДанные.Ссылка);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

