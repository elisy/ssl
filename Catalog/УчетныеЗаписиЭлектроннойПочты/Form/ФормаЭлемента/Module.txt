////////////////////////////////////////////////////////////////////////////////
// СЕКЦИЯ ОБРАБОТЧИКОВ СОБЫТИЙ ФОРМЫ И ЭЛЕМЕНТОВ ФОРМЫ
//

// Обработчик события "при создании на сервере" формы
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Пароль) Тогда
		ЗапомнитьПароль = Истина;
	КонецЕсли;
	
	// Если используется только подсистема работы с почтовыми сообщениями
	// скрываем неиспользуемые элементы
	Если Метаданные.ОбщиеМодули.Найти("УправлениеЭлектроннойПочтой") = Неопределено Тогда
		Элементы.ВПредставлениеВключатьИмяПользователя.Видимость = Ложь;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.ИспользоватьДляОтправки = Истина;
		Объект.ИспользоватьДляПолучения = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события "перед записью на сервере" формы
//
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект)
	
	Если НЕ ЗапомнитьПароль Тогда
		ТекущийОбъект.Пароль = "";
	КонецЕсли;
	
	Если Объект.SMTPАутентификация <> Перечисления.ВариантыSMTPАутентификации.ЗадаетсяПараметрами Тогда
		Объект.ПользовательSMTP = "";
		Объект.ПарольSMTP = "";
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события нажатия на кнопку "Дополнительно" формы
//
&НаКлиенте
Процедура ДополнительныеНастройкиВыполнить()
	
	УчетнаяЗаписьСтруктура = Новый Структура;
	УчетнаяЗаписьСтруктура.Вставить("ВремяОжидания",                    Объект.ВремяОжидания);
	УчетнаяЗаписьСтруктура.Вставить("ОставлятьКопииСообщенийНаСервере", Объект.ОставлятьКопииСообщенийНаСервере);
	УчетнаяЗаписьСтруктура.Вставить("ПериодХраненияСообщенийНаСервере", Объект.ПериодХраненияСообщенийНаСервере);
	УчетнаяЗаписьСтруктура.Вставить("ПользовательSMTP",                 Объект.ПользовательSMTP);
	УчетнаяЗаписьСтруктура.Вставить("ПарольSMTP",                       Объект.ПарольSMTP);
	УчетнаяЗаписьСтруктура.Вставить("ПортPOP3",                         Объект.ПортPOP3);
	УчетнаяЗаписьСтруктура.Вставить("ПортSMTP",                         Объект.ПортSMTP);
	УчетнаяЗаписьСтруктура.Вставить("SMTPАутентификация",               Объект.SMTPАутентификация);
	УчетнаяЗаписьСтруктура.Вставить("СпособSMTPАутентификации",         Объект.СпособSMTPАутентификации);
	УчетнаяЗаписьСтруктура.Вставить("СпособPOP3Аутентификации",         Объект.СпособPOP3Аутентификации);
	
	ПараметрыВызова = Новый Структура("Ссылка, УчетнаяЗаписьСтруктура", Объект.Ссылка, УчетнаяЗаписьСтруктура);
	
	ОткрытьФорму("Справочник.УчетныеЗаписиЭлектроннойПочты.Форма.ДополнительныеСвойстваУчетнойЗаписи", ПараметрыВызова);
	
	
КонецПроцедуры

// Обрабатывает оповещение, от формы настройки дополнительных свойств учетной записи,
// содержащее некоторые настройки учетной записи.
//
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "УстановкаДополнительныхПараметровУчетнойЗаписи" И Источник = Объект.Ссылка Тогда
		Объект.ВремяОжидания                    = Параметр.ДлительностьОжиданияСервера;
		Объект.ОставлятьКопииСообщенийНаСервере = Параметр.ОставлятьКопииСообщенийНаСервере;
		Объект.ПериодХраненияСообщенийНаСервере = Параметр.ПериодХраненияСообщенийНаСервере;
		Объект.ПользовательSMTP                 = Параметр.ПользовательSMTP;
		Объект.ПарольSMTP                       = Параметр.ПарольSMTP;
		Объект.ПортPOP3                         = Параметр.ПортPOP3;
		Объект.ПортSMTP                         = Параметр.ПортSMTP;
		Объект.SMTPАутентификация               = Параметр.SMTPАутентификация;
		Объект.СпособSMTPАутентификации         = Параметр.СпособSMTPАутентификации;
		Объект.СпособPOP3Аутентификации         = Параметр.СпособPOP3Аутентификации;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры
