Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоГруппа Тогда
		
		// Удалим дубли и пустые строки
		ВыбранныеСвойства = Новый Соответствие;
		УдаляемыеСвойства = Новый Массив;
		
		// Дополнительные реквизиты
		Для каждого ДополнительныйРеквизит Из ДополнительныеРеквизиты Цикл
			
			Если ДополнительныйРеквизит.Свойство.Пустая() ИЛИ ВыбранныеСвойства.Получить(ДополнительныйРеквизит.Свойство) <> Неопределено Тогда
				
				УдаляемыеСвойства.Добавить(ДополнительныйРеквизит);
				
			Иначе
				
				ВыбранныеСвойства.Вставить(ДополнительныйРеквизит.Свойство, Истина);
				
			КонецЕсли;
			
		КонецЦикла;
		
		Для каждого УдаляемоеСвойство Из УдаляемыеСвойства Цикл
			
			ДополнительныеРеквизиты.Удалить(УдаляемоеСвойство);
			
		КонецЦикла;
		
		ВыбранныеСвойства.Очистить();
		УдаляемыеСвойства.Очистить();
		
		// Дополнительные сведения
		Для каждого ДополнительноеСведение Из ДополнительныеСведения Цикл
			
			Если ДополнительноеСведение.Свойство.Пустая() ИЛИ ВыбранныеСвойства.Получить(ДополнительноеСведение.Свойство) <> Неопределено Тогда
				
				УдаляемыеСвойства.Добавить(ДополнительноеСведение);
				
			Иначе
				
				ВыбранныеСвойства.Вставить(ДополнительноеСведение.Свойство, Истина);
				
			КонецЕсли;
			
		КонецЦикла;
		
		Для каждого УдаляемоеСвойство Из УдаляемыеСвойства Цикл
			
			ДополнительныеСведения.Удалить(УдаляемоеСвойство);
			
		КонецЦикла;
		
	ИначеЕсли Предопределенный Тогда
		
		// Для корневого набора соберем все свойства связанных наборов
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Родитель", Ссылка);
		
		// Дополнительные реквизиты
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДополнительныеРеквизиты.Свойство КАК Свойство
		|ИЗ
		|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК ДополнительныеРеквизиты
		|ГДЕ
		|	ДополнительныеРеквизиты.Ссылка.Родитель = &Родитель
		|	И ДополнительныеРеквизиты.Свойство.ЭтоДополнительноеСведение = ЛОЖЬ";
		
		ДополнительныеРеквизиты.Загрузить(Запрос.Выполнить().Выгрузить());
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДополнительныеСведения.Свойство КАК Свойство
		|ИЗ
		|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	ДополнительныеСведения.Ссылка.Родитель = &Родитель
		|	И ДополнительныеСведения.Свойство.ЭтоДополнительноеСведение = ИСТИНА";
		
		ДополнительныеСведения.Загрузить(Запрос.Выполнить().Выгрузить());
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи()
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Родитель) Тогда
		ОсновнойОбъект = Родитель.ПолучитьОбъект();
		ЗаблокироватьДанныеДляРедактирования(ОсновнойОбъект.Ссылка);
		ОсновнойОбъект.Записать();
	КонецЕсли;
	
КонецПроцедуры
