
// Функция ДеревоПодсистем() возвращает дерево значений имен всех подсистем конфигурации.
//
// Возвращаемое значение:
//  ДеревоЗначений
//      ПолноеИмя   - Строка, например, <Имя родительской подсистемы>.<Имя подсистемы>
//      Имя         - Строка.
//      Синоним     - Строка.
// 
Функция ДеревоПодсистем() Экспорт
	
	Дерево = Новый ДеревоЗначений;
	Дерево.Колонки.Добавить("ПолноеИмя", Новый ОписаниеТипов("Строка"));
	Дерево.Колонки.Добавить("Имя",       Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(1000)));
	Дерево.Колонки.Добавить("Синоним",   Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(1000)));
	
	ЗаполнитьПодсистемы(Дерево.Строки);
	
	Возврат Дерево;
	
КонецФункции

// Функция ВсеРоли() возвращает таблицу значений имен всех ролей конфигурации.
//
// Параметры:
//  ТолькоИменаРолей - Булево. Если ложь возвращается доп. сведения: синонимы ролей, имена и синонимы подсистем.
// 
// Возвращаемое значение:
//  ТаблицаЗначений
//      Имя         - Строка.
//                          // Если ТолькоИменаРолей = Ложь, тогда:
//      Синоним           - Строка.
//      ИменаПодсистем    - Строка, например, "СтандартныеПодсистемы, СтандартныеПодсистемы.БазоваяФункциональность".
//      СинонимыПодсистем - Строка, например, "Стандартные подсистемы, Стандартные подсистемы.Базовая функциональность".
//
Функция ВсеРоли(ТолькоИменаРолей = Ложь) Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(1000)));
	
	Если НЕ ТолькоИменаРолей Тогда
		Таблица.Колонки.Добавить("Синоним",           Новый ОписаниеТипов("Строка"));
		Таблица.Колонки.Добавить("ИменаПодсистем",    Новый ОписаниеТипов("Строка"));
		Таблица.Колонки.Добавить("СинонимыПодсистем", Новый ОписаниеТипов("Строка"));
	КонецЕсли;
	
	Для каждого Роль Из Метаданные.Роли Цикл
	
		Строка = Таблица.Добавить();
		Строка.Имя = Роль.Имя;
		
		Если НЕ ТолькоИменаРолей Тогда
			Строка.Синоним = Роль.Синоним;
			ЗаполнитьПодсистемыРолей(Роль, Строка.ИменаПодсистем, Строка.СинонимыПодсистем);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Таблица;
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры модуля
////////////////////////////////////////////////////////////////////////////////

Процедура ЗаполнитьПодсистемы(КоллекцияСтрокДерева, Подсистемы = Неопределено, ИменаРодительскихПодсистем = "")
	
	Если Подсистемы = Неопределено Тогда
		Подсистемы = Метаданные.Подсистемы;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИменаРодительскихПодсистем) Тогда
		ИменаРодительскихПодсистем = ИменаРодительскихПодсистем + ".";
	КонецЕсли;
	
	Для каждого Подсистема Из Подсистемы Цикл
		
		СтрокаДерева = КоллекцияСтрокДерева.Добавить();
		
		СтрокаДерева.ПолноеИмя = ИменаРодительскихПодсистем + Подсистема.Имя;
		
		СтрокаДерева.Имя       = Подсистема.Имя;
		СтрокаДерева.Синоним   = Подсистема.Синоним;
			
		ЗаполнитьПодсистемы(СтрокаДерева.Строки, Подсистема.Подсистемы, ИменаРодительскихПодсистем + Подсистема.Имя);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПодсистемыРолей(ОбъектМетаданных, ИменаПодсистем, СинонимыПодсистем, Подсистемы = Неопределено, ИменаРодительскихПодсистемы = "", СинонимыРодительскихПодсистемы = "")
	
	
	Если Подсистемы = Неопределено Тогда
		Подсистемы = Метаданные.Подсистемы;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИменаРодительскихПодсистемы) Тогда
		ИменаРодительскихПодсистемы    = ИменаРодительскихПодсистемы    + ".";
		СинонимыРодительскихПодсистемы = СинонимыРодительскихПодсистемы + ".";
	КонецЕсли;
	
	Для каждого Подсистема Из Подсистемы Цикл
		Если Подсистема.Состав.Содержит(ОбъектМетаданных) Тогда
			Если ЗначениеЗаполнено(ИменаПодсистем) Тогда
				ИменаПодсистем    = ИменаПодсистем    + ", ";
				СинонимыПодсистем = СинонимыПодсистем + ", ";
			КонецЕсли;
			ИменаПодсистем    = ИменаПодсистем    + ИменаРодительскихПодсистемы    + Подсистема.Имя;
			СинонимыПодсистем = СинонимыПодсистем + СинонимыРодительскихПодсистемы + Подсистема.Синоним;
		КонецЕсли;
		ЗаполнитьПодсистемыРолей(ОбъектМетаданных, ИменаПодсистем, СинонимыПодсистем, Подсистема.Подсистемы, ИменаРодительскихПодсистемы + Подсистема.Имя, СинонимыРодительскихПодсистемы + Подсистема.Синоним);
	КонецЦикла;
	
КонецПроцедуры

