// Проверяет наличие обновлений адресного классификатора на веб сервере
// для тех объектов, которые ранее уже загружались (для тех объектов
// для которых есть запись в регистре ВерсииОбъектовАдресногоКлассификатора).
//
// Возвращаемое значение
// Массив структур, в котором каждая структура имеет формат:
// ключ КодАдресногоОбъекта - строка - код адресного объекта
// ключ Наименование       - строка - наименование адресного объекта
// ключ Сокращение         - строка - сокращение адресного объекта
// ключ Индекс             - строка - индекс адресного объекта
// ключ ОбновлениеДоступно - Булево
//
Функция ПроверитьОбновлениеАдресныхОбъектов() Экспорт
	
	ВерсияХранимыхСведений = АдресныйКлассификатор.ПолучитьВерсииАдресныхОбъектов();
	
	URLСтрока = ПутьКФайлуОписаниюДанныхКЛАДР();
	
#Если Клиент Тогда
	РезультатПолученияФайлов = ПолучениеФайловИзИнтернетаКлиент.СкачатьФайлНаКлиенте(URLСтрока);
#Иначе
	РезультатПолученияФайлов = ПолучениеФайловИзИнтернета.СкачатьФайлНаСервере(URLСтрока);
#КонецЕсли
	
	Если Не РезультатПолученияФайлов.Статус Тогда
		Возврат РезультатПолученияФайлов;
	КонецЕсли;
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(РезультатПолученияФайлов.Путь);
	ТекстXML = ТекстовыйДокумент.ПолучитьТекст();
	
	УдалитьФайлы(РезультатПолученияФайлов.Путь);
	
	Обновления = АдресныйКлассификатор.ПолучитьВерсииАдресныхСведений(ТекстXML);
	
	Результат = Новый Массив;
	
	Для Каждого ЭлементХранимаяВерсия Из ВерсияХранимыхСведений Цикл
		АдресныеСведения = АдресныйКлассификатор.ИнформацияПоАдресномуОбъекту(ЭлементХранимаяВерсия.Ключ);
		АдресныеСведения.Вставить("ОбновлениеДоступно",
		                          Обновления[ЭлементХранимаяВерсия.Ключ] > ЭлементХранимаяВерсия.Значение);
		Результат.Добавить(АдресныеСведения);
	КонецЦикла;
	
	Возврат ОбщегоНазначенияКлиентСервер.ЗаполнитьРезультат(Результат);
	
КонецФункции

// Функция получает файл с данными по версиям адресных сведений и обновляет версии
// адресных объектов в регистре ВерсииОбъектовАдресногоКлассификатора
// 
// Параметры
// АдресныеОбъекты - массив - каждый элемент - строка, номер адресного объекта в формате NN
//
// Возвращаемое значение: структура, ключ Статус - булево - истина или ложь
//                                   ключ значение - строка - если Статус ложь, содержит
//                                   пояснение об ошибке.
//
Функция ПолучитьФайлВерсийИОбновитьВерсиюАдресныхСведений(знач АдресныеОбъекты) Экспорт
	
#Если Клиент Тогда
	Результат = ПолучениеФайловИзИнтернетаКлиент.СкачатьФайлНаКлиенте(ПутьКФайлуОписаниюДанныхКЛАДР());
#Иначе
	Результат = ПолучениеФайловИзИнтернета.СкачатьФайлНаСервере(ПутьКФайлуОписаниюДанныхКЛАДР());
#КонецЕсли
	
	Если Не Результат.Статус Тогда
		Возврат Результат;
	КонецЕсли;
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(Результат.Путь);
	ТекстXML = ТекстовыйДокумент.ПолучитьТекст();
	
	УдалитьФайлы(Результат.Путь);
	
	АдресныйКлассификатор.ОбновитьВерсиюАдресныхСведений(ТекстXML, АдресныеОбъекты);
	
	Возврат Новый Структура("Статус", Истина);
	
КонецФункции

// Путь к файлу на веб сервере, содержащему информацию по версиям адресных сведений
//
Функция ПутьКФайлуОписаниюДанныхКЛАДР() Экспорт
	
	Возврат "http://downloads.1c.ru/ipp/ITSREPV/V8Update/Configs/kladr/versions.xml";
	
КонецФункции
