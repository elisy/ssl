
// Обработчик регламентного задания по загрузке курса валют
//
Процедура ЗагрузитьАктуальныйКурс() Экспорт
	
	ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
		УровеньЖурналаРегистрации.Информация, , ,
		НСтр("ru = 'Начата регламентная загрузка курсов валют'"));
		
	ТекущаяДата = ТекущаяДата();
	
	СообщениеОбОшибке = "";
	
	ОбработкаЗагрузкиКурсовВалют = Обработки.КурсыВалютРБК.Создать();
	ОбработкаЗагрузкиКурсовВалют.НачалоПериодаЗагрузки = ТекущаяДата;
	ОбработкаЗагрузкиКурсовВалют.ОкончаниеПериодаЗагрузки = ТекущаяДата;
	ОбработкаЗагрузкиКурсовВалют.ЗаполнитьСписокВалют();
	СтатусОперации = ОбработкаЗагрузкиКурсовВалют.ЗагрузитьКурсыВалют(СообщениеОбОшибке);
	Если СтатусОперации Тогда
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация, , ,
			НСтр("ru = 'Завершена регламентная загрузка курсов валют.'"));
	Иначе
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка, , ,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Во время регламентного задания загрузки курсов валют произошла ошибка: 
					|%1'"), 
				СообщениеОбОшибке));
	КонецЕсли;
	
КонецПроцедуры

Функция СобытиеЖурналаРегистрации()
	
	Возврат НСтр("ru = 'Валюты.Загрузка курсов валют'");
	
КонецФункции	
