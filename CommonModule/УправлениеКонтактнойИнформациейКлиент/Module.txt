
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ СВЯЗАННЫХ С ТАБЛИЦЕЙ КОНТАКТНОЙ ИНФОРМАЦИИ НА ФОРМАХ

// Событие ПриИзменении в колонке Представление таблицы контактной информации
Процедура ПредставлениеПриИзменении(Форма, Элемент) Экспорт
	
	СтрДанные = ПолучитьСтрокуДополнительныхЗначений(Форма, Элемент);
	Если (СтрДанные <> Неопределено) И (СтрДанные.ТипНомер = 2) Тогда
		Значение = Элемент.ТекстРедактирования;
		ЗаполнитьПоляВЗаписиПоПредставлениюТелефон(Значение, СтрДанные.ЗначенияПолей);
	КонецЕсли;
	
КонецПроцедуры

// Событие НачалоВыбора в колонке Представление таблицы контактной информации
Процедура ПредставлениеНачалоВыбора(Форма, Элемент, Модифицированность, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	СтрДанные = ПолучитьСтрокуДополнительныхЗначений(Форма, Элемент);
	Если (СтрДанные = Неопределено) И (СтрДанные.ТипНомер = 0) Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрДанные.ТипНомер = 1 Тогда
		ИмяФормыРедактирования =  "ОбщаяФорма.ВводАдреса";
	Иначе
		ИмяФормыРедактирования =  "ОбщаяФорма.ВводТелефона";
	КонецЕсли;
	
	Пар = Новый Структура;
	Пар.Вставить("ЗначенияПолей",                СтрДанные.ЗначенияПолей);
	Пар.Вставить("Вид",                          СтрДанные.Вид);
	Пар.Вставить("БылиВнесеныИзменения",         Ложь);
	Пар.Вставить("Представление",                Элемент.ТекстРедактирования);
	Пар.Вставить("РедактированиеТолькоВДиалоге", Не Элемент.РедактированиеТекста);
	Пар.Вставить("АдресТолькоРоссийский",        СтрДанные.ТолькоРоссийский);
	
	Результат = ОткрытьФормуМодально(ИмяФормыРедактирования, Пар);
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Форма[Элемент.Имя]   = Результат.Представление;
		СтрДанные.ЗначенияПолей = Результат.ЗначенияПолей;
		Модифицированность      = Истина;
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ

Функция ПолучитьСтрокуДополнительныхЗначений(Форма, Элемент)
	
	Отбор = Новый Структура("ИмяРеквизита", Элемент.Имя);
	Строки = Форма.__КИ_ОписаниеДополнительныхРеквизитов.НайтиСтроки(Отбор);
	
	Возврат ?(Строки.Количество() = 0, Неопределено, Строки[0]);
	
КонецФункции

// По полю Представление заполнить остальные поля в записи для телефона
Процедура ЗаполнитьПоляВЗаписиПоПредставлениюТелефон(Представление, СписокПолей)
	
	текСтр = СокрЛП(Представление);
	СписокПолей.Очистить();
	КодСтраны     = "";
	КодГорода     = "";
	НомерТелефона = "";
	Добавочный    = "";
	Комментарий   = "";
	
	// вырежем добавочный номер с комментарием
	ПозДоб = Найти(ВРЕГ(текСтр), "ДОБ.");
	Если ПозДоб <> 0 Тогда
		ДобавочныйСКомментарием = СокрЛП(Сред(текСтр, ПозДоб+4));
		
		текСтр = СокрЛП(Лев(текСтр, ПозДоб-1));
		
		Если Прав(текСтр, 1) = "," Тогда
			текСтр = Лев(текСтр, СтрДлина(текСтр)-1);
		КонецЕсли;
		
		ПозДоб = Найти(ВРЕГ(ДобавочныйСКомментарием), ", ");
		
		Если ПозДоб <> 0 Тогда
			Добавочный = СокрЛП(Лев(ДобавочныйСКомментарием, ПозДоб-1));
			Комментарий = СокрЛП(Сред(ДобавочныйСКомментарием, ПозДоб+2));
		Иначе
			Добавочный = ДобавочныйСКомментарием;
		КонецЕсли;
		
	КонецЕсли;
	
	// вырежем код города
	Поз = Найти(текСтр, "(");
	Если Поз <> 0 Тогда
		КодСтраны = СокрЛП(Лев(текСтр, Поз-1));
		
		текСтр = СокрЛП(Сред(текСтр, Поз+1));
		Поз = Найти(текСтр, ")");
		
		Если Поз <> 0 Тогда
			КодГорода = СокрЛП(Лев(текСтр, Поз-1));
			текСтр = СокрЛП(Сред(текСтр, Поз+1));
		КонецЕсли;
	КонецЕсли;
	
	Поз = Найти(текСтр, ", ");
	// Если добавочного номера нет - ориентируемся по номеру телефона и комментарию
	Если ПозДоб = 0 И Поз <> 0 Тогда
		// вырежем комментарий
		НомерТелефона = СокрЛП(Лев(текСтр, Поз-1));
		Комментарий = СокрЛП(Сред(текСтр, Поз+2));
	Иначе
		// все оставшееся это номер
		НомерТелефона = текСтр;
	КонецЕсли;
	
	// Поправим представление
	Представление = СформироватьПредставлениеТелефона(КодСтраны, КодГорода, НомерТелефона, Добавочный, Комментарий);
	СписокПолей.Добавить(КодСтраны,     "КодСтраны");
	СписокПолей.Добавить(КодГорода,     "КодГорода");
	СписокПолей.Добавить(НомерТелефона, "НомерТелефона");
	СписокПолей.Добавить(Добавочный,    "Добавочный");
	СписокПолей.Добавить(Комментарий,   "Комментарий");
	
КонецПроцедуры

// Процедура формирует строковое представление телефона
Функция СформироватьПредставлениеТелефона(КодСтраны, КодГорода, НомерТелефона, Добавочный, Комментарий) Экспорт
	
	Представление = СокрЛП(КодСтраны);
	
	Если Не ПустаяСтрока(КодГорода) Тогда
		Представление = Представление + ?(ПустаяСтрока(Представление), "", " ") + "(" + СокрЛП(КодГорода) + ")";
	КонецЕсли;
	
	Если Не ПустаяСтрока(НомерТелефона) Тогда
		Представление = Представление + ?(ПустаяСтрока(Представление), "", " ") + СокрЛП(НомерТелефона);
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(Добавочный) Тогда
		Представление = Представление + ?(ПустаяСтрока(Представление), "", ", ") + "доб. " + СокрЛП(Добавочный);
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(Комментарий) Тогда
		Представление = Представление + ?(ПустаяСтрока(Представление), "", ", ") + СокрЛП(Комментарий);
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции
