////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ВНЕШНИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// получает узел из регистра сведений УзлыРолиИсполнителей (если он там есть)
Функция ПолучитьУзелИзРегистраИсточникиВнешнихЗадач(ОбъектСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Подготовить структуру отбора по измерениям      
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Объект", ОбъектСсылка);
	   
	// Получить структуру с данными ресурсов записи
	СтруктураРесурсов = РегистрыСведений.ИсточникиВнешнихЗадач.Получить(СтруктураОтбора);
	   
	Возврат СтруктураРесурсов.УзелИсточник;
	
КонецФункции

// записывает запись в регистр сведений ИсточникиВнешнихЗадач 
Процедура ЗаписатьУзелВРегистрИсточникиВнешнихЗадач(ОбъектСсылка, УзелОбмена) Экспорт
	
	// Записать в регистр
	НаборЗаписей = РегистрыСведений.ИсточникиВнешнихЗадач.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.Объект.Установить(ОбъектСсылка);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Объект = ОбъектСсылка;
	НоваяЗапись.УзелИсточник = УзелОбмена;
	
	УстановитьПривилегированныйРежим(Истина);
	НаборЗаписей.Записать();
	
КонецПроцедуры

// очищает все записи с данным узлом
Процедура ОчиститьРегистрИсточникиВнешнихЗадач(УзелИсточник) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	СписокУдалить = Новый Массив;
	ТекущийПользователь = ОбщегоНазначения.ТекущийПользователь();

	// для каждой нашей записи находим запись в регистре сведений - оттуда берем поле Версия и Редактирует
	ЗапросВРегистр = Новый Запрос;
	ЗапросВРегистр.УстановитьПараметр("УзелИсточник", УзелИсточник);
	ЗапросВРегистр.Текст = "ВЫБРАТЬ
	                       |	ИсточникиВнешнихЗадач.Объект КАК Объект
	                       |ИЗ
	                       |	РегистрСведений.ИсточникиВнешнихЗадач КАК ИсточникиВнешнихЗадач
	                       |ГДЕ
	                       |	ИсточникиВнешнихЗадач.УзелИсточник = &УзелИсточник";
	
	РезультатЗапроса = ЗапросВРегистр.Выполнить(); 
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			СписокУдалить.Добавить(Выборка.Объект);
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого Объект из СписокУдалить Цикл
		// Создать набор записей
		НаборЗаписей = РегистрыСведений.ИсточникиВнешнихЗадач.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Объект.Установить(Объект);
		НаборЗаписей.Записать();
	КонецЦикла;
	
КонецПроцедуры	

// Вернет Истина, если ВерсияФайла принадлежит внешней роли
Функция ВерсияФайлаПринадлежитВнешнейРоли(Владелец) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Владелец);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЕСТЬNULL(ВЫРАЗИТЬ(ВЫРАЗИТЬ(Файлы.ВладелецФайла КАК БизнесПроцесс.Задание).Исполнитель КАК Справочник.РолиИсполнителей).ВнешняяРоль, ЛОЖЬ) КАК ВнешняяРоль
	               |ИЗ
	               |	Справочник.Файлы КАК Файлы
	               |ГДЕ
	               |	Файлы.Ссылка = &Ссылка";
						   
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ВнешняяРоль;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции	

// Вернет УзелОбмена роли, если ВерсияФайла принадлежит внешней роли
Функция ПолучитьУзелОбменаДляВерсииФайла(Владелец) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Владелец);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЕСТЬNULL(ВЫРАЗИТЬ(ВЫРАЗИТЬ(Файлы.ВладелецФайла КАК БизнесПроцесс.Задание).Исполнитель КАК Справочник.РолиИсполнителей).УзелОбмена, НЕОПРЕДЕЛЕНО) КАК УзелОбмена
	               |ИЗ
	               |	Справочник.Файлы КАК Файлы
	               |ГДЕ
	               |	Файлы.Ссылка = &Ссылка";
						   
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.УзелОбмена;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции	

// Вернет Истина, если Файл принадлежит внешней роли
Функция ФайлПринадлежитВнешнейРоли(ВладелецФайла) Экспорт
	
	Если ТипЗнч(ВладелецФайла) <> Тип("БизнесПроцессСсылка.Задание") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ВладелецФайла);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЕСТЬNULL(ВЫРАЗИТЬ(Задание.Исполнитель КАК Справочник.РолиИсполнителей).ВнешняяРоль, ЛОЖЬ) КАК ВнешняяРоль
	               |ИЗ
	               |	БизнесПроцесс.Задание КАК Задание
	               |ГДЕ
	               |	Задание.Ссылка = &Ссылка";
						   
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ВнешняяРоль;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции	

// Вернет УзелОбмена роли, если Файл принадлежит внешней роли
Функция ПолучитьУзелОбменаДляФайла(ВладелецФайла) Экспорт
	
	Если ТипЗнч(ВладелецФайла) <> Тип("БизнесПроцессСсылка.Задание") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ВладелецФайла);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЕСТЬNULL(ВЫРАЗИТЬ(Задание.Исполнитель КАК Справочник.РолиИсполнителей).УзелОбмена, НЕОПРЕДЕЛЕНО) КАК УзелОбмена
	               |ИЗ
	               |	БизнесПроцесс.Задание КАК Задание
	               |ГДЕ
	               |	Задание.Ссылка = &Ссылка";
						   
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.УзелОбмена;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции	
