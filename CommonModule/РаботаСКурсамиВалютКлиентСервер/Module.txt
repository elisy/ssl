// Процедура для загрузки курсов валют по определенному периоду.
//
// Параметры
// Валюты		- Любая коллекция - со следующими полями:
//					КодВалюты - числовой код валюты
//					Валюта - ссылка на валюту
// НачалоПериодаЗагрузки	- Дата - начало периода загрузки курсов
// ОкончаниеПериодаЗагрузки	- Дата - окончание периода загрузки курсов
//
// Возвращаемое значение:
// Массив состояния загрузки  - каждый элемент - структура с полями
//		Валюта - загружаемая валюта
//		СтатусОперации - завершилась ли загрузка успешно
//		Сообщение - пояснение о загрузке (текст сообщения об ошибке или поясняющее сообщение)
//
Функция ЗагрузитьКурсыВалютПоПараметрам(знач Валюты,
										знач НачалоПериодаЗагрузки,
										знач ОкончаниеПериодаЗагрузки,
										ПриЗагрузкеВозниклиОшибки = Ложь) Экспорт
	
	СостояниеЗагрузки = Новый Массив;
	
	ПриЗагрузкеВозниклиОшибки = Ложь;
	
	СерверИсточник = "cbrates.rbc.ru";
	
	Если НачалоПериодаЗагрузки = ОкончаниеПериодаЗагрузки Тогда
		Адрес = "tsv/";
		ТМП   = "/"+Формат(Год(ОкончаниеПериодаЗагрузки),"ЧРГ=; ЧГ=0")+"/"+Формат(Месяц(ОкончаниеПериодаЗагрузки),"ЧЦ=2;ЧДЦ=0;ЧВН=")+"/"+Формат(День(ОкончаниеПериодаЗагрузки),"ЧЦ=2;ЧДЦ=0;ЧВН=");
	Иначе
		Адрес = "tsv/cb/";
		ТМП   = "";
	КонецЕсли;
	
	Для Каждого Валюта из Валюты Цикл
		ФайлНаВебСервере = "http://" + СерверИсточник + "/" + Адрес + Прав(Валюта.КодВалюты, 3) + ТМП + ".tsv";
		
#Если НаКлиенте Тогда
		Результат = ПолучениеФайловИзИнтернетаКлиент.СкачатьФайлНаКлиенте(ФайлНаВебСервере);
#Иначе
		Результат = ПолучениеФайловИзИнтернета.СкачатьФайлНаСервере(ФайлНаВебСервере);
#КонецЕсли
		
		Если Результат.Статус Тогда
#Если НаКлиенте Тогда
			ДвоичныеДанные = Новый ДвоичныеДанные (Результат.Путь);
			АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
			ПоясняющееСообщение = РаботаСКурсамиВалют.ЗагрузитьКурсВалютыИзФайла(Валюта.Валюта, АдресВоВременномХранилище, НачалоПериодаЗагрузки, ОкончаниеПериодаЗагрузки) + Символы.ПС;
#Иначе
			ПоясняющееСообщение = РаботаСКурсамиВалют.ЗагрузитьКурсВалютыИзФайла(Валюта.Валюта, Результат.Путь, НачалоПериодаЗагрузки, ОкончаниеПериодаЗагрузки) + Символы.ПС;
#КонецЕсли
			УдалитьФайлы(Результат.Путь);
			СтатусОперации = Истина;
		Иначе
			ПоясняющееСообщение= НСтр("ru = 'Не возможно получить файл данных с курсами валюты (%1 - %2):
					|%3
					|Возможно, нет доступа к веб сайту с курсами валют, либо указана несуществующая валюта.'");
			ПоясняющееСообщение = 
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								ПоясняющееСообщение,
								Валюта.КодВалюты,
								Валюта.Валюта,
								Результат.СообщениеОбОшибке);
			СтатусОперации = Ложь;
			ПриЗагрузкеВозниклиОшибки = Истина;
		КонецЕсли;
		
		СостояниеЗагрузки.Добавить(Новый Структура("Валюта,СтатусОперации,Сообщение", Валюта.Валюта, СтатусОперации, ПоясняющееСообщение));
		
	КонецЦикла;
	
	
	Возврат СостояниеЗагрузки;
	
КонецФункции
