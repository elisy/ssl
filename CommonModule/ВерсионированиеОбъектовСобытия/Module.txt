
// Обработчик события ПриЗаписи. Определен для версионируемых объектов.
// Объект сериализуется в XML (Fast Infoset).
//
Процедура ВерсионированиеОбъектовПриЗаписиОбъекта(Источник, Отказ) Экспорт
	
	Перем ЧислоВерсийОбъекта;
	
	Если ОбъектВерсионируется(Источник, ЧислоВерсийОбъекта) Тогда
		ЗаписьXML = Новый ЗаписьFastInfoset;
		ЗаписьXML.УстановитьДвоичныеДанные();
		ЗаписьXML.ЗаписатьОбъявлениеXML();
		ЗаписатьXML(ЗаписьXML, Источник, НазначениеТипаXML.Явное);
		ДвоичныеДанные = ЗаписьXML.Закрыть();
		ХранилищеДанных = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9));
		
		ВерсионированиеОбъектов.ЗаписатьВерсиюОбъекта(Источник.Ссылка, ЧислоВерсийОбъекта, ХранилищеДанных);
	КонецЕсли;
	
КонецПроцедуры

// Проверяет настройки версионирования по переданному объекту и
// и возвращает вариант версионирования. Если по объекту не настроено
// версионирование, то он версионируется в соответствии с правилами
// версионирования "по умолчанию".
//
Функция ОбъектВерсионируется(знач Источник, ЧислоВерсийОбъекта)
	
	УстановитьПривилегированныйРежим(Истина);
	ИспользоватьВерсионированиеОбъектов = ПолучитьФункциональнуюОпцию("ИспользоватьВерсионированиеОбъектов");
	УстановитьПривилегированныйРежим(Ложь);
	
	// Проверяем, что подсистема версионирования включена
	Если ИспользоватьВерсионированиеОбъектов Тогда
		
		ПолноеИмяОбъектаМетаданных = Источник.Метаданные().ПолноеИмя();
		
		УстановитьПривилегированныйРежим(Истина);
		ВариантВерсионирования = ПолучитьФункциональнуюОпцию("ВариантыВерсионированияОбъектов",
										Новый Структура("ТипОбъектаКонфигурации", ПолноеИмяОбъектаМетаданных));
		УстановитьПривилегированныйРежим(Ложь);
		
		ЧислоВерсийОбъекта = ВерсионированиеОбъектов.ПолучитьКоличествоВерсийОбъекта(Источник.Ссылка);
		
		ВерсионироватьОбъект = Истина;
		
		Если ВариантВерсионирования = Перечисления.ВариантыВерсионированияОбъектов.НеВерсионировать Тогда
			ВерсионироватьОбъект = Ложь;
		ИначеЕсли ВариантВерсионирования = Перечисления.ВариантыВерсионированияОбъектов.ВерсионироватьПриПроведении Тогда
			Если ЧислоВерсийОбъекта = 0 И НЕ Источник.Проведен Тогда
				ВерсионироватьОбъект = Ложь;
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		ВерсионироватьОбъект = Ложь;
	КонецЕсли;
	
	Возврат ВерсионироватьОбъект;
	
КонецФункции

// Обработчик события ПриЗаписи Справочников и Документов ИБ.
// Подготавливает объект к записи в регистр версий.
//
Процедура ВерсионированиеОбъектовПередУдалениемОбъекта(Источник, Отказ) Экспорт
	
	ВерсионированиеОбъектов.УдалитьХранимыеВерсииПоОбъекту(Источник.Ссылка);
	
КонецПроцедуры
