
////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы
//

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Заполнение свойств разделов
	СвойстваРазделов = ДатыЗапретаИзмененияПовтИсп.СвойстваРазделов();
	ЗаполнитьЗначенияСвойств(ЭтаФорма, СвойстваРазделов, , "ТипыОбъектовРазделов");
	ЗначениеВРеквизитФормы(СвойстваРазделов.ТипыОбъектовРазделов, "ТипыОбъектовРазделов");
	ПервыйРаздел = ?(ТипыОбъектовРазделов.Количество() = 1, ТипыОбъектовРазделов[0].Раздел, РазделПустаяСсылка);
	
	// Настройка полей формы
	Если Параметры.ДатыЗапретаЗагрузкиДанных Тогда
		//
		Если НЕ СвойстваРазделов.ИспользоватьДатыЗапретаЗагрузкиДанных Тогда
			ВызватьИсключение(НСтр("ru = 'Даты запрета загрузки данных не используются.'"));
		КонецЕсли;
		//
		Заголовок = НСтр("ru = 'Даты запрета загрузки данных'");
		Элементы.УстановкаДатыЗапрета.СписокВыбора.НайтиПоЗначению("НетЗапрета").Представление =
			НСтр("ru = 'Нет запрета загрузки данных'");
		Элементы.УстановкаДатыЗапрета.СписокВыбора.НайтиПоЗначению("ДляВсехПользователей").Представление =
			НСтр("ru = 'Для всех узлов планов обмена'");
		Элементы.УстановкаДатыЗапрета.СписокВыбора.НайтиПоЗначению("ПоПользователям").Представление =
			НСтр("ru = 'По узлам планов обмена'");
		//
		Элементы.ФормаОтчетыПоПользователям.Подсказка             = НСтр("ru = 'Отчеты по датам запрета, установленным ""По узлам планов обмена""'");
		Команды.ОтчетПоПользователям.Заголовок                    = НСтр("ru = 'Отчет по узлам планов обмена'");
		Команды.ОтчетПоПользователям.Подсказка                    = НСтр("ru = 'Отчет по узлам планов обмена, планам обмена'");
		Команды.ОтчетПоРазделамОбъектамДляПользователей.Заголовок = НСтр("ru = 'Отчет по разделам и объектам для узлов планов обмена'");
		Команды.ОтчетПоРазделамОбъектамДляПользователей.Подсказка = НСтр("ru = 'Отчет по разделам и объектам для узлов планов обмена и планов обмена'");
		//
		Элементы.ПользователиПолноеПредставление.Заголовок = НСтр("ru = 'План обмена: узел'");
		//
		ЗначениеДляВсехПользователей = Перечисления.ВидыНазначенияДатЗапрета.ДляВсехУзловПлановОбмена;
		Для каждого ТипПользователя Из Метаданные.РегистрыСведений.ДатыЗапретаИзменения.Измерения.Пользователь.Тип.Типы() Цикл
			ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипПользователя);
			Если НЕ Метаданные.ПланыОбмена.Содержит(ОбъектМетаданных) Тогда
				Продолжить;
			КонецЕсли;
			ПустаяСсылкаУзлаПланаОбмена = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ОбъектМетаданных.ПолноеИмя()).ПустаяСсылка();
			СписокТиповПользователей.Добавить(ПустаяСсылкаУзлаПланаОбмена, ОбъектМетаданных.Представление());
		КонецЦикла;
		Элементы.Пользователи.КартинкаСтрок = БиблиотекаКартинок.ПиктограммыПланОбменаУзел;
	Иначе
		ЗначениеДляВсехПользователей = Перечисления.ВидыНазначенияДатЗапрета.ДляВсехПользователей;
		СписокТиповПользователей.Добавить(Тип("СправочникСсылка.Пользователи"),        НСтр("ru = 'Пользователь'"));
		СписокТиповПользователей.Добавить(Тип("СправочникСсылка.ВнешниеПользователи"), НСтр("ru = 'Внешний пользователь'"));
	КонецЕсли;
	
	Список = Элементы.СпособУказанияДатыЗапрета.СписокВыбора;
	
	Если БезРазделовИОбъектов Тогда
		Элементы.СпособУказанияДатыЗапрета.Видимость = ЗначениеЗаполнено(ТекущийСпособУказанияДатыЗапрета("*", ЕдинственныйРаздел, ПервыйРаздел, ЗначениеДляВсехПользователей));
		Список.Удалить(Список.НайтиПоЗначению("ПоРазделамИОбъектам"));
		Список.Удалить(Список.НайтиПоЗначению("ПоРазделам"));
		Список.Удалить(Список.НайтиПоЗначению("ПоОбъектам"));
		
	ИначеЕсли НЕ ПоказыватьРазделы Тогда
		Элементы.ОтчетПоРазделамОбъектамДляПользователей.Заголовок = ТекстЗаголовкаКомандыОтчетПоОбъектам();
		Список.Удалить(Список.НайтиПоЗначению("ПоРазделамИОбъектам"));
		Список.Удалить(Список.НайтиПоЗначению("ПоРазделам"));
		
	ИначеЕсли ВсеРазделыБезОбъектов Тогда
		Элементы.ОтчетПоРазделамОбъектамДляПользователей.Заголовок = ТекстЗаголовкаКомандыОтчетПоРазделам();
		Список.Удалить(Список.НайтиПоЗначению("ПоРазделамИОбъектам"));
		Список.Удалить(Список.НайтиПоЗначению("ПоОбъектам"));
	Иначе
		Список.Удалить(Список.НайтиПоЗначению("ПоОбъектам"));
	КонецЕсли;
	
	ИспользоватьВнешнихПользователей = ИспользоватьВнешнихПользователей И ВнешниеПользователи.ИспользоватьВнешнихПользователей();
	СправочникВнешниеПользователиДоступен = ПравоДоступа("Просмотр", Метаданные.Справочники.ВнешниеПользователи);
	
	ОбновитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	ТекстВопроса = ТекстВопросаОНесохраненныхДанных();
	
	Если ЗначениеЗаполнено(ТекстВопроса) Тогда
		ТекстВопроса = ТекстВопроса + Символы.ПС + ТекстВопросаЗакрытьФорму();
		Если Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	РазблокироватьВсеЗаписи(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий команд и элементов формы
//

&НаКлиенте
Процедура ПересчитатьДаты(Команда)
	
	ТекстВопроса = ТекстВопросаОНесохраненныхДанных();
	
	Если ЗначениеЗаполнено(ТекстВопроса) Тогда
		ТекстВопроса = ТекстВопроса + Символы.ПС + ТекстВопросаПересчитатьДатыЗапрета();
		Если Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ОписаниеРезультата = ПересчитатьДатыНаСервере();
	
	ОбновитьНаСервере();
	РазвернутьДанныеПользователя();
	
	Предупреждение(ОписаниеРезультата);
	
КонецПроцедуры

&НаСервере
Функция ПересчитатьДатыНаСервере()
	
	ОписаниеРезультата = "";
	ДатыЗапретаИзмененияВызовСервера.ПересчитатьТекущиеЗначенияОтносительныхДатЗапрета(Ложь, ОписаниеРезультата);
	
	ОбновитьНаСервере();
	
	Возврат ОписаниеРезультата;
	
КонецФункции

&НаКлиенте
Процедура Обновить(Команда)
	
	ТекстВопроса = ТекстВопросаОНесохраненныхДанных();
	
	Если ЗначениеЗаполнено(ТекстВопроса) Тогда
		ТекстВопроса = ТекстВопроса + Символы.ПС + ТекстВопросаОбновитьДанные();
		Если Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьНаСервере();
	РазвернутьДанныеПользователя();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаСервере()
	
	// Расчет установки даты запрета
	УстановкаДатыЗапрета = ТекущаяУстановкаДатыЗапрета(Параметры.ДатыЗапретаЗагрузкиДанных);
	// Установка видимости по рассчитанной установке дате запрета
	УстановитьВидимость();
	
	// Кэширование текущей даты на сервере
	ТекущаяДатаНаСервере = ТекущаяДата();
	
	СтарыйПользователь = ТекущийПользователь;
	
	ПрочитатьПользователей();
	
	Отбор = Новый Структура("Пользователь", СтарыйПользователь);
	НайденныеСтроки = ПользователиДатЗапрета.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() = 0 Тогда
		ТекущийПользователь = ЗначениеДляВсехПользователей;
	Иначе
		Элементы.Пользователи.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
		ТекущийПользователь = СтарыйПользователь;
	КонецЕсли;
		
	ПрочитатьДанныеПользователя(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановкаДатыЗапретаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановкаДатыЗапретаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	УдалитьЛишнее = Ложь;
	
	Если УстановкаДатыЗапрета = ВыбранноеЗначение Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяУстановкаДатыЗапрета = ТекущаяУстановкаДатыЗапрета(Параметры.ДатыЗапретаЗагрузкиДанных);
	
	Если ТекущаяУстановкаДатыЗапрета = "ПоПользователям" И ВыбранноеЗначение = "ДляВсехПользователей" Тогда
		ТекстВопроса = ТекстВопросаУдалитьВсеДатыЗапретаКромеДатДляВсехПользователей();
		
	ИначеЕсли ТекущаяУстановкаДатыЗапрета = "ПоПользователям" И ВыбранноеЗначение = "НетЗапрета" Тогда
		ТекстВопроса = ТекстВопросаУдалитьВсеДатыЗапрета();
		
	ИначеЕсли ТекущаяУстановкаДатыЗапрета = "ДляВсехПользователей" И ВыбранноеЗначение = "НетЗапрета" Тогда
		ТекстВопроса = ТекстВопросаУдалитьВсеДатыЗапрета();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстВопроса) Тогда
		Если Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
			СтандартнаяОбработка = Ложь;
		Иначе
			УдалитьЛишнее = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если СтандартнаяОбработка Тогда
		УстановкаДатыЗапрета = ВыбранноеЗначение;
		ИзменитьУстановкуДатыЗапрета(ВыбранноеЗначение, УдалитьЛишнее);
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ПодобратьПользователей(Команда)
	
	ВыбратьПодобратьПользователей(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетПоПользователям(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	Если Параметры.ДатыЗапретаЗагрузкиДанных Тогда
		ИмяФормыОтчета = "Отчет.ДатыЗапретаЗагрузки.Форма.ФормаОтчета";
	Иначе
		ИмяФормыОтчета = "Отчет.ДатыЗапретаИзменения.Форма.ФормаОтчета";
	КонецЕсли;
	
	ОткрытьФорму(ИмяФормыОтчета, ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетПоРазделамОбъектамДляПользователей(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПоРазделамОбъектам", Истина);
	
	Если Параметры.ДатыЗапретаЗагрузкиДанных Тогда
		ИмяФормыОтчета = "Отчет.ДатыЗапретаЗагрузки.Форма.ФормаОтчета";
	Иначе
		ИмяФормыОтчета = "Отчет.ДатыЗапретаИзменения.Форма.ФормаОтчета";
	КонецЕсли;
	
	ОткрытьФорму(ИмяФормыОтчета, ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Элементы.Пользователи.ИзменитьСтроку();
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиПриАктивизацииСтроки(Элемент)
	
	ОбновитьДанныеПользователя();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПользователя()
	
	ТекущиеДанные = Элементы.Пользователи.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено
	 ИЛИ НЕ ЗначениеЗаполнено(ТекущиеДанные.Представление) Тогда
		//
		НовыйПользователь = Неопределено;
	Иначе
		НовыйПользователь = ТекущиеДанные.Пользователь;
	КонецЕсли;
	
	Если НовыйПользователь = ТекущийПользователь Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПроверкаНесохраненныхЗаписей() Тогда
		ПодключитьОбработчикОжидания("ПользователиВосстановитьТекущуюСтрокуПослеОтказаПриАктивизацииСтроки", 0.1, Истина);
		Возврат;
	КонецЕсли;
	
	СпособУказанияЗначениеВСписке = Элементы.СпособУказанияДатыЗапрета.СписокВыбора.НайтиПоЗначению(СпособУказанияДатыЗапрета);
	Если ТекущийПользователь <> Неопределено И СпособУказанияЗначениеВСписке <> Неопределено Тогда
		ТекущийСпособУказания = ТекущийСпособУказанияДатыЗапрета(ТекущийПользователь, ЕдинственныйРаздел, ПервыйРаздел, ЗначениеДляВсехПользователей);
		ТекущийСпособУказания = ?(ЗначениеЗаполнено(ТекущийСпособУказания), ТекущийСпособУказания, "ОбщаяДата");
		СпособУказанияДатыЗапрета = ТекущийСпособУказания;
		Если ТекущийСпособУказания <> СпособУказанияЗначениеВСписке.Значение Тогда
			Предупреждение(ТекстСообщенияСпособУказанияНеИспользован(
				СпособУказанияЗначениеВСписке.Представление,
				?(Элементы.СпособУказанияДатыЗапрета.СписокВыбора.НайтиПоЗначению(ТекущийСпособУказания) = Неопределено,
				  ТекущийСпособУказания,
				  Элементы.СпособУказанияДатыЗапрета.СписокВыбора.НайтиПоЗначению(ТекущийСпособУказания).Представление)));
		КонецЕсли;
	КонецЕсли;
	
	ТекущийПользователь = НовыйПользователь;
	
	// Чтение данных текущего пользователя
	Если НовыйПользователь = Неопределено Тогда
		СпособУказанияДатыЗапрета = "ОбщаяДата";
		ДатыЗапрета.ПолучитьЭлементы().Очистить();
		Элементы.ДанныеПользователя.ТекущаяСтраница = Элементы.ПользовательНеВыбран;
	Иначе
		ПрочитатьДанныеПользователя(ЭтаФорма);
		РазвернутьДанныеПользователя();
	КонецЕсли;
	
	ОбновитьНаличиеДатЗапретаТекущегоПользователя();
	
	// Блокировка команд Подобрать, Добавить (объект) пока не выбран раздел
	ДатыЗапретаУстановитьДоступностьКоманд(Ложь);
	
КонецПроцедуры

&НаКлиенте
Функция ПроверкаНесохраненныхЗаписей()
	
	Отбор = Новый Структура("Пользователь", ТекущийПользователь);
	НайденныеСтроки = ПользователиДатЗапрета.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() > 0 Тогда
		ТекстВопроса = ТекстВопросаОНесохраненныхДанных(Истина);
		Если ЗначениеЗаполнено(ТекстВопроса) Тогда
			ТекстВопроса = ТекстВопроса + Символы.ПС + ТекстВопросаОчиститьНезаполненыеСтроки();
			Если Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
				ПользователиТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ПользователиВосстановитьТекущуюСтрокуПослеОтказаПриАктивизацииСтроки()
	
	Элементы.Пользователи.ТекущаяСтрока = ПользователиТекущаяСтрока;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	// Копирование не требуется, т.к. пользователи не могут повторяться
	Если Копирование Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ ПроверкаНесохраненныхЗаписей() Тогда
		Отказ = Истина;
	Иначе
		ДатыЗапрета.ПолучитьЭлементы().Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиПередНачаломИзменения(Элемент, Отказ)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Поле          = Элемент.ТекущийЭлемент;
	
	Если ТекущиеДанные.Пользователь = ЗначениеДляВсехПользователей Тогда
		// Предопределенное значение "<Для всех пользователей>" не изменяется
		Если Поле = Элементы.ПользователиПолноеПредставление Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщенияЗначениеДляВсехПользователейНеИзменяется());
			//
		ИначеЕсли Поле = Элементы.ПользователиКомментарий Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщенияКомментарийДляВсехПользователейНеИзменяется());
		КонецЕсли;
		Отказ = Истина;
	ИначеЕсли Поле <> Элементы.ПользователиПолноеПредставление
	        И НЕ ЗначениеЗаполнено(ТекущиеДанные.Представление) Тогда
		// Все значения, кроме преопределенного значения "<Для всех пользователей>",
		// должны быть заполнены до установки описания или даты запрета
		Элемент.ТекущийЭлемент = Элементы.ПользователиПолноеПредставление;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщенияСначалаВыберитеПользователя());
	КонецЕсли;
	
	Если Отказ Тогда
		Элементы.ПользователиПолноеПредставление.ТолькоПросмотр = Ложь;
		Элементы.ПользователиКомментарий.ТолькоПросмотр = Ложь;
	Иначе
		Элементы.ПользователиКомментарий.ТолькоПросмотр = НЕ ЗначениеЗаполнено(ТекущиеДанные.Представление);
		Если ЗначениеЗаполнено(ТекущиеДанные.Представление) Тогда
			ЗаблокироватьНаборЗаписейПользователя(ТекущиеДанные.Пользователь, Элемент.ТекущаяСтрока);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиПередУдалением(Элемент, Отказ)
	
	Если Элементы.Пользователи.ВыделенныеСтроки.Количество() > 1 Тогда
		Предупреждение(ТекстСообщенияДляУдаленияВыделитеОднуСтроку());
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Пользователи.ТекущиеДанные;
	
	// Элемент для всех пользователей присутствует всегда
	ДатыЗапретаДляВсехПользователей = ТекущиеДанные.Пользователь = ЗначениеДляВсехПользователей;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Представление)
	   И НЕ ТекущиеДанные.БезДатыЗапрета Тогда
		// Для удаления пользователей с записями требуется подтверждение
		Если ДатыЗапретаДляВсехПользователей Тогда
			ТекстВопроса = ТекстВопросаУдалитьДатыЗапретаДляВсехПользователей();
		Иначе
			Если ТипЗнч(ТекущиеДанные.Пользователь) = Тип("СправочникСсылка.Пользователи") Тогда
				ТекстВопроса = ТекстВопросаУдалитьДатыЗапретаДляПользователя();
				
			ИначеЕсли ТипЗнч(ТекущиеДанные.Пользователь) = Тип("СправочникСсылка.ГруппыПользователей") Тогда
				ТекстВопроса = ТекстВопросаУдалитьДатыЗапретаДляГруппыПользователей();
				
			ИначеЕсли ТипЗнч(ТекущиеДанные.Пользователь) = Тип("СправочникСсылка.ВнешниеПользователи") Тогда
				ТекстВопроса = ТекстВопросаУдалитьДатыЗапретаДляВнешнегоПользователя();
				
			ИначеЕсли ТипЗнч(ТекущиеДанные.Пользователь) = Тип("СправочникСсылка.ГруппыВнешнихПользователей") Тогда
				ТекстВопроса = ТекстВопросаУдалитьДатыЗапретаДляГруппыВнешнихПользователей();
			Иначе
				ТекстВопроса = ТекстВопросаУдалитьДатыЗапрета();
			КонецЕсли;
		КонецЕсли;
		Если Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
			//
			УдалитьНаборЗаписейПользователя(ТекущиеДанные.Пользователь);
			Если ДатыЗапретаДляВсехПользователей Тогда
				ТекущиеДанные.ДатаЗапрета         = '00000000';
				ТекущиеДанные.ОписаниеДатыЗапрета = "";
				ОбновитьНаличиеДатЗапретаТекущегоПользователя();
			КонецЕсли;
			ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ДатыЗапретаИзменения"));
		Иначе
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	Если НЕ Отказ И ДатыЗапретаДляВсехПользователей Тогда
		Отказ = Истина;
		НетДобавленныхСтрок = Истина;
		Если ПоказыватьРазделыТекущегоПользователя Тогда
			Для каждого ОписаниеРаздела Из ДатыЗапрета.ПолучитьЭлементы() Цикл
				Если ДатаЗапретаУстановлена(ОписаниеРаздела, ТекущийПользователь)
				 ИЛИ ОписаниеРаздела.ПолучитьЭлементы().Количество() > 0 Тогда
					НетДобавленныхСтрок = Ложь;
					ОписаниеРаздела.ДатаЗапрета         = '00000000';
					ОписаниеРаздела.ОписаниеДатыЗапрета = "";
					ОписаниеРаздела.ПолучитьЭлементы().Очистить();
				КонецЕсли;
			КонецЦикла;
		Иначе
			Если ДатыЗапрета.ПолучитьЭлементы().Количество() > 0 Тогда
				НетДобавленныхСтрок = Ложь;
				ДатыЗапрета.ПолучитьЭлементы().Очистить();
			КонецЕсли;
		КонецЕсли;
		ТекущиеДанные.БезДатыЗапрета = Истина;
		ТекущиеДанные.ПолноеПредставление = ТекущиеДанные.Представление;
		Если НетДобавленныхСтрок Тогда
			Предупреждение(ТекстСообщенияДляВсехПользователейДатыЗапретаНеУстановлены());
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		СпособУказанияДатыЗапрета = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.Пользователи.ТекущиеДанные;
	
	Если НЕ ЗначениеЗаполнено(ТекущиеДанные.Представление) Тогда
		ТекущиеДанные.НомерКартинки = -1;
		ПодключитьОбработчикОжидания("ОбработчикОжиданияВыбратьПользователей", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если УстановленныеБлокировки.Количество() > 0 Тогда
		РазблокироватьВсеЗаписи(ЭтаФорма);
	КонецЕсли;
	
	Элементы.ПользователиПолноеПредставление.ТолькоПросмотр = Ложь;
	Элементы.ПользователиКомментарий.ТолькоПросмотр = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПользователиОбработкаВыбораНаСервере(ВыбранноеЗначение);
	
КонецПроцедуры

&НаСервере
Процедура ПользователиОбработкаВыбораНаСервере(ВыбранноеЗначение)
	
	Отбор = Новый Структура("Пользователь");
	
	Для каждого Значение Из ВыбранноеЗначение Цикл
		Отбор.Пользователь = Значение;
		Если ПользователиДатЗапрета.НайтиСтроки(Отбор).Количество() = 0 Тогда
			ЗаблокироватьИЗаписатьПустыеДаты(РазделПустаяСсылка, РазделПустаяСсылка, Отбор.Пользователь, "");
			ОписаниеПользователя = ПользователиДатЗапрета.Добавить();
			ОписаниеПользователя.Пользователь = Отбор.Пользователь;
			ОписаниеПользователя.Представление = ТекстПредставленияПользователя(ЭтаФорма, Отбор.Пользователь);
			ОписаниеПользователя.ПолноеПредставление = ОписаниеПользователя.Представление;
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьНомераКартинокПользователейДатЗапрета();
	
	РазблокироватьВсеЗаписи(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиПолноеПредставлениеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Пользователи.ТекущиеДанные;
	
	Если НЕ ЗначениеЗаполнено(ТекущиеДанные.ПолноеПредставление) Тогда
		ТекущиеДанные.ПолноеПредставление = ТекущиеДанные.Представление;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиПолноеПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Пользователи.ТекущиеДанные;
	Если ТекущиеДанные.Пользователь = ЗначениеДляВсехПользователей Тогда
		// Элемент для всех пользователей присутствует всегда
		Отказ = Истина;
		Предупреждение(ТекстСообщенияЗначениеДляВсехПользователейНеИзменяется());
	Иначе
		// Возможна смена пользователя на себя и на еще не выбранного в списке
		ВыбратьПодобратьПользователей();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиПолноеПредставлениеОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиПолноеПредставлениеОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(Элементы.Пользователи.ТекущиеДанные.Пользователь) Тогда
		ОткрытьЗначение(Элементы.Пользователи.ТекущиеДанные.Пользователь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиПолноеПредставлениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Пользователи.ТекущиеДанные;
	Если ТекущиеДанные.Пользователь = ВыбранноеЗначение Тогда
		ТекущиеДанные.ПолноеПредставление = ТекущиеДанные.Представление;
		Возврат;
	КонецЕсли;
	
	// Замена пользователя возможна только на другого
	// пользователя, которого еще нет в списке
	Отбор = Новый Структура("Пользователь", ВыбранноеЗначение);
	Строки = ПользователиДатЗапрета.НайтиСтроки(Отбор);
	//
	Если Строки.Количество() = 0 Тогда
		Если НЕ ЗаменитьПользователяНабораЗаписей(ТекущийПользователь, ВыбранноеЗначение) Тогда
			Предупреждение(
				ТекстСообщенияЗначениеУжеДобавленоВСписок()
				+ Символы.ПС
				+ ТекстСообщенияОбновитьДанныеФормыF5());
			Возврат;
		КонецЕсли;
		// Установка выбранного пользователя
		ТекущийПользователь = Неопределено;
		ТекущиеДанные.Пользователь  = ВыбранноеЗначение;
		ТекущиеДанные.Представление = ТекстПредставленияПользователя(ЭтаФорма, ВыбранноеЗначение);
		ТекущиеДанные.ПолноеПредставление = ТекущиеДанные.Представление;
		Элементы.ПользователиКомментарий.ТолькоПросмотр = Ложь;
		ЗаполнитьНомераКартинокПользователейДатЗапрета(Элементы.Пользователи.ТекущаяСтрока);
		Элементы.Пользователи.ЗакончитьРедактированиеСтроки(Ложь);
		ОбновитьДанныеПользователя();
		ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ДатыЗапретаИзменения"));
		Элементы.Пользователи.ТекущийЭлемент = Элементы.ПользователиКомментарий;
		Элементы.Пользователи.ИзменитьСтроку();
	Иначе
		Предупреждение(ТекстСообщенияЗначениеУжеДобавленоВСписок());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиПолноеПредставлениеАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = Пользователи.СформироватьДанныеВыбораПользователя(Текст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиПолноеПредставлениеОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = Пользователи.СформироватьДанныеВыбораПользователя(Текст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиКомментарийПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Пользователи.ТекущиеДанные;
	
	ЗаписатьКомментарий(ТекущиеДанные.Пользователь, ТекущиеДанные.Комментарий);
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ДатыЗапретаИзменения"));
	
КонецПроцедуры


&НаКлиенте
Процедура СпособУказанияДатыЗапретаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СпособУказанияДатыЗапретаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	УдалитьЛишнее = Ложь;
	
	Если СпособУказанияДатыЗапрета = ВыбранноеЗначение Тогда
		Возврат;
	КонецЕсли;
	
	Данные = Неопределено;
	ТекущийСпособ = ТекущийСпособУказанияДатыЗапрета(ТекущийПользователь, ЕдинственныйРаздел, ПервыйРаздел, ЗначениеДляВсехПользователей, Данные);
	
	ТекстВопроса = "";
	Если ТекущийСпособ = "ПоРазделамИОбъектам" И ВыбранноеЗначение = "ОбщаяДата" Тогда
		ТекстВопроса = ТекстВопросаУдалитьДатыЗапретаДляРазделовИОбъектов();
		
	ИначеЕсли ТекущийСпособ = "ПоРазделамИОбъектам" И ВыбранноеЗначение = "ПоРазделам"
	      ИЛИ ТекущийСпособ = "ПоОбъектам"          И ВыбранноеЗначение = "ОбщаяДата" Тогда
		ТекстВопроса = ТекстВопросаУдалитьДатыЗапретаДляОбъектов();
		
	ИначеЕсли ТекущийСпособ = "ПоРазделамИОбъектам" И ВыбранноеЗначение = "ПоОбъектам"
	      ИЛИ ТекущийСпособ = "ПоРазделам"          И ВыбранноеЗначение = "ПоОбъектам"
	      ИЛИ ТекущийСпособ = "ПоРазделам"          И ВыбранноеЗначение = "ОбщаяДата" Тогда
		ТекстВопроса = ТекстВопросаУдалитьДатыЗапретаДляРазделов();
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстВопроса) Тогда
		Если Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
			СтандартнаяОбработка = Ложь;
		Иначе
			УдалитьЛишнее = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если СтандартнаяОбработка Тогда
		СпособУказанияДатыЗапрета = ВыбранноеЗначение;
		Если УдалитьЛишнее Тогда
			УдалитьЛишнееПриИзмененииСпособаУказанияДатыЗапрета(ВыбранноеЗначение, ТекущийПользователь, УстановкаДатыЗапрета);
			Элементы.Пользователи.Обновить();
		КонецЕсли;
		ПрочитатьДанныеПользователя(ЭтаФорма, ВыбранноеЗначение, Данные);
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура Разделы(Команда)
	
	ВыбратьРазделы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьОбъекты(Команда)
	
	Если ТекущийПользователь = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщенияСначалаВыберитеПользователя());
		Возврат;
	КонецЕсли;
	
	ВыбратьПодобратьОбъекты(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатыЗапретаИзменить(Команда)
	
	ТекущиеДанные = Элементы.ДатыЗапрета.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Элементы.ДатыЗапрета.ИзменитьСтроку();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатыЗапретаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Элементы.ДатыЗапрета.ИзменитьСтроку();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатыЗапретаПриАктивизацииСтроки(Элемент)
	
	ДатыЗапретаУстановитьДоступностьКоманд(Элементы.ДатыЗапрета.ТекущиеДанные <> Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатыЗапретаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Копирование
	 ИЛИ ВсеРазделыБезОбъектов
	 ИЛИ СпособУказанияДатыЗапрета = "ПоРазделам" Тогда
		//
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ТекущийПользователь = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщенияСначалаВыберитеПользователя());
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ТекущийРаздел = ТекущийРаздел(, Истина);
	Если ТекущийРаздел = РазделПустаяСсылка Тогда
		Предупреждение(ТекстСообщенияВВыбранномРазделеДатыЗапретаДляОбъектовНеУстанавливаются());
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура("Раздел", ТекущийРаздел);
	НайденныеСтроки = ТипыОбъектовРазделов.НайтиСтроки(Отбор);
	
	ТекущиеДанные = Элементы.ДатыЗапрета.ТекущиеДанные;
	
	Если НайденныеСтроки.Количество() > 0 
	   И НайденныеСтроки[0].ТипыОбъекта.Количество() > 0 Тогда
		Если ПоказыватьРазделыТекущегоПользователя Тогда
			Родитель = ТекущиеДанные.ПолучитьРодителя();
			Если НЕ ТекущиеДанные.ЭтоРаздел
			      И Родитель <> Неопределено Тогда
				// Добавление объекта в раздел
				Отказ = Истина;
				Элемент.ТекущаяСтрока = Родитель.ПолучитьИдентификатор();
				Элемент.ДобавитьСтроку();
			КонецЕсли;
		ИначеЕсли Элемент.ТекущаяСтрока <> Неопределено Тогда
			Отказ = Истина;
			Элемент.ТекущаяСтрока = Неопределено;
			Элемент.ДобавитьСтроку();
		КонецЕсли;
	Иначе
		Предупреждение(ТекстСообщенияВВыбранномРазделеДатыЗапретаДляОбъектовНеУстанавливаются());
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатыЗапретаПередНачаломИзменения(Элемент, Отказ)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Поле = Элементы.ДатыЗапрета.ТекущийЭлемент;
	
	РазделБезОбъектов = РазделыБезОбъектов.НайтиПоЗначению(ТекущиеДанные.Раздел) <> Неопределено;
	
	// Переход к доступному полю или открытие формы
	ОткрытьФормуРедактированияДатыЗапрета = Ложь;
	ОткрытьФормуВыбораРазделов = Ложь;
	
	Если Поле = Элементы.ДатыЗапретаПолноеПредставление Тогда
		Если ТекущиеДанные.ЭтоРаздел Тогда
			Если НазначениеДляВсех(ТекущийПользователь) Тогда
				// Все разделы всегда заполнены, их не требуется изменять
				Если ТекущиеДанные.ОписаниеДатыЗапрета <> "ПроизвольнаяДата"
				 ИЛИ Поле = Элементы.ДатыЗапретаОписаниеДатыЗапретаПредставление Тогда
					ОткрытьФормуРедактированияДатыЗапрета = Истина;
				Иначе
					ТекущийЭлемент = Элементы.ДатыЗапретаДатаЗапрета;
					Если НЕ ЗначениеЗаполнено(ТекущиеДанные.Раздел) Тогда
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
							ТекстСообщенияОбщаяДатаМожетБытьУстановлена());
					ИначеЕсли РазделБезОбъектов Тогда
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
							ТекстСообщенияРазделыУжеЗаполненыМожноУстановитьДатыЗапретаДляРазделов());
					Иначе
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
							ТекстСообщенияРазделыУжеЗаполненыМожноУстановитьДатыЗапретаДляРазделовИОбъектов());
					КонецЕсли;
				КонецЕсли;
			Иначе
				ОткрытьФормуВыбораРазделов = Истина;
			КонецЕсли;
			//
		ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.Представление) Тогда
			Если ТекущиеДанные.ОписаниеДатыЗапрета <> "ПроизвольнаяДата"
			 ИЛИ Поле = Элементы.ДатыЗапретаОписаниеДатыЗапретаПредставление Тогда
				ОткрытьФормуРедактированияДатыЗапрета = Истина;
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщенияОбъектУжеВыбранМожноУстановитьДатуЗапрета());
				ТекущийЭлемент = Элементы.ДатыЗапретаДатаЗапрета;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если НЕ ЗначениеЗаполнено(ТекущиеДанные.Представление) Тогда
			// Перед изменением описания или даты запрета
			// требуется заполнить объект, иначе нельзя выполнить запись в регистр
			ТекущийЭлемент = Элементы.ДатыЗапретаПолноеПредставление;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщенияСначалаВыберитеОбъект());
			//
		ИначеЕсли ТекущиеДанные.ОписаниеДатыЗапрета <> "ПроизвольнаяДата"
			  ИЛИ Поле = Элементы.ДатыЗапретаОписаниеДатыЗапретаПредставление Тогда
			ОткрытьФормуРедактированияДатыЗапрета = Истина;
		Иначе
			ТекущийЭлемент = Элементы.ДатыЗапретаДатаЗапрета;
		КонецЕсли;
	КонецЕсли;
	
	// Блокировка записи перед редактированием
	Если ЗначениеЗаполнено(ТекущиеДанные.Представление) Тогда
		ПрочитанныеСвойства = ЗаблокироватьЗаписьПользователя(ЭтаФорма, ТекущийРаздел(), ТекущиеДанные.Объект, ТекущийПользователь);
		ОбновитьЗначенияПрочитанныхСвойств(ТекущиеДанные, ПрочитанныеСвойства, Элементы.Пользователи.ТекущиеДанные);
	КонецЕсли;
	
	Если ОткрытьФормуРедактированияДатыЗапрета Тогда
		Отказ = Истина;
		РедактироватьДатуЗапретаВФорме();
		
	ИначеЕсли ОткрытьФормуВыбораРазделов Тогда
		Отказ = Истина;
		ВыбратьРазделы();
	КонецЕсли;
	
	Если Отказ Тогда
		Элементы.ДатыЗапретаПолноеПредставление.ТолькоПросмотр = Ложь;
		Элементы.ДатыЗапретаОписаниеДатыЗапретаПредставление.ТолькоПросмотр = Ложь;
		Элементы.ДатыЗапретаДатаЗапрета.ТолькоПросмотр = Ложь;
	Иначе
		// Блокировка недоступных полей
		Элементы.ДатыЗапретаПолноеПредставление.ТолькоПросмотр = ЗначениеЗаполнено(ТекущиеДанные.Представление);
		Элементы.ДатыЗапретаОписаниеДатыЗапретаПредставление.ТолькоПросмотр = Истина;
		Элементы.ДатыЗапретаДатаЗапрета.ТолькоПросмотр = НЕ ЗначениеЗаполнено(ТекущиеДанные.Представление)
		                                                 ИЛИ ТекущиеДанные.ОписаниеДатыЗапрета <> "ПроизвольнаяДата";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатыЗапретаПередУдалением(Элемент, Отказ)
	
	Если Элементы.ДатыЗапрета.ВыделенныеСтроки.Количество() > 1 Тогда
		Предупреждение(ТекстСообщенияДляУдаленияВыделитеОднуСтроку());
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ДатыЗапрета.ТекущиеДанные;
	
	Если ТекущиеДанные.ЭтоРаздел Тогда
		Если ЗначениеЗаполнено(ТекущиеДанные.Раздел) Тогда
			Если ТекущиеДанные.ПолучитьЭлементы().Количество() > 0 Тогда
				ТекстВопроса = ТекстВопросаУдалитьДатыЗапретаДляРазделаИОбъектов();
			Иначе
				ТекстВопроса = ТекстВопросаУдалитьДатыЗапретаДляРаздела();
			КонецЕсли;
		Иначе
			ТекстВопроса = ТекстВопросаУдалитьОбщуюДатуЗапрета();
		КонецЕсли;
	Иначе
		ТекстВопроса = ТекстВопросаУдалитьДатуЗапретаОбъекта();
	КонецЕсли;
	
	Если ТекущиеДанные.ЭтоРаздел Тогда
		Если НазначениеДляВсех(ТекущийПользователь) Тогда
			Отказ = Истина;
		КонецЕсли;
		ЭлементыРаздела = ТекущиеДанные.ПолучитьЭлементы();
		Если ДатаЗапретаУстановлена(ТекущиеДанные, ТекущийПользователь)
		 ИЛИ ЭлементыРаздела.Количество() > 0 Тогда
			// Удаление даты запрета для раздела (т.е. всех объектов раздела)
			Если Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
				ОбъектыРаздела = Новый Массив;
				ОбъектыРаздела.Добавить(ТекущиеДанные.Раздел);
				Для каждого ЭлементДанных Из ЭлементыРаздела Цикл
					ОбъектыРаздела.Добавить(ЭлементДанных.Объект);
				КонецЦикла;
				УдалитьЗаписьПользователя(
						ТекущиеДанные.Раздел,
						ОбъектыРаздела,
						ТекущийПользователь);
				ЭлементыРаздела.Очистить();
				ТекущиеДанные.ДатаЗапрета         = '00000000';
				ТекущиеДанные.ОписаниеДатыЗапрета = "ПроизвольнаяДата";
				Если Отказ Тогда
					ТекущиеДанные.ОписаниеДатыЗапретаПредставление = ПредставлениеОписанияДатыЗапрета(ТекущиеДанные);
				КонецЕсли;
				ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ДатыЗапретаИзменения"));
			Иначе
				Отказ = Истина;
			КонецЕсли;
		Иначе
			Если ЗначениеЗаполнено(ТекущиеДанные.Раздел) Тогда
				ТекстСообщения = ТекстСообщенияДляВсехПользователейРазделыВсегдаПоказываются()
								+ Символы.ПС
								+ ТекстСообщенияПриУдаленииДатаЗапретаОчищается();
			Иначе
				ТекстСообщения = ТекстСообщенияДляВсехПользователейОбщаяДатаВсегдаПоказывается()
								+ Символы.ПС
								+ ТекстСообщенияПриУдаленииДатаЗапретаОчищается();
			КонецЕсли;
			Предупреждение(ТекстСообщения);
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(ТекущиеДанные.Представление)
		   И ДатаЗапретаУстановлена(ТекущиеДанные, ТекущийПользователь) Тогда
			// Удаление даты запрета для объекта по разделу
			Если Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
				УдалитьЗаписьПользователя(
					ТекущийРаздел(),
					ТекущиеДанные.Объект,
					ТекущийПользователь);
				ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ДатыЗапретаИзменения"));
			Иначе
				Отказ = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатыЗапретаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Если НЕ Элементы.ДатыЗапрета.ТекущиеДанные.ЭтоРаздел Тогда
			Элементы.ДатыЗапрета.ТекущиеДанные.Раздел = ТекущийРаздел(, Истина);
		КонецЕсли;
		Если НазначениеДляВсех(ТекущийПользователь)
		 ИЛИ НЕ Элементы.ДатыЗапрета.ТекущиеДанные.ЭтоРаздел Тогда
			Элементы.ДатыЗапрета.ТекущиеДанные.ОписаниеДатыЗапрета = "ПроизвольнаяДата";
		КонецЕсли;
		Элементы.ДатыЗапрета.ТекущиеДанные.ОписаниеДатыЗапретаПредставление
			= ПредставлениеОписанияДатыЗапрета(Элементы.ДатыЗапрета.ТекущиеДанные);
		ПодключитьОбработчикОжидания("ОбработчикОжиданияВыбратьОбъекты", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатыЗапретаПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТекущиеДанные = Элементы.ДатыЗапрета.ТекущиеДанные;
	
	Если ТекущийПользователь <> Неопределено Тогда
		ЗаписатьОписаниеИДатуЗапрета(ТекущиеДанные);
	КонецЕсли;
	
	Если УстановленныеБлокировки.Количество() > 0 Тогда
		РазблокироватьВсеЗаписи(ЭтаФорма);
	КонецЕсли;
	
	Элементы.ДатыЗапретаПолноеПредставление.ТолькоПросмотр = Ложь;
	Элементы.ДатыЗапретаОписаниеДатыЗапретаПредставление.ТолькоПросмотр = Ложь;
	Элементы.ДатыЗапретаОписаниеДатыЗапретаПредставление.ТолькоПросмотр = Ложь;
	
	ОбновитьНаличиеДатЗапретаТекущегоПользователя();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатыЗапретаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ДатыЗапрета.ТекущиеДанные;
	//
	Если ТекущиеДанные <> Неопределено
	   И ТекущиеДанные.Объект = ВыбранноеЗначение Тогда
		//
		Возврат;
	КонецЕсли;
	
	ИдентификаторРаздела = Неопределено;
	//
	Если ПоказыватьРазделыТекущегоПользователя Тогда
		Родитель = ТекущиеДанные.ПолучитьРодителя();
		Если Родитель = Неопределено Тогда
			КоллекцияОбъектов    = ТекущиеДанные.ПолучитьЭлементы();
			ИдентификаторРаздела = ТекущиеДанные.ПолучитьИдентификатор();
		Иначе
			КоллекцияОбъектов    = Родитель.ПолучитьЭлементы();
			ИдентификаторРаздела = Родитель.ПолучитьИдентификатор();
		КонецЕсли;
	Иначе
		КоллекцияОбъектов = ДатыЗапрета.ПолучитьЭлементы();
	КонецЕсли;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		Объекты = ВыбранноеЗначение;
	Иначе
		Объекты = Новый Массив;
		Объекты.Добавить(ВыбранноеЗначение);
	КонецЕсли;
	
	ОбъектыДляДобавления = Новый Массив;
	Для каждого Объект Из Объекты Цикл
		ЗначениеНеНайдено = Истина;
		Для каждого Строка Из КоллекцияОбъектов Цикл
			Если Строка.Объект = Объект Тогда
				ЗначениеНеНайдено = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ЗначениеНеНайдено Тогда
			ОбъектыДляДобавления.Добавить(Объект);
		КонецЕсли;
	КонецЦикла;
	
	Если ОбъектыДляДобавления.Количество() > 0 Тогда
		Если ТекущийПользователь <> Неопределено Тогда
			Комментарий = КомментарийТекущегоПользователя(ЭтаФорма);
			ЗаблокироватьИЗаписатьПустыеДаты(ТекущийРаздел(, Истина), ОбъектыДляДобавления, ТекущийПользователь, Комментарий);
			ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ДатыЗапретаИзменения"));
		КонецЕсли;
		Для каждого ТекущийОбъект Из ОбъектыДляДобавления Цикл
			ОписаниеОбъекта = КоллекцияОбъектов.Добавить();
			ОписаниеОбъекта.Раздел        = ТекущийРаздел(, Истина);
			ОписаниеОбъекта.Объект        = Объект;
			ОписаниеОбъекта.Представление = Строка(Объект);
			ОписаниеОбъекта.ПолноеПредставление = ОписаниеОбъекта.Представление;
			ОписаниеОбъекта.ОписаниеДатыЗапрета = "ПроизвольнаяДата";
			ОписаниеОбъекта.ОписаниеДатыЗапретаПредставление =
				ПредставлениеОписанияДатыЗапрета(ОписаниеОбъекта);
		КонецЦикла;
		Если ИдентификаторРаздела <> Неопределено Тогда
			Элементы.ДатыЗапрета.Развернуть(ИдентификаторРаздела, Истина);
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьНаличиеДатЗапретаТекущегоПользователя();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатыЗапретаПолноеПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыбратьПодобратьОбъекты();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатыЗапретаПолноеПредставлениеОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатыЗапретаПолноеПредставлениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ДатыЗапрета.ТекущиеДанные;
	
	Если ТекущиеДанные.Объект = ВыбранноеЗначение Тогда
		Возврат;
	КонецЕсли;
	
	// Замена объекта возможна только другой объект, которого еще нет в списке
	Если ПоказыватьРазделыТекущегоПользователя Тогда
		КоллекцияОбъектов = ТекущиеДанные.ПолучитьРодителя().ПолучитьЭлементы();
	Иначе
		КоллекцияОбъектов = ДатыЗапрета.ПолучитьЭлементы();
	КонецЕсли;
	ЗначениеНеНайдено = Истина;
	Для каждого Строка Из КоллекцияОбъектов Цикл
		Если Строка.Объект = ВыбранноеЗначение Тогда
			ЗначениеНеНайдено = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	//
	Если ЗначениеНеНайдено Тогда
		Если ТекущиеДанные.Объект <> ВыбранноеЗначение Тогда
			ЗначенияСвойств = ПолучитьТекущиеЗначенияСвойств(ТекущиеДанные, Элементы.Пользователи.ТекущиеДанные);
			Если НЕ ЗаменитьОбъектВЗаписиПользователяНаСервере(
						ТекущиеДанные.Раздел,
						ТекущиеДанные.Объект,
						ВыбранноеЗначение,
						ТекущийПользователь,
						ЗначенияСвойств) Тогда
				//
				Предупреждение(ТекстСообщенияЗначениеУжеДобавленоВСписок()
				               + Символы.ПС
				               + ТекстСообщенияОбновитьДанныеФормыF5());
				Возврат;
			Иначе
				ОбновитьЗначенияПрочитанныхСвойств(ТекущиеДанные, ЗначенияСвойств, Элементы.Пользователи.ТекущиеДанные);
				ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ДатыЗапретаИзменения"));
			КонецЕсли;
		КонецЕсли;
		// Установка выбранного объекта
		ТекущиеДанные.Объект = ВыбранноеЗначение;
		ТекущиеДанные.Представление = Строка(ТекущиеДанные.Объект);
		ТекущиеДанные.ПолноеПредставление = ТекущиеДанные.Представление;
		Элементы.ДатыЗапрета.ЗакончитьРедактированиеСтроки(Ложь);
		Элементы.ДатыЗапрета.ТекущийЭлемент = Элементы.ДатыЗапретаДатаЗапрета;
		Элементы.ДатыЗапрета.ИзменитьСтроку();
	Иначе
		Предупреждение(ТекстСообщенияЗначениеУжеДобавленоВСписок());
	КонецЕсли;
	
	ОбновитьНаличиеДатЗапретаТекущегоПользователя();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатыЗапретаДатаЗапретаПриИзменении(Элемент)
	
	ЗаписатьОписаниеИДатуЗапрета();
	
	ОбновитьНаличиеДатЗапретаТекущегоПользователя();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчки событий этой формы и формы РедактированиеДатыЗапрета
//

&НаКлиенте
Процедура ОписаниеДатыЗапретаПриИзменении(Элемент)
	
	ОбщаяДатаЗапретаСОписаниемПриИзменении(ЭтаФорма);
	ЗаписатьОбщуюДатуЗапретаСОписанием();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаЗапретаПриИзменении(Элемент)
	
	ОбщаяДатаЗапретаСОписаниемПриИзменении(ЭтаФорма);
	ЗаписатьОбщуюДатуЗапретаСОписанием();
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьИзменениеДанныхДоДатыЗапретаПриИзменении(Элемент)
	
	ОбщаяДатаЗапретаСОписаниемПриИзменении(ЭтаФорма);
	ЗаписатьОбщуюДатуЗапретаСОписанием();
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоДнейРазрешенияПриИзменении(Элемент)
	
	ОбщаяДатаЗапретаСОписаниемПриИзменении(ЭтаФорма);
	ЗаписатьОбщуюДатуЗапретаСОписанием();
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоДнейРазрешенияАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	КоличествоДнейРазрешения = Текст;
	
	ОбщаяДатаЗапретаСОписаниемПриИзменении(ЭтаФорма);
	ЗаписатьОбщуюДатуЗапретаСОписанием();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры и функции формы
//

&НаСервере
Процедура ПрочитатьПользователей()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВсеРазделыБезОбъектов", ВсеРазделыБезОбъектов);
	Запрос.УстановитьПараметр("ДатыЗапретаЗагрузкиДанных", Параметры.ДатыЗапретаЗагрузкиДанных);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПРЕДСТАВЛЕНИЕ(ДатыЗапретаИзменения.Пользователь) КАК ПолноеПредставление,
	|	ДатыЗапретаИзменения.Пользователь,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Пользователь) = ТИП(Перечисление.ВидыНазначенияДатЗапрета)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ОбщееНазначение,
	|	ПРЕДСТАВЛЕНИЕ(ДатыЗапретаИзменения.Пользователь) КАК Представление,
	|	МАКСИМУМ(ДатыЗапретаИзменения.ДатаЗапрета) КАК ДатаЗапрета,
	|	МАКСИМУМ(ДатыЗапретаИзменения.ОписаниеДатыЗапрета) КАК ОписаниеДатыЗапрета,
	|	МАКСИМУМ(ДатыЗапретаИзменения.Комментарий) КАК Комментарий,
	|	ЛОЖЬ КАК БезДатыЗапрета,
	|	-1 КАК НомерКартинки
	|ИЗ
	|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|ГДЕ
	|	ДатыЗапретаИзменения.Объект <> НЕОПРЕДЕЛЕНО
	|	И (НЕ(ДатыЗапретаИзменения.Раздел <> ДатыЗапретаИзменения.Объект
	|				И ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Объект) = ТИП(ПланВидовХарактеристик.РазделыДатЗапретаИзменения)))
	|	И (НЕ(ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Объект) <> ТИП(ПланВидовХарактеристик.РазделыДатЗапретаИзменения)
	|				И &ВсеРазделыБезОбъектов))
	|
	|СГРУППИРОВАТЬ ПО
	|	ДатыЗапретаИзменения.Пользователь
	|
	|ИМЕЮЩИЕ
	|	ДатыЗапретаИзменения.Пользователь <> НЕОПРЕДЕЛЕНО И
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Пользователь) = ТИП(Справочник.Пользователи)
	|				ИЛИ ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Пользователь) = ТИП(Справочник.ГруппыПользователей)
	|				ИЛИ ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Пользователь) = ТИП(Справочник.ВнешниеПользователи)
	|				ИЛИ ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Пользователь) = ТИП(Справочник.ГруппыВнешнихПользователей)
	|				ИЛИ ДатыЗапретаИзменения.Пользователь = ЗНАЧЕНИЕ(Перечисление.ВидыНазначенияДатЗапрета.ДляВсехПользователей)
	|			ТОГДА &ДатыЗапретаЗагрузкиДанных = ЛОЖЬ
	|		ИНАЧЕ &ДатыЗапретаЗагрузкиДанных = ИСТИНА
	|	КОНЕЦ";
	
	// Некорректные записи исключаются из выборки условиями:
	// - объект со значением типа ПВХ.РазделыДатЗапретаИзменения может быть только равным разделу
	Выгрузка = Запрос.Выполнить().Выгрузить();
	
	// Заполнение полного представления пользователей
	Для каждого Строка Из Выгрузка Цикл
		Строка.Представление       = ТекстПредставленияПользователя(ЭтаФорма, Строка.Пользователь);
		Строка.ПолноеПредставление = Строка.Представление;
	КонецЦикла;
	
	// Заполнение представления всех пользователей
	ОписаниеВсехПользователей = Выгрузка.Найти(ЗначениеДляВсехПользователей, "Пользователь");
	Если ОписаниеВсехПользователей = Неопределено Тогда
		ОписаниеВсехПользователей = Выгрузка.Вставить(0);
		ОписаниеВсехПользователей.Пользователь = ЗначениеДляВсехПользователей;
		ОписаниеВсехПользователей.БезДатыЗапрета = Истина;
	КонецЕсли;
	ОписаниеВсехПользователей.Представление       = ТекстПредставленияДляВсехПользователей(ЭтаФорма);
	ОписаниеВсехПользователей.ПолноеПредставление = ОписаниеВсехПользователей.Представление;
	ОписаниеВсехПользователей.Комментарий         = ТекстКомментарияДляВсехПользователей();
	
	Выгрузка.Сортировать("ОбщееНазначение Возр, ПолноеПредставление Возр");
	ЗначениеВРеквизитФормы(Выгрузка, "ПользователиДатЗапрета");
	
	ЗаполнитьНомераКартинокПользователейДатЗапрета();
	
	ТекущийПользователь = ЗначениеДляВсехПользователей;
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДанныеПользователя()
	
	Если ПоказыватьРазделыТекущегоПользователя Тогда
		Для каждого ОписаниеРаздела Из ДатыЗапрета.ПолучитьЭлементы() Цикл
			Элементы.ДатыЗапрета.Развернуть(ОписаниеРаздела.ПолучитьИдентификатор(), Истина);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПрочитатьДанныеПользователя(Контекст, ТекущийСпособУказания = Неопределено, Данные = Неопределено)
	
	
	Если Контекст.УстановкаДатыЗапрета = "НетЗапрета" Тогда
		//
		РазблокироватьВсеЗаписи(Контекст);
		Возврат;
		//
	ИначеЕсли Контекст.УстановкаДатыЗапрета = "ПоПользователям" Тогда
		НайденныеСтроки = Контекст.ПользователиДатЗапрета.НайтиСтроки(Новый Структура("Пользователь", Контекст.ТекущийПользователь));
		Если НайденныеСтроки.Количество() > 0 Тогда
			Контекст.ТекущийПользовательПредставление = НайденныеСтроки[0].Представление;
		КонецЕсли;
	КонецЕсли;
	Контекст.Элементы.ДанныеПользователя.ТекущаяСтраница = Контекст.Элементы.ПользовательВыбран;
	Контекст.ДатыЗапрета.ПолучитьЭлементы().Очистить();
	
	Если ТекущийСпособУказания = Неопределено Тогда
		ТекущийСпособУказания = ТекущийСпособУказанияДатыЗапрета(Контекст.ТекущийПользователь, Контекст.ЕдинственныйРаздел, Контекст.ПервыйРаздел, Контекст.ЗначениеДляВсехПользователей, Данные);
		ТекущийСпособУказания = ?(ТекущийСпособУказания = "", "ОбщаяДата", ТекущийСпособУказания);
		Если Контекст.СпособУказанияДатыЗапрета <> ТекущийСпособУказания Тогда
			Контекст.СпособУказанияДатыЗапрета = ТекущийСпособУказания;
		КонецЕсли;
	КонецЕсли;
	
	Если Контекст.СпособУказанияДатыЗапрета = "ОбщаяДата" Тогда
		Контекст.Элементы.СпособыУказания.ТекущаяСтраница = Контекст.Элементы.СпособУказанияОбщаяДата;
		
		ЗаполнитьЗначенияСвойств(Контекст, Данные);
		Контекст.РазрешитьИзменениеДанныхДоДатыЗапрета = Контекст.КоличествоДнейРазрешения <> 0;
		ОбщаяДатаЗапретаСОписаниемПриИзменении(Контекст, Ложь);
		Контекст.Элементы.ОписаниеДатыЗапрета.ТолькоПросмотр = Ложь;
		Контекст.Элементы.ДатаЗапрета.ТолькоПросмотр = Ложь;
		Контекст.Элементы.РазрешитьИзменениеДанныхДоДатыЗапрета.ТолькоПросмотр = Ложь;
		Контекст.Элементы.КоличествоДнейРазрешения.ТолькоПросмотр = Ложь;
		Попытка
			ЗаблокироватьЗаписьПользователя(Контекст, Контекст.РазделПустаяСсылка, Контекст.РазделПустаяСсылка, Контекст.ТекущийПользователь, Истина);
		Исключение
			Контекст.Элементы.ОписаниеДатыЗапрета.ТолькоПросмотр = Истина;
			Контекст.Элементы.ДатаЗапрета.ТолькоПросмотр = Истина;
			Контекст.Элементы.РазрешитьИзменениеДанныхДоДатыЗапрета.ТолькоПросмотр = Истина;
			Контекст.Элементы.КоличествоДнейРазрешения.ТолькоПросмотр = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		Возврат;
	КонецЕсли;
	
	Контекст.Элементы.СпособыУказания.ТекущаяСтраница = Контекст.Элементы.СпособУказанияПоРазделамОбъектам;
	УстановитьКоманднуюПанельДатЗапрета(Контекст);
	
	ДанныеПользователя = Неопределено;
	ПрочитатьДанныеПользователяНаСервере(
					Контекст.ТекущийПользователь,
					Контекст.ПервыйРаздел,
					ДанныеПользователя,
					Контекст.ПоказыватьРазделы,
					Контекст.ПоказыватьРазделыТекущегоПользователя,
					Контекст.БезРазделовИОбъектов,
					Контекст.ВсеРазделыБезОбъектов,
					Контекст.ЕдинственныйРаздел,
					Контекст.РазделыБезОбъектов,
					ПолучитьЗначенияКлючейЗаблокированныхЗаписей(Контекст),
					Контекст.УникальныйИдентификатор,
					Контекст.СпособУказанияДатыЗапрета,
					Контекст.ЗначениеДляВсехПользователей);
	
	// Загрузка данных пользователя в коллекцию
	КоллекцияСтрок = Контекст.ДатыЗапрета.ПолучитьЭлементы();
	КоллекцияСтрок.Очистить();
	Для каждого Строка Из ДанныеПользователя Цикл
		НоваяСтрока = КоллекцияСтрок.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка.Значение);
		НоваяСтрока.ОписаниеДатыЗапретаПредставление = ПредставлениеОписанияДатыЗапрета(НоваяСтрока);
		КоллекцияПодстрок = НоваяСтрока.ПолучитьЭлементы();
		Для каждого Подстрока Из Строка.Значение.СписокПодстрок Цикл
			НоваяПодстрока = КоллекцияПодстрок.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяПодстрока, Подстрока.Значение);
			ЗаполнитьПоВнутреннемуОписаниюДатыЗапрета(НоваяПодстрока, НоваяПодстрока.ОписаниеДатыЗапрета);
			НоваяПодстрока.ОписаниеДатыЗапретаПредставление = ПредставлениеОписанияДатыЗапрета(НоваяПодстрока);
		КонецЦикла;
		Если НоваяСтрока.ЭтоРаздел Тогда
			НоваяСтрока.РазделБезОбъектов = Контекст.РазделыБезОбъектов.НайтиПоЗначению(НоваяСтрока.Раздел) <> Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	// Настройка поля формы ДатыЗапрета
	Если Контекст.ПоказыватьРазделыТекущегоПользователя Тогда
		Если Контекст.ВсеРазделыБезОбъектов Тогда
			// Используется дата только по измерению Раздел
			// Измерение Объект заполнено значением измерения Раздел
			// Отборажение объекта не требуется
			Контекст.Элементы.ДатыЗапретаПолноеПредставление.Заголовок = ТекстЗаголовкаРаздела();
			Контекст.Элементы.ДатыЗапрета.Отображение = ОтображениеТаблицы.Список;
			//
		Иначе
			Контекст.Элементы.ДатыЗапретаПолноеПредставление.Заголовок = ТекстЗаголовкаРазделаСОбъектами();
			Контекст.Элементы.ДатыЗапрета.Отображение = ОтображениеТаблицы.Дерево;
		КонецЕсли;
	Иначе
		ПредставленияТиповОбъектов = "";
		Для каждого ОписаниеТипаОбъекта Из Контекст.ТипыОбъектовРазделов[0].ТипыОбъекта Цикл
			ПредставленияТиповОбъектов = СокрЛП(   ПредставленияТиповОбъектов
												 + Символы.ПС
												 + ОписаниеТипаОбъекта.Представление );
		КонецЦикла;
		Контекст.Элементы.ДатыЗапретаПолноеПредставление.Заголовок = ПредставленияТиповОбъектов;
		Контекст.Элементы.ДатыЗапрета.Отображение = ОтображениеТаблицы.Список;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПрочитатьДанныеПользователяНаСервере(Знач Пользователь,
                                               Знач ПервыйРаздел,
                                               ДанныеПользователя,
                                               ПоказыватьРазделы,
                                               ПоказыватьРазделыТекущегоПользователя,
                                               БезРазделовИОбъектов,
                                               ВсеРазделыБезОбъектов,
                                               ЕдинственныйРаздел,
                                               РазделыБезОбъектов,
                                               ЗначенияКлючейЗаблокированныхЗаписей,
                                               ИдентификаторФормы,
                                               СпособУказанияДатыЗапрета,
                                               ЗначениеДляВсехПользователей)
	
	УстановитьПривилегированныйРежим(Истина);
	
	РазблокироватьВсеЗаписиНаСервере(ЗначенияКлючейЗаблокированныхЗаписей, ИдентификаторФормы);
	
	ПоказыватьРазделыТекущегоПользователя
			= ПоказыватьРазделы
			ИЛИ СпособУказанияДатыЗапрета = "ПоРазделам"
			ИЛИ СпособУказанияДатыЗапрета = "ПоРазделамИОбъектам";
	
	// Подготовка дерева значений дат запрета изменения
	Если ПоказыватьРазделыТекущегоПользователя Тогда
		ПрочитанныеДатыЗапрета = ПрочитатьДанныеПользователяСРазделами(Пользователь, ВсеРазделыБезОбъектов, РазделыБезОбъектов, ЗначениеДляВсехПользователей);
	Иначе
		ПрочитанныеДатыЗапрета = ПрочитатьДанныеПользователяБезРазделов(Пользователь, ПервыйРаздел, ЗначениеДляВсехПользователей);
	КонецЕсли;
	
	ДанныеПользователя = Новый СписокЗначений;
	ПоляСтроки = "ПолноеПредставление, Представление, Раздел, Объект, ДатаЗапрета, ОписаниеДатыЗапрета, КоличествоДнейРазрешения, БезДатыЗапрета, ЭтоРаздел, СписокПодстрок";
	Для каждого Строка Из ПрочитанныеДатыЗапрета.Строки Цикл
		СтруктураСтроки = Новый Структура(ПоляСтроки);
		ЗаполнитьЗначенияСвойств(СтруктураСтроки, Строка);
		СтруктураСтроки.СписокПодстрок = Новый СписокЗначений;
		Для каждого Подстрока Из Строка.Строки Цикл
			СтруктураПодстроки = Новый Структура(ПоляСтроки);
			ЗаполнитьЗначенияСвойств(СтруктураПодстроки, Подстрока);
			СтруктураСтроки.СписокПодстрок.Добавить(СтруктураПодстроки);
		КонецЦикла;
		ДанныеПользователя.Добавить(СтруктураСтроки);
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПрочитатьДанныеПользователяСРазделами(Знач Пользователь,
                                              Знач ВсеРазделыБезОбъектов,
                                              Знач РазделыБезОбъектов,
                                              Знач ЗначениеДляВсехПользователей)
	
	// Подготовка дерева значений дат запрета изменения
	// с первым уровнем по разделам
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Пользователь",                 Пользователь);
	Запрос.УстановитьПараметр("ВсеРазделыБезОбъектов",        ВсеРазделыБезОбъектов);
	Запрос.УстановитьПараметр("ОбщаяДатаПредставление",       ТекстПредставленияОбщейДаты());
	Запрос.УстановитьПараметр("ЗначениеДляВсехПользователей", ЗначениеДляВсехПользователей);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Разделы.Ссылка КАК Раздел,
	|	Разделы.Предопределенный КАК Предопределенный,
	|	Разделы.Представление КАК РазделПредставление,
	|	ДатыЗапретаИзменения.Объект,
	|	ПРЕДСТАВЛЕНИЕ(ДатыЗапретаИзменения.Объект) КАК ПолноеПредставление,
	|	ПРЕДСТАВЛЕНИЕ(ДатыЗапретаИзменения.Объект) КАК Представление,
	|	ВЫБОР
	|		КОГДА ДатыЗапретаИзменения.Объект ЕСТЬ NULL 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК БезДатыЗапрета,
	|	ДатыЗапретаИзменения.ДатаЗапрета,
	|	ДатыЗапретаИзменения.ОписаниеДатыЗапрета,
	|	ЛОЖЬ КАК ЭтоРаздел,
	|	0 КАК КоличествоДнейРазрешения
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.РазделыДатЗапретаИзменения.ПустаяСсылка) КАК Ссылка,
	|		&ОбщаяДатаПредставление КАК Представление,
	|		ЛОЖЬ КАК Предопределенный
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		РазделыДатЗапретаИзменения.Ссылка,
	|		РазделыДатЗапретаИзменения.Наименование,
	|		NULL
	|	ИЗ
	|		ПланВидовХарактеристик.РазделыДатЗапретаИзменения КАК РазделыДатЗапретаИзменения
	|	ГДЕ
	|		РазделыДатЗапретаИзменения.Предопределенный
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ДатыЗапретаИзменения.Раздел,
	|		ДатыЗапретаИзменения.Раздел.Наименование,
	|		NULL
	|	ИЗ
	|		РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|	ГДЕ
	|		ДатыЗапретаИзменения.Раздел <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.РазделыДатЗапретаИзменения.ПустаяСсылка)) КАК Разделы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|		ПО Разделы.Ссылка = ДатыЗапретаИзменения.Раздел
	|			И (ДатыЗапретаИзменения.Пользователь = &Пользователь)
	|			И (ДатыЗапретаИзменения.Объект <> НЕОПРЕДЕЛЕНО)
	|			И ((НЕ(ДатыЗапретаИзменения.Раздел <> ДатыЗапретаИзменения.Объект
	|					И ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Объект) = ТИП(ПланВидовХарактеристик.РазделыДатЗапретаИзменения))))
	|			И ((НЕ(ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Объект) <> ТИП(ПланВидовХарактеристик.РазделыДатЗапретаИзменения)
	|					И &ВсеРазделыБезОбъектов)))
	|ГДЕ
	|	(НЕ(&Пользователь <> &ЗначениеДляВсехПользователей
	|				И ДатыЗапретаИзменения.Раздел ЕСТЬ NULL ))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Предопределенный УБЫВ,
	|	РазделПредставление
	|ИТОГИ
	|	МАКСИМУМ(Предопределенный),
	|	МАКСИМУМ(РазделПредставление),
	|	МИНИМУМ(БезДатыЗапрета),
	|	МАКСИМУМ(ЭтоРаздел)
	|ПО
	|	Раздел";
	
	ПрочитанныеДатыЗапрета = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Для каждого Строка Из ПрочитанныеДатыЗапрета.Строки Цикл
		Строка.Представление = Строка.РазделПредставление;
		Строка.Объект    = Строка.Раздел;
		Строка.ЭтоРаздел = Истина;
		СтрокаРаздела = Строка.Строки.Найти(Строка.Раздел, "Объект");
		Если СтрокаРаздела <> Неопределено Тогда
			Строка.ДатаЗапрета = СтрокаРаздела.ДатаЗапрета;
			Если ЗначениеЗаполнено(СтрокаРаздела.ОписаниеДатыЗапрета) Тогда
				ЗаполнитьПоВнутреннемуОписаниюДатыЗапрета(Строка, СтрокаРаздела.ОписаниеДатыЗапрета);
			Иначе
				Строка.ОписаниеДатыЗапрета = "ПроизвольнаяДата";
			КонецЕсли;
			Строка.Строки.Удалить(СтрокаРаздела);
		Иначе
			Если НазначениеДляВсех(Пользователь) Тогда
				Строка.ОписаниеДатыЗапрета = "ПроизвольнаяДата";
			КонецЕсли;
			Если Строка.Строки.Количество() = 1
			   И Строка.Строки[0].Объект = Null Тогда
				Строка.Строки.Удалить(Строка.Строки[0]);
			КонецЕсли;
		КонецЕсли;
		Строка.ПолноеПредставление = Строка.Представление;
	КонецЦикла;
	
	Возврат ПрочитанныеДатыЗапрета;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПрочитатьДанныеПользователяБезРазделов(Знач Пользователь, Знач ПервыйРаздел, Знач ЗначениеДляВсехПользователей)
	
	// Дерево значений с первым уровнем по объектам
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Пользователь",                 Пользователь);
	Запрос.УстановитьПараметр("ПервыйРаздел",                 ПервыйРаздел);
	Запрос.УстановитьПараметр("ОбщаяДатаПредставление",       ТекстПредставленияОбщейДаты());
	Запрос.УстановитьПараметр("ЗначениеДляВсехПользователей", ЗначениеДляВсехПользователей);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.РазделыДатЗапретаИзменения.ПустаяСсылка) КАК Раздел,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.РазделыДатЗапретаИзменения.ПустаяСсылка) КАК Объект,
	|	&ОбщаяДатаПредставление КАК ПолноеПредставление,
	|	&ОбщаяДатаПредставление КАК Представление,
	|	ЕСТЬNULL(ОбщаяДата.ДатаЗапрета, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК ДатаЗапрета,
	|	ЕСТЬNULL(ОбщаяДата.ОписаниеДатыЗапрета, """") КАК ОписаниеДатыЗапрета,
	|	ИСТИНА КАК ЭтоРаздел,
	|	0 КАК КоличествоДнейРазрешения
	|ИЗ
	|	(ВЫБРАТЬ
	|		ИСТИНА КАК ЗначениеИстина) КАК Значение
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ДатыЗапретаИзменения.ДатаЗапрета КАК ДатаЗапрета,
	|			ДатыЗапретаИзменения.ОписаниеДатыЗапрета КАК ОписаниеДатыЗапрета
	|		ИЗ
	|			РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|		ГДЕ
	|			ДатыЗапретаИзменения.Пользователь = &Пользователь
	|			И ДатыЗапретаИзменения.Раздел = ЗНАЧЕНИЕ(ПланВидовХарактеристик.РазделыДатЗапретаИзменения.ПустаяСсылка)
	|			И ДатыЗапретаИзменения.Объект = ЗНАЧЕНИЕ(ПланВидовХарактеристик.РазделыДатЗапретаИзменения.ПустаяСсылка)) КАК ОбщаяДата
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &Пользователь = &ЗначениеДляВсехПользователей
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ (НЕ ОбщаяДата.ДатаЗапрета ЕСТЬ NULL )
	|		КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&ПервыйРаздел,
	|	ДатыЗапретаИзменения.Объект,
	|	ПРЕДСТАВЛЕНИЕ(ДатыЗапретаИзменения.Объект),
	|	ПРЕДСТАВЛЕНИЕ(ДатыЗапретаИзменения.Объект),
	|	ДатыЗапретаИзменения.ДатаЗапрета,
	|	ДатыЗапретаИзменения.ОписаниеДатыЗапрета,
	|	ЛОЖЬ,
	|	0
	|ИЗ
	|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|ГДЕ
	|	ДатыЗапретаИзменения.Пользователь = &Пользователь
	|	И ДатыЗапретаИзменения.Раздел = &ПервыйРаздел
	|	И ДатыЗапретаИзменения.Объект <> НЕОПРЕДЕЛЕНО
	|	И ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Объект) <> ТИП(ПланВидовХарактеристик.РазделыДатЗапретаИзменения)";
	
	ПрочитанныеДатыЗапрета = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Индекс = ПрочитанныеДатыЗапрета.Строки.Количество()-1;
	Пока Индекс >= 0 Цикл
		Строка = ПрочитанныеДатыЗапрета.Строки[Индекс];
		ЗаполнитьПоВнутреннемуОписаниюДатыЗапрета(Строка, Строка.ОписаниеДатыЗапрета);
		Индекс = Индекс - 1;
	КонецЦикла;
	
	Возврат ПрочитанныеДатыЗапрета;
	
КонецФункции


&НаСервере
Процедура ЗаблокироватьНаборЗаписейПользователя(Пользователь, ТекущаяСтрока = Неопределено)
	
	// Перед началом изменения комментария
	// Перед установкой нового выбранного пользователя
	// Перед удалением текущей строки пользователя
	
	Если ТекущаяСтрока = Неопределено Тогда
		ТекущиеДанные = Неопределено;
	Иначе
		ТекущиеДанные = ПользователиДатЗапрета.НайтиПоИдентификатору(ТекущаяСтрока);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДатыЗапретаИзменения.Раздел,
	|	ДатыЗапретаИзменения.Объект,
	|	ДатыЗапретаИзменения.Пользователь,
	|	ДатыЗапретаИзменения.ДатаЗапрета,
	|	ДатыЗапретаИзменения.ОписаниеДатыЗапрета,
	|	ДатыЗапретаИзменения.Комментарий
	|ИЗ
	|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|ГДЕ
	|	ДатыЗапретаИзменения.Пользователь = &Пользователь";
	
	Выгрузка = Запрос.Выполнить().Выгрузить();
	
	ЗначенияКлючаЗаписи = Новый Структура("Раздел, Объект, Пользователь");
	
	Попытка
		Для каждого ОписаниеЗаписи Из Выгрузка Цикл
			//
			ЗаполнитьЗначенияСвойств(ЗначенияКлючаЗаписи, ОписаниеЗаписи);
			НайденныеСтроки = УстановленныеБлокировки.НайтиСтроки(ЗначенияКлючаЗаписи);
			Если НайденныеСтроки.Количество() = 0 Тогда
				// Добавление новой блокировки
				КлючЗаписи = РегистрыСведений.ДатыЗапретаИзменения.СоздатьКлючЗаписи(ЗначенияКлючаЗаписи);
				ЗаблокироватьДанныеДляРедактирования(КлючЗаписи, , УникальныйИдентификатор);
				ЗаполнитьЗначенияСвойств(УстановленныеБлокировки.Добавить(), ЗначенияКлючаЗаписи);
				Если ТекущиеДанные <> Неопределено Тогда
					// Повторное чтение полей ДатаЗапрета, ОписаниеДатыЗапрета, Комментарий
					Если БезРазделовИОбъектов Тогда
						Если ОписаниеЗаписи.Раздел = РазделПустаяСсылка
						   И ОписаниеЗаписи.Объект = РазделПустаяСсылка Тогда
							ТекущиеДанные.ДатаЗапрета       = ОписаниеЗаписи.ДатаЗапрета;
							ТекущиеДанные.ОписаниеДатыЗапрета = ОписаниеЗаписи.ОписаниеДатыЗапрета;
							ТекущиеДанные.Комментарий = ОписаниеЗаписи.Комментарий;
						КонецЕсли;
					Иначе
						ТекущиеДанные.Комментарий = ОписаниеЗаписи.Комментарий;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	Исключение
		РазблокироватьВсеЗаписи(ЭтаФорма);
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РазблокироватьВсеЗаписи(Знач Контекст)
	
	РазблокироватьВсеЗаписиНаСервере(ПолучитьЗначенияКлючейЗаблокированныхЗаписей(Контекст), Контекст.УникальныйИдентификатор);
	
	Контекст.УстановленныеБлокировки.Очистить();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьЗначенияКлючейЗаблокированныхЗаписей(Знач Контекст)
	
	ЗначенияКлючей = Новый Массив;
	
	Для каждого ОписаниеБлокировки Из Контекст.УстановленныеБлокировки Цикл
		//
		ЗначенияКлючаЗаписи = Новый Структура("Раздел, Объект, Пользователь");
		ЗаполнитьЗначенияСвойств(ЗначенияКлючаЗаписи, ОписаниеБлокировки);
		ЗначенияКлючей.Добавить(ЗначенияКлючаЗаписи);
	КонецЦикла;
	
	Возврат ЗначенияКлючей
	
КонецФункции


&НаСервереБезКонтекста
Процедура РазблокироватьВсеЗаписиНаСервере(ЗначенияКлючейЗаблокированныхЗаписей, ИдентификаторФормы)
	
	Для каждого ЗначенияКлючаЗаписи Из ЗначенияКлючейЗаблокированныхЗаписей Цикл
		КлючЗаписи = РегистрыСведений.ДатыЗапретаИзменения.СоздатьКлючЗаписи(ЗначенияКлючаЗаписи);
		РазблокироватьДанныеДляРедактирования(КлючЗаписи, ИдентификаторФормы);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ЗаменитьПользователяНабораЗаписей(СтарыйПользователь, НовыйПользователь)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если СтарыйПользователь <> Неопределено Тогда
		ЗаблокироватьНаборЗаписейПользователя(СтарыйПользователь);
	КонецЕсли;
	ЗаблокироватьНаборЗаписейПользователя(НовыйПользователь);
	
	НаборЗаписей = РегистрыСведений.ДатыЗапретаИзменения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(НовыйПользователь, Истина);
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() > 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если СтарыйПользователь <> Неопределено Тогда
		НачатьТранзакцию();
		Попытка
			НаборЗаписей.Отбор.Пользователь.Установить(СтарыйПользователь, Истина);
			НаборЗаписей.Прочитать();
			ДанныеПользователя = НаборЗаписей.Выгрузить();
			НаборЗаписей.Очистить();
			НаборЗаписей.Записать();
			
			ДанныеПользователя.ЗаполнитьЗначения(НовыйПользователь, "Пользователь");
			НаборЗаписей.Отбор.Пользователь.Установить(НовыйПользователь, Истина);
			НаборЗаписей.Загрузить(ДанныеПользователя);
			НаборЗаписей.Записать();
		Исключение
			ОтменитьТранзакцию();
			РазблокироватьВсеЗаписи(ЭтаФорма);
			ВызватьИсключение;
		КонецПопытки;
		ЗафиксироватьТранзакцию();
	Иначе
		ЗаблокироватьИЗаписатьПустыеДаты(РазделПустаяСсылка, РазделПустаяСсылка, НовыйПользователь, "");
	КонецЕсли;
	
	РазблокироватьВсеЗаписи(ЭтаФорма);
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура УдалитьНаборЗаписейПользователя(Пользователь)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаблокироватьНаборЗаписейПользователя(Пользователь);
	
	НаборЗаписей = РегистрыСведений.ДатыЗапретаИзменения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(Пользователь, Истина);
	НаборЗаписей.Записать();
	
	РазблокироватьВсеЗаписи(ЭтаФорма);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьКомментарий(Пользователь, Комментарий);
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.ДатыЗапретаИзменения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(Пользователь, Истина);
	НаборЗаписей.Прочитать();
	ДанныеПользователя = НаборЗаписей.Выгрузить();
	ДанныеПользователя.ЗаполнитьЗначения(Комментарий, "Комментарий");
	НаборЗаписей.Загрузить(ДанныеПользователя);
	НаборЗаписей.Записать();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьЗначенияПрочитанныхСвойств(ТекущиеЗначенияСвойств, ПрочитанныеСвойства, КомментарийТекущиеДанные = Ложь)
	
	Если ПрочитанныеСвойства.Комментарий <> Неопределено Тогда
		//
		Если КомментарийТекущиеДанные = Ложь Тогда
			ТекущиеЗначенияСвойств.Комментарий = ПрочитанныеСвойства.Комментарий;
			//
		ИначеЕсли КомментарийТекущиеДанные <> Неопределено Тогда
			КомментарийТекущиеДанные.Комментарий = ПрочитанныеСвойства.Комментарий;
		КонецЕсли;
	КонецЕсли;
	
	Если ПрочитанныеСвойства.ДатаЗапрета <> Неопределено Тогда
		ТекущиеЗначенияСвойств.ДатаЗапрета              = ПрочитанныеСвойства.ДатаЗапрета;
		ТекущиеЗначенияСвойств.ОписаниеДатыЗапрета      = ПрочитанныеСвойства.ОписаниеДатыЗапрета;
		ТекущиеЗначенияСвойств.КоличествоДнейРазрешения = ПрочитанныеСвойства.КоличествоДнейРазрешения;
		РасчетныеСвойства = Новый Структура;
		РасчетныеСвойства.Вставить("ОписаниеДатыЗапретаПредставление", ПредставлениеОписанияДатыЗапрета(ПрочитанныеСвойства));
		ЗаполнитьЗначенияСвойств(ТекущиеЗначенияСвойств, РасчетныеСвойства);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьТекущиеЗначенияСвойств(ТекущиеДанные, КомментарийТекущиеДанные)
	
	Свойства = Новый Структура;
	Свойства.Вставить("ДатаЗапрета");
	Свойства.Вставить("ОписаниеДатыЗапрета");
	Свойства.Вставить("КоличествоДнейРазрешения");
	Свойства.Вставить("Комментарий");
	
	Если КомментарийТекущиеДанные <> Неопределено Тогда
		Свойства.Комментарий = КомментарийТекущиеДанные.Комментарий;
	КонецЕсли;
	
	Свойства.ДатаЗапрета              = ТекущиеДанные.ДатаЗапрета;
	Свойства.ОписаниеДатыЗапрета      = ТекущиеДанные.ОписаниеДатыЗапрета;
	Свойства.КоличествоДнейРазрешения = ТекущиеДанные.КоличествоДнейРазрешения;
	
	Возврат Свойства;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗаблокироватьЗаписьПользователя(Знач Контекст, Знач Раздел, Знач Объект, Знач Пользователь, Знач РазблокироватьРанееЗаблокированное = Ложь)
	
	ЗначенияКлючейЗаписей = Новый Массив;
	//
	Если РазблокироватьРанееЗаблокированное Тогда
		Для каждого ОписаниеБлокировки Из Контекст.УстановленныеБлокировки Цикл
			ЗначенияКлючаЗаписи = Новый Структура("Раздел, Объект, Пользователь");
			ЗаполнитьЗначенияСвойств(ЗначенияКлючаЗаписи, ОписаниеБлокировки);
			ЗначенияКлючейЗаписей.Добавить(ЗначенияКлючаЗаписи);
		КонецЦикла;
		Контекст.УстановленныеБлокировки.Очистить();
	КонецЕсли;
	
	
	ЗначенияКлючаЗаписи = Новый Структура;
	ЗначенияКлючаЗаписи.Вставить("Раздел",       Раздел);
	ЗначенияКлючаЗаписи.Вставить("Объект",       Объект);
	ЗначенияКлючаЗаписи.Вставить("Пользователь", Пользователь);
	
	ПрочитанныеСвойства = Новый Структура;
	ПрочитанныеСвойства.Вставить("ДатаЗапрета");
	ПрочитанныеСвойства.Вставить("ОписаниеДатыЗапрета");
	ПрочитанныеСвойства.Вставить("КоличествоДнейРазрешения");
	ПрочитанныеСвойства.Вставить("Комментарий");
	
	Если РазблокироватьРанееЗаблокированное
	 ИЛИ Контекст.УстановленныеБлокировки.НайтиСтроки(ЗначенияКлючаЗаписи).Количество() = 0 Тогда
		//
		ПрочитанныеСвойства = ЗаблокироватьЗаписьПользователяНаСервере(ЗначенияКлючаЗаписи, Контекст.УникальныйИдентификатор, ПрочитанныеСвойства, РазблокироватьРанееЗаблокированное, ЗначенияКлючейЗаписей);
		Если РазблокироватьРанееЗаблокированное Тогда
			Контекст.УстановленныеБлокировки.Очистить();
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(Контекст.УстановленныеБлокировки.Добавить(), ЗначенияКлючаЗаписи);
	КонецЕсли;
	
	Возврат ПрочитанныеСвойства;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗаблокироватьЗаписьПользователяНаСервере(Знач ЗначенияКлючаЗаписи, Знач УникальныйИдентификатор, Знач ПрочитанныеСвойства, Знач РазблокироватьРанееЗаблокированное, Знач ЗначенияКлючейЗаписей)
	
	Если РазблокироватьРанееЗаблокированное Тогда
		РазблокироватьВсеЗаписиНаСервере(ЗначенияКлючейЗаписей, УникальныйИдентификатор);
	КонецЕсли;
	
	КлючЗаписи = РегистрыСведений.ДатыЗапретаИзменения.СоздатьКлючЗаписи(ЗначенияКлючаЗаписи);
	ЗаблокироватьДанныеДляРедактирования(КлючЗаписи, , УникальныйИдентификатор);
	
	МенеджерЗаписи = РегистрыСведений.ДатыЗапретаИзменения.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ЗначенияКлючаЗаписи);
	МенеджерЗаписи.Прочитать();
	Если МенеджерЗаписи.Выбран() Тогда
		ПрочитанныеСвойства.ДатаЗапрета = МенеджерЗаписи.ДатаЗапрета;
		ПрочитанныеСвойства.Комментарий = МенеджерЗаписи.Комментарий;
		ЗаполнитьПоВнутреннемуОписаниюДатыЗапрета(ПрочитанныеСвойства, МенеджерЗаписи.ОписаниеДатыЗапрета);
	Иначе
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Пользователь", ЗначенияКлючаЗаписи.Пользователь);
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДатыЗапретаИзменения.Комментарий
		|ИЗ
		|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
		|ГДЕ
		|	ДатыЗапретаИзменения.Пользователь = &Пользователь";
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ПрочитанныеСвойства.Комментарий = Выборка.Комментарий;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПрочитанныеСвойства;
	
КонецФункции

&НаСервере
Функция ЗаменитьОбъектВЗаписиПользователяНаСервере(Знач Раздел, Знач СтарыйОбъект, Знач НовыйОбъект, Знач Пользователь, ТекущиеЗначенияСвойств)
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Блокировка новой записи и проверка, что её не существует
	ЗаблокироватьЗаписьПользователя(ЭтаФорма, Раздел, НовыйОбъект, Пользователь);
	
	ЗначенияКлючаЗаписи = Новый Структура;
	ЗначенияКлючаЗаписи.Вставить("Раздел",       Раздел);
	ЗначенияКлючаЗаписи.Вставить("Объект",       НовыйОбъект);
	ЗначенияКлючаЗаписи.Вставить("Пользователь", Пользователь);
	
	МенеджерЗаписи = РегистрыСведений.ДатыЗапретаИзменения.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ЗначенияКлючаЗаписи);
	МенеджерЗаписи.Прочитать();
	Если МенеджерЗаписи.Выбран() Тогда
		РазблокироватьВсеЗаписи(ЭтаФорма);
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтарыйОбъект) Тогда
		// Блокировка старой записи
		ПрочитанныеСвойства = ЗаблокироватьЗаписьПользователя(ЭтаФорма, Раздел, СтарыйОбъект, Пользователь);
		ОбновитьЗначенияПрочитанныхСвойств(ТекущиеЗначенияСвойств, ПрочитанныеСвойства);
		
		ЗначенияКлючаЗаписи.Объект = СтарыйОбъект;
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ЗначенияКлючаЗаписи);
		МенеджерЗаписи.Прочитать();
		Если МенеджерЗаписи.Выбран() Тогда
			МенеджерЗаписи.Удалить();
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаЗапрета)
	   И ЗначениеЗаполнено(ОписаниеДатыЗапрета) Тогда
		//
		МенеджерЗаписи.Раздел              = Раздел;
		МенеджерЗаписи.Объект              = НовыйОбъект;
		МенеджерЗаписи.Пользователь        = Пользователь;
		МенеджерЗаписи.ДатаЗапрета         = ТекущиеЗначенияСвойств.ДатаЗапрета;
		МенеджерЗаписи.ОписаниеДатыЗапрета = ВнутреннееОписаниеДатыЗапрета(ТекущиеЗначенияСвойств);
		МенеджерЗаписи.Комментарий         = ТекущиеЗначенияСвойств.Комментарий;
		//
		МенеджерЗаписи.Записать();
	КонецЕсли;
	
	РазблокироватьВсеЗаписи(ЭтаФорма);
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Функция ТекущийРаздел(ТекущиеДанные = Неопределено, РазделОбъектов = Ложь)
	
	Если ТекущиеДанные = Неопределено Тогда
		ТекущиеДанные = Элементы.ДатыЗапрета.ТекущиеДанные;
	КонецЕсли;
	
	Если БезРазделовИОбъектов
	 ИЛИ СпособУказанияДатыЗапрета = "ОбщаяДата" Тогда
		//
		ТекущийРаздел = РазделПустаяСсылка;
		// 
	ИначеЕсли ПоказыватьРазделыТекущегоПользователя Тогда
		Если ТекущиеДанные.ЭтоРаздел Тогда
			ТекущийРаздел = ТекущиеДанные.Раздел;
		Иначе
			ТекущийРаздел = ТекущиеДанные.ПолучитьРодителя().Раздел;
		КонецЕсли;
		
	Иначе // Единственный раздел, не показываемый пользователю
		Если ТекущиеДанные <> Неопределено
		   И ТекущиеДанные.Раздел = РазделПустаяСсылка
		   И НЕ РазделОбъектов Тогда
			//
			ТекущийРаздел = РазделПустаяСсылка;
		Иначе
			ТекущийРаздел = ПервыйРаздел;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТекущийРаздел;
	
КонецФункции

&НаКлиенте
Процедура ЗаписатьОбщуюДатуЗапретаСОписанием();
	
	// ЗаписатьОписаниеИДатуЗапрета
	Данные = Новый Структура;
	Данные.Вставить("Объект",                   РазделПустаяСсылка);
	Данные.Вставить("ОписаниеДатыЗапрета",      ОписаниеДатыЗапрета);
	Данные.Вставить("КоличествоДнейРазрешения", КоличествоДнейРазрешения);
	Данные.Вставить("ДатаЗапрета",              ДатаЗапрета);
	
	ЗаписатьОписаниеИДатуЗапрета(Данные);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьОписаниеИДатуЗапрета(ТекущиеДанные = Неопределено)
	
	Если ТекущиеДанные = Неопределено Тогда
		ТекущиеДанные = Элементы.ДатыЗапрета.ТекущиеДанные;
	КонецЕсли;
	
	Если ДатаЗапретаУстановлена(ТекущиеДанные, ТекущийПользователь) Тогда
		// Запись описания или даты запрета
		Комментарий = КомментарийТекущегоПользователя(ЭтаФорма);
		ЗаписатьДатуЗапретаСОписанием(
			ТекущийРаздел(ТекущиеДанные),
			ТекущиеДанные.Объект,
			ТекущийПользователь,
			ТекущиеДанные.ДатаЗапрета,
			ВнутреннееОписаниеДатыЗапрета(ТекущиеДанные),
			Комментарий);
	Иначе
		УдалитьЗаписьПользователя(
			ТекущийРаздел(ТекущиеДанные),
			ТекущиеДанные.Объект,
			ТекущийПользователь);
	КонецЕсли;
	
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ДатыЗапретаИзменения"));
	
КонецПроцедуры


&НаСервереБезКонтекста
Процедура ЗаписатьДатуЗапретаСОписанием(Знач Раздел, Знач Объект, Знач Пользователь, Знач ДатаЗапрета, Знач ВнутреннееОписаниеДатыЗапрета, Знач Комментарий)
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = РегистрыСведений.ДатыЗапретаИзменения.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Раздел              = Раздел;
	МенеджерЗаписи.Объект              = Объект;
	МенеджерЗаписи.Пользователь        = Пользователь;
	МенеджерЗаписи.ДатаЗапрета         = ДатаЗапрета;
	МенеджерЗаписи.ОписаниеДатыЗапрета = ВнутреннееОписаниеДатыЗапрета;
	МенеджерЗаписи.Комментарий = Комментарий;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьЗаписьПользователя(Знач Раздел, Знач Объект, Знач Пользователь)
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = РегистрыСведений.ДатыЗапретаИзменения.СоздатьМенеджерЗаписи();
	
	Если ТипЗнч(Объект) = Тип("Массив") Тогда
		Объекты = Объект;
	Иначе
		Объекты = Новый Массив;
		Объекты.Добавить(Объект);
	КонецЕсли;
	
	Для каждого ТекущийОбъект Из Объекты Цикл
		ЗаблокироватьЗаписьПользователя(ЭтаФорма, Раздел, ТекущийОбъект, Пользователь);
	КонецЦикла;
	
	Для каждого ТекущийОбъект Из Объекты Цикл
		МенеджерЗаписи.Раздел = Раздел;
		МенеджерЗаписи.Объект = ТекущийОбъект;
		МенеджерЗаписи.Пользователь = Пользователь;
		МенеджерЗаписи.Прочитать();
		Если МенеджерЗаписи.Выбран() Тогда
			МенеджерЗаписи.Удалить();
		КонецЕсли;
	КонецЦикла;
	
	РазблокироватьВсеЗаписи(ЭтаФорма);
	
КонецПроцедуры


&НаКлиенте
Процедура ОбновитьНаличиеДатЗапретаТекущегоПользователя()
	
	Если Элементы.Пользователи.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТекущиеДанные = Элементы.Пользователи.ТекущиеДанные;
	
	БезДатыЗапрета = Истина;
	Если СпособУказанияДатыЗапрета = "ОбщаяДата" Тогда
		БезДатыЗапрета = ТекущиеДанные.БезДатыЗапрета;
	Иначе
		Для каждого Строка Из ДатыЗапрета.ПолучитьЭлементы() Цикл
			БезДатыЗапретаРаздела = Истина;
			Если ДатаЗапретаУстановлена(Строка, ТекущийПользователь) Тогда
				БезДатыЗапретаРаздела = Ложь;
			КонецЕсли;
			Для каждого ПодчиненнаяСтрока Из Строка.ПолучитьЭлементы() Цикл
				Если ДатаЗапретаУстановлена(ПодчиненнаяСтрока, ТекущийПользователь) Тогда
					ПодчиненнаяСтрока.БезДатыЗапрета = Ложь;
					БезДатыЗапретаРаздела = Ложь;
				Иначе
					ПодчиненнаяСтрока.БезДатыЗапрета = Истина;
				КонецЕсли;
			КонецЦикла;
			Строка.ПолноеПредставление = Строка.Представление;
			Строка.БезДатыЗапрета = БезДатыЗапретаРаздела;
			БезДатыЗапрета = БезДатыЗапрета И БезДатыЗапретаРаздела;
		КонецЦикла;
	КонецЕсли;
	
	ТекущиеДанные.БезДатыЗапрета = БезДатыЗапрета;
	
КонецПроцедуры


&НаКлиенте
Процедура ОбработчикОжиданияВыбратьПользователей()
	
	ВыбратьПодобратьПользователей();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПодобратьПользователей(Подобрать = Ложь)
	
	Если Параметры.ДатыЗапретаЗагрузкиДанных Тогда
		ВыбратьПодобратьУзлыПлановОбмена(Подобрать);
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ТекущаяСтрока", ?(Элементы.Пользователи.ТекущиеДанные = Неопределено, Неопределено, Элементы.Пользователи.ТекущиеДанные.Пользователь));
	
	ВыборИПодборВнешнихПользователей = Ложь;
	Если ИспользоватьВнешнихПользователей Тогда
		Если НЕ ВыбранТипПользователиИлиВнешниеПользователи(ВыборИПодборВнешнихПользователей) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ВыборИПодборВнешнихПользователей Тогда
		ПараметрыФормы.Вставить("ВыборГруппВнешнихПользователей", Истина);
	Иначе
		ПараметрыФормы.Вставить("ВыборГруппПользователей", Истина);
	КонецЕсли;
	
	Если Подобрать Тогда
		ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
		ВладелецФормы = Элементы.Пользователи;
	Иначе
		ВладелецФормы = Элементы.ПользователиПолноеПредставление;
	КонецЕсли;
	
	Если ВыборИПодборВнешнихПользователей Тогда
	
		Если СправочникВнешниеПользователиДоступен Тогда
			Если Подобрать Тогда
				ОткрытьФорму("Справочник.ВнешниеПользователи.ФормаВыбора", ПараметрыФормы, ВладелецФормы);
			Иначе
				ОткрытьФормуМодально("Справочник.ВнешниеПользователи.ФормаВыбора", ПараметрыФормы, ВладелецФормы);
			КонецЕсли;
		Иначе
			Предупреждение(ТекстСообщенияНедостаточноПравДляВыбораВнешнихПользователей());
		КонецЕсли;
	Иначе
		Если Подобрать Тогда
			ОткрытьФорму("Справочник.Пользователи.ФормаВыбора", ПараметрыФормы, ВладелецФормы);
		Иначе
			ОткрытьФормуМодально("Справочник.Пользователи.ФормаВыбора", ПараметрыФормы, ВладелецФормы);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПодобратьУзлыПлановОбмена(Подобрать)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ТекущаяСтрока", ?(Элементы.Пользователи.ТекущиеДанные = Неопределено, Неопределено, Элементы.Пользователи.ТекущиеДанные.Пользователь));
	ПараметрыФормы.Вставить("ВыбиратьВсеУзлы", Истина);
	ПараметрыФормы.Вставить("ПланыОбменаДляВыбора", СписокТиповПользователей.ВыгрузитьЗначения());
	
	Если Подобрать Тогда
		ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
		ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
		ВладелецФормы = Элементы.Пользователи;
	Иначе
		ВладелецФормы = Элементы.ПользователиПолноеПредставление;
	КонецЕсли;
	
	Если Подобрать Тогда
		ОткрытьФорму("ОбщаяФорма.ВыборУзловПлановОбмена", ПараметрыФормы, ВладелецФормы);
	Иначе
		ОткрытьФормуМодально("ОбщаяФорма.ВыборУзловПлановОбмена", ПараметрыФормы, ВладелецФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ВыбранТипПользователиИлиВнешниеПользователи(ВыборИПодборВнешнихПользователей)
	
	ВыборИПодборВнешнихПользователей = Ложь;
	
	Если ИспользоватьВнешнихПользователей Тогда
		Элемент = СписокТиповПользователей.ВыбратьЭлемент(ТекстЗаголовкаВыборТипаДанных(), СписокТиповПользователей[0]);
		Если Элемент <> Неопределено Тогда
			ВыборИПодборВнешнихПользователей = Элемент.Значение = Тип("СправочникСсылка.ВнешниеПользователи");
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьНомераКартинокПользователейДатЗапрета(ТекущаяСтрока = Неопределено)
	
	Если Параметры.ДатыЗапретаЗагрузкиДанных Тогда
		//
		Для каждого Строка Из ПользователиДатЗапрета Цикл
			//
			Если Строка.Пользователь = Перечисления.ВидыНазначенияДатЗапрета.ДляВсехУзловПлановОбмена Тогда
				Строка.НомерКартинки = -1;
				//
			ИначеЕсли НЕ ЗначениеЗаполнено(Строка.Пользователь) Тогда
				Строка.НомерКартинки = 0;
				//
			ИначеЕсли Строка.Пользователь = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Строка.Пользователь).ЭтотУзел() Тогда
				Строка.НомерКартинки = 1;
			Иначе
				Строка.НомерКартинки = 2;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Пользователи.ЗаполнитьНомераКартинокПользователей(ПользователиДатЗапрета, "Пользователь", "НомерКартинки", ТекущаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОжиданияВыбратьОбъекты()
	
	ВыбратьПодобратьОбъекты();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатыЗапретаУстановитьДоступностьКоманд(Знач Доступность)
	
	Элементы.ДатыЗапретаИзменить1.Доступность = Доступность;
	Элементы.ДатыЗапретаИзменить2.Доступность = Доступность;
	Элементы.ДатыЗапретаИзменить3.Доступность = Доступность;
	Элементы.ДатыЗапретаИзменить4.Доступность = Доступность;
	Элементы.ДатыЗапретаИзменить5.Доступность = Доступность;
	
	Если СпособУказанияДатыЗапрета = "ПоОбъектам" Тогда
		Доступность = Истина;
	КонецЕсли;
	
	Элементы.ДатыЗапретаПодобрать1.Доступность = Доступность;
	Элементы.ДатыЗапретаПодобрать2.Доступность = Доступность;
	Элементы.ДатыЗапретаПодобрать3.Доступность = Доступность;
	
	Элементы.ДатыЗапретаДобавить1.Доступность = Доступность;
	Элементы.ДатыЗапретаДобавить2.Доступность = Доступность;
	Элементы.ДатыЗапретаДобавить3.Доступность = Доступность;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПодобратьОбъекты(Подобрать = Ложь)
	
	ТекущиеДанные = Элементы.ДатыЗапрета.ТекущиеДанные;
	
	// Выбор типа данных
	Отбор = Новый Структура("Раздел", ТекущийРаздел(, Истина));
	ТекстПредупреждения = ТекстСообщенияВВыбранномРазделеДатыЗапретаДляОбъектовНеУстанавливаются();
	Если Отбор.Раздел = РазделПустаяСсылка Тогда
		Предупреждение(ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	СписокТипов = ТипыОбъектовРазделов.НайтиСтроки(Отбор)[0].ТипыОбъекта;
	Если СписокТипов.Количество() = 0 Тогда
		Предупреждение(ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Если СписокТипов.Количество() = 1 Тогда
		Элемент = СписокТипов[0];
	Иначе
		Элемент = СписокТипов.ВыбратьЭлемент(ТекстЗаголовкаВыборТипаДанных(), СписокТипов[0]);
		Если Элемент = Неопределено Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ТекущаяСтрока", ?(ТекущиеДанные = Неопределено, Неопределено, ТекущиеДанные.Объект));
	
	Если Подобрать Тогда
		ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
		ВладелецФормы = Элементы.ДатыЗапрета;
		ОткрытьФорму(Элемент.Значение + ".ФормаВыбора", ПараметрыФормы, ВладелецФормы);
	Иначе
		ВладелецФормы = Элементы.ДатыЗапретаПолноеПредставление;
		ОткрытьФормуМодально(Элемент.Значение + ".ФормаВыбора", ПараметрыФормы, ВладелецФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ТекстВопросаОНесохраненныхДанных(БезПроверкиПользователей = Ложь)
	
	ВсеПользователиВыбраны            = Истина;
	ВсеОбъектыВыбраны                 = Истина;
	ВсеДатыЗапретаПоОбъектамЗаполнены = Истина;
	ВсеДатыЗапретаПоРазделамЗаполнены = Истина;
	
	Если НЕ БезПроверкиПользователей
	   И ПользователиДатЗапрета.НайтиСтроки(Новый Структура("Представление", "")).Количество() > 0 Тогда
		//
		ВсеПользователиВыбраны = Ложь;
	Иначе
		Если НЕ БезРазделовИОбъектов Тогда
			Если ПоказыватьРазделыТекущегоПользователя Тогда
				Для каждого ОписаниеРаздела Из ДатыЗапрета.ПолучитьЭлементы() Цикл
					Для каждого ОписаниеОбъекта Из ОписаниеРаздела.ПолучитьЭлементы() Цикл
						Если ЗначениеЗаполнено(ОписаниеОбъекта.Представление)Тогда
							Если НЕ ДатаЗапретаУстановлена(ОписаниеОбъекта, ТекущийПользователь) Тогда
								ВсеДатыЗапретаПоОбъектамЗаполнены = Ложь;
							КонецЕсли;
						Иначе
							ВсеОбъектыВыбраны = Ложь;
							Прервать;
						КонецЕсли;
					КонецЦикла;
					Если НЕ НазначениеДляВсех(ТекущийПользователь) Тогда
						Если ОписаниеРаздела.ПолучитьЭлементы().Количество() = 0 
						   И НЕ ДатаЗапретаУстановлена(ОписаниеРаздела, ТекущийПользователь) Тогда
							ВсеДатыЗапретаПоРазделамЗаполнены = Ложь;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			Иначе
				Для каждого ОписаниеОбъекта Из ДатыЗапрета.ПолучитьЭлементы() Цикл
					Если ЗначениеЗаполнено(ОписаниеОбъекта.Представление) Тогда
						Если НЕ НазначениеДляВсех(ТекущийПользователь)
						   И НЕ ДатаЗапретаУстановлена(ОписаниеОбъекта, ТекущийПользователь) Тогда
							//
							ВсеДатыЗапретаПоОбъектамЗаполнены = Ложь;
						КонецЕсли;
					Иначе
						ВсеОбъектыВыбраны = Ложь;
					КонецЕсли;
				КонецЦикла;
				Если НЕ БезПроверкиПользователей
				   И ВсеДатыЗапретаПоРазделамЗаполнены
				   И ВсеДатыЗапретаПоОбъектамЗаполнены
				   И ВсеОбъектыВыбраны Тогда
					НайденныеСтроки = ПользователиДатЗапрета.НайтиСтроки(Новый Структура("БезДатыЗапрета", Истина));
					Для каждого Строка Из НайденныеСтроки Цикл
						Если НазначениеДляВсех(Строка.Пользователь) И ЗначениеЗаполнено(Строка.Представление) Тогда
							Продолжить;
						КонецЕсли;
						ВсеДатыЗапретаЗаполнены = Ложь;
						Прервать;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Предупреждение пользователя
	ТекстВопроса = "";
	Если НЕ ВсеПользователиВыбраны Тогда
		ТекстВопроса = ТекстВопроса + ТекстСообщенияНастройкиСНевыбраннымиПользователямиНеСохранены(ЭтаФорма);
		//
	ИначеЕсли НЕ ВсеОбъектыВыбраны Тогда
		ТекстВопроса = ТекстВопроса + Символы.ПС + ТекстСообщенияНастройкиСНевыбраннымиОбъектамиНеСохранены();
		//
	ИначеЕсли НЕ ВсеДатыЗапретаПоРазделамЗаполнены Тогда
		ТекстВопроса = ТекстВопроса + Символы.ПС + ТекстСообщенияНастройкиСНезаполненнымиДатамиЗапретаДляРазделовНеСохранены();
		//
	ИначеЕсли НЕ ВсеДатыЗапретаПоОбъектамЗаполнены Тогда
		ТекстВопроса = ТекстВопроса + Символы.ПС + ТекстСообщенияНастройкиСНезаполненнымиДатамиЗапретаДляОбъектовНеСохранены();
	КонецЕсли;
	
	Возврат СокрЛ(ТекстВопроса);
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекущаяУстановкаДатыЗапрета(ДатыЗапретаЗагрузкиДанных)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатыЗапретаЗагрузкиДанных", ДатыЗапретаЗагрузкиДанных);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК ЕстьЗапреты
	|ИЗ
	|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|ГДЕ
	|	(ДатыЗапретаИзменения.Пользователь = НЕОПРЕДЕЛЕНО
	|			ИЛИ ВЫБОР
	|				КОГДА ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Пользователь) = ТИП(Справочник.Пользователи)
	|						ИЛИ ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Пользователь) = ТИП(Справочник.группыПользователей)
	|						ИЛИ ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Пользователь) = ТИП(Справочник.ВнешниеПользователи)
	|						ИЛИ ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Пользователь) = ТИП(Справочник.группыВнешнихПользователей)
	|						ИЛИ ДатыЗапретаИзменения.Пользователь = ЗНАЧЕНИЕ(Перечисление.ВидыНазначенияДатЗапрета.ДляВсехПользователей)
	|					ТОГДА &ДатыЗапретаЗагрузкиДанных = ЛОЖЬ
	|				ИНАЧЕ &ДатыЗапретаЗагрузкиДанных = ИСТИНА
	|			КОНЕЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК ПоПользователям
	|ИЗ
	|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|ГДЕ
	|	(ДатыЗапретаИзменения.Пользователь = НЕОПРЕДЕЛЕНО
	|			ИЛИ ВЫБОР
	|				КОГДА ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Пользователь) = ТИП(Справочник.Пользователи)
	|						ИЛИ ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Пользователь) = ТИП(Справочник.группыПользователей)
	|						ИЛИ ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Пользователь) = ТИП(Справочник.ВнешниеПользователи)
	|						ИЛИ ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Пользователь) = ТИП(Справочник.группыВнешнихПользователей)
	|					ТОГДА &ДатыЗапретаЗагрузкиДанных = ЛОЖЬ
	|				ИНАЧЕ &ДатыЗапретаЗагрузкиДанных = ИСТИНА
	|			КОНЕЦ)
	|	И ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Пользователь) <> ТИП(Перечисление.ВидыНазначенияДатЗапрета)";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	Если РезультатыЗапроса[0].Пустой() Тогда
		ТекущаяУстановкаДатЗапрета = "НетЗапрета";
		
	ИначеЕсли РезультатыЗапроса[1].Пустой() Тогда
		ТекущаяУстановкаДатЗапрета = "ДляВсехПользователей";
	Иначе
		ТекущаяУстановкаДатЗапрета = "ПоПользователям";
	КонецЕсли;
	
	Возврат ТекущаяУстановкаДатЗапрета;
	
КонецФункции

&НаСервере
Процедура УстановитьВидимость()
	
	ИзменитьВидимость(Элементы.НастройкаДатыЗапрета, УстановкаДатыЗапрета <> "НетЗапрета");
	
	Если УстановкаДатыЗапрета = "НетЗапрета" Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьВидимость(Элементы.УстановкаПоПользователям, УстановкаДатыЗапрета <> "ДляВсехПользователей");
	
	Если УстановкаДатыЗапрета <> "ПоПользователям" Тогда
		Элементы.ДанныеПользователя.ТекущаяСтраница = Элементы.ПользовательВыбран;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьВидимость(Элемент, Видимость)
	
	Если Элемент.Видимость <> Видимость Тогда
		Элемент.Видимость = Видимость;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ИзменитьУстановкуДатыЗапрета(Знач ВыбранноеЗначение, Знач УдалитьЛишнее)
	
	Если УдалитьЛишнее Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ДатыЗапретаЗагрузкиДанных", Параметры.ДатыЗапретаЗагрузкиДанных);
		//
		Если ВыбранноеЗначение = "НетЗапрета" Тогда
			Запрос.УстановитьПараметр("ОставитьДляВсехПользователей", Ложь);
			//
		ИначеЕсли ВыбранноеЗначение = "ДляВсехПользователей" Тогда
			Запрос.УстановитьПараметр("ОставитьДляВсехПользователей", Истина);
		Иначе
			Запрос.УстановитьПараметр("ДатыЗапретаЗагрузкиДанных", Неопределено);
		КонецЕсли;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ДатыЗапретаИзменения.Раздел,
		|	ДатыЗапретаИзменения.Объект,
		|	ДатыЗапретаИзменения.Пользователь
		|ИЗ
		|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
		|ГДЕ
		|	(ДатыЗапретаИзменения.Пользователь = НЕОПРЕДЕЛЕНО
		|			ИЛИ ВЫБОР
		|				КОГДА ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Пользователь) = ТИП(Справочник.Пользователи)
		|						ИЛИ ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Пользователь) = ТИП(Справочник.группыПользователей)
		|						ИЛИ ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Пользователь) = ТИП(Справочник.ВнешниеПользователи)
		|						ИЛИ ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Пользователь) = ТИП(Справочник.группыВнешнихПользователей)
		|						ИЛИ ДатыЗапретаИзменения.Пользователь = ЗНАЧЕНИЕ(Перечисление.ВидыНазначенияДатЗапрета.ДляВсехПользователей)
		|					ТОГДА &ДатыЗапретаЗагрузкиДанных = ЛОЖЬ
		|				ИНАЧЕ &ДатыЗапретаЗагрузкиДанных = ИСТИНА
		|			КОНЕЦ)
		|	И ВЫБОР
		|			КОГДА ТИПЗНАЧЕНИЯ(ДатыЗапретаИзменения.Пользователь) = ТИП(Перечисление.ВидыНазначенияДатЗапрета)
		|				ТОГДА &ОставитьДляВсехПользователей = ЛОЖЬ
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ";
		ЗначенияКлючейЗаписей = Запрос.Выполнить().Выгрузить();
		// Блокировка удаляемых записей
		Для каждого ЗначенияКлючаЗаписи Из ЗначенияКлючейЗаписей Цикл
			ЗаблокироватьЗаписьПользователя(ЭтаФорма, ЗначенияКлючаЗаписи.Раздел, ЗначенияКлючаЗаписи.Объект, ЗначенияКлючаЗаписи.Пользователь);
		КонецЦикла;
		// Удаление заблокированных записей
		Для каждого ЗначенияКлючаЗаписи Из ЗначенияКлючейЗаписей Цикл
			УдалитьЗаписьПользователя(ЗначенияКлючаЗаписи.Раздел, ЗначенияКлючаЗаписи.Объект, ЗначенияКлючаЗаписи.Пользователь);
		КонецЦикла;
		РазблокироватьВсеЗаписи(ЭтаФорма);
	КонецЕсли;
	
	ПрочитатьПользователей();
	ПрочитатьДанныеПользователя(ЭтаФорма);
	
	УстановитьВидимость();
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекущийСпособУказанияДатыЗапрета(Знач Пользователь, Знач ЕдинственныйРаздел, Знач ПервыйРаздел, Знач ЗначениеДляВсехПользователей, Данные = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Пользователь",                 Пользователь);
	Запрос.УстановитьПараметр("ПервыйРаздел",                 ПервыйРаздел);
	Запрос.УстановитьПараметр("ПустаяДата",                   '00000000');
	Запрос.УстановитьПараметр("ЗначениеДляВсехПользователей", ЗначениеДляВсехПользователей);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК ЗначениеИстина
	|ИЗ
	|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|ГДЕ
	|	НЕ (&Пользователь <> ДатыЗапретаИзменения.Пользователь И &Пользователь <> ""*"")
	|	И (НЕ(ДатыЗапретаИзменения.Раздел = ЗНАЧЕНИЕ(ПланВидовХарактеристик.РазделыДатЗапретаИзменения.ПустаяСсылка)
	|				И ДатыЗапретаИзменения.Объект = ЗНАЧЕНИЕ(ПланВидовХарактеристик.РазделыДатЗапретаИзменения.ПустаяСсылка)))
	|	И ВЫБОР
	|			КОГДА ДатыЗапретаИзменения.Пользователь = &ЗначениеДляВсехПользователей
	|				ТОГДА (НЕ(ДатыЗапретаИзменения.ДатаЗапрета = &ПустаяДата
	|							И ДатыЗапретаИзменения.ОписаниеДатыЗапрета = """"))
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК ЗначениеИстина
	|ИЗ
	|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|ГДЕ
	|	НЕ (&Пользователь <> ДатыЗапретаИзменения.Пользователь И &Пользователь <> ""*"")
	|	И ДатыЗапретаИзменения.Раздел <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.РазделыДатЗапретаИзменения.ПустаяСсылка)
	|	И ДатыЗапретаИзменения.Объект <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.РазделыДатЗапретаИзменения.ПустаяСсылка)
	|	И ДатыЗапретаИзменения.Объект <> ДатыЗапретаИзменения.Раздел
	|	И ДатыЗапретаИзменения.Объект <> НЕОПРЕДЕЛЕНО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК ЗначениеИстина
	|ИЗ
	|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|ГДЕ
	|	НЕ (&Пользователь <> ДатыЗапретаИзменения.Пользователь И &Пользователь <> ""*"")
	|	И ДатыЗапретаИзменения.Раздел <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.РазделыДатЗапретаИзменения.ПустаяСсылка)
	|	И ДатыЗапретаИзменения.Раздел <> &ПервыйРаздел
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА
	|ИЗ
	|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|ГДЕ
	|	НЕ (&Пользователь <> ДатыЗапретаИзменения.Пользователь И &Пользователь <> ""*"")
	|	И ДатыЗапретаИзменения.Раздел <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.РазделыДатЗапретаИзменения.ПустаяСсылка)
	|	И ДатыЗапретаИзменения.Раздел = ДатыЗапретаИзменения.Объект";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ТекущийСпособУказанияДатыЗапрета = "";
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДатыЗапретаИзменения.ДатаЗапрета,
	|	ДатыЗапретаИзменения.ОписаниеДатыЗапрета
	|ИЗ
	|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|ГДЕ
	|	ДатыЗапретаИзменения.Пользователь = &Пользователь
	|	И ДатыЗапретаИзменения.Раздел = ЗНАЧЕНИЕ(ПланВидовХарактеристик.РазделыДатЗапретаИзменения.ПустаяСсылка)
	|	И ДатыЗапретаИзменения.Объект = ЗНАЧЕНИЕ(ПланВидовХарактеристик.РазделыДатЗапретаИзменения.ПустаяСсылка)";
	Выборка = Запрос.Выполнить().Выбрать();
	ОбщаяДатаПрочитана = Выборка.Следующий();
	
	Если Данные = Неопределено Тогда
		Данные = Новый Структура;
		Данные.Вставить("ОписаниеДатыЗапрета", "ПроизвольнаяДата");
		Данные.Вставить("ДатаЗапрета", '00000000');
		Данные.Вставить("КоличествоДнейРазрешения", 0);
	КонецЕсли;
	Если ОбщаяДатаПрочитана Тогда
		Данные.ДатаЗапрета = Выборка.ДатаЗапрета;
		ЗаполнитьПоВнутреннемуОписаниюДатыЗапрета(Данные, Выборка.ОписаниеДатыЗапрета);
	КонецЕсли;
	
	Если РезультатыЗапроса[0].Пустой() Тогда // Нет по объектам и по разделам, когда пустой
		ТекущийСпособУказанияДатыЗапрета = ?(ОбщаяДатаПрочитана, "ОбщаяДата", "");
		//
	ИначеЕсли НЕ РезультатыЗапроса[1].Пустой() Тогда // Есть по объектам, когда не пустой
		Если РезультатыЗапроса[2].Пустой() // Только по ПервыйРаздел (без дат по разделам), когда пустой
		   И ЕдинственныйРаздел Тогда
			//
			ТекущийСпособУказанияДатыЗапрета = "ПоОбъектам";
		Иначе
			ТекущийСпособУказанияДатыЗапрета = "ПоРазделамИОбъектам";
		КонецЕсли;
		//
	Иначе
		ТекущийСпособУказанияДатыЗапрета = "ПоРазделам";
	КонецЕсли;
	
	Возврат ТекущийСпособУказанияДатыЗапрета;
	
КонецФункции

&НаСервере
Функция УдалитьЛишнееПриИзмененииСпособаУказанияДатыЗапрета(Знач ВыбранноеЗначение, Знач ТекущийПользователь, Знач УстановкаДатыЗапрета)
	
	НаборЗаписей = РегистрыСведений.ДатыЗапретаИзменения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(ТекущийПользователь);
	НаборЗаписей.Прочитать();
	Индекс = НаборЗаписей.Количество()-1;
	Пока Индекс >= 0 Цикл
		Запись = НаборЗаписей[Индекс];
		Если  ВыбранноеЗначение = "ОбщаяДата" Тогда
			Если НЕ (  Запись.Раздел = РазделПустаяСсылка
					 И Запись.Объект = РазделПустаяСсылка ) Тогда
				НаборЗаписей.Удалить(Запись);
			КонецЕсли;
		ИначеЕсли ВыбранноеЗначение = "ПоРазделам" Тогда
			Если Запись.Раздел <> Запись.Объект
			 ИЛИ Запись.Раздел = РазделПустаяСсылка
			   И Запись.Объект = РазделПустаяСсылка Тогда
				НаборЗаписей.Удалить(Запись);
			КонецЕсли;
		ИначеЕсли ВыбранноеЗначение = "ПоОбъектам" Тогда
			Если Запись.Раздел = Запись.Объект
			   И Запись.Раздел <> РазделПустаяСсылка
			   И Запись.Объект <> РазделПустаяСсылка Тогда
				НаборЗаписей.Удалить(Запись);
			КонецЕсли;
		КонецЕсли;
		Индекс = Индекс-1;
	КонецЦикла;
	НаборЗаписей.Записать();
	
	ПрочитатьДанныеПользователя(ЭтаФорма);
	
КонецФункции

&НаКлиенте
Процедура ВыбратьРазделы()
	
	КоллекцияДанных = ДатыЗапрета.ПолучитьЭлементы();
	
	РазделНеНайден = Истина;
	РазделыДляОтметки = Новый СписокЗначений;
	Для каждого ЭлементКоллекции Из КоллекцияДанных Цикл
		Если ЭлементКоллекции.Раздел = РазделПустаяСсылка Тогда
			РазделНеНайден = Ложь;
		КонецЕсли;
	КонецЦикла;
	Если РазделНеНайден Тогда
		РазделыДляОтметки.Добавить(РазделПустаяСсылка, ТекстПредставленияОбщейДаты());
	КонецЕсли;
	
	Если СпособУказанияДатыЗапрета <> "ПоОбъектам" Тогда
		Для каждого Строка Из ТипыОбъектовРазделов Цикл
			РазделНеНайден = Истина;
			Для каждого ЭлементКоллекции Из КоллекцияДанных Цикл
				Если ЭлементКоллекции.Раздел = Строка.Раздел Тогда
					РазделНеНайден = Ложь;
				КонецЕсли;
			КонецЦикла;
			Если РазделНеНайден Тогда
				РазделыДляОтметки.Добавить(Строка.Раздел);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если РазделыДляОтметки.Количество() = 0 Тогда
		Если СпособУказанияДатыЗапрета = "ПоОбъектам" Тогда
			Предупреждение(ТекстСообщенияОбщаяДатаУжеПоказана());
		Иначе
			Предупреждение(ТекстСообщенияВсеРазделыУжеПоказаны());
		КонецЕсли;
	ИначеЕсли РазделыДляОтметки.ОтметитьЭлементы(ЗаголовокВыборТребуемыхРазделов()) Тогда
		ДобавитьРазделы(РазделыДляОтметки, Элементы.Пользователи.ТекущиеДанные.Комментарий);
		ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ДатыЗапретаИзменения"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьРазделы(ОтмеченныеРазделы, Комментарий)
	
	ДатыЗапретаДерево = РеквизитФормыВЗначение("ДатыЗапрета");
	
	Для каждого ОписаниеРаздела Из ОтмеченныеРазделы Цикл
		Если ОписаниеРаздела.Пометка Тогда
			ЗаблокироватьИЗаписатьПустыеДаты(ОписаниеРаздела.Значение, ОписаниеРаздела.Значение, ТекущийПользователь, Комментарий);
			ЭлементДанных = ДатыЗапретаДерево.Строки.Добавить();
			ЭлементДанных.ЭтоРаздел = Истина;
			ЭлементДанных.Раздел = ОписаниеРаздела.Значение;
			ЭлементДанных.Объект = ОписаниеРаздела.Значение;
			Если ОписаниеРаздела.Значение = РазделПустаяСсылка Тогда
				ЭлементДанных.Представление = ТекстПредставленияОбщейДаты();
			Иначе
				ЭлементДанных.Представление = Строка(ОписаниеРаздела.Значение);
			КонецЕсли;
			ЭлементДанных.ПолноеПредставление = ЭлементДанных.Представление;
			ЭлементДанных.ОписаниеДатыЗапрета = "ПроизвольнаяДата";
			ЭлементДанных.ОписаниеДатыЗапретаПредставление = ПредставлениеОписанияДатыЗапрета(ЭлементДанных);
		КонецЕсли;
	КонецЦикла;
	
	ДатыЗапретаДерево.Строки.Сортировать("ПолноеПредставление Возр");
	
	ЗначениеВРеквизитФормы(ДатыЗапретаДерево, "ДатыЗапрета");
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьДатуЗапретаВФорме()
	
	ВыделенныеСтроки = Элементы.ДатыЗапрета.ВыделенныеСтроки;
	// Отмена выделения строк разделов с объектами
	Индекс = ВыделенныеСтроки.Количество()-1;
	ОбновитьВыделение = Ложь;
	Пока Индекс >= 0 Цикл
		Строка = ДатыЗапрета.НайтиПоИдентификатору(ВыделенныеСтроки[Индекс]);
		Если НЕ ЗначениеЗаполнено(Строка.Представление) Тогда
			ВыделенныеСтроки.Удалить(Индекс);
			ОбновитьВыделение = Истина;
		КонецЕсли;
		Индекс = Индекс - 1;
	КонецЦикла;
	Если ВыделенныеСтроки.Количество() > 0 Тогда
		Если ОбновитьВыделение Тогда
			Элементы.ДатыЗапрета.Обновить();
			Предупреждение(ТекстСообщенияСнятоВыделениеСНезаполненныхСтрок());
		КонецЕсли;
		// Блокировка записей выделенных строк
		Для каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
			ТекущиеДанные = ДатыЗапрета.НайтиПоИдентификатору(ВыделеннаяСтрока);
			ПрочитанныеСвойства = ЗаблокироватьЗаписьПользователя(ЭтаФорма, ТекущийРаздел(ТекущиеДанные), ТекущиеДанные.Объект, ТекущийПользователь);
			ОбновитьЗначенияПрочитанныхСвойств(ТекущиеДанные, ПрочитанныеСвойства, Элементы.Пользователи.ТекущиеДанные);
		КонецЦикла;
		// Изменение описания даты запрета
		ПараметрыФормы = Новый Структура;
		Если УстановкаДатыЗапрета = "ПоПользователям" Тогда
			ПараметрыФормы.Вставить("ПользовательПредставление", Элементы.Пользователи.ТекущиеДанные.Представление);
		Иначе
			ПараметрыФормы.Вставить("ПользовательПредставление", ТекстПредставленияДляВсехПользователей(ЭтаФорма));
		КонецЕсли;
		Если ВыделенныеСтроки.Количество() = 1 Тогда
			Если СпособУказанияДатыЗапрета = "ПоОбъектам" Тогда
				Если Элементы.ДатыЗапрета.ТекущиеДанные.ЭтоРаздел Тогда
					ПараметрыФормы.Вставить("РазделПредставление", Элементы.ДатыЗапрета.ТекущиеДанные.Представление);
					ПараметрыФормы.Вставить("ОбъектПредставление", "");
				Иначе
					ПараметрыФормы.Вставить("РазделПредставление", "");
					ПараметрыФормы.Вставить("ОбъектПредставление", Элементы.ДатыЗапрета.ТекущиеДанные.Представление);
				КонецЕсли;
			Иначе
				Если Элементы.ДатыЗапрета.ТекущиеДанные.ЭтоРаздел Тогда
					ПараметрыФормы.Вставить("РазделПредставление", Элементы.ДатыЗапрета.ТекущиеДанные.Представление);
					РазделБезОбъектов = РазделыБезОбъектов.НайтиПоЗначению(Элементы.ДатыЗапрета.ТекущиеДанные.Раздел) <> Неопределено;
					Если НЕ РазделБезОбъектов И ТипЗнч(ТекущийПользователь) <> Тип("ПеречислениеСсылка.ВидыНазначенияДатЗапрета") Тогда
						ПараметрыФормы.Вставить("РазрешитьПоУмолчанию", Истина);
					КонецЕсли;
				Иначе
					ПараметрыФормы.Вставить("РазделПредставление", Элементы.ДатыЗапрета.ТекущиеДанные.ПолучитьРодителя().Представление);
					ПараметрыФормы.Вставить("ОбъектПредставление", Элементы.ДатыЗапрета.ТекущиеДанные.Представление);
				КонецЕсли;
			КонецЕсли;
		Иначе
			ПараметрыФормы.Вставить("РазделПредставление", "<...>");
			ПараметрыФормы.Вставить("ОбъектПредставление", "<...>");
		КонецЕсли;
		ПараметрыФормы.Вставить("ОписаниеДатыЗапрета",      Элементы.ДатыЗапрета.ТекущиеДанные.ОписаниеДатыЗапрета);
		ПараметрыФормы.Вставить("КоличествоДнейРазрешения", Элементы.ДатыЗапрета.ТекущиеДанные.КоличествоДнейРазрешения);
		ПараметрыФормы.Вставить("ДатаЗапрета",              Элементы.ДатыЗапрета.ТекущиеДанные.ДатаЗапрета);
		НовыеДанные = ОткрытьФормуМодально("РегистрСведений.ДатыЗапретаИзменения.Форма.РедактированиеДатыЗапрета", ПараметрыФормы);
		Если НовыеДанные <> Неопределено Тогда
			Для каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
				Строка = ДатыЗапрета.НайтиПоИдентификатору(ВыделеннаяСтрока);
				Строка.ОписаниеДатыЗапрета              = НовыеДанные.ОписаниеДатыЗапрета;
				Строка.КоличествоДнейРазрешения         = НовыеДанные.КоличествоДнейРазрешения;
				Строка.ДатаЗапрета                      = НовыеДанные.ДатаЗапрета;
				ЗаписатьОписаниеИДатуЗапрета(Строка);
				Строка.ОписаниеДатыЗапретаПредставление = ПредставлениеОписанияДатыЗапрета(Строка);
			КонецЦикла;
			ОбновитьНаличиеДатЗапретаТекущегоПользователя();
		КонецЕсли;
		// Отмена блокировки выделенных строк
		РазблокироватьВсеЗаписи(ЭтаФорма);
	Иначе
		Предупреждение(ТекстСообщенияВыделенныеСтрокиНезаполнены());
	КонецЕсли;
	
КонецПроцедуры


&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеОписанияДатыЗапрета(Знач Данные)
	
	Представление = СписокОписанийДатыЗапрета().НайтиПоЗначению(Данные.ОписаниеДатыЗапрета).Представление;
	Если Данные.КоличествоДнейРазрешения > 0 Тогда
		Представление = Представление + " (" + Формат(Данные.КоличествоДнейРазрешения, "ЧГ=") + ")";
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВнутреннееОписаниеДатыЗапрета(Знач Данные)
	
	ВнутреннееОписание = "";
	Если Данные.ОписаниеДатыЗапрета <> "ПроизвольнаяДата" Тогда
		ВнутреннееОписание = СокрЛП(Данные.ОписаниеДатыЗапрета + Символы.ПС + Формат(Данные.КоличествоДнейРазрешения, "ЧГ=0"));
	КонецЕсли;
	
	Возврат ВнутреннееОписание;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПоВнутреннемуОписаниюДатыЗапрета(Знач Данные, Знач ВнутреннееОписание)
	
	Данные.ОписаниеДатыЗапрета = "ПроизвольнаяДата";
	Данные.КоличествоДнейРазрешения = 0;
	
	Если ЗначениеЗаполнено(ВнутреннееОписание) Тогда
		Строка1 = СтрПолучитьСтроку(ВнутреннееОписание, 1);
		Строка2 = СтрПолучитьСтроку(ВнутреннееОписание, 2);
		НайденныйЭлемент = СписокОписанийДатыЗапрета().НайтиПоЗначению(Строка1);
		Если НайденныйЭлемент <> Неопределено Тогда
			Данные.ОписаниеДатыЗапрета = НайденныйЭлемент.Значение;
			Если ЗначениеЗаполнено(Строка2) Тогда
				Попытка
					Данные.КоличествоДнейРазрешения = Число(Строка2);
				Исключение
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НазначениеДляВсех(Пользователь)
	
	Возврат ТипЗнч(Пользователь) = Тип("ПеречислениеСсылка.ВидыНазначенияДатЗапрета");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ДатаЗапретаУстановлена(Данные, Пользователь)
	
	Если НазначениеДляВсех(Пользователь) Тогда
		ДатаЗапретаУстановлена =
				ЗначениеЗаполнено(Данные.ДатаЗапрета)
			ИЛИ Данные.ОписаниеДатыЗапрета <> "ПроизвольнаяДата";
	Иначе
		ДатаЗапретаУстановлена =
				ЗначениеЗаполнено(Данные.ДатаЗапрета)
			ИЛИ Данные.ОписаниеДатыЗапрета <> "";
	КонецЕсли;
	
	Возврат ДатаЗапретаУстановлена;
	
КонецФункции

&НаСервере
Процедура ЗаблокироватьИЗаписатьПустыеДаты(Раздел, Объект, Пользователь, Комментарий)
	
	Если ТипЗнч(Объект) = Тип("Массив") Тогда
		ОбъектыДляДобавления = Объект;
	Иначе
		ОбъектыДляДобавления = Новый Массив;
		ОбъектыДляДобавления.Добавить(Объект);
	КонецЕсли;
	
	Для каждого ТекущийОбъект Из ОбъектыДляДобавления Цикл
		ЗаблокироватьЗаписьПользователя(ЭтаФорма, Раздел, ТекущийОбъект, Пользователь);
	КонецЦикла;
	
	Для каждого ТекущийОбъект Из ОбъектыДляДобавления Цикл
		ЗаписатьДатуЗапретаСОписанием(
			Раздел,
			ТекущийОбъект,
			Пользователь,
			'00000000',
			"",
			Комментарий);
	КонецЦикла;
	
	РазблокироватьВсеЗаписи(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьКоманднуюПанельДатЗапрета(Контекст)
	
	Если НазначениеДляВсех(Контекст.ТекущийПользователь) Тогда
		Если Контекст.СпособУказанияДатыЗапрета = "ПоРазделам" Тогда
			ТекущаяПанель = "ДатыЗапретаБезВыбораРазделовБезВыбораОбъектов";
		Иначе
			ТекущаяПанель = "ДатыЗапретаБезВыбораРазделовСВыборомОбъектов";
		КонецЕсли;
	Иначе
		Если Контекст.СпособУказанияДатыЗапрета = "ПоРазделам" Тогда
			ТекущаяПанель = "ДатыЗапретаСВыборомРазделовБезВыбораОбъектов";
			
		ИначеЕсли Контекст.СпособУказанияДатыЗапрета = "ПоОбъектам" Тогда
			ТекущаяПанель = "ДатыЗапретаСВыборомОбщейДатыСВыборомОбъектов";
		Иначе
			ТекущаяПанель = "ДатыЗапретаСВыборомРазделовСВыборомОбъектов";
		КонецЕсли;
	КонецЕсли;
	Контекст.Элементы.ДатыЗапретаКомандныеПанели.ТекущаяСтраница = Контекст.Элементы[ТекущаяПанель];
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция КомментарийТекущегоПользователя(Контекст)
	
	Если Контекст.УстановкаДатыЗапрета = "ПоПользователям" Тогда
		Комментарий = Контекст.Элементы.Пользователи.ТекущиеДанные.Комментарий;
	Иначе
		Комментарий = ТекстКомментарияДляВсехПользователей();
	КонецЕсли;
	
	Возврат Комментарий;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры и функции этой формы и формы РедактированиеДатыЗапрета
//

&НаКлиентеНаСервереБезКонтекста
Процедура ОбщаяДатаЗапретаСОписаниемПриИзменении(Знач Контекст, РассчитатьДатуЗапрета = Истина);
	
	Сутки = 60*60*24;
	
	Если Контекст.ОписаниеДатыЗапрета = "ПроизвольнаяДата" Тогда
		Контекст.Элементы.СвойстваНепроизвольнойДаты.ТекущаяСтраница = Контекст.Элементы.НепроизвольнаяДатаНеИспользуется;
		Контекст.Элементы.ПроизвольнаяДата.ТекущаяСтраница = Контекст.Элементы.ПроизвольнаяДатаИспользуется;
		Контекст.РазрешитьИзменениеДанныхДоДатыЗапрета = Ложь;
		Контекст.КоличествоДнейРазрешения = 0;
	Иначе
		Контекст.Элементы.СвойстваНепроизвольнойДаты.ТекущаяСтраница = Контекст.Элементы.НепроизвольнаяДатаИспользуется;
		Контекст.Элементы.ПроизвольнаяДата.ТекущаяСтраница = Контекст.Элементы.ПроизвольнаяДатаНеИспользуется;
		Если Контекст.ОписаниеДатыЗапрета = "ПредыдущийДень" Тогда
			Контекст.Элементы.РазрешитьИзменениеДанныхДоДатыЗапрета.Доступность = Ложь;
			Контекст.РазрешитьИзменениеДанныхДоДатыЗапрета = Ложь;
		Иначе
			Контекст.Элементы.РазрешитьИзменениеДанныхДоДатыЗапрета.Доступность = Истина;
		КонецЕсли;
		РасчетныеДатыЗапрета = РасчетДатыЗапрета(Контекст.ОписаниеДатыЗапрета, Контекст.ТекущаяДатаНаСервере);
		Если РассчитатьДатуЗапрета Тогда
			Контекст.ДатаЗапрета = РасчетныеДатыЗапрета.Текущая;
		КонецЕсли;
		ТекстНадписи = "";
		Если Контекст.РазрешитьИзменениеДанныхДоДатыЗапрета Тогда
			СкорректироватьКоличествоДнейРазрешения(Контекст.ОписаниеДатыЗапрета, Контекст.КоличествоДнейРазрешения);
			Контекст.Элементы.СвойствоКоличествоДнейРазрешенияИзменения.ТекущаяСтраница = Контекст.Элементы.ИзменениеДанныхДоДатыЗапретаРазрешено;
			СрокРазрешения = РасчетныеДатыЗапрета.Текущая + Контекст.КоличествоДнейРазрешения * Сутки;
			
			Если Контекст.ТекущаяДатаНаСервере > СрокРазрешения Тогда
				ТекстНадписи = Символы.ПС
				             + НСтр("ru = 'Срок возможности изменения данных с %3 по %4 истек %2'");
			Иначе
				Если РассчитатьДатуЗапрета Тогда
					Контекст.ДатаЗапрета = РасчетныеДатыЗапрета.Предыдущая;
				КонецЕсли;
				ТекстНадписи = Символы.ПС
				             + НСтр("ru = 'По %2 возможно изменение данных с %3 по %4'")
				             + Символы.ПС
				             + НСтр("ru = 'После %2 будет запрещено изменение данных по %4'")
				             + Символы.ПС;
			КонецЕсли;
		Иначе
			Контекст.Элементы.СвойствоКоличествоДнейРазрешенияИзменения.ТекущаяСтраница = Контекст.Элементы.ИзменениеДанныхДоДатыЗапретаНеРазрешено;
			Контекст.КоличествоДнейРазрешения = 0;
		КонецЕсли;
		Контекст.Элементы.ПояснениеНепроизвольнойДаты.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			  НСтр("ru = 'Запрещено изменение данных по %1'")
			+ ТекстНадписи,
			Формат(Контекст.ДатаЗапрета, "ДФ=dd.MM.yyyy"),
			Формат(СрокРазрешения, "ДФ=dd.MM.yyyy"),
			Формат(РасчетныеДатыЗапрета.Предыдущая + Сутки, "ДФ=dd.MM.yyyy"),
			Формат(РасчетныеДатыЗапрета.Текущая, "ДФ=dd.MM.yyyy"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РасчетДатыЗапрета(Знач ВариантДатыЗапрета, Знач ТекущаяДатаНаСервере)
	
	Сутки = 60*60*24;
	
	ТекущаяДатаЗапрета    = '00000000';
	ПредыдущаяДатаЗапрета = '00000000';
	
	Если ВариантДатыЗапрета = "КонецПрошлогоГода" Тогда
		ТекущаяДатаЗапрета    = НачалоГода(ТекущаяДатаНаСервере) - Сутки;
		ПредыдущаяДатаЗапрета = НачалоГода(ТекущаяДатаЗапрета) - Сутки;
		
	ИначеЕсли ВариантДатыЗапрета = "КонецПрошлогоКвартала" Тогда
		ТекущаяДатаЗапрета    = НачалоКвартала(ТекущаяДатаНаСервере) - Сутки;
		ПредыдущаяДатаЗапрета = НачалоКвартала(ТекущаяДатаЗапрета) - Сутки;
		
	ИначеЕсли ВариантДатыЗапрета = "КонецПрошлогоМесяца" Тогда
		ТекущаяДатаЗапрета    = НачалоМесяца(ТекущаяДатаНаСервере) - Сутки;
		ПредыдущаяДатаЗапрета = НачалоМесяца(ТекущаяДатаЗапрета) - Сутки;
		
	ИначеЕсли ВариантДатыЗапрета = "КонецПрошлойНедели" Тогда
		ТекущаяДатаЗапрета    = НачалоНедели(ТекущаяДатаНаСервере) - Сутки;
		ПредыдущаяДатаЗапрета = НачалоНедели(ТекущаяДатаЗапрета) - Сутки;
		
	ИначеЕсли ВариантДатыЗапрета = "ПредыдущийДень" Тогда
		ТекущаяДатаЗапрета    = НачалоДня(ТекущаяДатаНаСервере) - Сутки;
		ПредыдущаяДатаЗапрета = НачалоДня(ТекущаяДатаЗапрета) - Сутки;
	КонецЕсли;
	
	Возврат Новый Структура("Текущая, Предыдущая", ТекущаяДатаЗапрета, ПредыдущаяДатаЗапрета);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура СкорректироватьКоличествоДнейРазрешения(Знач ОписаниеДатыЗапрета, КоличествоДнейРазрешения)
	
	Если КоличествоДнейРазрешения = 0 Тогда
		КоличествоДнейРазрешения = 1;
		
	ИначеЕсли ОписаниеДатыЗапрета = "КонецПрошлогоГода" Тогда
		Если КоличествоДнейРазрешения > 90 Тогда
			КоличествоДнейРазрешения = 90;
		КонецЕсли;
		
	ИначеЕсли ОписаниеДатыЗапрета = "КонецПрошлогоКвартала" Тогда
		Если КоличествоДнейРазрешения > 60 Тогда
			КоличествоДнейРазрешения = 60;
		КонецЕсли;
		
	ИначеЕсли ОписаниеДатыЗапрета = "КонецПрошлогоМесяца" Тогда
		Если КоличествоДнейРазрешения > 25 Тогда
			КоличествоДнейРазрешения = 25;
		КонецЕсли;
		
	ИначеЕсли ОписаниеДатыЗапрета = "КонецПрошлойНедели" Тогда
		Если КоличествоДнейРазрешения > 5 Тогда
			КоличествоДнейРазрешения = 5;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Вспомогательные функции строк интерфейса пользователя
//

&НаКлиентеНаСервереБезКонтекста
Функция СписокОписанийДатыЗапрета()
	
	Список = Новый СписокЗначений;
	Список.Добавить("",                "(" + НСтр("ru = 'По умолчанию'") + ")");
	Список.Добавить("ПроизвольнаяДата",      НСтр("ru = 'Произвольная дата'"));
	Список.Добавить("КонецПрошлогоГода",     НСтр("ru = 'Конец прошлого года'"));
	Список.Добавить("КонецПрошлогоКвартала", НСтр("ru = 'Конец прошлого квартала'"));
	Список.Добавить("КонецПрошлогоМесяца",   НСтр("ru = 'Конец прошлого месяца'"));
	Список.Добавить("КонецПрошлойНедели",    НСтр("ru = 'Конец прошлой недели'"));
	Список.Добавить("ПредыдущийДень",        НСтр("ru = 'Предыдущий день'"));
	
	Возврат Список;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстПредставленияДляВсехПользователей(Контекст)
	
	Возврат "<" + Контекст.ЗначениеДляВсехПользователей + ">";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстПредставленияПользователя(Контекст, Пользователь)
	
	Если Контекст.Параметры.ДатыЗапретаЗагрузкиДанных Тогда
		Для каждого ЗначениеСписка Из Контекст.СписокТиповПользователей Цикл
			Если ТипЗнч(ЗначениеСписка.Значение) = ТипЗнч(Пользователь) Тогда
				Если ЗначениеЗаполнено(Пользователь) Тогда
					Возврат ЗначениеСписка.Представление + ": " + Строка(Пользователь);
				Иначе
					Возврат ЗначениеСписка.Представление + ": " + НСтр("ru = '<Все узлы>'");
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Пользователь) Тогда
		Возврат Строка(Пользователь);
	КонецЕсли;
	
	Возврат Строка(ТипЗнч(Пользователь));
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстКомментарияДляВсехПользователей()
	
	Возврат "(" + НСтр("ru = 'По умолчанию'") + ")";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстПредставленияОбщейДаты()
	
	Возврат "<" + НСтр("ru = 'Общая дата'") + ">";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстЗаголовкаКомандыОтчетПоОбъектам()
	
	Возврат НСтр("ru = 'Отчет по объектам'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстЗаголовкаКомандыОтчетПоРазделам()
	
	Возврат НСтр("ru = 'Отчет по разделам'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстВопросаЗакрытьФорму()
	
	Возврат НСтр("ru = 'Закрыть форму?'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстВопросаПересчитатьДатыЗапрета()
	
	Возврат НСтр("ru = 'Очистить незаполненые строки и пересчитать относительные даты запрета?'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстВопросаОбновитьДанные()
	
	Возврат НСтр("ru = 'Обновить данные?'");
	
КонецФункции

&НаКлиенте
Функция ТекстВопросаУдалитьВсеДатыЗапретаКромеДатДляВсехПользователей()
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Удалить все даты запрета, кроме установленных %1?'"), ЗначениеДляВсехПользователей);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстВопросаУдалитьВсеДатыЗапрета()
	
	Возврат НСтр("ru = 'Удалить все даты запрета?'");
	
КонецФункции

&НаКлиенте
Функция ТекстСообщенияЗначениеДляВсехПользователейНеИзменяется()
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Значение %1 не изменяется.'"), ТекстПредставленияДляВсехПользователей(ЭтаФорма));
	
КонецФункции

&НаКлиенте
Функция ТекстСообщенияКомментарийДляВсехПользователейНеИзменяется()
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Комментарий %1 не изменяется.'"), ТекстПредставленияДляВсехПользователей(ЭтаФорма));
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстСообщенияСначалаВыберитеПользователя()
	
	Возврат НСтр("ru = 'Сначала выберите пользователя.'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстСообщенияСпособУказанияНеИспользован(СпособУказанияВФорме, СпособУказанияВБазеДанных)
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Способ указания ""%1"" не использован,
				           |поэтому сохранен способ указания ""%2""'"),
				СпособУказанияВФорме,
				СпособУказанияВБазеДанных);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстВопросаОчиститьНезаполненыеСтроки()
	
	Возврат НСтр("ru = 'Очистить незаполненные строки?'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстСообщенияДляУдаленияВыделитеОднуСтроку()
	
	Возврат НСтр("ru = 'Для удаления выделите одну строку.'");
	
КонецФункции

&НаКлиенте
Функция ТекстВопросаУдалитьДатыЗапретаДляВсехПользователей()
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Удалить даты запрета %1?'"), НРег(ЗначениеДляВсехПользователей));
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстВопросаУдалитьДатыЗапретаДляПользователя()
	
	Возврат НСтр("ru = 'Удалить даты запрета для пользователя?'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстВопросаУдалитьДатыЗапретаДляГруппыПользователей()
	
	Возврат НСтр("ru = 'Удалить даты запрета для группы пользователей?'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстВопросаУдалитьДатыЗапретаДляВнешнегоПользователя()
	
	Возврат НСтр("ru = 'Удалить даты запрета для внешнего пользователя?'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстВопросаУдалитьДатыЗапретаДляГруппыВнешнихПользователей()
	
	Возврат НСтр("ru = 'Удалить даты запрета для группы внешних пользователей?'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстВопросаУдалитьДатыЗапрета()
	
	Возврат НСтр("ru = 'Удалить даты запрета?'");
	
КонецФункции

&НаКлиенте
Функция ТекстСообщенияДляВсехПользователейДатыЗапретаНеУстановлены()
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 даты запрета не установлены.'"), ЗначениеДляВсехПользователей);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстСообщенияЗначениеУжеДобавленоВСписок()
	
	Возврат НСтр("ru = 'Значение уже добавлено в список.'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстСообщенияОбновитьДанныеФормыF5()
	
	Возврат НСтр("ru = 'Обновите данные формы (F5).'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстВопросаУдалитьДатыЗапретаДляРазделовИОбъектов()
	
	Возврат НСтр("ru = 'Удалить даты запрета для разделов и объектов?'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстВопросаУдалитьДатыЗапретаДляОбъектов()
	
	Возврат НСтр("ru = 'Удалить даты запрета для объектов?'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстВопросаУдалитьДатыЗапретаДляРазделов()
	
	Возврат НСтр("ru = 'Удалить даты запрета для разделов?'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстСообщенияВВыбранномРазделеДатыЗапретаДляОбъектовНеУстанавливаются()
	
	Возврат НСтр("ru = 'В выбранном разделе даты запрета для объектов не устанавливаются.'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстСообщенияОбщаяДатаМожетБытьУстановлена()
	
	Возврат НСтр("ru = '<Общая дата> может быть установлена.'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстСообщенияРазделыУжеЗаполненыМожноУстановитьДатыЗапретаДляРазделов()
	
	Возврат НСтр("ru = 'Разделы уже заполнены,
	                   |можно установить даты запрета
	                   |для разделов.'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстСообщенияРазделыУжеЗаполненыМожноУстановитьДатыЗапретаДляРазделовИОбъектов()
	
	Возврат НСтр("ru = 'Разделы уже заполнены,
	                   |можно установить даты запрета
	                   |для разделов и объектов.'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстСообщенияОбъектУжеВыбранМожноУстановитьДатуЗапрета()
	
	Возврат НСтр("ru = 'Объект уже выбран,
	                   |можно установить дату запрета.'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстСообщенияСначалаВыберитеОбъект()
	
	Возврат НСтр("ru = 'Сначала выберите объект.'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстВопросаУдалитьДатыЗапретаДляРазделаИОбъектов()
	
	Возврат НСтр("ru = 'Удалить даты запрета для раздела и объектов?'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстВопросаУдалитьДатыЗапретаДляРаздела()
	
	Возврат НСтр("ru = 'Удалить дату запрета для раздела?'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстВопросаУдалитьОбщуюДатуЗапрета()
	
	Возврат НСтр("ru = 'Удалить общую дату запрета?'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстВопросаУдалитьДатуЗапретаОбъекта()
	
	Возврат НСтр("ru = 'Удалить дату запрета для объекта?'");
	
КонецФункции

&НаКлиенте
Функция ТекстСообщенияДляВсехПользователейРазделыВсегдаПоказываются()
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 разделы всегда показываются.'"), ЗначениеДляВсехПользователей);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстСообщенияПриУдаленииДатаЗапретаОчищается()
	
	Возврат НСтр("ru = 'При удалении дата запрета очищается.'");
	
КонецФункции

&НаКлиенте
Функция ТекстСообщенияДляВсехПользователейОбщаяДатаВсегдаПоказывается()
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 <Общая дата> всегда показывается.'"), ЗначениеДляВсехПользователей);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстЗаголовкаРаздела()
	
	Возврат НСтр("ru = 'Раздел'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстЗаголовкаРазделаСОбъектами()
	
	Возврат НСтр("ru = 'Раздел, объект'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстСообщенияНедостаточноПравДляВыбораВнешнихПользователей()
	
	Возврат НСтр("ru = 'Недостаточно прав для выбора внешних пользователей.'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстЗаголовкаВыборТипаДанных()
	
	Возврат НСтр("ru = 'Выбор типа данных'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстСообщенияНастройкиСНевыбраннымиПользователямиНеСохранены(Контекст)
	
	Если Контекст.Параметры.ДатыЗапретаЗагрузкиДанных Тогда
		Возврат НСтр("ru = 'Настройки с невыбранными узлами планов обмена не сохранены.'");
	КонецЕсли;
	
	Возврат НСтр("ru = 'Настройки с невыбранными пользователями не сохранены.'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстСообщенияНастройкиСНевыбраннымиОбъектамиНеСохранены()
	
	Возврат НСтр("ru = 'Настройки с невыбранными объектами не сохранены.'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстСообщенияНастройкиСНезаполненнымиДатамиЗапретаДляОбъектовНеСохранены()
	
	Возврат НСтр("ru = 'Настройки с незаполненными датами запрета для объектов не сохранены.'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстСообщенияНастройкиСНезаполненнымиДатамиЗапретаДляРазделовНеСохранены()
	
	Возврат НСтр("ru = 'Настройки с незаполненными датами запрета для разделов не сохранены.'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстСообщенияОбщаяДатаУжеПоказана()
	
	Возврат НСтр("ru = '<Общая дата> уже показана.'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстСообщенияВсеРазделыУжеПоказаны()
	
	Возврат НСтр("ru = 'Все разделы уже показаны.'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокВыборТребуемыхРазделов()
	
	Возврат НСтр("ru = 'Выбор требуемых разделов'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстСообщенияСнятоВыделениеСНезаполненныхСтрок()
	
	Возврат НСтр("ru = 'Снято выделение с незаполненных строк.'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстСообщенияВыделенныеСтрокиНезаполнены()
	
	Возврат НСтр("ru ='Выделенные строки не заполнены.'");
	
КонецФункции





