
// Процедура добавляет запись в регистр по переданным значениям структуры
Процедура ДобавитьЗапись(СтруктураЗаписи, Загрузка = Ложь) Экспорт
	
	ОбменДаннымиСервер.ДобавитьЗаписьВРегистрСведений(СтруктураЗаписи, "СоответствиеОбъектовИнформационныхБаз", Загрузка);
	
КонецПроцедуры

// Процедура удаляет набор записей в регистре по переданным значениям структуры
Процедура УдалитьЗапись(СтруктураЗаписи, Загрузка = Ложь) Экспорт
	
	ОбменДаннымиСервер.УдалитьНаборЗаписейВРегистреСведений(СтруктураЗаписи, "СоответствиеОбъектовИнформационныхБаз", Загрузка);
	
КонецПроцедуры

Функция ОбъектЕстьВРегистре(Объект, УзелИнформационнойБазы) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ 1
	|ИЗ
	|	РегистрСведений.СоответствиеОбъектовИнформационныхБаз КАК СоответствиеОбъектовИнформационныхБаз
	|ГДЕ
	|	  СоответствиеОбъектовИнформационныхБаз.УзелИнформационнойБазы           = &УзелИнформационнойБазы
	|	И СоответствиеОбъектовИнформационныхБаз.УникальныйИдентификаторИсточника = &УникальныйИдентификаторИсточника
	|";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УзелИнформационнойБазы",           УзелИнформационнойБазы);
	Запрос.УстановитьПараметр("УникальныйИдентификаторИсточника", Объект);
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Не Запрос.Выполнить().Пустой();
КонецФункции

Процедура УдалитьНеактуальныеЗаписиРежимаВыгрузкиПоСсылке(УзелИнформационнойБазы) Экспорт
	
	ТекстЗапроса = "
	|//////////////////////////////////////////////////////////{СоответствиеОбъектовИнформационныхБазПоСсылке}
	|ВЫБРАТЬ
	|	СоответствиеОбъектовИнформационныхБаз.УзелИнформационнойБазы,
	|	СоответствиеОбъектовИнформационныхБаз.УникальныйИдентификаторИсточника,
	|	СоответствиеОбъектовИнформационныхБаз.УникальныйИдентификаторПриемника,
	|	СоответствиеОбъектовИнформационныхБаз.ТипПриемника,
	|	СоответствиеОбъектовИнформационныхБаз.ТипИсточника
	|ПОМЕСТИТЬ СоответствиеОбъектовИнформационныхБазПоСсылке
	|ИЗ
	|	РегистрСведений.СоответствиеОбъектовИнформационныхБаз КАК СоответствиеОбъектовИнформационныхБаз
	|ГДЕ
	|	  СоответствиеОбъектовИнформационныхБаз.УзелИнформационнойБазы = &УзелИнформационнойБазы
	|	И СоответствиеОбъектовИнформационныхБаз.ОбъектВыгруженПоСсылке
	|;
	|
	|//////////////////////////////////////////////////////////{}
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоответствиеОбъектовИнформационныхБазПоСсылке.УзелИнформационнойБазы,
	|	СоответствиеОбъектовИнформационныхБазПоСсылке.УникальныйИдентификаторИсточника,
	|	СоответствиеОбъектовИнформационныхБазПоСсылке.УникальныйИдентификаторПриемника,
	|	СоответствиеОбъектовИнформационныхБазПоСсылке.ТипПриемника,
	|	СоответствиеОбъектовИнформационныхБазПоСсылке.ТипИсточника
	|ИЗ
	|	СоответствиеОбъектовИнформационныхБазПоСсылке КАК СоответствиеОбъектовИнформационныхБазПоСсылке
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеОбъектовИнформационныхБаз КАК СоответствиеОбъектовИнформационныхБаз
	|ПО   СоответствиеОбъектовИнформационныхБаз.УникальныйИдентификаторИсточника = СоответствиеОбъектовИнформационныхБазПоСсылке.УникальныйИдентификаторИсточника
	|	И СоответствиеОбъектовИнформационныхБаз.ОбъектВыгруженПоСсылке = Ложь
	|	И СоответствиеОбъектовИнформационныхБаз.УзелИнформационнойБазы = &УзелИнформационнойБазы
	|ГДЕ
	|	НЕ СоответствиеОбъектовИнформационныхБаз.УзелИнформационнойБазы ЕСТЬ NULL
	|";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УзелИнформационнойБазы", УзелИнформационнойБазы);
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			СтруктураЗаписи = Новый Структура("УзелИнформационнойБазы, УникальныйИдентификаторИсточника, УникальныйИдентификаторПриемника, ТипПриемника, ТипИсточника");
			
			ЗаполнитьЗначенияСвойств(СтруктураЗаписи, Выборка);
			
			УдалитьЗапись(СтруктураЗаписи, Истина);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры
